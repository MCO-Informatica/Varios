
#Include 'Protheus.ch'                
#Include "FwBrowse.ch"
#INCLUDE "FILEIO.CH"
#INCLUDE "RPTDEF.CH"                                      
#INCLUDE "FWPrintSetup.ch"

#Define ENTER CHR(13)+CHR(10)                                   

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma   DHOME04   ºAutor  ³ FONTANELLI         º Data ³  01/12/14   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Coleta de Caixas.                                          º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³                                                            º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
User Function DHOME04()

Private cCadastro 
Private cTitulo	  

Private oFont		:= TFont():New("MS Sans Serif",,018,,.T.,,,,,.F.,.F.)
Private oFont1 		:= TFont():New("Verdana",,024,,.T.,,,,,.F.,.F.)
Private oFont2 		:= TFont():New("Verdana",,038,,.T.,,,,,.F.,.F.)

Private cAliasTMP	:= ""
Private cArqTrab	:= ""
Private lExiste		:= .F.

if !(cEmpAnt+cFilAnt $ GetMv("MV_PARCONF"))
	Aviso("Aviso", GetMv("MV_MENCONF"),{"OK"})   
	Return()
endif

cCadastro := "Coleta de Caixas"
cTitulo	  := cCadastro

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta Browse da tela inicial.                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

FwMsgRun( ,{|| COLETAR()}, , 'Selecionando Registros, Por favor aguarde' )

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³COLETAR    ºAutor  ³ EthosX            º Data ³  01/12/14   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Conferencia de saida de produtos.                          º±±
±±º          ³ BROWSE.                                                    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³                                                            º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function COLETAR()

Local oDlg
Private aSize 		:= {}
Private aObjects 	:= {}
Private aInfo  		:= {}
Private aPosObj 	:= {}
Private aPosGet 	:= {}
Private cFiltro	:= ""

Static oFWLayer
Static oFWLayer2
Static oWin1
Static oWin2
Static oWin3
Static oWin4
Static oWin5

aSize	:= MsAdvSize( .F. )
aInfo 	:= { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 3, 3 }

AAdd( aObjects, { 100	, 050	, .T., .F. })
AAdd( aObjects, { 100	, 100	, .T., .T. })

aPosObj	:= MsObjSize(aInfo,aObjects)
aPosGet	:= MsObjGetPos((aSize[3]-aSize[1]),315,{{004,024,240,270}} )

cFiltro	:= "(C5_FILIAL=='"+xFilial("SC5")+"')" // Filtra apenas filial corrente.
cFiltro	+= ".And. (C5_XSTATUS=='COL' .or. C5_XSTATUS=='ENT' .or. C5_XSTATUS=='RES') "

DbSelectArea("SC5")
SC5->( DbSetFilter( {|| &(cFiltro)},cFiltro ) )
SC5->(DbGotop())
	
DEFINE MSDIALOG oDlg TITLE cCadastro FROM aSize[7],aSize[1] TO aSize[6],aSize[5] OF oMainWnd STYLE nOR( WS_VISIBLE,WS_POPUP ) PIXEL

	oDlg:lEscClose := .F.
	
	oFWLayer := FWLayer():New()		
	oFWLayer:Init(oDlg,.F.)

	oFWLayer:AddCollumn("Col01",10,.T.)
	oFWLayer:AddCollumn("Col02",90,.T.)

	oFWLayer:AddWindow("Col01","Win01"	,"Açoes"	,100,.F.,.F.,/*bAction*/,/*cIDLine*/,/*bGotFocus*/)
	oFWLayer:AddWindow("Col02","Win02"	,cCadastro	,100,.F.,.F.,/*bAction*/,/*cIDLine*/,/*bGotFocus*/)
	
	oWin1 := oFWLayer:GetWinPanel('Col01','Win01')
	oWin2 := oFWLayer:GetWinPanel('Col02','Win02')

	// Botões da tela
	oBtn0 := TButton():New(0,0,"Sair",oWin1,{|| oDlg:End() },00,14,,,.F.,.T.,.F.,,.F.,,,.F.) 
	oBtn0:Align  := CONTROL_ALIGN_BOTTOM

	oBtn2 := TButton():New(0,0,"Inicia Coleta",oWin1,{|| VeCole06('SC5',SC5->(Recno()),3) },00,14,,,.F.,.T.,.F.,,.F.,,,.F.) 
	oBtn2:Align  := CONTROL_ALIGN_TOP

	@ 000, 000 SAY oBtn22 PROMPT Space(100) SIZE 049, 001 OF oWin1 PIXEL
	oBtn22:Align  := CONTROL_ALIGN_TOP
	
	oBtn3 := TButton():New(0,0,"Altera Coleta",oWin1,{|| VeCole06('SC5',SC5->(Recno()),4) },00,14,,,.F.,.T.,.F.,,.F.,,,.F.) 
	oBtn3:Align  := CONTROL_ALIGN_TOP

	@ 000, 000 SAY oBtn33 PROMPT Space(100) SIZE 049, 001 OF oWin1 PIXEL
	oBtn33:Align  := CONTROL_ALIGN_TOP
	
	oBtn4 := TButton():New(0,0,"Exclui Coleta",oWin1,{|| VeCole06('SC5',SC5->(Recno()),5) },00,14,,,.F.,.T.,.F.,,.F.,,,.F.) 
	oBtn4:Align  := CONTROL_ALIGN_TOP
                                
	@ 000, 000 SAY oBtn44 PROMPT Space(100) SIZE 049, 001 OF oWin1 PIXEL
	oBtn44:Align  := CONTROL_ALIGN_TOP
	
	//oBtn5 := TButton():New(0,0,"Finaliza Coleta",oWin1,{|| VeCole06('SC5',SC5->(Recno()),6) },00,14,,,.F.,.T.,.F.,,.F.,,,.F.) 
	//oBtn5:Align  := CONTROL_ALIGN_TOP
                                
	@ 000, 000 SAY oBtn55 PROMPT Space(100) SIZE 049, 001 OF oWin1 PIXEL
	oBtn55:Align  := CONTROL_ALIGN_TOP

	//Browse
	DEFINE FWBROWSE oBrowse DATA TABLE ALIAS "SC5" OF oWin2
		
	//Adiciona Legenda no Browse
	ADD LEGEND DATA 'C5_COLST == "1"'		COLOR "ENABLE"		TITLE "Aguardando Coleta."				OF oBrowse
	ADD LEGEND DATA 'C5_COLST == "2"'		COLOR "BR_LARANJA"	TITLE "Coleta Incompleta."				OF oBrowse
	ADD LEGEND DATA 'C5_COLST == "3"'  		COLOR "DISABLE"		TITLE "Coleta Finalizada"			 	OF oBrowse

	// Adiciona as colunas do Browse
	ADD COLUMN oColumn DATA { || C5_FILIAL   	}	TITLE "Filial"     	 		SIZE  01 OF oBrowse
	ADD COLUMN oColumn DATA { || C5_NUM	  	 	}	TITLE "Pedido"     	 		SIZE  06 OF oBrowse
	ADD COLUMN oColumn DATA { || C5_CLIENTE  	}	TITLE "Cliente"     		SIZE  06 OF oBrowse
	ADD COLUMN oColumn DATA { || C5_LOJACLI  	}	TITLE "Loja"	     		SIZE  02 OF oBrowse
	ADD COLUMN oColumn DATA { || Posicione("SA1",1,xFilial("SA1") + C5_CLIENTE + C5_LOJACLI, "A1_NOME")  }	TITLE "Nome Cliente"		SIZE  80 OF oBrowse
	ADD COLUMN oColumn DATA { || C5_TRANSP   	}	TITLE "Transp."			SIZE  06 OF oBrowse
	ADD COLUMN oColumn DATA { || Posicione("SA4",1,xFilial("SA4") + C5_TRANSP, "A4_NOME")  	}	TITLE "Nome Transp."		SIZE  20 OF oBrowse
	ADD COLUMN oColumn DATA { || C5_EMISSAO  	}	TITLE "Emissao"     		SIZE  08 OF oBrowse
	ADD COLUMN oColumn DATA { || C5_NOTA 	  	}	TITLE "Nota"	     		SIZE  09 OF oBrowse
	ADD COLUMN oColumn DATA { || C5_SERIE   	}	TITLE "Serie"	     		SIZE  03 OF oBrowse
		
	//Ativa o Browse
	ACTIVATE FWBROWSE oBrowse
		
ACTIVATE MSDIALOG oDlg CENTERED
Return


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³VeCole06   ºAutor  ³ Ethosx	         º Data ³  01/12/14   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Coleta de caixas.                                          º±±
±±º          ³ GetDados.                                                  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³                                                            º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function VeCole06(cAlias,nReg,nOpc)

Local nI
Local nUsado	:= 0
Local nOpca		:= 0
Local lWhen		:= .F.

Private cGetConf	:= SC5->C5_NUM
Private cGetData	:= Date()
Private cGetHora	:= "00:00"
Private cGetCod	    := SC5->C5_CLIENTE
Private cGetLj   	:= SC5->C5_LOJACLI
Private cGetNom	    := Posicione("SA1",1,xFilial("SA1") + C5_CLIENTE + C5_LOJACLI, "A1_NOME")
Private cGetCaixa	:= SPACE(10)
Private nQtd		:= 1
Private cMot		:= SPACE(30)

Private cGetUser	:= Space( 20 )
Private cMemoDv	:= ""
Private aHeader	:= {}
Private aCols	:= {}
Private aItens	:= {}
Private _oDlg
Private oGetdados
Private oGetCaixa
Private oQtd
Private oGetValor
Private INCLUI	:= (nOpc == 3)
Private ALTERA	:= (nOpc == 4)
Private EXCLUI	:= (nOpc == 5)
Private FINALI	:= (nOpc == 6)
Private nQtdCaixas	:= 0
Private nQtdConf	:= 0
Private oFont1	:= TFont():New("Verdana",,024,,.T.,,,,,.F.,.F.)
Private oFont2	:= TFont():New("Verdana",,044,,.T.,,,,,.F.,.F.)
Private oFont18	:= TFont():New("Verdana",,018,,.T.,,,,,.F.,.F.)

if SC5->C5_COLST == "1" .And. ((ALTERA) .or. (EXCLUI))
	Aviso("Aviso","Escolha, Iniciar Coleta !", {"OK"})
	oBrowse:Refresh()
	oBrowse:SetFocus()
	Return
elseIf SC5->C5_COLST == "3" .And. (INCLUI .Or. ALTERA .OR. FINALI)
	Aviso("Aviso","Pedido com Coleta Finaliza !", {"OK"})
	oBrowse:Refresh()
	oBrowse:SetFocus()
	Return
ElseIf SC5->C5_COLST == "2" .And. (INCLUI)
	Aviso("Aviso","Escolha, Alterar Coleta !", {"OK"})
	oBrowse:Refresh()
	oBrowse:SetFocus()
	Return
EndIf

/*
If FINALI 
	//Caso já exista NOTA na tabela ZZ2
	cQuery := "SELECT DISTINCT ZZ2_NOTA FROM "+RetSqlName("ZZ2")+" "
	cQuery += " WHERE ZZ2_FILIAL = '"+xFilial("ZZ2")+"' "
	cQuery += "   AND ZZ2_NUM = '"+SC5->C5_NUM+"' "
	cQuery += "   AND ZZ2_NOTA <> ' ' "
	cQuery += "   AND D_E_L_E_T_ = '' "
	cQuery := ChangeQuery(cQuery)
	dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),"QryZZ2",.T.,.T.)
	dbSelectArea("QryZZ2")
	dbGoTop()
	nQTDNOTA := 0
	While !QryZZ2->(Eof())
		nQTDNOTA += 1
		QryZZ2->(DbSkip())
	EndDo
	QryZZ2->(dbCloseArea())	


	IF SC5->C5_COLST == "2" 
		If MsgYesNo("Confirma a Finalização ?")
			RecLock("SC5",.F.)
			SC5->C5_COLST := "3"  
			SC5->C5_XSTATUS := "ENT"   

			if nQTDNOTA == 1
				SC5->C5_XLDT   := dDataBase
				SC5->C5_XLHR   := SUBSTR(Time(),1,5)
				SC5->C5_XLUSER := cUserName
				SC5->C5_XLMOT  := cMot
			endif
			
			SC5->(MsUnlock())
			Return Nil
		Else
			Return Nil	
		EndIf
    elseif SC5->C5_COLST == "3"
    	Aviso("Aviso","Coleta já Finalizada !", {"OK"})
    	Return Nil
    else 
    	Aviso("Aviso","Não foi coletado nenhuma Caixa !", {"OK"})
    	Return Nil
	EndIf
Endif
*/

nQtdConf := 0     
nQtdCaixas := 0

DbSelectArea("ZZ2")
ZZ2->(DbSetOrder(1))
ZZ2->(DbSeek(xFilial("ZZ2") + "C" + SC5->C5_NUM))
While !ZZ2->(Eof()) .And. ZZ2->ZZ2_NUM == SC5->C5_NUM
	
	if !(ZZ2->ZZ2_SCX)
		nPosProd := aScan(aItens, {|x| Alltrim(x[1]) == ZZ2->ZZ2_CAIXA})
		If nPosProd == 0    
			aAdd(aItens,{ZZ2->ZZ2_CAIXA,ZZ2->ZZ2_SCX})
			nQtdCaixas += 1
			if ZZ2->ZZ2_SCX
				nQtdConf += 1
			endif
	    endif                                
	endif
	ZZ2->(DbSkip())
EndDo

//Carrega o Header
DbSelectArea("SX3")
DbSetOrder(1)
DbSeek("ZZ2")
While !Eof() .And. X3_ARQUIVO == "ZZ2"
	If X3USO( X3_USADO ) .And. cNivel >= X3_NIVEL
		aAdd( aHeader, { Trim( X3Titulo() ), X3_CAMPO, X3_PICTURE, X3_TAMANHO, X3_DECIMAL, X3_VALID, X3_USADO, X3_TIPO, X3_ARQUIVO, X3_CONTEXT } )
	Endif
	dbSkip()
Enddo

cGetConf 	:= SC5->C5_NUM
cGetHora	:= SubStr(Time(),1,5)
cGetUser	:= cUserName
lWhen		:= .T.

DbSelectArea("ZZ2")
ZZ2->(DbSetOrder(1))
ZZ2->(DbSeek(xFilial("ZZ2") + "C" + SC5->C5_NUM ))
While !ZZ2->(Eof()) .And. SC5->C5_NUM == ZZ2->ZZ2_NUM .And. "C" == ZZ2->ZZ2_TIPOSC

	if (ZZ2->ZZ2_SCX)
	   ZZ2->(dbSkip()) 
	   LOOP
	endif 
		
	aAdd( aCols, Array( Len( aHeader ) + 1 ) )
		
	For nI := 1 To Len( aHeader )
		aCols[Len(aCols), nI] := &(Alltrim(aHeader[nI, 2]))
	Next nI
	
	aCols[Len(aCols), Len( aHeader )+1 ] := .F.
	ZZ2->(DbSkip())
EndDo        


DEFINE MSDIALOG _oDlg TITLE cCadastro FROM aSize[7],aSize[1] TO aSize[6],aSize[5] OF oMainWnd PIXEL

	oFWLayer2 := FWLayer():New()		
	oFWLayer2:Init(_oDlg,.F.)

	oFWLayer2:AddCollumn("Col03",10,.T.)
	oFWLayer2:AddCollumn("Col04",90,.T.)

	oFWLayer2:AddWindow("Col03","Win03"	,"Açoes"			,040,.F.,.F.,/*bAction*/,/*cIDLine*/,/*bGotFocus*/)
	oFWLayer2:AddWindow("Col03","Win06"	,"Controle"			,060,.F.,.F.,/*bAction*/,/*cIDLine*/,/*bGotFocus*/)
	oFWLayer2:AddWindow("Col04","Win04"	,"Dados do pedido"	,040,.F.,.F.,/*bAction*/,/*cIDLine*/,/*bGotFocus*/)
	oFWLayer2:AddWindow("Col04","Win05"	,"Itens do pedido"	,060,.F.,.F.,/*bAction*/,/*cIDLine*/,/*bGotFocus*/)

	oWin3 := oFWLayer2:GetWinPanel("Col03","Win03")
	oWin6 := oFWLayer2:GetWinPanel("Col03","Win06")
	oWin4 := oFWLayer2:GetWinPanel("Col04","Win04")
	oWin5 := oFWLayer2:GetWinPanel("Col04","Win05")

	@ 000, 000 BTNBMP oBitmap1  RESNAME "FINAL"  SIZE 008, 035 OF oWin6 MESSAGE "Sair" Action( _oDlg:End() )
	oBitmap1:cCaption := " <F4>"
	oBitmap1:Align  := CONTROL_ALIGN_BOTTOM
	SetKey(VK_F4 ,{|| _oDlg:End() })

	@ 000, 000 SAY oSay3 PROMPT "Total Caixas"			SIZE 049, 007 OF oWin6 COLORS CLR_RED,16777215 PIXEL
	oSay3:Align  := CONTROL_ALIGN_TOP
	@ 000, 000 MSGET oGetVal3 VAR nQtdCaixas 			SIZE 044, 014 OF oWin6 COLORS CLR_RED,16777215 ;
	WHEN .F. PICTURE "@E 999999.00" FONT oFont18 PIXEL	
	oGetVal3:Align  := CONTROL_ALIGN_TOP

	@ 000, 000 SAY oSay7 PROMPT Space(100)				SIZE 049, 005 OF oWin6 COLORS CLR_RED,16777215 PIXEL
	oSay7:Align  := CONTROL_ALIGN_TOP

	@ 000, 000 SAY oSay4 PROMPT "Caixas Checados"		SIZE 049, 007 OF oWin6 COLORS CLR_RED,16777215 PIXEL
	oSay4:Align  := CONTROL_ALIGN_TOP
	@ 000, 000 MSGET oGetValor VAR nQtdConf				SIZE 044, 014 OF oWin6 COLORS CLR_RED,16777215 ;
	WHEN .F. PICTURE "@E 999999.00" FONT oFont18 PIXEL
	oGetValor:Align  := CONTROL_ALIGN_TOP	

	@ 000, 000 BTNBMP oBitmap2  RESNAME "OK"  SIZE 008, 035 OF oWin3 MESSAGE "Confirmar" Action( Iif(VE06Valid(""),(nOpcA:=1,_oDlg:End()),Nil) )
	oBitmap2:cCaption := "<F10>"
	oBitmap2:Align  := CONTROL_ALIGN_TOP
	SetKey(VK_F10,{|| Iif(VE06Valid(""),(nOpcA:=1,_oDlg:End()),Nil) })
		
	@ 005, 010 SAY oSay1 PROMPT "Pedido: " 			SIZE 055, 007 OF oWin4 FONT oFont COLORS 16711680, 16777215 PIXEL
	@ 020, 010 SAY oSay3 PROMPT "Data: " 			SIZE 050, 007 OF oWin4 FONT oFont COLORS 16711680, 16777215 PIXEL
	@ 035, 010 SAY oSay4 PROMPT "Hora: " 			SIZE 055, 007 OF oWin4 FONT oFont COLORS 16711680, 16777215 PIXEL
	@ 050, 010 SAY oSay5 PROMPT "Usuário: "			SIZE 055, 007 OF oWin4 FONT oFont COLORS 16711680, 16777215 PIXEL
	@ 065, 010 SAY oSay2 PROMPT "Cliente: "			SIZE 050, 007 OF oWin4 FONT oFont COLORS 16711680, 16777215 PIXEL
	@ 065, 117 SAY oSay5 PROMPT "Loja: " 			SIZE 022, 007 OF oWin4 FONT oFont COLORS 16711680, 16777215 PIXEL
	@ 080, 010 SAY oSay5 PROMPT "Nome: " 			SIZE 022, 007 OF oWin4 FONT oFont COLORS 16711680, 16777215 PIXEL
	
	@ 005, 062 MSGET oGetConf 	VAR cGetConf		SIZE 050, 007 OF oWin4 COLORS 0, 16777215 PIXEL When .F.
	@ 020, 062 MSGET oGetData 	VAR cGetData		SIZE 050, 007 OF oWin4 COLORS 0, 16777215 PIXEL When .F.
	@ 035, 062 MSGET oGetHora 	VAR cGetHora		SIZE 050, 007 OF oWin4 COLORS 0, 16777215 PIXEL When .F.
	@ 050, 062 MSGET oGetUser 	VAR cGetUser		SIZE 050, 007 OF oWin4 COLORS 0, 16777215 PIXEL When .F.
	@ 065, 062 MSGET oGetCod	VAR cGetCod			SIZE 050, 007 OF oWin4 COLORS 0, 16777215 PIXEL When .F.
	@ 065, 142 MSGET oGetLj 	VAR cGetLj			SIZE 015, 007 OF oWin4 COLORS 0, 16777215 PIXEL When .F.
	@ 080, 062 MSGET oGetNom 	VAR cGetNom			SIZE 155, 007 OF oWin4 COLORS 0, 16777215 PIXEL When .F.

	@ 003, 154-20 GROUP oGroup TO 061, 570 PROMPT "" OF oWin4 COLOR 0, 16777215 PIXEL

	@ 010, 160-20     SAY oSay5 PROMPT "Motorista: " 	SIZE 050, 007 OF oWin4 FONT oFont COLORS 16711680, 16777215 PIXEL
	@ 010, 270-20+100 SAY oSay5 PROMPT "Quantidade: " 	SIZE 050, 007 OF oWin4 FONT oFont COLORS 16711680, 16777215 PIXEL
	@ 010, 330-20+100 SAY oSay5 PROMPT "Caixa: " 		SIZE 050, 007 OF oWin4 FONT oFont COLORS 16711680, 16777215 PIXEL

	@ 022, 160-20 	  MSGET oMot 		VAR cMot      SIZE 200, 030 OF oWin4 COLORS 0, 16777215 When lWhen FONT oFont2 Picture "@!" PIXEL 
	@ 022, 270-20+100 MSGET oQtd 		VAR nQtd      SIZE 035, 030 OF oWin4 COLORS 0, 16777215 When .F. FONT oFont2 Picture "99" PIXEL 
	@ 022, 330-20+100 MSGET oGetCaixa 	VAR cGetCaixa SIZE 140, 030 OF oWin4 COLORS 0, 16777215 When lWhen FONT oFont2 Picture "9999999999" PIXEL Valid( VALCX(nOpca) )

	oGetDados := MsNewGetDados():New(0,0,0,0,nOpc,,,,{""},,,,,,oWin5,aHeader,aCols)
	oGetDados:oBrowse:Align := CONTROL_ALIGN_ALLCLIENT
	oGetDados:oBrowse:bGotFocus := {|| oGetCaixa:SetFocus() }	

	oMot:SetFocus()
	
ACTIVATE MSDIALOG _oDlg CENTERED


oBrowse:Refresh()
oBrowse:SetFocus()

Return


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³VALCX      ºAutor  ³ Ethosx            º Data ³  01/12/14   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Conferencia de saida de produtos.                          º±±
±±º          ³ Validacao da caixa.                                        º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³                                                            º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function VALCX( nOpca )

Local nPosProd	:= 0
Local nPosAcol	:= 0
Local nLenAcol	:= 0
Local lRet	:= .T.

If Empty(cGetCaixa)
	oMot:Refresh()
	cGetCaixa := Space(10)
	oGetCaixa:Refresh()
	nQtd := 1
	oQtd:Refresh()
	oMot:SetFocus()
	Return lRet
EndIf
      
if Empty(cMot)
	Alert("Motorista não informada !")
	lRet := .F.               
else            
                   
	a2Itens := {}
	lAchou := .F.
	For nI := 1 To Len(oGetDados:aCols)         
		if oGetDados:aCols[nI,4] == cGetCaixa
		   oGetDados:aCols[nI,8] := .T.
		   lAchou := .T.
		endif
        
		if oGetDados:aCols[nI,8] == .T.
			nPosProd := aScan(a2Itens, {|x| Alltrim(x[1]) == oGetDados:aCols[nI,4]})
			If nPosProd == 0    
				aAdd(a2Itens,{oGetDados:aCols[nI,4]})
			endif                                
		endif
			
	Next nI
                  
	if !lAchou
		Alert("Caixa não localizado !")
		lRet := .F.
	EndIf

	nQtdConf := len(a2Itens)

	If  nQtdCaixas == nQtdConf
		Aviso("Aviso","Coleta Concluida !", {"OK"})
		If VE06Valid("finaliza")					
			_oDlg:End()
		EndIf					
	EndIf


Endif 
		                                                                             
oGetValor:Refresh()
oGetDados:Refresh()

oMot:Refresh()
cGetCaixa := Space(10)
oGetCaixa:Refresh()
nQtd := 1
oQtd:Refresh()
oGetCaixa:SetFocus()

Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³VE06Valid  ºAutor  ³ Ethosx            º Data ³  01/12/14   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Conferencia de saida de produtos.                          º±±
±±º          ³ Validacao.                                                 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³                                                            º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function VE06Valid(Tipo)

Local lRet		:= .T.
Local lConfOk	:= .F.    

if TIPO = "finaliza"  

	lConfOk	:= .T.
	GrvZZ2(.T.)

ElseIf Len(oGetDados:aCols) == 1 .And. Empty(oGetDados:aCols[1,1]) .And. !EXCLUI

	Alert("Coleta sem Caixa !")
	lRet := .F.

ElseIf (INCLUI .Or. ALTERA)

	GrvZZ2(.f.)

ElseIf EXCLUI

	GrvZZ2(.F.)

EndIf

Return lRet


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³GrvZZ2     ºAutor  ³ Ethosx            º Data ³  01/12/14   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Conferencia de saida de produtos.                          º±±
±±º          ³ Gravacao da ZZ2.                                           º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³                                                            º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function GrvZZ2(lConfOk)

Local nI
          
 //Caso já exista NOTA na tabela ZZ2
cQuery := "SELECT DISTINCT ZZ2_NOTA FROM "+RetSqlName("ZZ2")+" "
cQuery += " WHERE ZZ2_FILIAL = '"+xFilial("ZZ2")+"' "
cQuery += "   AND ZZ2_NUM = '"+SC5->C5_NUM+"' "
cQuery += "   AND ZZ2_NOTA <> ' ' "
cQuery += "   AND D_E_L_E_T_ = '' "
cQuery += "   ORDER BY ZZ2_NOTA "
cQuery := ChangeQuery(cQuery)
dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),"QryZZ2",.T.,.T.)
dbSelectArea("QryZZ2")
dbGoTop()
nQTDNOTA := 0
cNUMNOTA := ""
While !QryZZ2->(Eof())
	nQTDNOTA += 1
	cNUMNOTA  := QryZZ2->ZZ2_NOTA
	QryZZ2->(DbSkip())
EndDo
QryZZ2->(dbCloseArea())	
          
                        
RecLock("SC5",.F.)
If lConfOk .And. (INCLUI .Or. ALTERA)
	
	cQuery := "SELECT COUNT(*) QTD FROM "+RetSqlName("SC6")+"" 
	cQuery += " WHERE C6_FILIAL = '"+xFilial("SC6")+"'"
	cQuery += "   AND C6_NUM = '"+SC5->C5_NUM+"'"
	cQuery += "   AND C6_QTDVEN <> C6_QTDENT" 
	cQuery += "   AND C6_BLQ <> 'R' " 
	cQuery += "   AND D_E_L_E_T_ = ' '"
	cQuery := ChangeQuery(cQuery)
	dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),"QrySC6",.T.,.T.)
	dbSelectArea("QrySC6")
	dbGoTop()
	xQTD := QrySC6->QTD
	QrySC6->(dbCloseArea())
	
	If xQTD > 0 
		SC5->C5_COLST	:= "3"
		SC5->C5_XSTATUS := "RES"   
		if nQTDNOTA == 1 	
			SC5->C5_XLDT    := dDataBase
			SC5->C5_XLHR    := SUBSTR(Time(),1,5)
			SC5->C5_XLUSER  := cUserName
			SC5->C5_XLMOT   := cMot
		endif
	else
		SC5->C5_COLST	:= "3"
		SC5->C5_XSTATUS := "ENT"   
		if nQTDNOTA == 1 	
			SC5->C5_XLDT    := dDataBase
			SC5->C5_XLHR    := SUBSTR(Time(),1,5)
			SC5->C5_XLUSER  := cUserName
			SC5->C5_XLMOT   := cMot
		endif
	endif	
ElseIf (INCLUI .Or. ALTERA)
	SC5->C5_COLST	:= "2"
	SC5->C5_XSTATUS := "COL"
	if nQTDNOTA == 1 	
		SC5->C5_XLDT    := dDataBase
		SC5->C5_XLHR    := SUBSTR(Time(),1,5)
		SC5->C5_XLUSER  := cUserName
		SC5->C5_XLMOT   := cMot
	endif
ElseIf EXCLUI
	SC5->C5_COLST	:= "1"
	SC5->C5_XSTATUS := "COL"
	if nQTDNOTA == 1 	
		SC5->C5_XLDT    := CTOD("  /  /    ")
		SC5->C5_XLHR    := SPACE(5)
		SC5->C5_XLUSER  := SPACE(30)
		SC5->C5_XLMOT   := SPACE(30)
	endif
EndIf
SC5->(MsUnlock())

If (INCLUI .Or. ALTERA)

	// EXCLUIR TUDO
	DbSelectArea("ZZ2")
	ZZ2->(DbSetOrder(1))
	If ZZ2->(DbSeek(xFilial("ZZ2") + "C" + SC5->C5_NUM  ))
		While !ZZ2->(Eof()) .And. SC5->C5_NUM == ZZ2->ZZ2_NUM .And. "C" == ZZ2->ZZ2_TIPOSC

			if ZZ2->ZZ2_NOTA <> cNUMNOTA 
	   		   ZZ2->(dbSkip()) 
			   LOOP
			endif 
			
			RecLock("ZZ2",.F.)
			ZZ2->(DbDelete())
			ZZ2->(MsUnlock())
			ZZ2->(DbSkip())
		EndDo
	EndIf

	For nI := 1 To Len(oGetDados:aCols)

		RecLock("ZZ2",.T.)
		ZZ2->ZZ2_FILIAL := xFilial("ZZ2")
		ZZ2->ZZ2_TIPOSC := "C"
		ZZ2->ZZ2_NUM    := SC5->C5_NUM
		ZZ2->ZZ2_PRODUT := oGetDados:aCols[nI,1]
		ZZ2->ZZ2_DESC   := "["+ALLTRIM(Posicione("SB1",1,xFilial("SB1")+oGetDados:aCols[nI,1],"B1_UMRES"))+"] - "+ALLTRIM(Posicione("SB1",1,xFilial("SB1")+oGetDados:aCols[nI,1],"B1_DESC"))
		ZZ2->ZZ2_QTDLID := oGetDados:aCols[nI,3]
		ZZ2->ZZ2_CAIXA  := oGetDados:aCols[nI,4]
		ZZ2->ZZ2_DATA   := oGetDados:aCols[nI,5]
		ZZ2->ZZ2_HORA   := oGetDados:aCols[nI,6]
		ZZ2->ZZ2_USER   := oGetDados:aCols[nI,7]
		ZZ2->ZZ2_SCX    := oGetDados:aCols[nI,8]
		ZZ2->ZZ2_NOTA   := oGetDados:aCols[nI,9]
		ZZ2->(MsUnlock())
	
	Next nI
    
ElseIf EXCLUI
    
	DbSelectArea("ZZ2")
	ZZ2->(DbSetOrder(1))
	If ZZ2->(DbSeek(xFilial("ZZ2") + "C" + SC5->C5_NUM  ))
		While !ZZ2->(Eof()) .And. SC5->C5_NUM == ZZ2->ZZ2_NUM .And. "C" == ZZ2->ZZ2_TIPOSC
			if ZZ2->ZZ2_NOTA == cNUMNOTA 
				RecLock("ZZ2",.F.)
				ZZ2->ZZ2_SCX := .F.
				ZZ2->(MsUnlock())
			endif
			ZZ2->(DbSkip())
		EndDo
	EndIf
    
EndIf

Return

