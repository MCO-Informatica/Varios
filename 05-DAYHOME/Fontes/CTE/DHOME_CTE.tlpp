#include "rwmake.ch"
#Include "Protheus.Ch"
#include "TbiConn.ch"
#include "TbiCode.ch" 
#INCLUDE "topconn.ch"  

#Define ENTER Chr(13)+Chr(10)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³DHOME_CTE º Autor ³ FONTANELLI         º Data ³  30/04/19   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Tela CTE - Nota de Origem                                  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ DAYHOME                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

User Function DHOME_CTE()

	Local nX        := 0
	Local nOpcA     := 0         
	
	Local nOpc      := 2 // Alteracao    
	
	Local oMainWnd     := Nil  
	Local aTitles   := {"Nota de Origem"} 
 
 	Private cCadastro:= "CTE - Nota de Origem"

	Private aObjects := {}
	Private aPosObj  := {}	
	Private aSize    := MsAdvSize()
	Private aInfo    := {aSize[1],aSize[2],aSize[3],aSize[4],0,0}
	Private aPosGet  := MsObjGetPos(aSize[3]-aSize[1],305,	{{05,50,140,175},{105}})

	Private oDlg    := Nil
	Private oFolder

	Private aHeader := {}
	Private aCols   := {}             
	Private bCampo  := { |nField| Field(nField)}

	Private aHeader1 := {}	
	Private aCols1   := {}
	Private aGets1   := {}	
	Private nUsado1  := 0

	Private cCOD
	Private cNREDUZ
	
 	AAdd(aObjects,{  0, 100,.T.,.F.}) // TAMANHO QUADRO SUPERIOR 1
	AAdd(aObjects,{  0,   0,.T.,.F.})
	AAdd(aObjects,{  0, 200,.T.,.F.}) // TAMANHO PALET INFERIOR  2
	aPosObj := MsObjSize(aInfo,aObjects)

	if alltrim(SF1->F1_ESPECIE) <> "CTE"
	   // Alert("Esta Nota Fiscal não é CTE - Conhecimento de Transporte!")
	   Return()
	endif

	cF1_FILIAL  := SF1->F1_FILIAL
	cF1_DOC 	:= SF1->F1_DOC
	cF1_SERIE 	:= SF1->F1_SERIE
	cF1_FORNECE := SF1->F1_FORNECE
	cF1_LOJA 	:= SF1->F1_LOJA


	DbSelectArea("ZCT")
	ZCT->(DbSetOrder(1))
	ZCT->(DbGoTop())
	if !ZCT->(DbSeek(cF1_FILIAL+cF1_DOC+cF1_SERIE+cF1_FORNECE+cF1_LOJA))    
		cMODO := "SIM"
	else
		cMODO := "NAO"
	endif
	
	DbSelectArea("ZCT")
	ZCT->(DbSetOrder(1))
	ZCT->(DbGoTop())
	ZCT->(DbSeek(cF1_FILIAL+cF1_DOC+cF1_SERIE+cF1_FORNECE+cF1_LOJA))    

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Inicia as variaveis para Enchoice³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	For nX:= 1 To FCount()
		If (Eval(bCampo,nX)) $ 'ZCT_FILIAL,ZCT_DOC,ZCT_SERIE,ZCT_FORNEC,ZCT_LOJA' 			
   			M->&(Eval(bCampo,nX)) := FieldGet(nX)
		EndIf
	Next nX                                 
	                    
	M->ZCT_FILIAL := cF1_FILIAL	
	M->ZCT_DOC    := cF1_DOC
	M->ZCT_SERIE  := cF1_SERIE	
	M->ZCT_FORNEC := cF1_FORNECE	
	M->ZCT_LOJA   := cF1_LOJA	
                                
	altaCols  := {"ZCT_ORGFL","ZCT_ORGNF","ZCT_ORGSER"}    

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Monta o aHeader1³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	DHCTEHeader1('ZCT_ORGITE,ZCT_ORGFL,ZCT_ORGNF,ZCT_ORGSER',nOpc)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Monta o aCols³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	DbSelectArea("ZCT")
	ZCT->(DbSetOrder(1))
	ZCT->(DbGoTop()) 
	if 	ZCT->(DbSeek(cF1_FILIAL+cF1_DOC+cF1_SERIE+cF1_FORNECE+cF1_LOJA)) 
	
		nCols1 := 0
		While !Eof() .and. ZCT->ZCT_FILIAL == cF1_FILIAL;
		             .and. ZCT->ZCT_DOC == cF1_DOC;
		             .and. ZCT->ZCT_SERIE == cF1_SERIE; 
		             .and. ZCT->ZCT_FORNEC == cF1_FORNECE; 
		             .and. ZCT->ZCT_LOJA == cF1_LOJA  

			aAdd(aCols1,Array(nUsado1+1))
			nCols1 ++
	
			For nX := 1 To nUsado1
				If ( aHeader1[nX][10] != "V")                                      
					aCols1[nCols1][nX] := FieldGet(FieldPos(aHeader1[nX][2]))
				Else
					aCols1[nCols1][nX] := CriaVar(aHeader1[nX][2],.T.)
				Endif
			Next nX
			
			aCols1[nCols1][nUsado1+1] := .F.
			
			DbSelectArea("ZCT")
			ZCT->(DbSkip())
		End
	endif		
	     
	 	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Envia para processamento dos Gets³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ	
	
	DEFINE MSDIALOG oDlg TITLE cCadastro FROM aSize[7],aSize[1] TO aSize[6],aSize[5] OF oMainWnd PIXEL
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Monta Enchoice³    
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
                                                                            
                                                                                                                   	
	@ 025+20,aPosGet[1,1] SAY "Doc" OF oDlg PIXEL SIZE 050,008
	@ 024+20,aPosGet[1,2] MSGET M->ZCT_DOC PICTURE PesqPict('ZCT','ZCT_DOC') F3 CpoRetF3('ZCT_DOC') WHEN .F. OF oDlg PIXEL SIZE 100,006

	@ 025+20,aPosGet[1,3] SAY "Serie" OF oDlg PIXEL SIZE 050,008
	@ 024+20,aPosGet[1,4] MSGET M->ZCT_SERIE PICTURE PesqPict('ZCT','ZCT_SERIE') F3 CpoRetF3('ZCT_SERIE') WHEN .F. OF oDlg PIXEL SIZE 050,006

	@ 037+20,aPosGet[1,1] SAY "Fornecedor" OF oDlg PIXEL SIZE 050,008
	@ 036+20,aPosGet[1,2] MSGET M->ZCT_FORNEC PICTURE PesqPict('ZCT','ZCT_FORNEC') F3 CpoRetF3('ZCT_FORNEC') WHEN .F. OF oDlg PIXEL SIZE 100,006

	@ 037+20,aPosGet[1,3] SAY "Loja" OF oDlg PIXEL SIZE 050,008
	@ 036+20,aPosGet[1,4] MSGET M->ZCT_LOJA PICTURE PesqPict('ZCT','ZCT_LOJA') F3 CpoRetF3('ZCT_LOJA') WHEN .F. OF oDlg PIXEL SIZE 050,006
    
 	oFolder := TFolder():New(aPosObj[3,1],aPosObj[3,2]+1,aTitles,{"HEADER"},oDlg,,,, .T., .F.,aPosObj[3,4]-aPosObj[3,2],aPosObj[3,3]-aPosObj[3,1]-40,) 

	//Acerto no folder para nao perder o foco
	For nX := 1 to Len(oFolder:aDialogs)
		DEFINE SBUTTON FROM 5000,5000 TYPE 5 ACTION Allwaystrue() ENABLE OF oFolder:aDialogs[nX]
	Next nX      

	IF cMODO == "SIM"
		oFolder:aDialogs[1]:oFont := oDlg:oFont                                        
		oGet1 := MsNewGetDados():New(	001,;
										001,;
										001,;
										001,;
										GD_INSERT+GD_DELETE+GD_UPDATE,;
										"u_DHCTELOK()",; 
										"u_DHCTETOK()",; 
										"+ZCT_ORGITE",;
										altaCols,;
										,;
										,;
										,;
										,;
										,;
										oFolder:aDialogs[1],;
										aHeader1,;
										aCols1)   
	endif
	

	IF cMODO == "NAO"
		oFolder:aDialogs[1]:oFont := oDlg:oFont                                        
		oGet1 := MsNewGetDados():New(	001,;
										001,;
										001,;
										001,;
										,;
										"u_DHCTELOK()",; 
										"u_DHCTETOK()",; 
										"+ZCT_ORGITE",;
										{""},;
										,;
										,;
										,;
										,;
										,;
										oFolder:aDialogs[1],;
										aHeader1,;
										aCols1)   
	endif

	oGet1:lF3Header = .T.	
    oGet1:oBrowse:Align := CONTROL_ALIGN_ALLCLIENT									
	
	ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{||nOpcA:=1,Iif(oGet1:TudoOk(),oDlg:End(),nOpcA := 0)},{||oDlg:End()})
	
	If nOpcA == 1
		Begin Transaction
			IF cMODO == "SIM"
				If DHCTEGR(nOpc)
					EvalTrigger()
					If __lSX8
						ConfirmSX8()
					Endif
				EndIf
			Endif
		End Transaction
	Else
		If __lSX8
			RollBackSX8()
		Endif
	Endif  
	
Return(.T.)


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFun‡„o    ³DHCTEGR   º Autor ³Fontanelli          º Data ³ 30/04/19    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescri‡„o ³Funcao Gravar                                               º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function DHCTEGR(nOpc)

Local lGravou  := .F.

Local nUsado   := 0

Local nX       := 0
Local nI       := 0

Private bCampo := { |nField| FieldName(nField) }

nUsado1  := Len(aHeader1) + 1

If nOpc == 2 //Alterar	

	// Apaga todos os Itens
	DbSelectArea("ZCT")
	ZCT->(DbSetOrder(1))
	ZCT->(dbGoTop())   
	If DbSeek(cF1_FILIAL+cF1_DOC+cF1_SERIE+cF1_FORNECE+cF1_LOJA)
		While !Eof() .And. ZCT->ZCT_FILIAL == cF1_FILIAL .and. ZCT->ZCT_DOC == cF1_DOC .and. ZCT->ZCT_SERIE == cF1_SERIE .and. ZCT->ZCT_FORNEC == cF1_FORNECE .and. ZCT->ZCT_LOJA == cF1_LOJA  
			Reclock("ZCT",.F.)
			DbDelete()
			MsUnlock()
			ZCT->(DbSkip())
		EndDo
	Endif

	For nX := 1 To Len(oGet1:aCols)
         
		If !oGet1:aCols[nX][nUsado1] // Não Deletados 	
		
			RecLock("ZCT",.T.)	

			ZCT->ZCT_FILIAL := cF1_FILIAL	
			ZCT->ZCT_DOC    := cF1_DOC
			ZCT->ZCT_SERIE  := cF1_SERIE	
			ZCT->ZCT_FORNEC := cF1_FORNECE	
			ZCT->ZCT_LOJA   := cF1_LOJA	
			For nI := 1 To Len(aHeader1)     
				if (Alltrim(aHeader1[nI][02]) $ 'ZCT_ORGITE,ZCT_ORGFL,ZCT_ORGNF,ZCT_ORGSER') 
					FieldPut(FieldPos(Trim(aHeader1[nI,2])),oGet1:aCols[nX,nI])
				endif	
			Next nI
			
			MsUnLock()
							
	    endif    
	Next nX

	lGravou := .T.         
endif	

if lGravou
	cQuery := "	SELECT ZCT_FILIAL, ZCT_DOC, ZCT_SERIE, ZCT_FORNEC, ZCT_LOJA,"
	cQuery += "			TOTCTE, ZCT_ORGFL, ZCT_ORGNF, ZCT_ORGSER,"
	cQuery += "			D2_PEDIDO PEDIDO, SUM(D2_TOTAL) TOTPED"
	cQuery += "		FROM ("
	cQuery += "			SELECT" 
	cQuery += "					ZCT_FILIAL, ZCT_DOC, ZCT_SERIE, ZCT_FORNEC, ZCT_LOJA," 
	cQuery += "					("  
	cQuery += "						SELECT F1_VALMERC FROM "+RetSqlName("SF1")+" SF1"
	cQuery += "						 WHERE F1_FILIAL = '"+cF1_FILIAL+"'"   
	cQuery += "						   AND F1_DOC = '"+cF1_DOC+"'"
	cQuery += "						   AND F1_SERIE = '"+cF1_SERIE+"'"
	cQuery += "						   AND F1_FORNECE = '"+cF1_FORNECE+"'"
	cQuery += "						   AND F1_LOJA = '"+cF1_LOJA+"'"  
	cQuery += "						   AND SF1.D_E_L_E_T_ = ''"
	cQuery += "					) TOTCTE,"
	cQuery += "					ZCT_ORGFL, ZCT_ORGNF, ZCT_ORGSER"
	cQuery += "				FROM "+RetSqlName("ZCT")+" ZCT"
	cQuery += "			   WHERE ZCT_FILIAL = '"+cF1_FILIAL+"'"   
	cQuery += "				 AND ZCT_DOC = '"+cF1_DOC+"'"
	cQuery += "				 AND ZCT_SERIE = '"+cF1_SERIE+"'"
	cQuery += "				 AND ZCT_FORNEC = '"+cF1_FORNECE+"'"
	cQuery += "				 AND ZCT_LOJA = '"+cF1_LOJA+"'"  
	cQuery += "				 AND ZCT.D_E_L_E_T_ = ' '" 
	cQuery += "			) TMP1"
	cQuery += "			LEFT OUTER JOIN "+RetSqlName("SD2")+" SD2"
	cQuery += "	                ON D2_FILIAL = ZCT_ORGFL"
	cQuery += "	               AND D2_DOC = ZCT_ORGNF"
	cQuery += "	               AND D2_SERIE = ZCT_ORGSER" 
	cQuery += "	               AND SD2.D_E_L_E_T_ = ' '" 
	cQuery += "	GROUP BY ZCT_FILIAL, ZCT_DOC, ZCT_SERIE, ZCT_FORNEC, ZCT_LOJA,"
	cQuery += "			 TOTCTE, ZCT_ORGFL, ZCT_ORGNF, ZCT_ORGSER,"
	cQuery += "			 D2_PEDIDO"
	dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),"TMPQry",.T.,.T.)
	dbSelectArea("TMPQry")
	TMPQry->(dbGoTop())
	nTOT_CTE:=0 
	nGER_PED:=0
	While !TMPQry->(Eof())
		nTOT_CTE:= TMPQry->TOTCTE
		nGER_PED := nGER_PED + TMPQry->TOTPED
		TMPQry->(DbSkip())
	EndDo
	
	TMPQry->(dbGoTop())
	nPER_PED:=0 
	nVLR_PED:=0
	While !TMPQry->(Eof())
		DbSelectArea("SC5")
		SC5->(DbSetOrder(1))
		SC5->(DbGoTop())
		If SC5->(DbSeek(TMPQry->ZCT_ORGFL+TMPQry->PEDIDO))
		   IF Empty(SC5->C5_CTENF) .or. (SC5->C5_CTENF == TMPQry->ZCT_DOC)
			   RecLock("SC5",.f.)
			   SC5->C5_CTEFIL  := TMPQry->ZCT_FILIAL
			   SC5->C5_CTENF   := TMPQry->ZCT_DOC
			   SC5->C5_CTESER  := TMPQry->ZCT_SERIE
			   SC5->C5_CTEVLOR := nTOT_CTE
			   SC5->C5_CTEPERC := ROUND((TMPQry->TOTPED/nGER_PED),2)
			   SC5->C5_CTEVAL  := ROUND((nTOT_CTE*(TMPQry->TOTPED/nGER_PED)),2)
			   MsUnLock()
			endif
		endif
		TMPQry->(DbSkip())
	EndDo
	TMPQry->(dbCloseArea())   	

endif

Return(lGravou)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFun‡„o    ³DHCTELOK  º  Autor³Fontanelli          º Data ³ 30/04/19    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescri‡„o ³Funcao Linha OK                                             º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
User Function DHCTELOK()

	Local lRet       := .T.

	If oGet1:aCols[n][nUsado1+1]
		Return(.t.)	
	Endif

	if lRet 
		if Empty(oGet1:aCols[n][aScan(aHeader1,{|x| Trim(x[2])=="ZCT_ORGFL"})]) 
		   Aviso("Aviso", "Obrigatório preencher a Filial de Origem ! Referente ao Item "+oGet1:aCols[n][aScan(aHeader1,{|x| Trim(x[2])=="ZCT_ORGITE"})],{"Ok"},1,"")
		   lRet := .f. 
		endif
    endif
    
	if lRet 
		if Empty(oGet1:aCols[n][aScan(aHeader1,{|x| Trim(x[2])=="ZCT_ORGNF"})]) 
		   Aviso("Aviso", "Obrigatório preencher a Nota de Origem ! Referente ao Item "+oGet1:aCols[n][aScan(aHeader1,{|x| Trim(x[2])=="ZCT_ORGITE"})],{"Ok"},1,"")
		   lRet := .f. 	
		endif
    endif

	if lRet 
		if Empty(oGet1:aCols[n][aScan(aHeader1,{|x| Trim(x[2])=="ZCT_ORGSER"})]) 
		   Aviso("Aviso", "Obrigatório preencher a Serie de Origem ! Referente ao Item "+oGet1:aCols[n][aScan(aHeader1,{|x| Trim(x[2])=="ZCT_ORGITE"})],{"Ok"},1,"")
		   lRet := .f. 	
		endif
    endif

	if lRet 
		xFILIAL := oGet1:aCols[n][aScan(aHeader1,{|x| Trim(x[2])=="ZCT_ORGFL"})]
		xDOC    := oGet1:aCols[n][aScan(aHeader1,{|x| Trim(x[2])=="ZCT_ORGNF"})]
		xSERIE  := oGet1:aCols[n][aScan(aHeader1,{|x| Trim(x[2])=="ZCT_ORGSER"})]
		
		dbSelectArea("SF2")
		SF2->(dbSetOrder(1))// F2_FILIAL, F2_DOC, F2_SERIE, F2_CLIENTE, F2_LOJA
		SF2->(DbGoTop())
		If !(DbSeek(xFILIAL+xDOC+xSERIE))	
		   Aviso("Aviso", "Informações de Nota de Origem não encontrada! Referente ao Item "+oGet1:aCols[n][aScan(aHeader1,{|x| Trim(x[2])=="ZCT_ORGITE"})],{"Ok"},1,"")
		   lRet := .f. 
		endif
    endif
	
	    
Return(lRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFun‡„o    ³DHCTETOK  º Autor ³Fontanelli          º Data ³ 30/04/19    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescri‡„o ³Funcao Tudo OK                                              º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
User Function DHCTETOK()

	Local I
	Local lRet := .T.    
	           
	if lRet 
		
		For I:= 1 To Len(oGet1:aCols)
			
			If oGet1:aCols[I][nUsado1+1]
				Loop
			Endif         

			if lRet 
				if Empty(oGet1:aCols[I][aScan(aHeader1,{|x| Trim(x[2])=="ZCT_ORGFL"})]) 
				   Aviso("Aviso", "Obrigatório preencher a Filial de Origem ! Referente ao Item "+oGet1:aCols[I][aScan(aHeader1,{|x| Trim(x[2])=="ZCT_ORGITE"})],{"Ok"},1,"")
				   lRet := .f. 
				endif
		    endif
		    
			if lRet 
				if Empty(oGet1:aCols[I][aScan(aHeader1,{|x| Trim(x[2])=="ZCT_ORGNF"})]) 
				   Aviso("Aviso", "Obrigatório preencher a Nota de Origem ! Referente ao Item "+oGet1:aCols[I][aScan(aHeader1,{|x| Trim(x[2])=="ZCT_ORGITE"})],{"Ok"},1,"")
				   lRet := .f. 	
				endif
		    endif
		
			if lRet 
				if Empty(oGet1:aCols[I][aScan(aHeader1,{|x| Trim(x[2])=="ZCT_ORGSER"})]) 
				   Aviso("Aviso", "Obrigatório preencher a Serie de Origem ! Referente ao Item "+oGet1:aCols[I][aScan(aHeader1,{|x| Trim(x[2])=="ZCT_ORGITE"})],{"Ok"},1,"")
				   lRet := .f. 	
				endif
		    endif
		
			if lRet 
				xFILIAL := oGet1:aCols[I][aScan(aHeader1,{|x| Trim(x[2])=="ZCT_ORGFL"})]
				xDOC    := oGet1:aCols[I][aScan(aHeader1,{|x| Trim(x[2])=="ZCT_ORGNF"})]
				xSERIE  := oGet1:aCols[I][aScan(aHeader1,{|x| Trim(x[2])=="ZCT_ORGSER"})]
				dbSelectArea("SF2")
				SF2->(dbSetOrder(1))// F2_FILIAL, F2_DOC, F2_SERIE, F2_CLIENTE, F2_LOJA
				SF2->(DbGoTop())
				If !(DbSeek(xFILIAL+xDOC+xSERIE))	
				   Aviso("Aviso", "Informações de Nota de Origem não encontrada! Referente ao Item "+oGet1:aCols[I][aScan(aHeader1,{|x| Trim(x[2])=="ZCT_ORGITE"})],{"Ok"},1,"")
				   lRet := .f. 
				endif
		    endif

		Next i
	endif
		    
Return(lRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFun‡„o    ³DHCTEHeader1  º Autor ³Fontanelli          º Data ³  30/04/19   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescri‡„o ³Funcao Header1                                                  º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function DHCTEHeader1(cItens,nOpc)

	nUsado1   := 0
	aHeader1 := {}  

	DbSelectArea("SX3")
	DbSetOrder(1)
	SX3->(DbSeek("ZCT")) 
	While ( !Eof() .And. SX3->X3_ARQUIVO == "ZCT" )
		If ( X3USO(SX3->X3_USADO) .And. cNivel >= SX3->X3_NIVEL ) .And. Alltrim(X3_CAMPO) $ cItens
			
			aAdd(aHeader1,{ Trim(X3Titulo()), ;
								alltrim(SX3->X3_CAMPO), ; 
								SX3->X3_PICTURE , ;
								SX3->X3_TAMANHO , ;
								SX3->X3_DECIMAL , ;
								SX3->X3_VALID   , ; 
								SX3->X3_USADO   , ;
								SX3->X3_TIPO    , ;
								SX3->X3_F3		, ;
								SX3->X3_CONTEXT , ;
								SX3->X3_CBOX	, ;
								SX3->X3_RELACAO } )								
								nUsado1++                
								
		Endif	

		SX3->(DbSkip())
	End  	

Return()



