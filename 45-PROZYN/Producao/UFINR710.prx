#INCLUDE "FINR710.CH"
#Include "PROTHEUS.CH"

Static lFWCodFil := .T. 

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³ FinR710	³ Autor ³ Wagner Xavier 	    ³ Data ³ 05.10.92 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Bordero de Pagamento.									  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ FinR710(void)						  					  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ 															  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Generico 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
User Function FinR710()

	Local oReport
	Local aAreaR4	:= GetArea()

	//If TRepInUse()
		oReport := ReportDef()
		oReport:PrintDialog()
	/*Else
		Return FinR710R3() // Executa versão anterior do fonte
	Endif*/

	RestArea(aAreaR4)

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ ReportDef º Autor ³ Marcio Menon		   º Data ³  27/07/06 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Definicao do objeto do relatorio personalizavel e das      º±±
±±º          ³ secoes que serao utilizadas.                               º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ EXPC1 - Grupo de perguntas do relatorio                    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ 												              º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function ReportDef()

	Local oReport
	Local oSection1
	Local oSection2
	Local cReport 	:= "UFINR710" 				// Nome do relatorio
	Local cDescri 	:= STR0001 + STR0002   		//"Este programa tem a fun‡„o de emitir os borderos de pagamen-" ### "tos."
	Local cTitulo 	:= STR0003 					//"Emiss„o de Borderos de Pagamentos"
	Local cPerg		:= "UFIN710"					// Nome do grupo de perguntas

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica as perguntas selecionadas 		   				     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	pergunte(cPerg,.F.)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Vari veis utilizadas para parametros						³
	//³ mv_par01				// Do Bordero						³
	//³ mv_par02				// At‚ o Bordero					³
	//³ mv_par03				// Data para d‚bito					³
	//³ mv_par04				// Qual Moeda						³
	//³ mv_par05				// Outras Moedas					³
	//³ mv_par06				// Converte por						³
	//³ mv_par07				// Compoen saldo por				³
	//³ mv_par08				// Considera Filial					³
	//³ mv_par09				// Da Filial						³
	//³ mv_par10				// Ate Filia						³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Criacao do componente de impressao                                      ³
	//³                                                                        ³
	//³TReport():New                                                           ³
	//³ExpC1 : Nome do relatorio                                               ³
	//³ExpC2 : Titulo                                                          ³
	//³ExpC3 : Pergunte                                                        ³
	//³ExpB4 : Bloco de codigo que sera executado na confirmacao da impressao  ³
	//³ExpC5 : Descricao                                                       ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oReport := TReport():New(cReport, cTitulo, cPerg, {|oReport| ReportPrint(oReport)}, cDescri)
	oreport:nfontbody	:= 8
	oReport:HideHeader()	//Oculta o cabecalho do relatorio
	oReport:SetPortrait()	//Imprime o relatorio no formato retrato
	oReport:HideFooter()	//Oculta o rodape do relatorio

	//³                      Definicao das Secoes                              ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Secao 01                                                               ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oSection1 := TRSection():New(oReport, STR0054 , {"SEA"})	//"CABECALHO"

	TRCell():New(oSection1, "CABEC", "", STR0054 , "", 80,/*lPixel*/,/*CodeBlock*/)		//"CABECALHO"
	oSection1:SetHeaderSection(.F.)	//Nao imprime o cabecalho da secao
	oSection1:SetPageBreak(.T.)		//Salta a pagina na quebra da secao

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Secao 02                                                               ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oSection2 := TRSection():New(oSection1, "BORDERO" , {"SEA","SA6","SEF","SA2","SE2"})		//"BORDERO"
	TRCell():New(oSection2, "BENEF"     , ""   , "BENEFICIARIO" , PesqPict("SA2","A2_NOME")   	, 33						,/*lPixel*/,/*CodeBlock*/)	//"B E N E F I C I A R I O"
	TRCell():New(oSection2, "E2_VENCREA", "SE2", "DT.VENCTO" 	, PesqPict("SE2","E2_VENCREA")	, 20						,/*lPixel*/,/*CodeBlock*/)  //"DT.VENC"
	TRCell():New(oSection2, "VALORPAGAR", ""   , "VALOR" 		, TM(0,15)					  	, 20						,/*lPixel*/,/*CodeBlock*/)										    //"VALOR A PAGAR"
	TRCell():New(oSection2, "E2_HIST2"	, "SE2", "HISTORICO" 	, PesqPict("SE2","E2_HIST2")	, TAMSX3("E2_HIST2")[1]-20	,/*lPixel*/,/*CodeBlock*/) 
	TRCell():New(oSection2, "E2_CTAINFO", "SE2", "CTA.CONTABIL" , PesqPict("SE2","E2_CTAINFO")	, TAMSX3("E2_CTAINFO")[1]	,/*lPixel*/,/*CodeBlock*/) 
	
	//oSection2:Cell("VALORPAGAR"):cTitle := PadL(oSection2:Cell("VALORPAGAR"):cTitle,oSection2:Cell("VALORPAGAR"):nSize)

	oSection2:SetTotalInLine (.F.) 	//O totalizador da secao sera impresso em coluna
	oSection2:SetHeaderBreak(.T.)   //Imprime o cabecalho das celulas apos a quebra

Return oReport

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ReportPrint ºAutor³ Marcio Menon       º Data ³  27/07/06   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Imprime o objeto oReport definido na funcao ReportDef.     º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ EXPO1 - Objeto TReport do relatorio                        º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³                                                            º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function ReportPrint(oReport)

	Local oSection1 	:= oReport:Section(1)
	Local oSection2 	:= oReport:Section(1):Section(1)
	Local oBreak1
	Local oFunction1
	Local cChave      	:= ""
	Local cFiltro     	:= ""
	Local cAliasSea		:= "SEA"
	Local cJoin 		:= ""
	Local lBaixa		:= .F.
	Local lCheque     	:= .F.
	Local lAbatimento 	:= .F.
	Local cLoja  		:= ""
	Local nVlrPagar		:= 0
	Local cModelo   	:= CriaVar("EA_MODELO")
	Local cNumConta		:= CriaVar("EA_NUMCON")
	Local lSeaEof     	:= .F.
	Local cDvCta		:= ""
	Local cFilialSEA
	Local nLen := 0
	Local cFilSA6 := ""

	Private nJuros := 0
	Private dBaixa := CriaVar("E2_BAIXA")

	If oReport:lXlsTable
		ApMsgAlert(STR0055) //"Formato de impressão Tabela não suportado neste relatório"
		oReport:CancelPrint()
		Return
	Endif

	//Valida data de d‚bito (mv_par03)
	If Empty(mv_par03)
		HELP (" ",1,'DTDEBITO',,STR0057,1,0,,,,,,{STR0058} ) 	//"Data de d‚bito n?o informada na parametriza‡?o do relat¢rio."###"Por favor, informe a data de d‚bito nos parƒmetros do relat¢rio (pergunte)."
		oReport:CancelPrint()
		Return
	Endif

	SEA->(DbGoTop())
	SE2->(DbGoTop())

	cChave := SEA->(IndexKey())

	#IFDEF TOP

	cAliasSea 		:= GetNextAlias()

	cChave 	:= "%"+SqlOrder(cChave)+"%"

	oSection1:BeginQuery()

	If MV_PAR08 == 1 //Considera Filial?
		If lFWCodFil .And. (MsModoFil("SEA")[3]=="E".Or.MsModoFil("SEA")[2]=="E".Or.MsModoFil("SEA")[1]=="E")
			If MsModoFil("SEA")[3] == "E"
				nLen := Len(FwCompany(MV_PAR09))
			EndIf
			If MsModoFil("SEA")[2] == "E"
				nLen += Len(FwUnitBusiness(MV_PAR10))
			EndIf
			If MsModoFil("SEA")[1] == "E"
				nLen += Len(FwFilial(MV_PAR10))
			EndIf
			cFilialSEA := "EA_FILIAL BETWEEN '" + SubStr(MV_PAR09,1,nLen) + "' AND '" + SubStr(MV_PAR10,1,nLen) + "' "
		Else
			cFilialSEA := "EA_FILIAL = '" + xFilial("SEA") + "' "
		EndIf
	Else
		cFilialSEA := "EA_FILIAL = '" + xFilial("SEA") + "' "
	EndIf

	cFilialSEA := "%"+cFilialSEA+"%"

	BeginSql Alias cAliasSea
		SELECT 	SEA.EA_FILIAL, SEA.EA_FILORIG, SEA.EA_NUMBOR, SEA.EA_CART, SEA.EA_PREFIXO, SEA.EA_NUM,
		SEA.EA_PARCELA, SEA.EA_TIPO, SEA.EA_FORNECE, SEA.EA_LOJA, SEA.EA_MODELO ,
		SEA.EA_PORTADO, SEA.EA_AGEDEP,SEA.EA_NUMCON, SEA.EA_DATABOR, SA2.A2_NREDUZ
		FROM
		%table:SEA% SEA
		
		INNER JOIN %table:SA2% SA2
		ON SA2.A2_FILIAL = %Exp:xFilial("SA2")%
		AND SA2.A2_COD = SEA.EA_FORNECE
		AND SA2.A2_LOJA = SEA.EA_LOJA
		AND SA2.%notDel%
		
		WHERE
		%Exp:cFilialSEA% AND
		SEA.EA_NUMBOR >= %Exp:mv_par01% AND
		SEA.EA_NUMBOR <= %Exp:mv_par02% AND
		SEA.EA_CART = 'P' AND
		SEA.%notDel%



		ORDER BY SA2.A2_NREDUZ//%Exp:cChave%
	EndSql
	oSection1:EndQuery()
	oSection2:SetParentQuery()

	#ELSE

	If MV_PAR08 == 1 //Considera Filial?
		cFiltro := "EA_FILIAL >= '" + MV_PAR09 + "' .AND. EA_FILIAL <= '" + MV_PAR10+ "' .AND."
	Else
		cFiltro := "EA_FILIAL == '" + xFilial("SEA") + "' .And. "
	EndIf

	cFiltro += "EA_NUMBOR >= '" + mv_par01 + "' .And. "
	cFiltro += "EA_NUMBOR <= '" + mv_par02 + "' .And. "
	cFiltro += "EA_CART = 'P'"

	TRPosition():New(oSection2,"SE2",1,{|| If( Empty((cAliasSea)->EA_FILORIG) .AND. !Empty(xFilial("SE2")),;
	xFilial("SE2")+(cAliasSea)->EA_PREFIXO+(cAliasSea)->EA_NUM+(cAliasSea)->EA_PARCELA+(cAliasSea)->EA_TIPO+(cAliasSea)->EA_FORNECE+AllTrim((cAliasSea)->EA_LOJA),;
	(cAliasSea)->EA_FILORIG+(cAliasSea)->EA_PREFIXO+(cAliasSea)->EA_NUM+(cAliasSea)->EA_PARCELA+(cAliasSea)->EA_TIPO+(cAliasSea)->EA_FORNECE+AllTrim((cAliasSea)->EA_LOJA)) } )
	oSection1:SetFilter(cFiltro,cChave)

	#ENDIF

	oSection1:SetLineCondition( { ||	 FR710Chk(1,cAliasSea) } )
	oSection2:SetLineCondition( { ||	 FR710Chk(2,cAliasSea) } )
	cFilSA6 := xFilial("SA6", (cAliasSea)->EA_FILIAL)

	TRPosition():New(oSection2,"SE2",1,{|| If( Empty((cAliasSea)->EA_FILORIG) .AND. !Empty(xFilial("SE2")),;
	xFilial("SE2")+(cAliasSea)->EA_PREFIXO+(cAliasSea)->EA_NUM+(cAliasSea)->EA_PARCELA+(cAliasSea)->EA_TIPO+(cAliasSea)->EA_FORNECE+AllTrim((cAliasSea)->EA_LOJA),;
	(cAliasSea)->EA_FILORIG+(cAliasSea)->EA_PREFIXO+(cAliasSea)->EA_NUM+(cAliasSea)->EA_PARCELA+(cAliasSea)->EA_TIPO+(cAliasSea)->EA_FORNECE+AllTrim((cAliasSea)->EA_LOJA)) } )

	oSection2:OnPrintLine( { || lBaixa := Fr710BxVL(cAliasSea, IIf (Empty((cAliasSea)->EA_LOJA), "", (cAliasSea)->EA_LOJA)), If(!lBaixa, lBaixa := Fr710BxBA(cAliasSea), Nil ),;
	lCheque := (!Empty(SE5->E5_NUMCHEQ) .And. SEF->(MsSeek(xFilial("SEF")+SE5->E5_BANCO+SE5->E5_AGENCIA+SE5->E5_CONTA+SE5->E5_NUMCHEQ))),Nil, .T. } )

	oReport:OnPageBreak( { || ReportCabec(oReport, cModelo := (cAliasSea)->EA_MODELO, cAliasSea, lBaixa, lSeaEof) } )

	oSection2:Cell("BENEF"):SetBlock( { || cDvCta := Posicione( "SA6", 1, cFilSA6 + (cAliasSEA)->(EA_PORTADO+EA_AGEDEP+EA_NUMCON), "A6_DVCTA" ), ;
	cNumConta := RTrim( (cAliasSea)->EA_NUMCON ) + Iif( !Empty(cDvCta),"-","" ) + cDvCta, ;
	Fr710Benef(cAliasSea, lBaixa, lCheque, lAbatimento) } )

	oSection2:Cell("E2_VENCREA"):SetBlock( { || SE2->E2_VENCREA } )
	oSection2:Cell("VALORPAGAR"):SetBlock( { || Fr710VPagar(cAliasSea, lBaixa, lCheque, lAbatimento) } )

	oBreak1 := TRBreak():New(oSection1, { || (cAliasSea)->EA_NUMBOR }, STR0007)

	oFunction1 := TRFunction():New(oSection2:Cell("VALORPAGAR"),,"SUM", oBreak1,,,,.F.,.F.)

	oBreak1:OnPrintTotal( { || ReportTxtAut(oReport, cModelo, cNumConta, oFunction1:GetLastValue()), "" } )

	oSection2:SetParentFilter({|cParam| (cAliasSea)->EA_NUMBOR == cParam },{|| (cAliasSea)->EA_NUMBOR } )

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Inicia a impressao.						 								  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oSection1:Print()

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Fr710BxVL ºAutor  ³ Marcio Menon       º Data ³  28/07/06   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Verifica se existe movimentação bancaria ou baixas que     º±±
±±º          ³ movimentam banco.                                          º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ 	                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Fr710BxVL(cAliasSea, cLoja, lBaixa)
	Local cChvSE2 := ""
	Local cChvSE5 := ""
	Local aArea := GetArea()

	DbSelectArea("SE2")
	dbSetOrder(1)
	If MsSeek(xFilial("SE2", (cAliasSea)->EA_FILORIG)+(cAliasSea)->(EA_PREFIXO+EA_NUM+EA_PARCELA+EA_TIPO+EA_FORNECE)+cLoja)
		cChvSE2 := xFilial("SE2", (cAliasSea)->EA_FILORIG)+"VL"+(cAliasSea)->(EA_PREFIXO+EA_NUM+EA_PARCELA+EA_TIPO+EA_FORNECE)+cLoja
		dbSelectArea("SE5")
		dbSetOrder(2)

		If SE5->(MsSeek(xFilial("SE5", (cAliasSea)->EA_FILORIG)+"VL"+SE2->(E2_PREFIXO+E2_NUM+E2_PARCELA+E2_TIPO)+DTOS(SE2->E2_BAIXA)+SE2->(E2_FORNECE+E2_LOJA)))
			cChvSE5 := SE5->(E5_FILIAL+E5_TIPODOC+E5_PREFIXO+E5_NUMERO+E5_PARCELA+E5_TIPO)+DTOS(SE5->E5_DATA)+SE5->(E5_CLIFOR+E5_LOJA)

			While SE5->(!Eof()) .And. cChvSE2 == cChvSE5
				//Se considera baixas que nao possuem estorno
				If !TemBxCanc(SE5->(E5_PREFIXO+E5_NUMERO+E5_PARCELA+E5_TIPO+E5_CLIFOR+E5_LOJA+E5_SEQ))
					If SubStr( SE5->E5_DOCUMEN,1,6 ) == (cAliasSea)->EA_NUMBOR .And. SE5->E5_MOTBX != "PCC"
						lBaixa := .T.
						Exit
					Endif
				EndIf

				SE5->(dbSkip())
				cChvSE5 := SE5->(E5_FILIAL+E5_TIPODOC+E5_PREFIXO+E5_NUMERO+E5_PARCELA+E5_TIPO)+DTOS(SE5->E5_DATA)+SE5->(E5_CLIFOR+E5_LOJA)
			Enddo

		EndIf
	EndIf

	RestArea(aArea)

Return lBaixa

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Fr710BxBA ºAutor  ³ Marcio Menon       º Data ³  28/07/06   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Verifica se existe baixa automatica ou baixa que nao tenha º±±
±±º          ³ movimentacao bancaria.                                     º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ 	                                                          º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function Fr710BxBA(cAliasSea, lBaixa)
	Local cChvSE2 := ""
	Local cChvSE5 := ""
	Local aArea := GetArea()
	Local nRecNo := 0

	DbSelectArea("SE2")
	dbSetOrder(1)
	If SE2->(MsSeek(xFilial("SE2", (cAliasSea)->EA_FILORIG)+(cAliasSea)->(EA_PREFIXO+EA_NUM+EA_PARCELA+EA_TIPO+EA_FORNECE+EA_LOJA)))
		cChvSE2 := xFilial("SE2", SE2->E2_FILORIG)+"BA"+SE2->(E2_PREFIXO+E2_NUM+E2_PARCELA+E2_TIPO)+DTOS(SE2->E2_BAIXA)+SE2->(E2_FORNECE+E2_LOJA) 
		dbSelectArea("SE5")
		dbSetOrder(2)

		If SE5->(MsSeek(xFilial("SE5", (cAliasSea)->EA_FILORIG)+"BA"+SE2->(E2_PREFIXO+E2_NUM+E2_PARCELA+E2_TIPO)+DTOS(SE2->E2_BAIXA)+SE2->(E2_FORNECE+E2_LOJA)))
			cChvSE5 := SE5->(E5_FILIAL+E5_TIPODOC+E5_PREFIXO+E5_NUMERO+E5_PARCELA+E5_TIPO)+DTOS(SE5->E5_DATA)+SE5->(E5_CLIFOR+E5_LOJA)  
			nRecNo := SE5->(RecNo())

			While SE5->(!Eof()) .And. cChvSE2 == cChvSE5 
				//Se considera baixas que nao possuem estorno
				If !TemBxCanc(SE5->(E5_PREFIXO+E5_NUMERO+E5_PARCELA+E5_TIPO+E5_CLIFOR+E5_LOJA+E5_SEQ))
					nRecNo := SE5->(RecNo())

					If SubStr( SE5->E5_DOCUMEN,1,6 ) == (cAliasSea)->EA_NUMBOR .And. SE5->E5_MOTBX != "PCC"
						lBaixa := .T.
						Exit
					Endif
				EndIf

				SE5->(dbSkip())
				cChvSE5 := SE5->(E5_FILIAL+E5_TIPODOC+E5_PREFIXO+E5_NUMERO+E5_PARCELA+E5_TIPO)+DTOS(SE5->E5_DATA)+SE5->(E5_CLIFOR+E5_LOJA)
			EndDo

			//Posiciono no primeiro movimento encontrado ou no £ltimo movimento n?o cancelado, ‚ importante manter a SE5 com a chave do t¡tulo da SE2
			SE5->(dbGoTo(nRecNo))
		Endif
	EndIf

	RestArea(aArea)

Return lBaixa

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Fr710BenefºAutor  ³  Marcio Menon      º Data ³  28/07/06   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Retorna o campo Beneficiario conforme o modelo do          º±±
±±º          ³ bordero.                                                   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Fr710Benef(cAliasSea, lBaixa, lCheque, lAbatimento)

	Local cBenef 	:= ""

	SE2->(dbSetOrder(1))

	If SE2->(DbSeek(xFilial("SE2", (cAliasSea)->EA_FILORIG)+(cAliasSea)->(EA_PREFIXO+EA_NUM+EA_PARCELA+EA_TIPO+EA_FORNECE+EA_LOJA)))
		SA2->(dbSetOrder(1))
		SA2->(MsSeek(xFilial("SA2",SE2->E2_FILORIG)+SE2->E2_FORNECE+SE2->E2_LOJA))

		If (cAliasSea)->EA_MODELO $ "CH/02"
			If !lAbatimento
				If lCheque
					cBenef := SEF->EF_BENEF
				ElseIf lBaixa
					cBenef := SE5->E5_BENEF
				Else
					cBenef := SA2->A2_NREDUZ
				Endif
			EndIf
		Else
			cBenef := SA2->A2_NREDUZ
		Endif
	EndIf

Return cBenef

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Fr710VPagar ºAutor  ³ Marcio Menon       º Data ³  28/07/06   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Faz os calculos dos valores a pagar dos titulos.		        º±±
±±º          ³ 						                                        º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ 	                                                            º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Fr710VPagar(cAliasSea, lBaixa, lCheque, lAbatimento)
	Local nAbat  	:= 0
	Local nVlrPagar	:= 0
	Local aArea 	:= GetArea()
	Local aAreaSA2 	:= SA2->(GetArea())
	Local nValAcess	:= 0
	
	DEFAULT lBaixa 	:= .F. 

	SE2->(dbSetOrder(1))
	If SE2->(DbSeek(FwxFilial("SE2",(cAliasSea)->EA_FILORIG)+(cAliasSea)->(EA_PREFIXO+EA_NUM+EA_PARCELA+EA_TIPO+EA_FORNECE+EA_LOJA))) 
		If lAbatimento
			nAbat 	:= SE2->E2_SALDO
		Else
			nAbat := SomaAbat(SE2->E2_PREFIXO,SE2->E2_NUM,SE2->E2_PARCELA,"P",SE2->E2_MOEDA,dDataBase,SE2->E2_FORNECE,SE2->E2_LOJA, SE2->E2_FILIAL)
		EndIf

		//Efetua calculo dos juros do titulo posicionado
		fa080Juros(1)	

		If ! lAbatimento
			nValAcess := GetVlAcesE2(SE2->E2_PREFIXO, SE2->E2_NUM, SE2->E2_TIPO, SE2->E2_PARCELA, SE2->E2_FORNECE, SE2->E2_LOJA)
			
			If mv_par07 == 3
				//vALOR DA BAIXA
				
				If Empty(SE2->E2_BAIXA)
					nValAcess := 0
				EndIf
				
				nVlrPagar := GetValorBx(SE2->E2_PREFIXO, SE2->E2_NUM, SE2->E2_TIPO, SE2->E2_PARCELA, SE2->E2_FORNECE, SE2->E2_LOJA)+nValAcess 
			ElseIf mv_par07 == 1
				//Valor atual
				//Retirado dedução de impostos direto no relatório, uma vez que configuramos o Totvs para deduzir
				//nVlrPagar := Round(NoRound(xMoeda(SE2->(SE2->E2_SALDO-E2_IRRF-E2_COFINS-E2_PIS-E2_CSLL)-SE2->E2_SDDECRE+SE2->E2_SDACRES-nAbat+nJuros,SE2->E2_MOEDA,MV_PAR04,Iif(MV_PAR06==1,(cAliasSea)->EA_DATABOR,dDataBase),MsDecimais(1)+1,SE2->E2_TXMOEDA ,SM2->M2_MOEDA2),MsDecimais(1)+1),MsDecimais(1))+nValAcess
				nVlrPagar := Round(NoRound(xMoeda(SE2->(SE2->E2_SALDO)-SE2->E2_SDDECRE+SE2->E2_SDACRES-nAbat+nJuros,SE2->E2_MOEDA,MV_PAR04,Iif(MV_PAR06==1,(cAliasSea)->EA_DATABOR,dDataBase),MsDecimais(1)+1,SE2->E2_TXMOEDA ,SM2->M2_MOEDA2),MsDecimais(1)+1),MsDecimais(1))+nValAcess
			Else
				//Valor original
				//Retirado dedução de impostos direto no relatório, uma vez que configuramos o Totvs para deduzir
				//nVlrPagar := Round(NoRound(xMoeda(SE2->(E2_VALOR-E2_IRRF-E2_COFINS-E2_PIS-E2_CSLL)-SE2->E2_DECRESC+SE2->E2_ACRESC-nAbat+nJuros,SE2->E2_MOEDA,MV_PAR04,Iif(MV_PAR06==1,(cAliasSea)->EA_DATABOR,dDataBase),MsDecimais(1)+1,SE2->E2_TXMOEDA ,SM2->M2_MOEDA2),MsDecimais(1)+1),MsDecimais(1))+nValAcess
				nVlrPagar := Round(NoRound(xMoeda(SE2->(E2_VALOR)-SE2->E2_DECRESC+SE2->E2_ACRESC-nAbat+nJuros,SE2->E2_MOEDA,MV_PAR04,Iif(MV_PAR06==1,(cAliasSea)->EA_DATABOR,dDataBase),MsDecimais(1)+1,SE2->E2_TXMOEDA ,SM2->M2_MOEDA2),MsDecimais(1)+1),MsDecimais(1))+nValAcess
			EndIf
		Endif

		RestArea(aAreaSA2)
		RestArea(aArea)
	EndIf

Return nVlrPagar

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ReportCabec ºAutor  ³ Marcio Menon       º Data ³  28/07/06   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Monta o cabecalho do relatorio.				 		        º±±
±±º          ³ 						                                        º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ 	                                                            º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function ReportCabec(oReport, cModelo, cAliasSea, lBaixa, lSeaEof)

	Local cStartPath	:= GetSrvProfString("Startpath","")
	Local cLogo			:= ""
	Local cTexto 		:= ""
	Local lHlpNoTab 	:= .F.

	//Se a quebra de secao for na impressao do texto da autorizacao
	//Volta o registro para imprimir o cabecalho
	If (cAliasSea)->(EOF())
		lSeaEof := .T.
		(cAliasSea)->(dbSkip(-1))
		cModelo := (cAliasSea)->EA_MODELO
	EndIf

	If lBaixa
		SA6->(MsSeek(xFilial("SA6", (cAliasSea)->EA_FILIAL)+SE5->(E5_BANCO+E5_AGENCIA+E5_CONTA)))
	Else
		SA6->(MsSeek(xFilial("SA6", (cAliasSea)->EA_FILIAL)+(cAliasSea)->(EA_PORTADO+EA_AGEDEP+EA_NUMCON)))
	Endif

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica o modelo do documento.				  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	lHlpNoTab := IIf(Empty(cModelo),.F.,.T.)
	If cModelo $ "CH/02"
		cTexto := Tabela("58",@cModelo,lHlpNoTab)
	Elseif cModelo $ "CT/30"
		cTexto := Tabela("58",@cModelo,lHlpNoTab)
	Elseif cModelo $ "CP/31"
		cTexto := Tabela("58",@cModelo,lHlpNoTab)
	ElseIf cModelo $ "CC/01/03/04/05/10/41/43"
		cTexto := Tabela("58",@cModelo,lHlpNoTab)
	Else
		cTexto := Tabela("58",@cModelo,lHlpNoTab)
	Endif

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Define o cabecalho.							  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oReport:ThinLine()

	cLogo := cStartPath + "LGRL" + SM0->M0_CODIGO + IIf( lFWCodFil, FWGETCODFILIAL, SM0->M0_CODFIL ) + ".BMP" 	// Empresa+Filial

	If !File( cLogo )
		cLogo := cStartPath + "LGRL" + SM0->M0_CODIGO + ".BMP" 						// Empresa
	endif

	oReport:SkipLine()
	oReport:SayBitmap (oReport:Row(),005,cLogo,291,057)
	oReport:SkipLine()
	oReport:SkipLine()
	oReport:SkipLine()
	oReport:ThinLine()
	oReport:SkipLine()
	oReport:SkipLine()
	//Texto do tipo de bordero
	oReport:PrintText(SM0->M0_NOME + PadC(OemToAnsi(STR0034),50) + Space(20)+OemToAnsi(STR0035)+DtoC(dDataBase))
	oReport:PrintText(Space(Len(SM0->M0_NOME)) + PadC(cTexto,50) + Space(20)+OemToAnsi(STR0036) + (cAliasSea)->EA_NUMBOR)
	oReport:SkipLine()
	oReport:SkipLine()
	//Dados do Banco
	oReport:PrintText(Pad(OemToAnsi(STR0037) + SA6->A6_NOME,50))
	oReport:PrintText(Pad(	OemToAnsi(STR0038) + RTrim(SA6->A6_AGENCIA) + Iif( !Empty(SA6->A6_DVAGE),"-","") + RTrim(SA6->A6_DVAGE) + ;
	OemToAnsi(STR0040) + RTrim(SA6->A6_NUMCON)  + Iif( !Empty(SA6->A6_DVCTA),"-","") + RTrim(SA6->A6_DVCTA),70))
	oReport:PrintText(Pad(SA6->A6_END + " "  + SA6->A6_MUN + " " + SA6->A6_EST,70))
	oReport:SkipLine()
	oReport:SkipLine()
	oReport:ThinLine()

	If lSeaEof
		(cAliasSea)->(dbSkip())
	EndIf

Return ""

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Fr710Config ºAutor  ³ Marcio Menon       º Data ³  01/08/06   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Exibe ou oculta as colunas do relatorio, conforme o modelo   º±±
±±º          ³ do bordero.			                                        º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ 	                                                            º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Fr710Config(cAliasSea, oSection2)

	Do Case


		Case	(cAliasSea)->EA_MODELO $ "CH/02"
		oSection2:Cell("EF_NUM"    ):Enable()
		oSection2:Cell("EF_NUM"    ):SetBlock({ || "CH. " + SEF->EF_NUM})
		oSection2:Cell("A2_BANCO"  ):Disable()
		oSection2:Cell("A2_AGENCIA"):Disable()
		oSection2:Cell("A6_DVAGE"  ):Disable()
		oSection2:Cell("A2_NUMCON" ):Disable()
		oSection2:Cell("A6_DVCTA"  ):Disable()
		oSection2:Cell("A2_CGC"    ):Disable()

		Case	(cAliasSea)->EA_MODELO $ "CT/30"
		oSection2:Cell("A6_NREDUZ" ):Disable()
		oSection2:Cell("A2_BANCO"  ):Disable()
		oSection2:Cell("A2_AGENCIA"):Disable()
		oSection2:Cell("A6_DVAGE"  ):Disable()
		oSection2:Cell("A2_NUMCON" ):Disable()
		oSection2:Cell("A6_DVCTA"  ):Disable()
		oSection2:Cell("A2_CGC"    ):Disable()
		//Verifica se existe numero de cheque
		If (SEF->(MsSeek(xFilial("SEF") + SE5->E5_BANCO + SE5->E5_AGENCIA + SE5->E5_CONTA + SE5->E5_NUMCHEQ)))
			oSection2:Cell("EF_NUM"    ):SetTitle("NUM. CHEQUE")
			oSection2:Cell("EF_NUM"    ):SetBlock({ || SEF->EF_NUM})
			oSection2:Cell("EF_NUM"    ):Enable()
		Else
			oSection2:Cell("EF_NUM"    ):SetTitle("")
		EndIf

		Case	(cAliasSea)->EA_MODELO $ "CT/31"
		oSection2:Cell("A6_NREDUZ" ):Enable()
		oSection2:Cell("A2_BANCO"  ):Disable()
		oSection2:Cell("A2_AGENCIA"):Disable()
		oSection2:Cell("A6_DVAGE"  ):Disable()
		oSection2:Cell("A2_NUMCON" ):Disable()
		oSection2:Cell("A6_DVCTA"  ):Disable()
		oSection2:Cell("A2_CGC"    ):Disable()
		//Verifica se existe numero de cheque
		If (SEF->(MsSeek(xFilial("SEF") + SE5->E5_BANCO + SE5->E5_AGENCIA + SE5->E5_CONTA + SE5->E5_NUMCHEQ)))
			oSection2:Cell("EF_NUM"    ):SetTitle("NUM. CHEQUE")
			oSection2:Cell("EF_NUM"    ):SetBlock({ || SEF->EF_NUM})
			oSection2:Cell("EF_NUM"    ):Enable()
		Else
			oSection2:Cell("EF_NUM"    ):SetTitle("NUM. CHEQUE")
		EndIf

		Case	(cAliasSea)->EA_MODELO $ "CC/01/03/04/05/10/41/43"
		oSection2:Cell("A6_NREDUZ" ):Enable()
		oSection2:Cell("A2_BANCO"  ):Enable()
		oSection2:Cell("A2_AGENCIA"):Enable()
		oSection2:Cell("A6_DVAGE"  ):Enable()
		oSection2:Cell("A2_NUMCON" ):Enable()
		oSection2:Cell("A6_DVCTA"  ):Enable()
		oSection2:Cell("A2_CGC"    ):Enable()
		oSection2:Cell("EF_NUM"    ):Disable()
	EndCase


Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ReportTxtAutºAutor  ³ Marcio Menon       º Data ³  01/08/06   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Imprime o Total Geral por extenso e as mensagens de	        º±±
±±º          ³ autorizacao, conforme o modelo do bordero.                   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ 	                                                            º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function ReportTxtAut(oReport, cModelo, cNumConta, nVlrSecao)
	Local nCount

	oReport:SkipLine()
	oReport:PrintText(Extenso(nVlrSecao,.F.,MV_PAR04))
	oReport:SkipLine()

	If cModelo $ "CH/02"
		//"AUTORIZAMOS V.SAS. A EMITIR OS CHEQUES NOMINATIVOS AOS BENEFICIARIOS EM REFERENCIA,"
		oReport:PrintText(STR0008)
		//"DEBITANDO EM NOSSA CONTA CORRENTE NO DIA "
		oReport:PrintText(STR0009 + DtoC( mv_par03 ))
		//"PELO VALOR ACIMA TOTALIZADO."
		oReport:PrintText(STR0010)

	Elseif cModelo $ "CT/30"
		//"AUTORIZAMOS V.SAS. A PAGAR OS TITULOS ACIMA RELACIONADOS EM NOSSA"
		oReport:PrintText(STR0011)
		//"CONTA MOVIMENTO NO DIA "###", PELO VALOR ACIMA TOTALIZADO."
		oReport:PrintText(STR0012 + DtoC( mv_par03 ) + OemToAnsi(STR0013))

	Elseif cModelo $ "CP/31"
		//"AUTORIZAMOS V.SAS. A PAGAR OS TITULOS EM REFERENCIA, LEVANDO A DEBITO DE NOSSA"
		oReport:PrintText(STR0014)
		//"CONTA CORRENTE NUM. "###" NO DIA "###" PELO VALOR ACIMA TOTALIZADO."
		oReport:PrintText(STR0015 + cNumConta + OemToAnsi(STR0016) + DtoC( mv_par03 ) + OemToAnsi(STR0017))

	Elseif cModelo $ "CC/01/03/04/05/10/41/43"
		//"AUTORIZAMOS V.SAS. A EMITIREM ORDEM DE PAGAMENTO, OU DOC PARA OS BANCOS/CONTAS ACIMA."
		oReport:PrintText(STR0018)
		//"DOS TITULOS RESPECTIVOS DEBITANDO EM NOSSA C/CORRENTE NUM "
		oReport:PrintText(STR0019 + cNumConta)
		//"NO DIA "### " PELO VALOR ACIMA TOTALIZADO."
		oReport:PrintText(STR0020 + dToC( mv_par03 ) + OemToAnsi(STR0021))

	Else
		//"AUTORIZAMOS V.SAS. A PAGAR OS TITULOS EM REFERENCIA, LEVANDO A DEBITO DE NOSSA"
		oReport:PrintText(STR0022)
		//"CONTA CORRENTE NUM. "###" NO DIA "###" PELO VALOR ACIMA TOTALIZADO."
		oReport:PrintText(STR0023 + cNumConta + OemToAnsi(STR0016) + DtoC( mv_par03 ) + OemToAnsi(STR0017))
	EndIf

	For nCount := 1 to 5
		oReport:SkipLine()
	Next

	//oReport:PrintText("-----------------------------------",/*nRow*/,900)
	//oReport:PrintText(SM0->M0_NOMECOM,/*nRow*/,900)
	oReport:PrintText("---------------------------     ---------------------------     ---------------------------     ---------------------------",/*nRow*/,010)
	oReport:PrintText("Emitente                        Aprovador 1                     Aprovador 2                     Diretor Financeiro      "	 ,/*nRow*/,010)


Return ""

/*
---------------------------------------------- Release 3 ---------------------------------------------------------
*/

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³fa710DtDeb³ Autor ³ Mauricio Pequim Jr.	³ Data ³ 12.01.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Validacao da data de d‚bito para o bordero	  			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³fa710DtDeb() 												  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Generico 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function fa710DtDeb()

	Local lRet := .T.
	lRet := IIf (mv_par03 < dDataBase, .F. , .T. )
Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o    ³		     ³ Autor ³ Adilson H Yamaguchi  ³  Data ³ 13/12/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o ³Monta Cabecalho do relatorio                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Sintaxe   ³ConCGCCabec(Titulo, Cabec1, Cabec2, NomeProg, nTam,         ³±±
±±³			  |			   dDataRef, aCustomText, lFirst)                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Retorno   ³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso       ³ SIGAFIN                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros ³ExpC1 = Conteudo da cabec1               		           ³±±
±±³           ³ExpC2 = Conteudo da cabec2               				   ³±±
±±³           ³ExpC3 = Titulo                           				   ³±±
±±³           ³ExpC4 = Nome da Rotina                   				   ³±±
±±³           ³ExpN1 = Tamanho                          				   ³±±
±±³           ³ExpD1 = Data de Referencia              				       ³±±
±±³           ³ExpL1 = Tamanho                          				   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function FinCgcCabec(Titulo, Cabec1, cabec2, nomeprog, nTam, dDataRef, aCustomText, lFirst)
	Local Tamanho := "M"
	Local aCabec

	nTam 	 := 130
	dDataRef := If(dDataRef = Nil, mv_par01, dDataBase)

	aCabec :=	{"","__LOGOEMP__"}

	cabec(Titulo,"","","",tamanho,	nTam, aCabec)

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³FR170Chk  ºAutor  ³Pedro Pereira Lima  º Data ³  14/06/10   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Verifica quais titulos tem amarracao com o bordero e faz as º±±
±±º          ³devidas validacoes.                                         º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³FINR710                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function FR710Chk(nValid,cAliasQuery)
	Local aArea    := GetArea()
	Local aAreaQry := (cAliasQuery)->(GetArea())
	Local lRetorno := .T.

	If !Empty((cAliasQuery)->EA_FILORIG)//Se possui EA_FILORIG, utilizo esse campo na chave de busca

		dbSelectArea("SE2")
		SE2->(dbSetOrder(1))
		SE2->(dbSeek((cAliasQuery)->(EA_FILORIG+EA_PREFIXO+EA_NUM+EA_PARCELA+EA_TIPO+EA_FORNECE+EA_LOJA)))

		While !SE2->(Eof()) .And. (cAliasQuery)->(EA_FILORIG+EA_PREFIXO+EA_NUM+EA_PARCELA+EA_TIPO+EA_FORNECE+EA_LOJA) ==;
		SE2->(E2_FILIAL+E2_PREFIXO+E2_NUM+E2_PARCELA+E2_TIPO+E2_FORNECE+E2_LOJA)
			If nValid == 1 //oSection1
				lRetorno :=	(MV_PAR05 == 1 .Or. SE2->E2_NUMBOR == (cAliasQuery)->EA_NUMBOR)
			Else //oSection2
				lRetorno :=	(MV_PAR05 == 1 .Or. SE2->E2_MOEDA == MV_PAR04)
			EndIf
			SE2->(dbSkip())
		EndDo
	Else//Validação anterior, caso o EA_FILORIG esteja em branco
		dbSelectArea("SE2")
		SE2->(dbSetOrder(1))
		SE2->(dbSeek(xFilial("SE2")+(cAliasQuery)->(EA_PREFIXO+EA_NUM+EA_PARCELA+EA_TIPO+EA_FORNECE+EA_LOJA)))

		While !SE2->(Eof()) .And. xFilial("SE2")+(cAliasQuery)->(EA_PREFIXO+EA_NUM+EA_PARCELA+EA_TIPO+EA_FORNECE+EA_LOJA) ==;
		SE2->(E2_FILIAL+E2_PREFIXO+E2_NUM+E2_PARCELA+E2_TIPO+E2_FORNECE+E2_LOJA)
			If nValid == 1 //oSection1
				lRetorno :=	(MV_PAR05 == 1 .Or. SE2->E2_NUMBOR == (cAliasQuery)->EA_NUMBOR)
			Else //oSection2
				lRetorno :=	(MV_PAR05 == 1 .Or. SE2->E2_MOEDA == MV_PAR04)
			EndIf

			SE2->(dbSkip())

		EndDo
	EndIf

	RestArea(aAreaQry)
	RestArea(aArea)

Return lRetorno


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ MsModoFil º Autor  ³ Jose Lucas       º Data ³17.06.2011   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Retornar o modo de compartilhamento de cada tabela.        º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºSintaxe   ³ ExpA1 := MsModoFil(ExpC1)                                  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ ExpC1 := Alias da tabela a pesquisar.                      º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ FINR170                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function MsModoFil(cAlias)
	Local aSavArea := GetArea()
	Local aModo := {"","",""}

	SX2->(dbSetOrder(1))
	If SX2->(dbSeek(cAlias))
		aModo[1] := SX2->X2_MODO
		aModo[2] := SX2->X2_MODOUN
		aModo[3] := SX2->X2_MODOEMP
	EndIf
	RestArea(aSavArea)
Return aModo



/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³GetValorBx	ºAutor  ³Microsiga       º Data ³  12/06/19   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Retorna o valor baixado					                  º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                       	              º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function GetValorBx(cPref, cNum, cTipo, cParc, cForn, cLoja)

Local aArea 	:= GetArea()
Local cQuery	:= ""
Local cArqTmp	:= GetNextAlias()
Local nRet		:= 0

Default cPref	:= "" 
Default cNum	:= "" 
Default cTipo	:= "" 
Default cParc	:= "" 
Default cForn	:= "" 
Default cLoja	:= ""

cQuery	:= " SELECT SUM(E5_VALOR) E5_VALOR FROM "+RetSqlName("SE5")+" SE5 "+CRLF
cQuery	+= " WHERE SE5.E5_FILIAL = '"+xFilial("SE5")+"' "+CRLF
cQuery	+= " AND SE5.E5_PREFIXO = '"+cPref+"' "+CRLF
cQuery	+= " AND SE5.E5_NUMERO = '"+cNum+"' "+CRLF
cQuery	+= " AND SE5.E5_TIPO = '"+cTipo+"' "+CRLF
cQuery	+= " AND SE5.E5_PARCELA = '"+cParc+"' "+CRLF
cQuery	+= " AND SE5.E5_CLIFOR = '"+cForn+"' "+CRLF
cQuery	+= " AND SE5.E5_LOJA = '"+cLoja+"' "+CRLF
cQuery	+= " AND SE5.E5_TIPODOC  = 'VL' " +CRLF
cQuery	+= " AND SE5.D_E_L_E_T_ = ' ' "+CRLF

DbUseArea( .T. , "TOPCONN" , TcGenQry(,,cQuery) , cArqTmp,.T.,.T.)

If (cArqTmp)->(!Eof())
	nRet := (cArqTmp)->E5_VALOR 
EndIf

If Select(cArqTmp) > 0 
	(cArqTmp)->(DbCloseArea())
EndIf

RestArea(aArea)
Return nRet



/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³GetVlAcesE2	ºAutor  ³Microsiga   º Data ³  14/01/2020     º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Retorna o valor acessorio				                  º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                       	              º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function GetVlAcesE2(cPref, cNum, cTipo, cParc, cForn, cLoja)

Local aArea 	:= GetArea()
Local cQuery	:= ""
Local cArqTmp	:= GetNextAlias()
Local nRet		:= 0

Default cPref		:= "" 
Default cNum		:= "" 
Default cTipo		:= "" 
Default cParc		:= "" 
Default cForn		:= "" 
Default cLoja		:= ""


cQuery	:= " SELECT E2_PREFIXO, E2_NUM, E2_PARCELA, E2_TIPO, E2_FORNECE, E2_LOJA, "+CRLF 
cQuery	+= " 		FK7_IDDOC, FK7_CHAVE, "+CRLF
cQuery	+= " 		FKD_VALOR, FKD_VLCALC, FKD_VLINFO, FKD_SALDO, FKC_ACAO "+CRLF  
cQuery	+= " 		FROM "+RetSqlName("SE2")+" SE2 "+CRLF

cQuery	+= " INNER JOIN "+RetSqlName("FK7")+" FK7 "+CRLF 
cQuery	+= " ON FK7_FILIAL = SE2.E2_FILIAL "+CRLF
cQuery	+= " AND FK7_ALIAS = 'SE2' "+CRLF
cQuery	+= " AND FK7.FK7_CHAVE = SE2.E2_FILIAL+'|'+SE2.E2_PREFIXO+'|'+SE2.E2_NUM+'|'+SE2.E2_PARCELA+'|'+SE2.E2_TIPO+'|'+SE2.E2_FORNECE+'|'+SE2.E2_LOJA "+CRLF
cQuery	+= " AND FK7.D_E_L_E_T_ = ' ' "+CRLF

cQuery	+= " INNER JOIN "+RetSqlName("FKD")+" FKD "+CRLF
cQuery	+= " ON FKD.FKD_FILIAL = FK7.FK7_FILIAL "+CRLF
cQuery	+= " AND FKD.FKD_IDDOC = FK7.FK7_IDDOC "+CRLF
cQuery	+= " AND FKD.D_E_L_E_T_ = ' ' "+CRLF

cQuery	+= "  INNER JOIN "+RetSqlName("FKC")+" FKC "+CRLF
cQuery	+= "  ON FKC.FKC_FILIAL = FKD.FKD_FILIAL "+CRLF
cQuery	+= "  AND FKC.FKC_CODIGO = FKD.FKD_CODIGO "+CRLF
cQuery	+= "  AND FKC.D_E_L_E_T_ = ' ' "+CRLF

cQuery	+= " WHERE SE2.E2_FILIAL = '"+xFilial("SE2")+"' "+CRLF
cQuery	+= " AND SE2.E2_PREFIXO = '"+cPref+"' "+CRLF
cQuery	+= " AND SE2.E2_NUM = '"+cNum+"' "+CRLF
cQuery	+= " AND SE2.E2_PARCELA = '"+cParc+"' "+CRLF
cQuery	+= " AND SE2.E2_TIPO = '"+cTipo+"' "+CRLF
cQuery	+= " AND SE2.E2_FORNECE = '"+cForn+"' "+CRLF
cQuery	+= " AND SE2.E2_LOJA = '"+cLoja+"' "+CRLF
cQuery	+= " AND SE2.D_E_L_E_T_ = ' ' "+CRLF

DbUseArea( .T. , "TOPCONN" , TcGenQry(,,cQuery) , cArqTmp,.T.,.T.)

If (cArqTmp)->(!Eof())
	
	If Alltrim((cArqTmp)->FKC_ACAO) == "2"
		nRet := (cArqTmp)->FKD_VALOR*-1
	Else
		nRet := (cArqTmp)->FKD_VALOR
	EndIf
	
EndIf

If Select(cArqTmp) > 0 
	(cArqTmp)->(DbCloseArea())
EndIf

RestArea(aArea)
Return nRet