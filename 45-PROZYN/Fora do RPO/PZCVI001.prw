#INCLUDE "rwmake.ch"
#INCLUDE "TOPCONN.ch"
#INCLUDE "protheus.ch"

//Cabeçalho
#DEFINE CBASE		1
#DEFINE ITEM        2
#DEFINE AQUISIC     3
#DEFINE DESCRIC     4
#DEFINE QUANTD      5
#DEFINE CHAPA       6
#DEFINE PATRIM      7
#DEFINE GRUPO       8
#DEFINE NFISCAL		9

//Itens
#DEFINE TIPO        10
#DEFINE BAIXA       11
#DEFINE HISTOR      12
#DEFINE CCONTAB     13
#DEFINE CUSTBEM     14
#DEFINE CDEPREC     15
#DEFINE CDESP       16
#DEFINE CCORREC     17
#DEFINE CCUSTO      18
#DEFINE DINDEPR     19
#DEFINE VORIG1      20
#DEFINE TXDEPR1     21
#DEFINE VORIG2      22
#DEFINE TXDEPR2     23
#DEFINE VORIG3      24
#DEFINE TXDEPR      25
#DEFINE VORIG4      26
#DEFINE TXDEPR4     27
#DEFINE VORIG5      28
#DEFINE TXDEPR5     29
#DEFINE PERDEPR		30 
#DEFINE VRDACM1		31


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³PZCVI001  ºAutor  ³Microsiga           º Data ³  28/11/19   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Importação de ativo imobilizado			                  º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
User Function PZCVI001()
	Local aBotoes		:= {}
	Local aSays		:= {}
	Local nOpcao		:= 0   
	Local aPerg		:= {}


	//Tela de aviso e acesso aos parametros
	AAdd(aSays,"[Importação de planilha]")
	AAdd(aSays,"Esse programa efetuara a importação de dados  ")
	AAdd(aSays,"referente o ativo.")


	AAdd(aBotoes,{ 5,.T.,{|| aPerg := PergFile() 		}} )
	AAdd(aBotoes,{ 1,.T.,{|| nOpcao := 1, FechaBatch() 	}} )
	AAdd(aBotoes,{ 2,.T.,{|| FechaBatch() 				}} )        
	FormBatch( "[Importação]", aSays, aBotoes )

	//Verifica se o parametro com o endereço do arquivo foi preenchido
	If Len(aPerg) > 0

		If aPerg[1]
			If nOpcao == 1
				Processa( {|| ImpArqPla(aPerg) },"","" )
			EndIf
		Else
			Aviso("Erro","Erro ao ler arquivo...",{"Ok"},2)
		EndIf
	Else
		Aviso("Atenção","O parâmetro com o nome do arquivo não foi preenchido ! ",{"Ok"},2) 
	EndIf

Return(Nil) 


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³PergFile  ºAutor  ³Microsiga           º Data ³  13/01/18   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³                                                            º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±	
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function PergFile()

	Local aArea 		:= GetArea()
	Local aRetPath  	:= {}
	Local aParamBox	:= {} 
	Local lRet			:= .F.
	Local aRet			:= {}		

	aAdd(aParamBox 	,{6,"Local do arquivo","","","File(&(ReadVar()))","",100,.T.,"Arquivos .CSV |*.CSV","",GETF_LOCALHARD+GETF_NETWORKDRIVE})


	//Monta a pergunta
	lRet := ParamBox(aParamBox ,"Parametros",@aRetPath,,,.T.,50,50,				,			,.T.			,.T.)

	aRet := {lRet, aRetPath}

	RestArea(aArea)
Return aRet  


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ImpArqPla ºAutor  ³Microsiga           º Data ³  05/14/15   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³                                                            º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function ImpArqPla(aParam)

	Local aArquivo 		:= {}
	Local cLinha   		:= "" 
	Local aLinha  		:= {}
	Local lRetImp		:= .T.
	Local cArqImp 		:= aParam[2][1]//Local do Arquivo
	Local aMsgErr		:= {}
	Local nCont			:= 1

	FT_FUse(cArqImp)
	FT_FGoTop()
	ProcRegua(FT_FLastRec())
	FT_FGoTop()

	While !FT_FEof() 

		IncProc("Efetuando a leitura do arquivo...")

		//Pula a primeira linha do arquivo (Cabeçalho)
		If nCont == 1
			++nCont
			FT_FSkip()
			loop
		EndIf



		//Inicia as variaveis com vazio
		cLinha 	:= ""
		aLinha 	:= {}

		cLinha   	:= FT_FReadLn()   
		aLinha		:= Separa(StrTran(cLinha,'"',""),";") //Quebra a linha em colunas de acordo com o delimitador ';'

		//Verifica se o arquivo esta com a quantidade de colunas correta
		If (Len(aLinha) >= 11) 

			//Adiciona a linha ao arquivo
			aAdd(aArquivo,aLinha )

		Else
			Aviso("ERROLAYOUT","Formato de arquivo inesperado, verifique se o layout está correto.",{"Ok"},3)
			lRetImp := .F.

			Exit
			Return
		EndIf

		FT_FSkip()
	EndDo 

	If Len(aArquivo) > 0

		If VldArq(aArquivo, @aMsgErr)	
			PrepArqProc(aArquivo, aMsgErr)

			If Len(aMsgErr) > 0
				If Aviso("Log Erro", "Erro ao realizar a importação do arquivo. Deseja visualizar o log de processamento ?", {"Sim","Não"},2) == 1
					//Log de processamento
					GetMsgErr(aMsgErr)	
				EndIf				
			EndIf
		Else
			If Aviso("Log Erro", "Erro ao realizar a importação do arquivo. Deseja visualizar o log de processamento ?", {"Sim","Não"},2) == 1
				//Log de processamento
				GetMsgErr(aMsgErr)	
			EndIf	
		EndIf
	Else
		Aviso("ARQVAZIO", "Não existe dados a serem importados", {"Ok"},2)
		lRetImp := .F.
	EndIf

Return lRetImp  



/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³VldArq	 ºAutor  ³Microsiga           º Data ³  13/11/2019º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Pre-validação do arquivo                                    º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function VldArq(aArquivo, aMsgErr)

	Local aArea 		:= GetArea()
	Local lRet			:= .T.
	Local cMsgAux		:= ""
	Local nX			:= 0
	Local cLinAux		:= ""

	Default aMsgErr := {}

	
	DbSelectArea("CT1")
	DbSetOrder(1)

	DbSelectArea("CTT")
	DbSetOrder(1)

	For nX := 1 To Len(aArquivo)

		cLinAux		:= "Linha: "+Alltrim(Str(nX+1))+"->"
		cMsgAux		:= ""


		//Validação do codigo base
		If Empty(aArquivo[nX][CBASE])
			cMsgAux	+= cLinAux+"#Cod Base do Bem não preenchido;"+CRLF
			lRet		:= .F.			
		EndIf
		
		//Validação do item
		If Empty(aArquivo[nX][ITEM])
			cMsgAux	+= cLinAux+"#Item não preenchido;"+CRLF
			lRet		:= .F.			
		EndIf

		//Validação da data de aquisição
		If Empty(aArquivo[nX][AQUISIC])
			cMsgAux	+= cLinAux+"#Data de aquisição não preenchida;"+CRLF
			lRet		:= .F.			
		EndIf

		//Validação da descrição do bem
		If Empty(aArquivo[nX][DESCRIC])
			cMsgAux	+= cLinAux+"#Descrição do bem não preenchida;"+CRLF
			lRet		:= .F.			
		EndIf

		//Validação quantidade
		If Empty(aArquivo[nX][QUANTD]) .Or. NCONVVAL(aArquivo[nX][QUANTD])<=0  
			cMsgAux	+= cLinAux+"#Quantidade do bem não preenchido ou com valor zero;"+CRLF
			lRet		:= .F.			
		EndIf

		//Validação quantidade
		If Empty(aArquivo[nX][CHAPA])  
			cMsgAux	+= cLinAux+"#Num.Plaqueta não preenchido;"+CRLF
			lRet		:= .F.			
		EndIf

		//Validação tipo
		If Empty(aArquivo[nX][TIPO])  
			cMsgAux	+= cLinAux+"#Tipo não preenchido;"+CRLF
			lRet		:= .F.			
		EndIf
		
		//Validação historico da baixa
		/*If Empty(aArquivo[nX][HISTOR])  
			cMsgAux	+= cLinAux+"#Historico não preenchido;"+CRLF
			lRet		:= .F.			
		EndIf*/

		
		If Empty(aArquivo[nX][GRUPO])
			//Validação historico da baixa
			If Empty(aArquivo[nX][CCONTAB])  
				cMsgAux	+= cLinAux+"#Conta não preenchida;"+CRLF
				lRet		:= .F.
			ElseIf CT1->(!DbSeek(xFilial("CT1") + aArquivo[nX][CCONTAB] ))
				cMsgAux	+= cLinAux+"#Conta não encontrada;"+CRLF
				lRet		:= .F.
			EndIf
		EndIf
		
		If !Empty(aArquivo[nX][CUSTBEM]) .And. CTT->(!DbSeek(xFilial("CTT") + aArquivo[nX][CUSTBEM] ))  
			cMsgAux	+= cLinAux+"#C.Custo Bem não encontrada;"+CRLF
			lRet		:= .F.
		EndIf

		If !Empty(aArquivo[nX][CDEPREC]) .And. CT1->(!DbSeek(xFilial("CT1") + aArquivo[nX][CDEPREC] ))  
			cMsgAux	+= cLinAux+"#Cta Desp Dep não encontrada;"+CRLF
			lRet		:= .F.
		EndIf
		
		If Empty(aArquivo[nX][VORIG1]) .Or. NCONVVAL(aArquivo[nX][VORIG1])<=0  
			cMsgAux	+= cLinAux+"#Val Orig M1 não preenchido ou com valor zero;"+CRLF
			lRet		:= .F.			
		EndIf		

		If Empty(aArquivo[nX][TXDEPR1]) //.Or. NCONVVAL(aArquivo[nX][TXDEPR1])<=0  
			cMsgAux	+= cLinAux+"#Tx.An.Depr.1 não preenchido ou com valor zero;"+CRLF
			lRet		:= .F.			
		EndIf		


		If !lRet .And. !Empty(cMsgAux) 
			//Log de processamento
			Aadd(aMsgErr,"------------------------------------------------------------"+CRLF+cMsgAux)
		EndIf

	Next

	RestArea(aArea)
Return lRet



/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³PrepArqProc ºAutor  ³Microsiga      º Data ³  13/11/19      º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Preparação dos dados a serem importados pelo execauto		  º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function PrepArqProc(aArquivo, aMsgErr) 

	Local aArea 		:= GetArea()
	Local cMsgLog	:= ""
	Local nX		:= 0
	Local aCab		:= {}
	Local aItens	:= {}
	Local aLinha	:= {}


	Default aArquivo 	:= {}

	ProcRegua(Len(aArquivo))                    

	For nX := 1 To Len(aArquivo)
		
		aCab 	:= {}
		aItens 	:= {}
		aLinha	:= {}
		
		IncProc("Importando...")

		//Preenchimento do cabeçalho
		If Len(aCab) == 0 
			
			//Cabeçalho
			Aadd(aCab, {"N1_CBASE"		 , Alltrim(aArquivo[nX][CBASE])		, NIL})
			Aadd(aCab, {"N1_ITEM"        , Alltrim(aArquivo[nX][ITEM]) 		, NIL})
			Aadd(aCab, {"N1_AQUISIC"     , CTOD( Alltrim(aArquivo[nX][AQUISIC]))	, NIL})
			Aadd(aCab, {"N1_DESCRIC"     , UPPER(Alltrim(aArquivo[nX][DESCRIC]))	, NIL})
			Aadd(aCab, {"N1_QUANTD"      , NCONVVAL(Alltrim(aArquivo[nX][QUANTD ]))	, NIL})
			Aadd(aCab, {"N1_CHAPA"       , Alltrim(aArquivo[nX][CHAPA  ])	, NIL})
			Aadd(aCab, {"N1_PATRIM"      , Alltrim(aArquivo[nX][PATRIM ])	, NIL})
			Aadd(aCab, {"N1_GRUPO"       , Alltrim(aArquivo[nX][GRUPO  ])	, NIL})
			Aadd(aCab, {"N1_NFISCAL"     , Alltrim(aArquivo[nX][NFISCAL  ])	, NIL})
		EndIf

		//Itens
		Aadd(aLinha, {"N3_CBASE"	, Alltrim(aArquivo[nX][CBASE])		, NIL})
		Aadd(aLinha, {"N3_ITEM"     , Alltrim(aArquivo[nX][ITEM]) 		, NIL})		
		Aadd(aLinha, {"N3_TIPO"     , Alltrim(aArquivo[nX][TIPO])		, NIL})
		Aadd(aLinha, {"N3_BAIXA"    , Alltrim(aArquivo[nX][BAIXA])		, NIL})
		Aadd(aLinha, {"N3_HISTOR"   , UPPER(Alltrim(aArquivo[nX][HISTOR]))		, NIL})
		Aadd(aLinha, {"N3_CCONTAB"  , Alltrim(aArquivo[nX][CCONTAB])	, NIL})
		Aadd(aLinha, {"N3_CUSTBEM"  , Alltrim(aArquivo[nX][CUSTBEM])	, NIL})
		Aadd(aLinha, {"N3_CDEPREC"  , Alltrim(aArquivo[nX][CDEPREC])	, NIL})
		Aadd(aLinha, {"N3_CDESP"    , Alltrim(aArquivo[nX][CDESP])		, NIL})
		Aadd(aLinha, {"N3_CCORREC"  , Alltrim(aArquivo[nX][CCORREC])	, NIL})
		Aadd(aLinha, {"N3_CCUSTO"   , Alltrim(aArquivo[nX][CCUSTO])		, NIL})
		Aadd(aLinha, {"N3_DINDEPR"  , CTOD(Alltrim(aArquivo[nX][DINDEPR]))		, NIL})
		Aadd(aLinha, {"N3_VORIG1"   , NCONVVAL(Alltrim(aArquivo[nX][VORIG1]))	, NIL})
		Aadd(aLinha, {"N3_TXDEPR1"  , NCONVVAL(Alltrim(aArquivo[nX][TXDEPR1]))	, NIL})
		Aadd(aLinha, {"N3_VORIG2"   , NCONVVAL(Alltrim(aArquivo[nX][VORIG2]))	, NIL})
		Aadd(aLinha, {"N3_TXDEPR2"  , NCONVVAL(Alltrim(aArquivo[nX][TXDEPR2]))	, NIL})
		Aadd(aLinha, {"N3_VORIG3"   , NCONVVAL(Alltrim(aArquivo[nX][VORIG3]))	, NIL})
		Aadd(aLinha, {"N3_TXDEPR3"  , NCONVVAL(Alltrim(aArquivo[nX][TXDEPR]))	, NIL})
		Aadd(aLinha, {"N3_VORIG4"   , NCONVVAL(Alltrim(aArquivo[nX][VORIG4]))	, NIL})
		Aadd(aLinha, {"N3_TXDEPR4"  , NCONVVAL(Alltrim(aArquivo[nX][TXDEPR4]))	, NIL})
		Aadd(aLinha, {"N3_VORIG5"   , NCONVVAL(Alltrim(aArquivo[nX][VORIG5]))	, NIL})
		Aadd(aLinha, {"N3_TXDEPR5"  , NCONVVAL(Alltrim(aArquivo[nX][TXDEPR5]))	, NIL})
		Aadd(aLinha, {"N3_PERDEPR"  , NCONVVAL(Alltrim(aArquivo[nX][PERDEPR]))	, NIL})
		Aadd(aLinha, {"N3_VRDACM1"  , NCONVVAL(Alltrim(aArquivo[nX][VRDACM1]))	, NIL})		
		Aadd(aLinha, {"N3_VRDACM2"  , 0	, NIL})
		Aadd(aLinha, {"N3_VRDACM3"  , 0	, NIL})
		Aadd(aLinha, {"N3_VRDACM4"  , 0	, NIL})
		Aadd(aLinha, {"N3_VRDACM5"  , 0	, NIL})
		
			
		
		AaDD(aItens, aLinha)
		
		//Gravação do lançamento contabil
		cMsgLog := RunArq(aCab, aItens, 3 )

		If !Empty(cMsgLog)
			Aadd(aMsgErr,"------------------------------------------------------------"+CRLF)
			Aadd(aMsgErr,"Linha '"+Alltrim(Str(nX+1))+"'-> Erro ao importar o ativo '"+Alltrim(aArquivo[nX][CBASE]))

			Aadd(aMsgErr,cMsgLog+CRLF)				
			Aadd(aMsgErr,"------------------------------------------------------------"+CRLF)
		EndIf

	Next


	RestArea(aArea)
Return


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³RunMovContºAutor  ³Microsiga           º Data ³  14/01/18   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Executa a geração do movimento contabil atraves de rotina   º±±
±±º          ³automatica                                                  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ 				                                              º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function RunArq(aCab, aItem, nOpc )

	Local aArea 	:= GetArea()
	Local cRet		:= ""
	Local cMsgErro	:= ""

	Private lMsErroAuto := .F.

	Default aCab	:= {} 
	Default aItem 	:= {}
	Default nOpc	:= 3

	//Inicio da transação
	Begin Transaction

		//Verifica se os dados foram informados
		If Len(aCab) > 0 .And. Len(aItem) > 0  

			//Chama a rotina automatica para gravar os dados
			MSExecAuto({|x,y,Z| ATFA012(x,y,Z)},aCab,aItem,nOpc)

			//Verifica se ocorreru algum erro no processamento
			If lMSErroAuto

				//Captura a mensagem de erro
				cMsgErro := AllTrim(MemoRead(NomeAutoLog()))
				MemoWrite(NomeAutoLog()," ")

				cRet := cMsgErro

				//Rollback da transação
				DisarmTransaction()

			EndIf

		Else
			cRet := "Dados não informados"
		EndIf

		//Finalisa a transação
	End Transaction

	RestArea(aArea)
Return cRet


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³NCONVVAL ºAutor  ³Microsiga      		 º Data ³  14/01/18   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³                                                            º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function NCONVVAL(cValor)

	Local aArea 	:= GetArea()
	Local nRet		:= ""
	Local cValorAux	:= ""

	Default cValor := ""

	cValorAux := STRTRAN(cValor,"R$","")
	cValorAux := STRTRAN(cValorAux,".","")
	cValorAux := STRTRAN(cValorAux,",",".")

	nRet := Val(cValorAux)

	RestArea(aArea)
Return nRet 


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³GetMsgErr	ºAutor  ³Microsiga			º Data ³  05/11/18   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Função utilizada para apresentar as mensagens de log        º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³			                                                  º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function GetMsgErr(aMsg)

	Default aMsg := {}


	If !Empty(aMsg)

		//Imprime o relatorio com os erros da importação
		CtRConOut(aMsg)

	EndIf

Return 


