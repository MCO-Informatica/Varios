#INCLUDE "PROTHEUS.CH" 
#include "RWMAKE.CH" 
//#include "apvt100.ch"
/*


Ŀ
Funo     ACDHCI01  Autor  Luiz Enrique           Data  24/10/07 
Ĵ
Descrio  Chama funo geradora de etiqueta c/ parametrizaao        
Ĵ
Parametros                                                            
Ĵ
Retorno    Nil                                                        
Ĵ
Uso        HCI                                                        
ٱ

  
*/                                                                             

User Function ACDHCI01

Local cMsg:= ""

If ! ExistBlock('IMG01')
	CBAlert("Imagem da Etiqueta de Produto nao encontrada.")		   
	Return
Endif  
 
CriaPerg() 
SB8->(dbsetOrder(3)) //Filial+Produto+Lote

While .t.

	If ! Pergunte("PHCI01", .T.) 
	   Exit
	Endif 
	
	cMsg:= If (GetMV("MV_SLDSB8",.F.) .And. !SB8->(dbSeek(xFilial()+MV_PAR04+MV_PAR03+MV_PAR05)),"Atribuido Saldo por Lote.","")
	
	If Empty(MV_PAR03)
		CBAlert("Almoxarifado deve ser declarado.")
	    Loop
	 ElseIf Empty(MV_PAR05)
		CBAlert("Rastreabilidade Interna deve ser declarada.")
	    Loop
	 ElseIf Empty(MV_PAR06)
		CBAlert("Corrida deve ser declarada.")
	    Loop 
	 ElseIf Empty(MV_PAR08)
		CBAlert("Pedido (OC) deve ser declarado.")
	    Loop
	 ElseIf Empty(MV_PAR09)
		CBAlert("Item deve ser declarado.")
	    Loop
	 ElseIf Empty(MV_PAR10)
		CBAlert("Empenho deve ser declarado.")
	    Loop
	 ElseIf Empty(MV_PAR11)
		CBAlert("Inspetor deve ser declarado.")
	    Loop
	 ElseIf Empty(MV_PAR12)
		CBAlert("Data deve ser declarada.")
	    Loop
	 ElseIf Empty(MV_PAR13)
		CBAlert("Numero de Copias deve ser declarada.")
	    Loop
	    ElseIf Empty(MV_PAR14)
		CBAlert("Local de Impressao deve ser declarado.")
	    Loop
	Endif 	
		   
	//Configura a Impressora				
	IF !  CB5SetImp(MV_PAR14,.F.) //CB5SetImp(cCod,lVerServer,nDensidade,nTam,cPorta)
	   CBAlert("Tipo de Impresso Invlido") 
	   Return
	Endif
	
	MsgRun("Imprimindo Etiqueta",,{|| Imprime() }) 
	
	If !Empty(cMsg)
		MsgRun("Atribuindo Saldo Por Lote. Aguarde.",,{|| CriaSBJSB8() })			 					
	Endif
	
	CBAlert("IMPRESSAO FINALIZADA."+ Chr(10)+Chr(13)+cMsg)
	
Enddo
	
Return 

/*


Ŀ
Funo     CriaPerg  Autor  Luiz Enrique           Data  23/10/07 
Ĵ
Descrio  Verifica grupo de perguntas no SX1 e cria se necessario    
Ĵ
Parametros Pergute                                                    
Ĵ
Retorno    Nil                                                        
Ĵ
Uso        HCI                                                        
ٱ

  
*/ 

Static Function CriaPerg()

PutSx1("PHCI01","01","Fornecedor","Fornecedor","Fornecedor","mv_ch1","C",06,0,1,"G","U_ExistFL(MV_PAR01,Nil)","SA2","","","MV_PAR01","","","","","","","","","","","","","","","","","","","","")
PutSx1("PHCI01","02","Loja","Loja","Loja","mv_ch2","C",02,0,1,"C","U_ExistFL(MV_PAR01,MV_PAR02)","","","","MV_PAR02","","","","","","","","","","","","","","","","","","","","")
PutSx1("PHCI01","03","Almoxarifado","Almoxarifado","Almoxarifado","mv_ch3","C",02,0,1,"C","","","","","MV_PAR03","","","","","","","","","","","","","","","","","","","","")
PutSx1("PHCI01","04","Produto","Produto","Produto","mv_ch4","C",15,0,1,"G","U_ExistProd(MV_PAR04)","SB1","","","MV_PAR04","","","","","","","","","","","","","","","","","","","","")
PutSx1("PHCI01","05","RI","RI","RI","mv_ch5","C",10,0,1,"C","","","","","MV_PAR05","","","","","","","","","","","","","","","","","",)
PutSx1("PHCI01","06","Corrida","Corrida","Corrida","mv_ch6","C",18,0,1,"C","","","","","MV_PAR06","","","","","","","","","","","","","","","","","",)
PutSx1("PHCI01","07","Qtd Original","Qtd Original","Qtd Original","mv_ch7","N",11,2,1,"C","U_NaoZeros (MV_PAR07,'Quantidade')","","","","MV_PAR07","","","","","","","","","","","","","","","","","",)
PutSx1("PHCI01","08","OC","OC","OC","mv_ch8","C",06,0,1,"C","","","","","MV_PAR08","","","","","","","","","","","","","","","","","",)
PutSx1("PHCI01","09","Item","Item","Item","mv_ch9","C",04,0,1,"C","","","","","MV_PAR09","","","","","","","","","","","","","","","","","",)
PutSx1("PHCI01","10","Empenho","Empenho","Empenho","mv_cha","C",10,0,1,"C","","","","","MV_PAR10","","","","","","","","","","","","","","","","","",)
PutSx1("PHCI01","11","Inspetor","Inspetor","Inspetor","mv_chb","C",10,0,1,"C","","","","","MV_PAR11","","","","","","","","","","","","","","","","","",)
PutSx1("PHCI01","12","Data","Data","Data","mv_chc","D",08,0,1,"C","","","","","MV_PAR12","","","","","","","","","","","","","","","","","",)
PutSx1("PHCI01","13","Copias","Copias","Copias","mv_chd","N",3,0,1,"G","U_NaoZeros (MV_PAR13,'Copias')","","","","MV_PAR13","","","","","","","","","","","","","","","","","",)
PutSx1("PHCI01","14","Local de Impressao","Local de Impressao","Local de Impressao","mv_che","C",3,0,1,"G","U_LocalImp(MV_PAR14)","CB5","","","MV_PAR14","","","","","","","","","","","","","","","","","")

Return

/*


Ŀ
Funo     Imprime   Autor  Luiz Enrique           Data  12/08/05 
Ĵ
Descrio  Gera e Imprime Etiqueta de produto atravs de parametrizao
Ĵ
Parametros Pergute                                                    
Ĵ
Retorno    Nil                                                        
Ĵ
Uso        SIGAACD                                                    
ٱ

  
*/ 

Static Function Imprime() 
	
ExecBlock('IMG01',,,{MV_PAR07,,,MV_PAR13,,,,,,,,,,,,,,,,,,0,.T.})

MSCBCLOSEPRINTER()

Return 

/*


Ŀ
Funo     ExistProd   Autor  Luiz Enrique         Data  23/10/07 
Ĵ
Descrio  Verifica se Codigo do Produto  valido                     
Ĵ
Parametros CodProduto    - COdigo do Produto                          
Ĵ
Retorno    Boolean                                                    
Ĵ
Uso        HCI                                                        
ٱ


*/

User Function ExistProd (CodProduto)
Local cAreaSB1 := GetArea('SB1')
           
SB1->(dbsetOrder(1))
If ! SB1->(dbSeek(xFilial()+Trim(CodProduto))) 
   CBAlert("Cdigo do Produto Invlido") 
   RestArea(cAreaSB1)
   Return .f.
Endif 

Return .t.  

/*


Ŀ
Funo     ExistFL     Autor  Luiz Enrique         Data  23/10/07 
Ĵ
Descrio  Verifica Fornecedor ou se Loja Valida para o Fornecedor    
			  declarado.  	     										  
Ĵ
Parametros CodForn,CodLoj   - Codigo do Fornecedor/Codigo da Loja     
Ĵ
Retorno    Boolean                                                    
Ĵ
Uso        HCI                                                        
ٱ


*/
User Function ExistFL (CodForn,CodLoj)
Local cAreaSA2 := GetArea('SA2')
           
SA2->(dbsetOrder(1))
If Empty(CodLoj)
	If ! SA2->(dbSeek(xFilial()+AllTrim(CodForn))) 
		CBAlert("Cdigo do Fornecedor Invlido") 
	   	RestArea(cAreaSA2)
	   	Return .f.
	Endif 
Else
	If ! SA2->(dbSeek(xFilial()+CodForn+CodLoj)) 
		CBAlert("Loja invalida para Fornecedor " + AllTrim(CodForn) ) 
		RestArea(cAreaSA2)
		Return .f. 
	Endif
Endif

Return .t.

/*


Ŀ
Funo     ExistEnde   Autor  Luiz Enrique         Data  23/10/07 
Ĵ
Descrio  Verifica se Aramazem e endereo  vlido                   
Ĵ
Parametros Armaz,Ender    - Aramazem, Endereco                        
Ĵ
Retorno    Boolean                                                    
Ĵ
Uso        HCI                                                        
ٱ


*/
User Function ExistEnde (Armaz,Ender) 
	
Local cAreaSBE := GetArea('SBE') 
	   
SBE->(dbsetOrder(1))
If !SBE->(dbSeek(xFilial()+Trim(Armaz+Ender))) 
	CBAlert("Armazem/Endereo no  vlido")
	RestArea(cAreaSBE) 
	Return .F.
Endif
    
Return .t.

/*


Ŀ
Funo     NaoZeros    Autor  Luiz Enrique         Data  23/10/07 
Ĵ
Descrio  Verifica campos vazios ou Zerados                          
Ĵ
Parametros cCampo,cMens - Campo verificado, Mensagem enviada          
Ĵ
Retorno    Boolean                                                    
Ĵ
Uso        HCI                                                        
ٱ


*/
User Function NaoZeros (cCampo,cMens) 
   
    If  cCampo <= 0 
       	cMens += " No pode ser Zero ou no declarada."
    	CBAlert(cMens) 
    	Return .F.
    Endif
    
Return .t.

/*


Ŀ
Funo     LocalImp    Autor  Luiz Enrique         Data  23/10/07 
Ĵ
Descrio  Verifica se local de impresso  valido                    
Ĵ
Parametros CodLocal    - Cdigo do Local                              
Ĵ
Retorno    Boolean                                                    
Ĵ
Uso        HCI                                                        
ٱ


*/
User Function LocalImp (CodLocal)
 	
Local cAreaCB5 := GetArea('CB5')
     
CB5->(dbsetOrder(1))
If !CB5->(dbSeek(xFilial()+Trim(CodLocal))) 
	CBAlert("Local de Impresso Invlido")
	RestArea(cAreaCB5) 
	Return .F.
Endif  
  
Return .T. 
	

/*


Ŀ
Funo     CriaSBJSB8  Autor  Luiz Enrique         Data  06/11/07 
Ĵ
Descrio  Gera Saldo iniciais por Lote na geracao da Etiqueta Avulsa.
Ĵ
Parametros 									                          
Ĵ
Retorno    Grava o saldo inicial no SBJ                               
Ĵ
Uso        HCI                                                        
ٱ


*/  

Static Function CriaSBJSB8() 

Local cNumLote:=CriaVar("B8_NUMLOTE")                
Local dDataFec:=GETMV("MV_ULMES")

SBJ->(dbsetOrder(1))

If !SBJ->(dbSeek(xFilial('SBJ')+MV_PAR04+MV_PAR03+MV_PAR05)) //Filial+Produto+Local+Lote
	cNumLote:=NextLote(MV_PAR04,"S")
	RecLock('SBJ',.T.)  
	Replace SBJ->BJ_FILIAL  With xFilial('SBJ')
	Replace SBJ->BJ_COD     With MV_PAR04 	//Produto
	Replace SBJ->BJ_LOCAL   With MV_PAR03 	//Almoxarifado
	Replace SBJ->BJ_LOTECTL With MV_PAR05	//Rastreabilidade Interna RI - LOTE
	Replace SBJ->BJ_DTVALID With MV_PAR12	//Data de Validade do lote
	Replace SBJ->BJ_NUMLOTE With cNumLote	
	Replace SBJ->BJ_DATA    With dDataFec	//Data do Saldo - Data da Criacao do lote
Else
	RecLock('SBJ',.F.)
EndIf
Replace SBJ->BJ_QINI    With SBJ->BJ_QINI + MV_PAR07	//-- Quantidade inicial do lote
Replace SBJ->BJ_QISEGUM With SBJ->BJ_QISEGUM + ConvUm(MV_PAR04,MV_PAR07,0,2)//-- Quantidade inicial 2UM
SBJ->(MsUnlock())

//Gera SB8
CriaLote("SBJ",SBJ->BJ_COD,SBJ->BJ_LOCAL,SBJ->BJ_LOTECTL,SBJ->BJ_NUMLOTE,"","","",Nil,"MN","","","IMPLAN",;
				"","",SBJ->BJ_QINI,SBJ->BJ_QISEGUM,dDatabase,SBJ->BJ_DTVALID,.T.)

Return