

/*

Ŀ
Programa   HCIR016   Autor  Aline Catarina         Data  30/01/07 
Ĵ
Descrio  Pick-List (Pedido de Venda)                                
Ĵ
Uso        SIGAFAT                                                    
Ĵ
OBS		 | Ulizado como base para alterao o programa MATR775.prx    |
ٱ

*/
User Function HCIR016()

Local oReport
Private lPyme      := Iif(Type("__lPyme") <> "U",__lPyme,.F.)	
Private lAglutGrad := .F.

If FindFunction("TRepInUse") .And. TRepInUse()
	//-- Interface de impressao
	oReport := ReportDef()
	oReport:PrintDialog()
Else
	U_HCIR016R3()
EndIf

Return

/*

Ŀ
Programa  ReportDef  Autor  Marco Bianchi          Data  19/07/06 
Ĵ
Descrio A funcao estatica ReportDef devera ser criada para todos os 
          relatorios que poderao ser agendados pelo usuario.          
                                                                      
Ĵ
Retorno   ExpO1: Objeto do relatrio                                  
Ĵ
ParametrosNenhum                                                      
                                                                      
Ĵ
   DATA    Programador   Manutencao efetuada                         
Ĵ
 30/01/07 Aline Catarina Alteracoes p/ impressao do Pick-List        |            
     	 |		  		 |do Pedido de Vendas.						  |     
ٱ


/*/
Static Function ReportDef()

Local oReport
Local cCodProd 	 := ""
Local cDescProd	 := ""
Local cLote		 := ""
Local cSubLote	 := ""
Local nTotQuant	 := 0
Local dDtValid   := dDatabase
Local cNPedido	 := ""
Local cLocal	 := ""
Local cPotenci	 := ""

#IFDEF TOP
	Local cAliasSC6 := GetNextAlias()
#ELSE
	Local cAliasSC6 := "SC6"
#ENDIF	

//Ŀ
//Criacao do componente de impressao                                      
//                                                                        
//TReport():New                                                           
//ExpC1 : Nome do relatorio                                               
//ExpC2 : Titulo                                                          
//ExpC3 : Pergunte                                                        
//ExpB4 : Bloco de codigo que sera executado na confirmacao da impressao  
//ExpC5 : Descricao                                                       
//                                                                        
//
oReport := TReport():New("HCIR016","PICK-LIST","HCR016", {|oReport| ReportPrint(oReport,cAliasSC6)},"Emissao de produtos a serem separados pela expedicao, para determinada faixa de Pedido de Vendas.") 
oReport:SetPortrait() 
oReport:SetTotalInLine(.F.)

//Ŀ
// Verifica as perguntas selecionadas                          
//
AjustaSX1("HCR016")
Pergunte(oReport:uParam,.F.)

//Ŀ
//Criacao da secao utilizada pelo relatorio                               
//                                                                        
//TRSection():New                                                         
//ExpO1 : Objeto TReport que a secao pertence                             
//ExpC2 : Descricao da seao                                              
//ExpA3 : Array com as tabelas utilizadas pela secao. A primeira tabela   
//        sera considerada como principal para a seo.                   
//ExpA4 : Array com as Ordens do relatrio                                
//ExpL5 : Carrega campos do SX3 como celulas                              
//        Default : False                                                 
//ExpL6 : Carrega ordens do Sindex                                        
//        Default : False                                                 
//                                                                        
//
//Ŀ
//Criacao da celulas da secao do relatorio                                
//                                                                        
//TRCell():New                                                            
//ExpO1 : Objeto TSection que a secao pertence                            
//ExpC2 : Nome da celula do relatrio. O SX3 ser consultado              
//ExpC3 : Nome da tabela de referencia da celula                          
//ExpC4 : Titulo da celula                                                
//        Default : X3Titulo()                                            
//ExpC5 : Picture                                                         
//        Default : X3_PICTURE                                            
//ExpC6 : Tamanho                                                         
//        Default : X3_TAMANHO                                            
//ExpL7 : Informe se o tamanho esta em pixel                              
//        Default : False                                                 
//ExpB8 : Bloco de cdigo para impressao.                                 
//        Default : ExpC2                                                 
//                                                                        
//
oPickList := TRSection():New(oReport,"PICK-LIST",{"SC6","SB1","SB2","SB4"},/*{Array com as ordens do relatrio}*/,/*Campos do SX3*/,/*Campos do SIX*/)
oPickList:SetTotalInLine(.F.)

TRCell():New(oPickList,"CCODPROD"	,/*Tabela*/	,RetTitle("C6_PRODUTO"	),PesqPict("SC6","C6_PRODUTO"	),TamSx3("C6_PRODUTO"	)[1],/*lPixel*/,{|| IIF(lAglutGrad ,Substr(cCodProd,1,ntamref),cCodProd)	})
TRCell():New(oPickList,"CDESCPROD"	,/*Tabela*/	,RetTitle("B1_DESC"	),PesqPict("SB1","B1_DESC"	),TamSx3("B1_DESC"	)[1],/*lPixel*/,{|| cDescProd 												})
TRCell():New(oPickList,"B1_UM"		,"SB1"		,RetTitle("B1_UM"	),PesqPict("SB1","B1_UM"	),TamSx3("B1_UM"	)[1],/*lPixel*/,{|| SB1->B1_UM												})
TRCell():New(oPickList,"NTOTQUANT"	,/*Tabela*/	,RetTitle("C6_QTDVEN"),PesqPict("SC6","C6_QTDVEN"	),TamSx3("C6_QTDVEN"	)[1],/*lPixel*/,{|| nTotQuant 												})
TRCell():New(oPickList,"CLOCAL"		,/*Tabela*/	,RetTitle("C6_LOCAL"),PesqPict("SC6","C6_LOCAL"	),TamSx3("C6_LOCAL"	)[1],/*lPixel*/,{|| cLocal													})
If !lPyme
	TRCell():New(oPickList,"B2_LOCALIZ"	,"SB2"		,RetTitle("B2_LOCALIZ"),PesqPict("SB2","B2_LOCALIZ"),TamSx3("B2_LOCALIZ")[1],/*lPixel*/,{|| SB2->B2_LOCALIZ	})
	TRCell():New(oPickList,"CLOTE"		,/*Tabela*/	,RetTitle("C6_LOTECTL"),PesqPict("SC6","C6_LOTECTL"),TamSx3("C6_LOTECTL")[1],/*lPixel*/,{|| cLote				})
	TRCell():New(oPickList,"CSUBLOTE"	,/*Tabela*/	,RetTitle("C6_NUMLOTE"),PesqPict("SC6","C6_NUMLOTE"),TamSx3("C6_NUMLOTE")[1],/*lPixel*/,{|| cSubLote			})
	TRCell():New(oPickList,"DDTVALID"	,/*Tabela*/	,RetTitle("C6_DTVALID"),PesqPict("SC6","C6_DTVALID"),TamSx3("C6_DTVALID")[1],/*lPixel*/,{|| dDtValid			})
	TRCell():New(oPickList,"CPOTENCI"	,"SC6"		,RetTitle("C6_POTENCI"),PesqPict("SC6","C6_POTENCI"),TamSx3("C6_POTENCI")[1],/*lPixel*/,{|| cPotenci			})
EndIf
TRCell():New(oPickList,"cNPedido",/*Tabela*/,RetTitle("C6_NUM"),PesqPict("SC6","C6_NUM"),TamSx3("C6_NUM")[1],/*lPixel*/,{|| cNPedido })

Return(oReport)

/*/


Ŀ
Programa  ReportPrin Autor  Marco Bianchi          Data  19/07/06 
Ĵ
Descrio A funcao estatica ReportDef devera ser criada para todos os 
          relatorios que poderao ser agendados pelo usuario.          
                                                                      
Ĵ
Retorno   Nenhum                                                      
Ĵ
ParametrosExpO1: Objeto Report do Relatrio                           
                                                                      
Ĵ
   DATA    Programador   Manutencao efetuada                         
Ĵ
                                                                     
ٱ


/*/
Static Function ReportPrint(oReport,cAliasSC6)

Local nTamRef  	 := Val(Substr(GetMv("MV_MASCGRD"),1,2))
Local lRet       := .F.
Local cProdRef	 := ""
Local lSkip		 := .F.    


#IFNDEF TOP
	Local cCondicao := ""
	Local cKey 		:= ""
	Local cIndexSC6 := ""
#ELSE	
	Local cWhere := ""
#ENDIF               

oReport:Section(1):Cell("CCODPROD" 	):SetBlock({|| cCodProd		})
oReport:Section(1):Cell("CDESCPROD"	):SetBlock({|| cDescProd	})
oReport:Section(1):Cell("CLOTE"		):SetBlock({|| cLote   		})
oReport:Section(1):Cell("CSUBLOTE"	):SetBlock({|| cSubLote		})
oReport:Section(1):Cell("NTOTQUANT"	):SetBlock({|| nTotQuant	})
oReport:Section(1):Cell("CLOCAL"	):SetBlock({|| cLocal		})
oReport:Section(1):Cell("DDTVALID"	):SetBlock({|| dDtValid		})
oReport:Section(1):Cell("cNPedido"	):SetBlock({|| cNPedido		})
oReport:Section(1):Cell("CPOTENCI"	):SetBlock({|| cPotenci		})

nTotQuant := 0


//Ŀ
//Transforma parametros Range em expressao SQL                            
//
MakeSqlExpr(oReport:uParam)

//Ŀ
//Filtragem do relatrio                                                  
//
#IFDEF TOP
	If TcSrvType() <> "AS/400"
		oReport:Section(1):BeginQuery()
		BeginSql Alias cALiasSC6
		SELECT SC6.R_E_C_N_O_ SC6REC,
		SC6.C6_NUM,SC6.C6_FILIAL,SC6.C6_QTDVEN,SC6.C6_PRODUTO,
		SC6.C6_LOCAL,SC6.C6_GRADE,SC6.C6_LOTECTL,SC6.C6_POTENCI,
		SC6.C6_NUMLOTE,SC6.C6_DTVALID
		FROM %Table:SC6% SC6
		WHERE (SC6.C6_QTDVEN > 0) AND
			SC6.C6_NUM >= %Exp:mv_par01% AND
			SC6.C6_NUM <= %Exp:mv_par02% AND
			SC6.C6_FILIAL = %xFilial:SC6% AND
			SC6.%Notdel%
			ORDER BY SC6.C6_FILIAL,SC6.C6_NUM,SC6.C6_CLI,SC6.C6_LOJA,SC6.C6_PRODUTO,SC6.C6_LOTECTL,
				SC6.C6_NUMLOTE,SC6.C6_DTVALID
		EndSql
		oReport:Section(1):EndQuery()		
				
	Else
#ENDIF	         
		dbSelectArea(cAliasSC6)
		cIndexSC6  := CriaTrab(nil,.f.)
		cKey :="C6_FILIAL+C6_NUM+C6_CLI+C6_LOJA+C6_PRODUTO+C6_LOTECTL+C6_NUMLOTE+DTOS(C6_DTVALID)"		
		cCondicao := "C6_FILIAL = '" + xFilial("SC6") + "' .And. "
		cCondicao += "(C6_QTDVEN > 0) .And. "
		cCondicao += "C6_NUM >= '"+mv_par01+"' .And. " 
		cCondicao += "C6_NUM <= '"+mv_par02+"'" 

		IndRegua(cAliasSC6,cIndexSC6,cKey,,cCondicao,"Selecionando Registros...")	   
		#IFNDEF TOP
			DbSetIndex(cIndexSC6+OrdBagExt())
		#ENDIF                           
		
#IFDEF TOP
	Endif
#ENDIF	

//Ŀ
//Metodo TrPosition()                                                     
//                                                                        
//Posiciona em um registro de uma outra tabela. O posicionamento ser     
//realizado antes da impressao de cada linha do relatrio.                
//                                                                        
//                                                                        
//ExpO1 : Objeto Report da Secao                                          
//ExpC2 : Alias da Tabela                                                 
//ExpX3 : Ordem ou NickName de pesquisa                                   
//ExpX4 : String ou Bloco de cdigo para pesquisa. A string ser macroexe-
//        cutada.                                                         
//                                                                        				
//
TRPosition():New(oReport:Section(1),"SB2",1,{|| xFilial("SB1")+cCodProd+cLocal})


dbSelectArea(cAliasSC6)
dbGoTop()
oReport:SetMeter(RecCount())
oReport:Section(1):Init()
While !oReport:Cancel() .And. (cAliasSC6)->(!Eof()) .And. (cALiasSC6)->C6_FILIAL = xFilial("SC6")

	//	Ŀ
	//	 Valida o produto conforme a mascara              
	//	
	lRet:=ValidMasc((cAliasSC6)->C6_PRODUTO,MV_PAR03)
	If !lRet .and. !lSkip   
		MsgAlert("Mscara do produto no encontrada, processo finalizado.")   
		dbCloseArea() 
	   	Return(.F.)
	EndIf
	
	If lRet

		cCodProd := (cAliasSC6)->C6_PRODUTO
		cLote	 := (cAliasSC6)->C6_LOTECTL
		cSubLote := (cAliasSC6)->C6_NUMLOTE              
		dDtValid := (cAliasSC6)->C6_DTVALID
		cNPedido := (cAliasSC6)->C6_NUM
		cLocal   := (cAliasSC6)->C6_LOCAL
		cPotenci := (cAliasSC6)->C6_POTENCI
		
		lSkip := .F.
		lAglutGrad := (!lPyme .And. (cAliasSC6)->C6_GRADE == "S" .and. MV_PAR04 == 1) 
		If lAglutGrad
			cProdRef 	:=Substr(cCodProd,1,nTamRef)
			SB4->(DbSeek(xFilial("SB4") + cProdRef))
			cDescProd:= SB4->B4_DESC
		Else
			SB1->(DbSeek(xFilial("SB1") + (cAliasSC6)->C6_PRODUTO))
			cDescProd:= SB1->B1_DESC
		Endif
		dbSelectArea(cAliasSC6)
		If MV_PAR04 == 1
			nTotQuant := 0
			While (cAliasSC6)->(!Eof()) .And.;
				If(lAglutGrad,(cProdRef == Substr((cAliasSC6)->C6_PRODUTO,1,nTamRef)),cCodProd == (cAliasSC6)->C6_PRODUTO) .And.;
				(cLote == (cAliasSC6)->C6_LOTECTL .And. cSubLote == (cAliasSC6)->C6_NUMLOTE) .And. ;
				(cAliasSC6)->C6_NUM == cNPedido
				
				nTotQuant += (cAliasSC6)->C6_QTDVEN
				(cAliasSC6)->(dbSkip())
				lSkip := .T.
			Enddo
		Else
			IF !lPyme .And. (cAliasSC6)->C6_GRADE == "S" .and. MV_PAR04 == 1
				cProdRef 	:=Substr(cCodProd,1,nTamRef)
				nTotQuant	:=0
				While (cAliasSC6)->(!Eof()) .And. cProdRef == Substr((cAliasSC6)->C6_PRODUTO,1,nTamRef) .And. (cAliasSC6)->C6_GRADE == "S" .And.;
					(cLote == (cAliasSC6)->C6_LOTECTL .And. cSubLote == (cAliasSC6)->C6_NUMLOTE) .And. ;
					(cAliasSC6)->C6_NUM == cNPedido 
					nTotQuant += (cAliasSC6)->C6_QTDVEN
					(cAliasSC6)->(dbSkip())
					lSkip := .T.
				End
			Endif
		Endif
		
		If !(lAglutGrad .Or. MV_PAR04 == 1)
			nTotQuant :=(cAliasSC6)->C6_QTDVEN
		EndIf
		
		oReport:Section(1):PrintLine()
		
	EndIf

	dbSelectArea(cAliasSC6)
	If !lSkip	
		dbSkip()
	EndIf	
	
End
oReport:Section(1):Finish()


Return


/*/


Ŀ
Funo     HCIR016R3 Autor  Claudinei M. Benzi     Data  23.05.94 
Ĵ
Descrio  Pick-List (Pedido de Venda)                                
Ĵ
Sintaxe e  HCIR016(void)                                              
Ĵ
 Uso       Generico                                                   
Ĵ
 ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                     
Ĵ
 PROGRAMADOR   DATA    BOPS   MOTIVO DA ALTERACAO                   
Ĵ
 Edson   M.   30/03/99XXXXXXPassar o tamanho na SetPrint.           
Ĵ
 Aline Catarin30/01/07XXXXXXAlteracoes p/ impressao do Pick-List    |            
 			 |		  |		 |do Pedido de Vendas.					  |
ٱ


/*/
User Function HCIR016R3
//Ŀ
// Define Variaveis                                             
//
LOCAL wnrel		:= "HCIR016"
LOCAL tamanho	:= "M"
LOCAL titulo	:= OemToAnsi("Pick-List (Pedido de Venda)")	
LOCAL cDesc1	:= OemToAnsi("Emisso de produtos a serem separados pela expedicao, para")	
LOCAL cDesc2	:= OemToAnsi("determinada faixa de pedidos de venda.")
LOCAL cDesc3	:= ""
LOCAL cString	:= "SC6"
LOCAL cPerg  	:= "HCR016"

PRIVATE aReturn		:= {"Zebrado", 1,"Administracao", 2, 2, 1, "",0 }
PRIVATE nomeprog	:= "HCIR016"
PRIVATE nLastKey 	:= 0
PRIVATE nBegin		:= 0
PRIVATE aLinha		:= {}
PRIVATE li			:= 80
PRIVATE limite		:= 132
PRIVATE lRodape		:= .F.
PRIVATE m_pag       :=1

//Ŀ
// Verifica as perguntas selecionadas                          
//
AjustaSX1(cPerg)
pergunte(cPerg,.F.)
//Ŀ
// Variaveis utilizadas para parametros                      
// mv_par01	     	  Do Pedido                           
// mv_par02	     	  Ate o Pedido                        
// mv_par03	     	  Mascara                             
// mv_par04	     	  Aglutina itens grade                
//
wnrel:=SetPrint(cString,wnrel,cPerg,@Titulo,cDesc1,cDesc2,cDesc3,.F.,,,Tamanho,,.T.)

If nLastKey == 27
	dbClearFilter()
	Return
Endif

SetDefault(aReturn,cString)

If nLastKey == 27
	dbClearFilter()
	Return
Endif

RptStatus({|lEnd| C775Imp(@lEnd,wnRel,cString,cPerg,tamanho,@titulo,@cDesc1,;
			@cDesc2,@cDesc3)},Titulo)
Return

/*/


Ŀ
Funo     C775IMP   Autor  Rosane Luciane Chene   Data  09.11.95 
Ĵ
Descrio  Chamada do Relatorio                                       
Ĵ
 Uso       MATR775			                                          
ٱ


/*/
Static Function C775Imp(lEnd,WnRel,cString,cPerg,tamanho,titulo,cDesc1,cDesc2,;
						cDesc3)
//Ŀ
// Define Variaveis                                             
//

LOCAL cabec1 	 := OemToAnsi("Codigo          Desc. do Material              UM Quantidade  Amz Endereco       Lote      SubLote  Validade   Potencia    Pedido") 
LOCAL cabec2	 := ""
LOCAL lContinua  := .T.
LOCAL lFirst 	 := .T.
LOCAL cPedAnt	 := ""
LOCAL nI		 := 0
LOCAL aTam    	 := {}
LOCAL cMascara 	 := GetMv("MV_MASCGRD")
LOCAL nTamRef  	 := Val(Substr(cMascara,1,2))
LOCAL cbtxt      := SPACE(10)
LOCAL cbcont	 := 0
LOCAL nTotQuant	 := 0
LOCAL aStruSC6   := {}
LOCAL nSC6       := 0
LOCAL cFilter    := ""
LOCAL cAliasSC6  := "SC6"
LOCAL cIndexSC6  := "" 
LOCAL cKey 	     := ""
LOCAL lQuery     := .F.
LOCAL lRet       := .F.
LOCAL cProdRef	 := ""
LOCAL lSkip		 := .F.    
LOCAL cCodProd 	 := ""
LOCAL nQtdIt   	 := 0
LOCAL cDescProd	 := ""
LOCAL cGrade   	 := ""
LOCAL cUnidade 	 := ""
LOCAL cLocaliza	 := ""
LOCAL cLote	 	 := ""
LOCAL cLocal 	 := ""                
LOCAL cSubLote   := ""
LOCAL dDtValid   := dDatabase
LOCAL nPotencia  := 0
Local lPyme      := Iif(Type("__lPyme") <> "U",__lPyme,.F.)
Local nX         := 0
Local cName      := ""
Local cQryAd     := ""
Local cNPedido	 := ""
Local lAglutGrad := .F.

If lPyme
	cabec1 	 := OemToAnsi("Codigo          Desc. do Material              UM Quantidade  Amz Pedido") 
EndIf
//Ŀ
// Variaveis utilizadas para Impressao do Cabecalho e Rodape    
//
li := 80
//Ŀ
// Definicao dos cabecalhos                                     
//
titulo := OemToAnsi("PICK-LIST")	// 
// "Codigo          Desc. do Material              UM Quantidade  Amz Endereco       Lote      SubLote  Dat.de Validade Potencia"
//            1         2         3         4         5         6         7         8         9        10        11        12        13      
//  0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
#IFDEF TOP
	If TcSrvType() <> "AS/400"
	    cAliasSC6:= "C775Imp"
	    aStruSC6  := SC6->(dbStruct())		
		lQuery    := .T.
		cQuery := "SELECT SC6.R_E_C_N_O_ SC6REC,"
		cQuery += "SC6.C6_NUM,SC6.C6_FILIAL,SC6.C6_QTDVEN,SC6.C6_PRODUTO, "
		cQuery += "SC6.C6_LOCAL,SC6.C6_GRADE,SC6.C6_LOTECTL,SC6.C6_POTENCI,"
		cQuery += "SC6.C6_NUMLOTE,SC6.C6_DTVALID"

		//Ŀ
		//Esta rotina foi escrita para adicionar no select os campos do SC6 usados no filtro do usuario 
		//quando houver, a rotina acrecenta somente os campos que forem adicionados ao filtro testando  
	    //se os mesmo ja existem no selec ou se forem definidos novamente pelo o usuario no filtro.     
		//
		If !Empty(aReturn[7])
			For nX := 1 To SC6->(FCount())
		 		cName := SC6->(FieldName(nX))
				If AllTrim( cName ) $ aReturn[7]
					If aStruSC6[nX,2] <> "M"
						If !cName $ cQuery .And. !cName $ cQryAd
							cQryAd += ",SC6."+ cName
						EndIf
					EndIf
				EndIf
			Next nX
		EndIf
		cQuery += cQryAd
		
		cQuery += " FROM "
		cQuery += RetSqlName("SC6") + " SC6 "
		cQuery += "WHERE "                   
		cQuery += "( SC6.C6_QTDVEN > 0) AND "
		cQuery += "SC6.C6_NUM >= '"+mv_par01+"' AND " 
		cQuery += "SC6.C6_NUM <= '"+mv_par02+"' AND " 
		cQuery += "SC6.C6_FILIAL = '"+xFilial("SC6")+"' AND "
		cQuery += "SC6.D_E_L_E_T_ = ' ' "
		cQuery += "ORDER BY SC6.C6_FILIAL,SC6.C6_NUM,SC6.C6_CLI,SC6.C6_LOJA,SC6.C6_PRODUTO,SC6.C6_LOTECTL,"
		cQuery += "SC6.C6_NUMLOTE,SC6.C6_DTVALID"
				
		cQuery := ChangeQuery(cQuery)
    	
		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSC6,.T.,.T.)

		For nSC6 := 1 To Len(aStruSC6)
			If aStruSC6[nSC6][2] <> "C" .and.  FieldPos(aStruSC6[nSC6][1]) > 0
				TcSetField(cAliasSC6,aStruSC6[nSC6][1],aStruSC6[nSC6][2],aStruSC6[nSC6][3],aStruSC6[nSC6][4])
			EndIf
		Next nSC6
	Else
#ENDIF	         
		dbSelectArea(cString)
		cIndexSC6  := CriaTrab(nil,.f.)
		cKey :="C6_FILIAL+C6_NUM+C6_CLI+C6_LOJA+C6_PRODUTO+C6_LOTECTL+C6_NUMLOTE+DTOS(C6_DTVALID)"		
		cFilter := "C6_FILIAL = '" + xFilial("SC6") + "' .And. "
		cFilter += "(C6_QTDVEN > 0) .And. "
		cFilter += "C6_NUM >= '"+mv_par01+"' .And. " 
		cFilter += "C6_NUM <= '"+mv_par02+"'" 

		IndRegua(cAliasSC6,cIndexSC6,cKey,,cFilter,"Selecionando Registros...")	   
		#IFNDEF TOP
			DbSetIndex(cIndexSC6+OrdBagExt())
		#ENDIF                           
		SetRegua(RecCount())		// Total de Elementos da regua
		DbGoTop()
		
#IFDEF TOP
	Endif
#ENDIF	

While (cAliasSC6)->(!Eof())
	//	Ŀ
	//	 Valida o produto conforme a mascara         
	//	
	lRet:=ValidMasc((cAliasSC6)->C6_PRODUTO,MV_PAR03)
	If lRet .and. !Empty(aReturn[7])    
		lRet := &(aReturn[7])
	Endif
	If !lRet .and. !lSkip   
		MsgAlert("Mscara do produto no encontrada, processo finalizado.")   
		dbCloseArea() 
	   	Return(.F.)
	EndIf	
	If lRet
		IF lEnd
			@PROW()+1,001 Psay "CANCELADO PELO OPERADOR"
			lContinua := .F.
			Exit
		Endif 
		If !lQuery
			IncRegua()
		EndIf	
		IF li > 55 .or. lFirst
			lFirst  := .f.
			cabec(titulo,cabec1,cabec2,nomeprog,tamanho,15)
			lRodape := .T.
		Endif
		dbSelectArea("SB1")
		dbSeek(xFilial("SB1") + (cAliasSC6)->C6_PRODUTO)
		dbSelectArea("SB2")
		dbSeek(xFilial("SB2") + (cAliasSC6)->C6_PRODUTO + (cAliasSC6)->C6_LOCAL )
		cCodProd := (cAliasSC6)->C6_PRODUTO
		nQtdIt   := (cAliasSC6)->C6_QTDVEN
		cDescProd:= Subs(SB1->B1_DESC,1,30)
		cGrade   := (cAliasSC6)->C6_GRADE
		cUnidade := SB1->B1_UM		             
		cLocaliza:= SB2->B2_LOCALIZ                      
		cLote	 := (cAliasSC6)->C6_LOTECTL
		cLocal 	 := (cAliasSC6)->C6_LOCAL                
		cSubLote := (cAliasSC6)->C6_NUMLOTE              
		dDtValid := (cAliasSC6)->C6_DTVALID
		nPotencia:= (cAliasSC6)->C6_POTENCI
		cNPedido := (cAliasSC6)->C6_NUM
		lSkip := .F.
		lAglutGrad := (!lPyme .And. cGrade == "S" .and. MV_PAR04 == 1) 
		If lAglutGrad
			cProdRef 	:=Substr(cCodProd,1,nTamRef)
			SB4->(DbSeek(xFilial("SB4") + cProdRef))
			cDescProd:= Subs(SB4->B4_DESC,1,30)
		Endif
		If MV_PAR04 == 1
			nTotQuant := 0
			While (cAliasSC6)->(!Eof()) .And.;
				If(lAglutGrad,(cProdRef == Substr((cAliasSC6)->C6_PRODUTO,1,nTamRef)),cCodProd == (cAliasSC6)->C6_PRODUTO) .And.;
				(cLote == (cAliasSC6)->C6_LOTECTL .And. cSubLote == (cAliasSC6)->C6_NUMLOTE) .And. ;
				(cAliasSC6)->C6_NUM == cNPedido 
				
				nTotQuant += (cAliasSC6)->C6_QTDVEN
				(cAliasSC6)->(dbSkip())
				lSkip := .T.
			Enddo
		Else
			IF !lPyme .And. cGrade == "S" .and. MV_PAR04 == 1
				cProdRef 	:=Substr(cCodProd,1,nTamRef)
				nTotQuant	:=0
				While (cAliasSC6)->(!Eof()) .And. cProdRef == Substr((cAliasSC6)->C6_PRODUTO,1,nTamRef) .And. (cAliasSC6)->C6_GRADE == "S" .And.;
					(cLote == (cAliasSC6)->C6_LOTECTL .And. cSubLote == (cAliasSC6)->C6_NUMLOTE) .And. ;
					(cAliasSC6)->C6_NUM == cNPedido 
					nTotQuant += (cAliasSC6)->C6_QTDVEN
					(cAliasSC6)->(dbSkip())
					lSkip := .T.
				End
			Endif
		Endif
		@ li, 00 Psay IIF(lAglutGrad ,Substr(cCodProd,1,ntamref),cCodProd)  Picture "@!"
		@ li, 16 Psay cDescProd	Picture "@!"
		@ li, 47 Psay cUnidade Picture "@!"
		@ li, 50 Psay IIF(lAglutGrad .Or. MV_PAR04 == 1,nTotQuant,nQtdIt) Picture "@E 999,999.99"
		@ li, 62 Psay cLocal
		If !lPyme
			@ li, 66 Psay cLocaliza
			@ li, 81 Psay cLote	Picture "@!"
			@ li, 91 Psay cSubLote	Picture "@!"
			@ li,101 Psay dDtValid	Picture PesqPict("SC6","C6_DTVALID")
			@ li,113 PSay nPotencia Picture PesqPict("SC6","C6_POTENCI")
			@ li,123 Psay cNPedido Picture "@!"
		Else
			@ li,66 Psay cNPedido Picture "@!"
		EndIf	
		
		li++
	EndIf

	dbSelectArea(cAliasSC6)
	If !lSkip	
		dbSkip()
	EndIf	
	
End

IF lRodape
	roda(cbcont,cbtxt,"M")
Endif

If lQuery   
    dbSelectArea(cAliasSC6)
	dbCloseArea()  
	dbSelectArea("SC6")
Else
	RetIndex("SC6")   
	Ferase(cIndexSC6+OrdBagExt())
	dbSelectArea("SC6")
	dbClearFilter()
	dbSetOrder(1)
	dbGotop()
Endif	

If aReturn[5] = 1
	Set Printer TO
	dbCommitAll()
	OurSpool(wnrel)
Endif

MS_FLUSH()

/*


ͻ
Programa  AjustaSX1 Autor  Aline Catarina       Data 30/01/07     
͹
Uso        HCIR016                                                    
ͼ


*/
Static Function AjustaSX1(cPerg)        
DbSelectArea("SX1")                                  
SX1->(DbSetOrder(1)) // x1_grupo 
If !SX1->(DbSeek(cPerg))                                                      
	PutSx1(cPerg, "01", "Do pedido            ?", "", "", "mv_ch1", "C",06,0,0,"G","","SC5","",  "", "mv_par01",    "", "", "",          "      ",    "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "","SC5", "", "", "", "", "", "", "", "")
	PutSx1(cPerg, "02", "At pedido           ?", "", "", "mv_ch2", "C",06,0,0,"G","","SC5","",  "", "mv_par02",    "", "", "",          "ZZZZZZ",    "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "","SC5", "", "", "", "", "", "", "", "")
	PutSx1(cPerg, "03", "Mscara do Produto   ?", "", "", "mv_ch3", "C",15,0,0,"G","",   "","", "S", "mv_par03",    "", "", "", "***************",    "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "",   "", "", "", "", "", "", "", "", "")
	PutSX1(cPerg, "04", "Aglutinar prod.Grade ?", "", "", "mv_ch4", "N",01,0,2,"C","",   "","", "S", "mv_par04", "Sim", "", "",                "", "No", "", "")
EndIf
Return
