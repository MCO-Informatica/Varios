#INCLUDE "PROTHEUS.CH" 

User Function VNDA390(cAnexo, cFileName, cDesc , cAliasArq, cCodReg )

Local aArea		:= GetArea()
Local nPos 		:= RAt(If(IsSrvUnix(), "/", "\"), cFileName)
Local nPos2		:= 0
Local cDirDoc	:= MsDocPath()
Local cAnexoGrv	:= Upper(SubStr( cFileName , nPos+1 ))
Local nCount	:= 1

//?????????????????????????????????????????????????????????????????????????????
//?Busca por um nome de arquivo nao utilizado, incrementando o arquivo        ?
//?com um sequencial ao final do nome, exemplo: arquivo(1).txt, arquivo(2).txt?
//?????????????????????????????????????????????????????????????????????????????
DbSelectArea("ACB")
DbSetOrder(2) //ACB_FILIAL+ACB_OBJETO

While DbSeek(xFilial("ACB") + AllTrim(cAnexoGrv ))
	nPos2		:= Rat(".",cAnexoGrv)
	cAnexoGrv	:= SubStr(cAnexoGrv,1,nPos2-1)+"("+cValToChar(nCount)+")"+SubStr(cAnexoGrv,nPos2,Len(cAnexoGrv))
	nCount++
End

__CopyFile(cAnexo, cDirDoc + "\" + cAnexoGrv )

//??????????????????????????????????????????
//?Inclui registro no banco de conhecimento?
//??????????????????????????????????????????
RecLock("ACB",.T.)
ACB->ACB_FILIAL := xFilial("ACB")
ACB->ACB_CODOBJ := GetSxeNum("ACB","ACB_CODOBJ")
ACB->ACB_OBJETO	:= Upper(cAnexoGrv)
ACB->ACB_DESCRI	:= cDesc 
MsUnLock()

ConfirmSx8()

//?????????????????????????????????????????????????????
//?Inclui amarra??o entre registro do banco e entidade?
//?????????????????????????????????????????????????????
RecLock("AC9",.T.)
AC9->AC9_FILIAL	:= xFilial("AC9")
AC9->AC9_FILENT	:= xFilial(cAliasArq)
AC9->AC9_ENTIDA	:= cAliasArq
AC9->AC9_CODENT	:= xFilial(cAliasArq)+cCodReg
AC9->AC9_CODOBJ	:= ACB->ACB_CODOBJ
MsUnLock()

RestArea(aArea)

Return .T.