#include "Protheus.ch"
#include "ap5mail.ch"
#define _EOL chr(13) + chr(10)

/*
?????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????????
????????????????????????????????????????????????????????????????????????????
???Programa  ?M440STTS  ?Autor  ?Opvs (David)        ? Data ?  23/08/10   ???
????????????????????????????????????????????????????????????????????????????
???Desc.     ?Ponto de Entrada para envio de e-mail na liberacao de       ???
???          ?Pedidos de Vendas                                           ???
????????????????????????????????????????????????????????????????????????????
???Uso       ? AP                                                         ???
????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????????
*/

User Function M440STTS()
Local cDest 	:= ""
Local cCopia	:= GetNewPar("TLV_COPMAIL", "")
Local cAssunto	:= "" 
Local cMensagem	:= ""
Local cSql		:= ""

If !IsBlind() .and. !Empty(SC5->C5_ATDTLV)
	cSql := " SELECT U7_CODUSU "
	cSql += " FROM "+RetSqlName("SU7")
	cSql += " WHERE U7_FILIAL = '"+xFilial("SU7")+"' AND "
	cSql += " (U7_CODVEN = '"+SC5->C5_VEND1+"' "+iif(!Empty(SC5->C5_VEND2), "OR U7_CODVEN = '"+SC5->C5_VEND2+"')",")")+" AND "
	cSql += " D_E_L_E_T_ = ' ' "
	cSql += " GROUP BY U7_CODUSU "  
	
	If Select("TMPU7") > 0
		TMPU7->(DbCloseArea())				
	EndIf

	dbUseArea(.T.,"TOPCONN",TcGenQry(,,cSql),"TMPU7",.F.,.T.)				
	
	PswOrder(1) // ID do usuário.
	While !TMPU7->(EoF())
	    If PswSeek(TMPU7->U7_CODUSU)
	    	cDest += alltrim(PswRet(1)[1][14])+";"
	    EndIf
		TMPU7->(DbSKip())
	EndDo
	TMPU7->(DbCloseArea())
	
	cDest 		:= SubStr(cDest,1,Len(cDest)-1)
	cAssunto	:= "[LIBERADO PEDIDO DE VENDA] CÓDIGO["+SC5->C5_NUM+"] CÓDIGO ATEND. TLV. ["+SC5->C5_ATDTLV+"] " 
	cMensagem	:= CRLF + CRLF 
	cMensagem	+= " STATUS PEDIDO : LIBERADO"+CRLF  
	cMensagem	+= " DATA LIBERAÇÃO : "+DtoC(dDataBase)+CRLF  	
	cMensagem	+= " CÓDIGO PEDIDO DE VENDA : "+SC5->C5_NUM+CRLF  
	cMensagem	+= " CÓDIGO ATENDIMENTO TELEVENDAS: "+SC5->C5_ATDTLV+CRLF  
	cMensagem	+= " CÓDIGO CLIENTE: "+SC5->C5_CLIENTE+"/"+SC5->C5_LOJACLI+CRLF  	
	cMensagem	+= " NOME CLIENTE: "+Posicione("SA1",1,xFilial("SA1")+SC5->(C5_CLIENTE+C5_LOJACLI),"A1_NOME")+CRLF  	
	cMensagem	+= " OBSERVAÇÕES: "+CRLF  	
	cMensagem	+= SC5->C5_XOBS+CRLF  	
	cMensagem	+= CRLF + CRLF + CRLF + CRLF
	cMensagem	+= "***MENSAGEM AUTOMÁTICA GERADA PELO SISTEMA ERP TOTVS PROTHEUS, FAVOR NÃO RESPONDER ESTE E-MAIL***"
	
	lRet := MandEmail(cMensagem, cDest, cAssunto, "", cCopia)	
	
	If !lRet
		MsgAlert("E-mail de aviso de Liberacao do Pedido nao foi enviado. Favor entrar em contato com Sistemas Corporativos.")
	Else
		MsgInfo("E-mail de Aviso de Liberacao do pedido enviado com Sucesso.")	
	Endif

Endif

Return

/*
?????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????????
????????????????????????????????????????????????????????????????????????????
???Programa  ?MANDAEMAIL?Autor  ?Opvs (David)        ? Data ?  13/09/10   ???
????????????????????????????????????????????????????????????????????????????
???Desc.     ?FUNCAO PARA ENVIAR MENSAGENS                                ???
???          ?                                                            ???
????????????????????????????????????????????????????????????????????????????
????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????????
*/
Static Function MandEmail(xCorpo, xDest, xAssunto, xAnexo, xCC, xBCC, xAcc)

Local   cAccount  := AllTrim(GetNewPar("MV_RELACNT"," ")) 
Local   cPassword := AllTrim(GetNewPar("MV_RELPSW" ," "))
Local   cServer   := AllTrim(GetNewPar("MV_RELSERV"," "))
Local   cUserAut  := Alltrim(GetMv("MV_RELAUSR",,"")) //Usu?rio para Autentica??o no Servidor de Email
Local   cPassAut  := Alltrim(GetMv("MV_RELAPSW",,""))//Senha para Autentica??o no Servidor de Email
Local   nTimeOut    := GetMv("MV_RELTIME",,120) //Tempo de Espera antes de abortar a Conex?o
Local   lAutentica  := GetMv("MV_RELAUTH",,.F.) //Determina se o Servidor de Email necessita de Autentica??o
Local   nI        := 1
Local   lRet      := .T. 

Default xDest     := ""
Default xCC		  := ""
Default xBCC      := ""
Default xCorpo	  := ""
Default xAnexo    := ""
Default xAssunto  := "<< Mensagem sem assunto >>"  

If Empty(xDest+xCC+xBCC)
//	ConOut("Nao Foram Econtrados endere?os para Enviar e-mail Mensagem: "+xAssunto)
	Return(lRet)
EndIf

_cMsg := "Conectando a " + cServer + _EOL +;
"Conta: " + cAccount + _EOL +;
"Senha: " + cPassword
//ConOut(_cMsg) 

CONNECT SMTP SERVER cServer ACCOUNT cAccount PASSWORD cPassword TIMEOUT nTimeOut Result lOk

If ( lOk )

    // Realiza autenticacao caso o servidor seja autenticado.
	If lAutentica
		If !MailAuth(cUserAut,cPassAut)
//			ConOut("Falha na Autentica??o do Usu?rio")
			DISCONNECT SMTP SERVER RESULT lOk
			IF !lOk
				GET MAIL ERROR cErrorMsg
//				ConOut("Erro na Desconex?o: "+cErrorMsg)
			ENDIF
			Return .F.
		EndIf
	EndIf

	SEND MAIL FROM cAccount TO xDest CC xCC BCC xBCC SUBJECT xAssunto BODY xCorpo ATTACHMENT xAnexo RESULT lOk

	If !lOk
		GET MAIL ERROR cErro
		cErro := "Erro durante o envio - destinatrio: " + xDest + _EOL + _EOL + cErro
//		conout(cErro)
		lRet:= .F.
	Endif
	
	DISCONNECT SMTP SERVER RESULT lOk
	If !lOk
		GET MAIL ERROR cErro
//		conout(cErro)
	Endif
Else
	GET MAIL ERROR cErro
//	conout(cErro)
	lRet:= .F.
EndIf

Return(lRet)
