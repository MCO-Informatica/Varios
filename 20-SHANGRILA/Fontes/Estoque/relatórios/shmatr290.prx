#INCLUDE "SHMATR290.CH"
#INCLUDE "PROTHEUS.CH"

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MATR290  ³ Autor ³ Ricardo Berti         ³ Data ³18.07.2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Relacao da Analise de Estoques                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SIGAEST                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
User Function SHMATR290()

Local oReport
Private cAliasSB1 := "SB1"
Private cAliasSB2 := "SB2"

If FindFunction("TRepInUse") .And. TRepInUse()
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Funcao utilizada para verificar a ultima versao dos fontes      ³
	//³ SIGACUS.PRW, SIGACUSA.PRX e SIGACUSB.PRX, aplicados no rpo do   |
	//| cliente, assim verificando a necessidade de uma atualizacao     |
	//| nestes fontes. NAO REMOVER !!!							        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	IF !(FindFunction("SIGACUS_V") .and. SIGACUS_V() >= 20050512)
	    Final(STR0023+"SIGACUS.PRW !!!") //"Atualizar "
	Endif
	IF !(FindFunction("SIGACUSA_V") .and. SIGACUSA_V() >= 20050512)
	    Final(STR0023+"SIGACUSA.PRX !!!") //"Atualizar "
	Endif
	IF !(FindFunction("SIGACUSB_V") .and. SIGACUSB_V() >= 20050512)
	    Final(STR0023+"SIGACUSB.PRX !!!") //"Atualizar "
	Endif
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Interface de impressao                                                  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oReport := ReportDef()
	oReport:PrintDialog()
Else
	MATR290R3()
EndIf

Return Nil


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³ReportDef ³ Autor ³ Ricardo Berti 		³ Data ³18.07.2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³A funcao estatica ReportDef devera ser criada para todos os ³±±
±±³          ³relatorios que poderao ser agendados pelo usuario.          ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpO1: Objeto do relatorio                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³MATR290                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ReportDef()

Local lVEIC		:= UPPER(GETMV("MV_VEICULO"))=="S"  // Se = "S" Nao ha' ordem por Consumo
Local aOrdem	:= {STR0005,STR0006,STR0007,STR0008}  //" Por Codigo         "###" Por Tipo           "###" Por Descricao     "###" Por Grupo         "
Local cAliasTOP := "SB1"
Local cAliasSB3 := "SB3"
Local cPerg		:= PadR("MTR290",LEn(SX1->X1_GRUPO))
Local oReport
Local oCell
Local oSection1
Local aSB1Cod	:= TAMSX3("B1_COD")
Local aSB1Ite	:= TAMSX3("B1_CODITE")
Local lQuery	:= .f.
Local nSaldoPro := 0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ajustar o SX1 para SIGAVEI, SIGAPEC e SIGAOFI                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
AjustaSX1(cPerg,lVeic,aSB1Cod,aSB1Ite)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao do componente de impressao                                      ³
//³                                                                        ³
//³TReport():New                                                           ³
//³ExpC1 : Nome do relatorio                                               ³
//³ExpC2 : Titulo                                                          ³
//³ExpC3 : Pergunte                                                        ³
//³ExpB4 : Bloco de codigo que sera executado na confirmacao da impressao  ³
//³ExpC5 : Descricao                                                       ³
//³                                                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !lVeic
	Aadd(aOrdem,STR0020)  //" Por Consumo      "
EndIf

oReport := TReport():New("MATR290",STR0001,cPerg, {|oReport| ReportPrint(oReport,lVeic,@lQuery,@cAliasTOP,@cAliasSB3,@nSaldoPro)},STR0002+" "+STR0003+" "+STR0004) //"Relacao para Analise dos Estoques"##"Este relatorio demonstra a situacao de cada item em relacao ao"###"seu saldo , seu empenho , suas entradas previstas e sua classe"###"ABC."
oReport:SetLandScape()    
oReport:SetTotalInLine(.F.)
oReport:SetTotalText(STR0016) //"T o t a l   G e r a l :"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica as perguntas selecionadas                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Pergunte(cPerg,.F.)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para parametros                         ³
//³ mv_par01     // Produto de                                   ³
//³ mv_par02     // Produto ate                                  ³
//³ mv_par03     // tipo de                                      ³
//³ mv_par04     // tipo ate                                     ³
//³ mv_par05     // grupo de                                     ³
//³ mv_par06     // grupo ate                                    ³
//³ mv_par07     // descricao de                                 ³
//³ mv_par08     // descricao ate                                ³
//³ mv_par09     // Tudo ou so a Comprar                         ³
//³ mv_par10     // Subtrai empenho para Calculo Saldo           ³
//³ mv_par11     // Lista apenas itens obsoletos                 ³
//³ mv_par12     // data limite p/itens obsoletos                ³
//³ mv_par13     // Cons. Saldo do Almox. Processo ?  Sim , Nao  ³
//³ mv_par14     // Desconsidera P.V. que N Atu.Est.? Sim , Nao  ³
//³ mv_par15     // Lista produto com Saldo zerado  ? Sim , Nao  ³
//³ mv_par16     // Considera OPs 1- Firmes 2- Previstas 3- Ambas³
//³ mv_par17     // Almoxarifado De  ?                           ³
//³ mv_par18     // Almoxarifado Ate ?                           ³
//³ mv_par19     // Consid Residuos PV ?  Sim, Nao               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao da secao utilizada pelo relatorio                               ³
//³                                                                        ³
//³TRSection():New                                                         ³
//³ExpO1 : Objeto TReport que a secao pertence                             ³
//³ExpC2 : Descricao da seçao                                              ³
//³ExpA3 : Array com as tabelas utilizadas pela secao. A primeira tabela   ³
//³        sera considerada como principal para a seção.                   ³
//³ExpA4 : Array com as Ordens do relatório                                ³
//³ExpL5 : Carrega campos do SX3 como celulas                              ³
//³        Default : False                                                 ³
//³ExpL6 : Carrega ordens do Sindex                                        ³
//³        Default : False                                                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oSection1 := TRSection():New(oReport,STR0034,{"SB1","SB3"},aOrdem,/*Campos do SX3*/,/*Campos do SIX*/)
oSection1:SetHeaderPage()

TRCell():New(oSection1,"B1_COD","SB1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,"B1_CODITE","SB1")
TRCell():New(oSection1,"B1_DESC","SB1")
TRCell():New(oSection1,"B1_TIPO","SB1")
TRCell():New(oSection1,"B1_GRUPO","SB1")
TRCell():New(oSection1,"B1_UM","SB1")
TRCell():New(oSection1,'B2_QATU' 	,'SB2')
TRCell():New(oSection1,'B2_QEMP' 	,'SB2')
TRCell():New(oSection1,'C1_QUANT' 	,'SC1',STR0024+CRLF+STR0025) //"SC's"###"Colocadas"
TRCell():New(oSection1,'C7_QUANT' 	,'SC7',STR0026+CRLF+STR0027) //"PC's"###"Colocados"
TRCell():New(oSection1,'C2_QUANT' 	,'SC2',STR0028+CRLF+STR0025) //"OP's"###"Colocadas"
TRCell():New(oSection1,'C6_QTDVEN' 	,'SC6',STR0029+CRLF+STR0027) //"PV's"###"Colocados"
TRCell():New(oSection1,'B1_EMIN' 	,'SB1',,,,,{|| RetFldProd((cAliasTop)->B1_COD,"B1_EMIN",If(lQuery,cAliasTop,)) })
TRCell():New(oSection1,'B1_LE' 	    ,'SB1',,,,,{|| RetFldProd((cAliasTop)->B1_COD,"B1_LE",If(lQuery,cAliasTop,))   })
TRCell():New(oSection1,'B1_PE' 	    ,'SB1',,,,,{|| CalcPrazo((cAliasTop)->B1_COD,RetFldProd((cAliasTop)->B1_COD,"B1_LE",If(lQuery,cAliasTop,))) })
TRCell():New(oSection1,'B1_TIPE'	,'SB1'," ",,,,{|| RetFldProd((cAliasTop)->B1_COD,"B1_TIPE",If(lQuery,cAliasTop,)) })
TRCell():New(oSection1,'ESTMES'		,'   ',STR0030+CRLF+STR0031,,,,{|| If((cAliasSB3)->B3_MEDIA != 0,Transform(nSaldoPro/(cAliasSB3)->B3_MEDIA,"99999"),STR0032) }) //"Estoque"###"em Meses"###"  N/D"
TRCell():New(oSection1,'B3_MEDIA' 	,'SB3')
TRCell():New(oSection1,'B2_USAI' 	,'SB2')
TRCell():New(oSection1,'B3_CLASSE' 	,'SB3',STR0033) //"CL"

Return(oReport)


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³ReportPrin³ Autor ³ Ricardo Berti         ³ Data ³18.07.2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³A funcao estatica ReportDef devera ser criada para todos os ³±±
±±³          ³relatorios que poderao ser agendados pelo usuario.          ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpO1: Objeto Report do Relatorio                           ³±±
±±³          ³ExpL1: Se .T. = gestao de Concessionarias(MV_VEICULO ="S")  ³±±
±±³          ³ExpL2: Se .T. = base TOP 									  ³±±
±±³          ³ExpC1: Alias do arquivo SB1								  ³±±
±±³          ³ExpC2: Alias do arquivo SB3								  ³±±
±±³          ³ExpN1: var.calculada do saldo em estoque					  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³MATR290                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ReportPrint(oReport,lVeic,lQuery,cAliasTOP,cAliasSB3,nSaldoPro)

Local XSB1, XSB2, XSB3, XSC1, XSC2, XSC6, XSC7, XSC9, XSF4
Local oSection1	:= oReport:Section(1)
Local cOrdem
Local nOrdem    := oReport:Section(1):GetOrder()
Local oBreak
Local nEmpPro,nSCPro,nPCPro,nOPPro,nPVPro
Local dUsai,nSaldo,nEmin,lT
Local cLocProc := GETMV("MV_LOCPROC")
Local cFiltro,cCondicao
Local cArqTmp	:= ""
Local cChave	:= ""
#IFNDEF TOP
	Local cCondSB1
#ELSE
  	Local cOrderBy
	Local cWhere
	lQuery		:= .T.
#ENDIF
Private cVar ,cCondSec

If nOrdem == 1
	cOrdem := STR0005
ElseIf nOrdem == 2
	cOrdem := STR0006
ElseIf nOrdem == 3
	cOrdem := STR0007
ElseIf nOrdem == 4
	cOrdem := STR0008
ElseIf nOrdem == 5
	cOrdem := STR0020
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Adiciona a ordem escolhida ao titulo do relatorio          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oReport:SetTitle(oReport:Title()+" ("+AllTrim(cOrdem)+")" )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Inicializa os Arquivos e Ordens a serem utilizados           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SF4")
dbSetOrder(1)
XSF4	:= XFILIAL("SF4")

dbSelectArea("SC9")
dbSetOrder(1)
XSC9	:= XFILIAL("SC9")

dbSelectArea("SC7")
dbSetOrder(1)
XSC7	:= XFILIAL("SC7")

dbSelectArea("SC6")
dbSetOrder(1)
XSC6	:= XFILIAL("SC6")

dbSelectArea("SC2")
dbSetOrder(1)
XSC2	:= XFILIAL("SC2")

dbSelectArea("SC1")
dbSetOrder(1)
XSC1	:= XFILIAL("SC1")

dbSelectArea("SB3")
dbSetOrder(1)
XSB3	:= XFILIAL("SB3")

dbSelectArea("SB2")
dbSetOrder(1)
XSB2	:= XFILIAL("SB2")

dbSelectArea("SB1")
dbSetOrder(1)
XSB1	:= XFILIAL("SB1")

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³	Visualizacao de colunas conforme parametrizacao				  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lVeic
	oSection1:Cell('B1_COD'):Disable()
	oSection1:Cell('C2_QUANT'):Disable()
Else
	oSection1:Cell('B1_CODITE'):Disable()
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Filtragem do relatorio                                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lQuery
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Transforma parametros Range em expressao SQL                            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	MakeSqlExpr(oReport:GetParam())

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Query do relatorio                                                      ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cCondicao := "!Eof()"
	cWhere := "%"
	If lVeic
		cWhere += " AND SB1.B1_CODITE >= '" + mv_par01 + "' AND SB1.B1_CODITE <='" + mv_par02 + "' "
	Else
		cWhere += " AND SB1.B1_COD    >= '" + mv_par01 + "' AND SB1.B1_COD    <='" + mv_par02 + "' "
	EndIf
	cWhere += "%"

	cOrderBy := "%"
	If ! lVEIC
		If nOrdem == 5
			cOrderBy += " B3_MEDIA"
		ElseIf nOrdem == 4
			cOrderBy += " B1_GRUPO, B1_COD"   // Por Grupo, Codigo
		ElseIf nOrdem == 3
			cOrderBy += " B1_DESC, B1_COD"   // Por Descricao, Codigo
		ElseIf nOrdem == 2
			cOrderBy += " B1_TIPO, B1_COD"   // Por Tipo, Codigo
		Else
			cOrderBy += " B1_COD"      // Por Codigo
		EndIf
	Else
		If nOrdem == 4
			cOrderBy += " B1_GRUPO, B1_CODITE"   // Por Grupo, Codite
		ElseIf nOrdem == 3
			cOrderBy += " B1_DESC, B1_CODITE"   // Por Descricao, Codite
		ElseIf nOrdem == 2
			cOrderBy += " B1_TIPO, B1_CODITE"   // Por Tipo, Codite
		Else
			cOrderBy += " B1_CODITE"      // Por Codite
		Endif
	EndIf
	cOrderBy += "%"
	
	oReport:Section(1):BeginQuery()	
	
	cAliasTOP := GetNextAlias()
	cAliasSB3 := cAliasTOP

	If nOrdem <> 5
	
		BeginSql Alias cAliasTOP

		SELECT B1_FILIAL,B1_COD,B1_CODITE,B1_DESC,B1_TIPO,B1_GRUPO,B1_UM,B1_EMIN,B1_LE,B1_TIPE,
	           B3_FILIAL,B3_COD,B3_MEDIA,B3_CLASSE

		FROM %table:SB1% SB1
		  LEFT JOIN %table:SB3% SB3
		  ON	B3_FILIAL = %xFilial:SB3% AND
				B3_COD = B1_COD AND
				SB3.%NotDel%

	 	  WHERE	B1_FILIAL  = %xFilial:SB1% AND
				B1_TIPO   >= %Exp:mv_par03% AND 
				B1_TIPO   <= %Exp:mv_par04% AND 
				B1_GRUPO  >= %Exp:mv_par05% AND
				B1_GRUPO  <= %Exp:mv_par06% AND
				B1_DESC   >= %Exp:mv_par07% AND
				B1_DESC   <= %Exp:mv_par08% AND
				SB1.%NotDel%
				%Exp:cWhere%

	 		ORDER BY %Exp:cOrderBy%
			
		EndSql 

	Else
		BeginSql Alias cAliasTOP

		SELECT B1_FILIAL,B1_COD,B1_CODITE,B1_DESC,B1_TIPO,B1_GRUPO,B1_UM,B1_EMIN,B1_LE,B1_TIPE,
	           B3_FILIAL,B3_COD,B3_MEDIA,B3_CLASSE

		FROM %table:SB1% SB1
		  RIGHT JOIN %table:SB3% SB3
		  ON	B3_FILIAL = %xFilial:SB3% AND
				B3_COD = B1_COD AND
				SB3.%NotDel%

	 	  WHERE	B1_FILIAL  = %xFilial:SB1% AND
				B1_TIPO   >= %Exp:mv_par03% AND 
				B1_TIPO   <= %Exp:mv_par04% AND 
				B1_GRUPO  >= %Exp:mv_par05% AND
				B1_GRUPO  <= %Exp:mv_par06% AND
				B1_DESC   >= %Exp:mv_par07% AND
				B1_DESC   <= %Exp:mv_par08% AND
				SB1.%NotDel%
				%Exp:cWhere%

	 		ORDER BY %Exp:cOrderBy%
			
		EndSql 

	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Metodo EndQuery ( Classe TRSection )                                    ³
	//³                                                                        ³
	//³Prepara o relatorio para executar o Embedded SQL.                       ³
	//³                                                                        ³
	//³ExpA1 : Array com os parametros do tipo Range                           ³
	//³                                                                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oReport:Section(1):EndQuery(/*Array com os parametros do tipo Range*/)

Else

	dbSelectArea("SB1")
	Set Softseek On
	If nOrdem == 5
		dbSetOrder(1)
	Else
		dbSetOrder(nOrdem)
	EndIf
	cIndex := Indexkey()
	
	If lVeic
		cCondSB1 := "B1_CODITE >= mv_par01 .And. B1_CODITE <= mv_par02"
	Else
		cCondSB1 := "B1_COD    >= mv_par01 .And. B1_COD    <= mv_par02"
	EndIf
	cCondSB1 += ".And. B1_TIPO  >= mv_par03 .And. B1_TIPO  <= mv_par04"
	cCondSB1 += ".And. B1_GRUPO >= mv_par05 .And. B1_GRUPO <= mv_par06"
	cCondSB1 += ".And. B1_DESC  >= mv_par07 .And. B1_DESC  <= mv_par08"

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Transforma parametros Range em expressao ADVPL                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	MakeAdvplExpr(oReport:GetParam())
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Condicao de Filtragem do SB1                                 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ 
	oSection1:SetFilter(cCondSB1,cIndex)

	if ! lVEIC
		If nOrdem == 4
			Seek xSB1 + mv_par05
			cCondicao := "!Eof() .And. B1_GRUPO <= mv_par06"
		ElseIf nOrdem == 3
			Seek xSB1 + mv_par07
			cCondicao := "!Eof() .And. B1_DESC <= mv_par08"
		ElseIf nOrdem == 2
			Seek xSB1 + mv_par03
			cCondicao := "!Eof() .And. B1_TIPO <= mv_par04"
		Else
			Seek xSB1 + mv_par01
			cCondicao := "!Eof() .And. B1_COD <= mv_par02"
		Endif
	else
		If nOrdem == 4
			dbSetOrder( 7 ) // B1_FILIAL+B1_GRUPO+B1_CODITE
			Seek xSB1 + mv_par05
			cCondicao := "!Eof() .And. B1_GRUPO <= mv_par06"
		ElseIf nOrdem == 3
			Seek xSB1 + mv_par07
			cCondicao := "!Eof() .And. B1_DESC <= mv_par08"
		ElseIf nOrdem == 2 // por tipo
			cFiltro   :=      "B1_TIPO>='" + mv_par03 + "'" 
			cFiltro   += ".And.B1_TIPO<='" + mv_par04 + "'"
			cFiltro   += ".and.B1_FILIAL=='" + xSB1 + "'"
			cArqTmp   := CriaTrab('',.F.)
			IndRegua('SB1',cArqTmp,'B1_FILIAL+B1_TIPO+B1_CODITE',,cFiltro,STR0021)    //"Selecionando Registros"
			nIndr	:=	RetIndex("SB1")
			dbSetIndex(cArqTmp+OrdBagExt())
			dbSetOrder(nIndr + 1)
			dbSeek(xSB1 + mv_par03, .T.)
			cCondicao := "!Eof() .And. B1_TIPO <= mv_par04"
		Else
			cFiltro   :=      "B1_CODITE>='" + mv_par01 + "'"
			cFiltro   += ".And.B1_CODITE<='" + mv_par02 + "'"
			cFiltro   += ".and.B1_FILIAL=='" + xSB1 + "'"
			cArqTmp   := CriaTrab('',.F.)
			IndRegua('SB1',cArqTmp,'B1_FILIAL+B1_CODITE',,cFiltro,STR0021)    //"Selecionando Registros"
			nIndr	:=	RetIndex("SB1")
			dbSetIndex(cArqTmp+OrdBagExt())
			dbSetOrder(nIndr + 1)
			dbSeek(xSB1 + mv_par01, .T.)
			cCondicao := "!Eof() .And. B1_CODITE <= mv_par02"
		Endif
	endif
	Set SoftSeek Off
	
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Definindo a quebra do relatorio³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If nOrdem == 2
	oBreak := TRBreak():New(oSection1,oSection1:Cell('B1_TIPO'),STR0014,.F.) //"T o t a l  d o   T i p o : "
ElseIf nOrdem == 4
	oBreak := TRBreak():New(oSection1,oSection1:Cell('B1_GRUPO'),STR0015,.F.) //"T o t a l   d o   G r u p o : "
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Definindo os totalizadores do relatorio³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
TRFunction():New(oSection1:Cell('B2_QATU'  ),NIL,"SUM",oBreak,NIL,/*Picture*/,/*uFormula*/,.F.,.T.)
TRFunction():New(oSection1:Cell('B2_QEMP'  ),NIL,"SUM",oBreak,NIL,/*Picture*/,/*uFormula*/,.F.,.T.)
TRFunction():New(oSection1:Cell('C1_QUANT' ),NIL,"SUM",oBreak,NIL,/*Picture*/,/*uFormula*/,.F.,.T.)
TRFunction():New(oSection1:Cell('C7_QUANT' ),NIL,"SUM",oBreak,NIL,/*Picture*/,/*uFormula*/,.F.,.T.)
TRFunction():New(oSection1:Cell('C2_QUANT' ),NIL,"SUM",oBreak,NIL,/*Picture*/,/*uFormula*/,.F.,.T.)
TRFunction():New(oSection1:Cell('C6_QTDVEN'),NIL,"SUM",oBreak,NIL,/*Picture*/,/*uFormula*/,.F.,.T.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Posiciona os indices dos arquivos consultados                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SC1")
dbSetOrder(2)

dbSelectArea("SC2")
dbSetOrder(2)

dbSelectArea("SC6")
dbSetOrder(2)

dbSelectArea("SC7")
dbSetOrder(2)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Inicio da impressao do fluxo do relatorio                               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oReport:SetMeter( SB1->(LastRec()) )

If nOrdem == 5 .And. !lQuery
	dbSelectArea("SB3")
	cCondicao := " SB3->(!Eof())"
	cFiltro   :=      "B3_COD>='" + mv_par01 + "'"
	cFiltro   += ".And.B3_COD<='" + mv_par02 + "'"
	cFiltro   += ".and.B3_FILIAL=='" + xSB3 + "'"
	cArqTmp   := CriaTrab('',.F.)
	IndRegua('SB3',cArqTmp,'STR(B3_MEDIA)',,cFiltro,STR0021)    //"Selecionando Registros"
	nIndSb3:=RetIndex("SB3")
	dbSetIndex(cArqTmp+OrdBagExt())
	dbSetOrder(nIndSb3 + 1)
	dbGotop()
Else
	dbSelectArea(cAliasTOP)
	cCondicao += ".and.B1_FILIAL=='" + xSB1 + "'"
Endif

oSection1:Init()
While !oReport:Cancel() .And. &(cCondicao)
	cCondSec := "B1_FILIAL=='" + xSB1 + "' .And. "
	if ! lVEIC
		If nOrdem == 5 .And. !lQuery
	  		dbSelectArea("SB1")
	  		dbSetOrder(1)
			dbSeek(xSB1 + SB3->B3_COD)
			cCondSec += "B1_COD == SB3->B3_COD"
		ElseIf nOrdem == 4
			cVar     := B1_GRUPO
			cCondSec += "B1_GRUPO == cVar"
		ElseIf nOrdem == 3
			cVar     := B1_DESC
			cCondSec += "B1_DESC == cVar"
		ElseIf nOrdem == 2
			cVar     := B1_TIPO
			cCondSec += "B1_TIPO == cVar"
		Else
			cVar     := B1_COD
			cCondSec += "B1_COD == cVar"
		Endif
	ELSE
		If nOrdem == 4
			cVar     := B1_GRUPO
			cCondSec += "B1_GRUPO == cVar"
		ElseIf nOrdem == 3
			cVar     := B1_DESC
			cCondSec += "B1_DESC == cVar"
		ElseIf nOrdem == 2
			cVar     := B1_TIPO
			cCondSec += "B1_TIPO == cVar"
		Else
			cVar     := B1_CODITE
			cCondSec += "B1_CODITE == cVar"
		Endif
	ENDIF

	DO While !oReport:Cancel() .And. &(cCondicao) .And. &(cCondSec)

		oReport:IncMeter()

		lT := .F.
		If ! lVEIC
			If B1_COD < mv_par01 .Or. B1_COD > mv_par02
	   			lT := .T.
	        endif
		Else
			if B1_CODITE < mv_par01 .Or. B1_CODITE > mv_par02
	   			lT := .T.
			endif
		EndIf
		If lT .Or. B1_TIPO < mv_par03 .Or. B1_TIPO > mv_par04 .Or.;
			   B1_GRUPO < mv_par05 .Or. B1_GRUPO > mv_par06 .Or.;
			   B1_DESC < mv_par07 .Or. B1_DESC > mv_par08
			dbSkip()
			Loop
		EndIf
		
		Store 0 To nSaldoPro,nEmpPro,nSCPro,nPCPro,nOPPro,nPVPro
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Soma os saldos iniciais e os empenhos do SB2                 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea("SB2")
		Seek xSB2 + (cAliasTop)->B1_COD
		dUsai := B2_USAI
		Do While !oReport:Cancel() .And. !Eof() .And. B2_FILIAL + B2_COD == xSB2 + (cAliasTop)->B1_COD
			
			IF B2_LOCAL == cLocProc .and. mv_par13 == 2
				dbSkip()
				Loop
			Endif

			If B2_LOCAL < mv_par17 .Or. B2_LOCAL > mv_par18
				dbSkip()
				Loop
			EndIf

			nSaldoPro += B2_QATU
			nEmpPro   += If(mv_par16==1,B2_QEMP,If(mv_par16==2,B2_QEMPPRE,B2_QEMP+B2_QEMPPRE))
			nEmpPro   += If(mv_par20==1,B2_QEMPPRJ,0)
			nEmpPro   += B2_RESERVA
			If dUsai < B2_USAI
				dUsai := B2_USAI
			EndIf
			dbSkip()
			
		EndDo
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica se o relatorio foi interrompido pelo usuario        ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If oReport:Cancel()
			Exit
		EndIf
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica se deve subtrair empenho do saldo atual             ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If mv_par10 == 1
			nSaldoPro -= nEmpPro
		EndIf
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica se deve listar apenas os obsoletos                  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If mv_par11 == 1 .And. (Empty(dUsai) .Or. dUsai > mv_par12)
			dbSelectArea(cAliasTop)
			dbSkip()
			Loop
		EndIf
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Posiciona-se no arquivo de Demandas para pegar dados         ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If nOrdem <> 5 .And. !lQuery
			dbSelectArea("SB3")
			dbSeek(xSB3 + (cAliasTop)->B1_COD)
		EndIf
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Aglutina as Solicitacos de Compra sem pedido colocado        ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea("SC1")
		Seek xSC1 + (cAliasTop)->B1_COD
		Do While !oReport:Cancel() .And. !Eof() .And. C1_FILIAL + C1_PRODUTO == xSC1 + (cAliasTop)->B1_COD
			
			If C1_LOCAL < mv_par17 .Or. C1_LOCAL > mv_par18
				dbSkip()
				Loop
			EndIf

			If C1_QUJE < C1_QUANT .And. MtrAvalOp(mv_par16,"SC1")
				nSCPro += (C1_QUANT - C1_QUJE)
			EndIf
			dbSkip()
			
		EndDo
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica se o relatorio foi interrompido pelo usuario        ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If oReport:Cancel()
			Exit
		EndIf

		if ! lVEIC
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Aglutina as Ordens de Producao em aberto                     ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			dbSelectArea("SC2")
			dbSeek(xSC2 + (cAliasTop)->B1_COD)
			
			Do While !oReport:Cancel() .And. !Eof() .And. C2_FILIAL + C2_PRODUTO == xSC2 + (cAliasTop)->B1_COD
				
				If C2_LOCAL < mv_par17 .Or. C2_LOCAL > mv_par18
					dbSkip()
					Loop
				EndIf
	
				If Empty(C2_DATRF) .And. (aSC2Sld()) > 0 .And. MtrAvalOp(mv_par16)
					nOPPro += (aSC2Sld())
				EndIf
				dbSkip()
				
			EndDo
		endif
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica se o relatorio foi interrompido pelo usuario        ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If oReport:Cancel()
			Exit
		EndIf
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Aglutina os Pedidos de Vendas ainda nao entregues            ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea("SC6")
		Seek xSC6 + (cAliasTop)->B1_COD
		
		Do While !oReport:Cancel() .And. !Eof() .And. C6_FILIAL + C6_PRODUTO == xSC6 + (cAliasTop)->B1_COD
			
			If C6_LOCAL < mv_par17 .Or. C6_LOCAL > mv_par18
				dbSkip()
				Loop
			EndIf

			If mv_par14 == 1
				dbSelectArea("SF4")
				dbSetOrder(1)
				dbSeek(xSF4 + SC6->C6_TES)
				dbSelectArea("SC6")
				If SF4->F4_ESTOQUE != "S"
					dbSkip()
					Loop
				End
			End
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Verifica se devera considerar ou nao os Residuos             ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If mv_par19 == 2 .And. AllTrim(C6_BLQ) == "R"
				dbSkip()
				Loop
			EndIf
			
			nPVPro += SC6->C6_QTDVEN
			dbSelectArea("SC9")
			dbSetOrder(1)
			cChave := xSC9 + SC6->C6_NUM + SC6->C6_ITEM
			If dbSeek(cChave)
				While !oReport:Cancel() .And. ! SC9->(EOF()) .And. cChave = SC9->C9_FILIAL + SC9->C9_PEDIDO + SC9->C9_ITEM
					If C9_LOCAL < mv_par17 .Or. C9_LOCAL > mv_par18
						dbSkip()
						Loop
					EndIf
					If (Empty(SC9->C9_BLEST) .And. Empty(SC9->C9_BLCRED)) .And.+;
						SC9->C9_PRODUTO == SC6->C6_PRODUTO
						nPvPro -= SC9->C9_QTDLIB
					ElseIf SC9->C9_BLEST == "10" .AND. SC9->C9_PRODUTO == SC6->C6_PRODUTO
						nPvPro -= SC9->C9_QTDLIB
					EndIF
					dbskip()
				End
			Endif
			nPVPro := Max(0,nPVPro)			
			dbSelectArea("SC6")
			dbSkip()
			
		EndDo
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica se o relatorio foi interrompido pelo usuario        ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If oReport:Cancel()
			Exit
		EndIf
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Aglutina os Pedidos de Compra ainda nao entregues            ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea("SC7")
		Seek xSC7 + (cAliasTop)->B1_COD
		
		While !oReport:Cancel() .And. !Eof() .And. C7_FILIAL + C7_PRODUTO == xSC7 + (cAliasTop)->B1_COD

			If C7_LOCAL < mv_par17 .Or. C7_LOCAL > mv_par18
				dbSkip()
				Loop
			EndIf

			If (C7_QUANT-C7_QUJE) > 0 .And. Empty(C7_RESIDUO) .And. MtrAvalOp(mv_par16,"SC7")
				nPCPro += (C7_QUANT-C7_QUJE)
			EndIf
			dbSkip()
			
		EndDo
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica se o relatorio foi interrompido pelo usuario        ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If oReport:Cancel()
			Exit
		EndIf
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica deve listar apenas os itens a comprar               ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If mv_par09 == 2
			nSaldo	:= nSaldoPro+nSCPro+nPCPro+nOPPro
			nEmin	:= RetFldProd((cAliasTop)->B1_COD,"B1_EMIN",If(lQuery,cAliasTop,)) 
			If nSaldo > nEmin .Or. (nEmin == 0 .And. nSaldo == 0)
				dbSelectArea(cAliasTop)
				dbSkip()
				Loop
			EndIf
		EndIf
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica deve listar saldo de produto zerado                 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If mv_par15 == 2 .And. nSaldoPro == 0
			dbSelectArea(cAliasTop)
			dbSkip()
			Loop
		Endif                                                                        
		
		dbSelectArea(cAliasTop)
		oSection1:Cell('B2_QATU'   ):SetValue( nSaldoPro )
		oSection1:Cell('B2_QEMP'   ):SetValue( nEmpPro )
		oSection1:Cell('C1_QUANT'  ):SetValue( nSCPro )
		oSection1:Cell('C7_QUANT'  ):SetValue( nPCPro )
		If !lVeic
			oSection1:Cell('C2_QUANT'  ):SetValue( nOPPro )
		EndIf
		oSection1:Cell('C6_QTDVEN' ):SetValue( nPVPro )
		oSection1:Cell('B2_USAI' ):SetValue( dUsai )

		oSection1:PrintLine()
		
		dbSelectArea(cAliasTop)
		dbSkip()
		
	EndDo
	
	If nOrdem == 5 .And. !lQuery
		dbSelectArea("SB3")
		dbSkip()
	Else
		dbSelectArea(cAliasTop)
	EndIf
	
EndDo

oSection1:Finish()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Devolve a condicao original dos arquivos			         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SC1")
dbSetOrder(1)
dbSelectArea("SC2")
dbSetOrder(1)
dbSelectArea("SC6")
dbSetOrder(1)
dbSelectArea("SC7")
dbSetOrder(1)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Apaga Arquivos Temporarios                                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If nOrdem == 5 .And. !lQuery
	dbSelectArea("SB3")
	Ferase(cArqTmp+OrdBagExt())
	dbSetOrder(1)
EndIf

Return Nil



/*

ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MATR290R3³ Autor ³ Wagner Xavier         ³ Data ³ 05.09.91 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Relacao da Analise de Estoques                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Marcelo P.S.³17/11/97³12867A³ Incluir perg. Lista prod.c/saldo zerado. ³±±
±±³Edson   M.  ³19/01/98³XXXXXX³ Inclusao do Campo C2_SLDOP.              ³±±
±±³Edson   M.  ³02/02/98³XXXXXX³ Subst. do Campo C2_SLDOP p/ Funcao.      ³±±
±±³FernandoJoly³10/11/98³XXXXXX³ Ajuste para o Ano 2000.                  ³±±
±±³Rodrigo Sart³07/02/99³META  ³ Filtragem por Tipo de OP.                ³±±
±±³Patricia Sal³13/03/00³Melhor³ Inclusao perg. Almoxarifado de/ate.      ³±±
±±³Patricia Sal³19/10/00³006423³ Incluir perg. "Consid Residuos PV ?"     ³±±
±±³Fabio Rogeri³06/02/02³XXXXXX³ Utilizado campos B2_QEMPPRJ e B2_QEMPPR2 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Marcos Hirak³07/07/04³XXXXXX³Imprimir B1_CODITE quando for gestao de   ³±±
±±³            ³        ³      ³Concessionarias ( MV_VEICULO = "S")       ³±±
±±³            ³        ³      ³Nao sera' impresso a Coluna referente OP  ³±±
±±³            ³        ³      ³(SC2). obs.: nao tera' a ordem de consumo!³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Matr290R3()
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
LOCAL Tamanho  := "G"
LOCAL titulo   := STR0001	//"Relacao para Analise dos Estoques"
LOCAL cDesc1   := STR0002	//"Este relatorio demonstra a situacao de cada item em relacao ao"
LOCAL cDesc2   := STR0003	//"seu saldo , seu empenho , suas entradas previstas e sua classe"
LOCAL cDesc3   := STR0004	//"ABC."
LOCAL cString  := "SB1"
LOCAL nTipo    := 0
LOCAL aOrd     := {OemToAnsi(STR0005),OemToAnsi(STR0006),OemToAnsi(STR0007),OemToAnsi(STR0008),OemToAnsi(STR0020)}  //############" Por Consumo //" Por Codigo         "###" Por Tipo           "###" Por Descricao     "###" Por Grupo        "
LOCAL wnrel := "MATR290"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis tipo Private para SIGAVEI, SIGAPEC e SIGAOFI       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private lVEIC		:= UPPER(GETMV("MV_VEICULO"))=="S"
Private aSB1Cod	:= {}
Private aSB1Ite	:= {}
Private nCOL1		:= 0
Private nCOL2		:= 0
Private XSB1, XSB2, XSB3, XSC1, XSC2, XSC6, XSC7, XSC9, XSF4

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis tipo Private padrao de todos os relatorios         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
PRIVATE aReturn:= {OemToAnsi(STR0009), 1,OemToAnsi(STR0010), 1, 2, 1, "",1 }			//"Zebrado"###"Administracao"
PRIVATE nLastKey:= 0 ,cPerg := "MTR290"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Funcao utilizada para verificar a ultima versao dos fontes      ³
//³ SIGACUS.PRW, SIGACUSA.PRX e SIGACUSB.PRX, aplicados no rpo do   |
//| cliente, assim verificando a necessidade de uma atualizacao     |
//| nestes fontes. NAO REMOVER !!!							        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
IF !(FindFunction("SIGACUS_V") .and. SIGACUS_V() >= 20050512)
    Final(STR0023+"SIGACUS.PRW !!!") //"Atualizar "
Endif
IF !(FindFunction("SIGACUSA_V") .and. SIGACUSA_V() >= 20050512)
    Final(STR0023+"SIGACUSA.PRX !!!") //"Atualizar "
Endif
IF !(FindFunction("SIGACUSB_V") .and. SIGACUSB_V() >= 20050512)
    Final(STR0023+"SIGACUSB.PRX !!!") //"Atualizar "
Endif

dbSelectArea("SF4")
dbSetOrder(1)
XSF4	:= XFILIAL("SF4")

dbSelectArea("SC9")
dbSetOrder(1)
XSC9	:= XFILIAL("SC9")

dbSelectArea("SC7")
dbSetOrder(1)
XSC7	:= XFILIAL("SC7")

dbSelectArea("SC6")
dbSetOrder(1)
XSC6	:= XFILIAL("SC6")

dbSelectArea("SC2")
dbSetOrder(1)
XSC2	:= XFILIAL("SC2")

dbSelectArea("SC1")
dbSetOrder(1)
XSC1	:= XFILIAL("SC1")

dbSelectArea("SB3")
dbSetOrder(1)
XSB3	:= XFILIAL("SB3")


dbSelectArea("SB2")
dbSetOrder(1)
XSB2	:= XFILIAL("SB2")

dbSelectArea("SB1")
dbSetOrder(1)
XSB1	:= XFILIAL("SB1")

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ajustar o SX1 para SIGAVEI, SIGAPEC e SIGAOFI                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aSB1Cod	:= TAMSX3("B1_COD")
aSB1Ite	:= TAMSX3("B1_CODITE")

if lVEIC
   // nao tera'  a ordem de Consumo na gestao de veiculos. Marcos Hirakawa. 
	aOrd     := {OemToAnsi(STR0005),OemToAnsi(STR0006),OemToAnsi(STR0007),OemToAnsi(STR0008)}  //############" Por Consumo //" Por Codigo         "###" Por Tipo           "###" Por Descricao     "###" Por Grupo        "
	nCOL1		:= ABS(aSB1Cod[1] - aSB1Ite[1])
	nCOL2		:= -15
endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ajustar o SX1 para SIGAVEI, SIGAPEC e SIGAOFI                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
AjustaSX1(cPerg,lVeic,aSB1Cod,aSB1Ite)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica as perguntas selecionadas                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Pergunte(cPerg,.F.)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para parametros                         ³
//³ mv_par01     // Produto de                                   ³
//³ mv_par02     // Produto ate                                  ³
//³ mv_par03     // tipo de                                      ³
//³ mv_par04     // tipo ate                                     ³
//³ mv_par05     // grupo de                                     ³
//³ mv_par06     // grupo ate                                    ³
//³ mv_par07     // descricao de                                 ³
//³ mv_par08     // descricao ate                                ³
//³ mv_par09     // Tudo ou so a Comprar                         ³
//³ mv_par10     // Subtrai empenho para Calculo Saldo           ³
//³ mv_par11     // Lista apenas itens obsoletos                 ³
//³ mv_par12     // data limite p/itens obsoletos                ³
//³ mv_par13     // Cons. Saldo do Almox. Processo ?  Sim , Nao  ³
//³ mv_par14     // Desconsidera P.V. que N Atu.Est.? Sim , Nao  ³
//³ mv_par15     // Lista produto com Saldo zerado  ? Sim , Nao  ³
//³ mv_par16     // Considera OPs 1- Firmes 2- Previstas 3- Ambas³
//³ mv_par17     // Almoxarifado De  ?                           ³
//³ mv_par18     // Almoxarifado Ate ?                           ³
//³ mv_par19     // Consid Residuos PV ?  Sim, Nao               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Envia controle para a funcao SETPRINT                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
wnrel:=SetPrint(cString,wnrel,cPerg,@titulo,cDesc1,cDesc2,cDesc3,.F.,aOrd,.F.,Tamanho)

If nLastKey = 27
	Set Filter to
	Return
Endif

SetDefault(aReturn,cString)

If nLastKey = 27
	Set Filter to
	Return
Endif
RptStatus({|lEnd| C290Imp(aOrd,@lEnd,wnRel,cString,tamanho,titulo)},titulo)
Return NIL

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ C290IMP  ³ Autor ³ Rodrigo de A. Sartorio³ Data ³ 12.12.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Chamada do Relatorio                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATR290                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function C290Imp(aOrd,lEnd,WnRel,cString,tamanho,titulo)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis locais exclusivas deste programa                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
LOCAL cRodaTxt := STR0022 //"PRODUTO(S)"
LOCAL nCntImpr := 0
LOCAL nSaldoPro,nEmpPro,nSCPro,nPCPro,nOPPro,nPVPro
LOCAL nSaldoSub,nEmpSub,nSCSub,nPCSub,nOPSub,nPVSub
LOCAL nSaldoTot,nEmpTot,nSCTot,nPCTot,nOPTot,nPVTot
LOCAL lPAssou1,lPassou2,nFirst := 0,dUsai,nSaldo, cLocProc := GETMV("MV_LOCPROC")
LOCAL cArqTmp,cFiltro
Local cChave := ""

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis tipo Local para SIGAVEI, SIGAPEC e SIGAOFI         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
LOCAL nAux1
LOCAL nI
LOCAL nLenc
Local nIndr
Local lT

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Contadores de linha e pagina                                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
PRIVATE li := 80 ,m_pag := 1

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis privadas exclusivas deste programa                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
PRIVATE cCondicao ,lContinua ,cVar ,cCondSec

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Inicializa os codigos de caracter Comprimido/Normal da impressora ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nTipo  := IIF(aReturn[4]==1,GetMV("MV_COMP"),GetMV("MV_NORM"))

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Adiciona a ordem escolhida ao titulo do relatorio          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Type("NewHead")#"U"
	NewHead += " ("+AllTrim(aOrd[aReturn[8]])+")"
Else
	Titulo += " ("+AllTrim(aOrd[aReturn[8]])+")"
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta os Cabecalhos                                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cabec1 := STR0011		//"CODIGO          DESCRICAO                      TP GRUP UM    SALDO ATUAL   EMPENHO PARA           SC's PC's/CONTRATOS           OP's           PV's     PONTO DE         LOTE   PRAZO  ESTOQUE       CONSUMO   ULTIMA   CL  "
cabec2 := STR0012		//"                                                                         REQ/PV/RESERVA      COLOCADAS      COLOCADOS      COLOCADAS      COLOCADOS       PEDIDO    ECONOMICO ENTREGA EM MESES         MEDIO    SAIDA       "
*****                   123456789012345 123456789012345678901234567890 12 1234 12 999,999,999.99 999,999,999.99 999,999,999.99 999,999,999.99 999,999,999.99 999,999,999.99 9,999,999.99 9,999,999.99 99999 D    99999 99,999,999.99 99/99/9999 A
*****                   0         1         2         3         4         5         6         7         8         9        10        11        12        13        14        15        16        17        18        19        20        21        22
*****                   01234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890
/*
cabec1 := STR0011		//"CODIGO         678901234567 DESCRICAO                      TP GRUP UM    SALDO ATUAL   EMPENHO PARA           SC's PC's/CONTRATOS           PV's     PONTO DE         LOTE   PRAZO  ESTOQUE       CONSUMO   ULTIMA   CL  "
cabec2 := STR0012		//"               678901234567                                                          REQ/PV/RESERVA      COLOCADAS      COLOCADOS      COLOCADOS       PEDIDO    ECONOMICO ENTREGA EM MESES         MEDIO    SAIDA       "
*****                   123456789012345678901234567 123456789012345678901234567890 12 1234 12 999,999,999.99 999,999,999.99 999,999,999.99 999,999,999.99 999,999,999.99 9,999,999.99 9,999,999.99 99999 D    99999 99,999,999.99 99/99/9999 A
*****                   0         1         2         3         4         5         6         7         8         9        10        11        12        13        14        15        16        17        18        19        20        21        22
*****                   01234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890
*/

cArqTmp	:= ""
if lVEIC
	cabec1:= substr(cabec1 ,1,aSB1Cod[1]) + SPACE(nCOL1) + substr(cabec1 ,aSB1Cod[1]+1)
	cabec2:= substr(cabec2 ,1,aSB1Cod[1]) + SPACE(nCOL1) + substr(cabec2 ,aSB1Cod[1]+1)
	
	// Para retirar a coluna referente a OP ( SC2 )
	nAux1	:= AT('CONTRA',UPPER(cabec1))
	nLenc	:= LEN(cabec1)
	IF nAux1 > 0
	   FOR nI := nAux1 TO nLenc
	      IF SUBStr(cabec1,nI, 1 ) == ' '
	         exit
	      endif
	   NEXT nI
		cabec1:= substr(cabec1 ,1, nI-1 ) + substr(cabec1 ,nI + 15)
		cabec2:= substr(cabec2 ,1, nI-1 ) + substr(cabec2 ,nI + 15)
	ENDIF
endif	

dbSelectArea("SB1")
SetRegua(LastRec())

Set Softseek On

if ! lVEIC
	Set Order To aReturn[8]
	If aReturn[8] == 4
		Seek xSB1 + mv_par05
		cCondicao := "lContinua .And. !Eof() .And. B1_GRUPO <= mv_par06"
	ElseIf aReturn[8] == 3
		Seek xSB1 + mv_par07
		cCondicao := "lContinua .And. !Eof() .And. B1_DESC <= mv_par08"
	ElseIf aReturn[8] == 2
		Seek xSB1 + mv_par03
		cCondicao := "lContinua .And. !Eof() .And. B1_TIPO <= mv_par04"
	Else
		Seek xSB1 + mv_par01
		cCondicao := "lContinua .And. !Eof() .And. B1_COD <= mv_par02"
	Endif
else
	Set Order To aReturn[8]
	If aReturn[8] == 4
		dbSetOrder( 7 ) // B1_FILIAL+B1_GRUPO+B1_CODITE
		Seek xSB1 + mv_par05
		cCondicao := "lContinua .And. !Eof() .And. B1_GRUPO <= mv_par06"
	ElseIf aReturn[8] == 3
		Seek xSB1 + mv_par07
		cCondicao := "lContinua .And. !Eof() .And. B1_DESC <= mv_par08"
	ElseIf aReturn[8] == 2 // por tipo
		cFiltro   :=      "B1_TIPO>='" + mv_par03 + "'" 
		cFiltro   += ".And.B1_TIPO<='" + mv_par04 + "'"
		cFiltro   += ".and.B1_FILIAL=='" + xSB1 + "'"
		cArqTmp   := CriaTrab('',.F.)
		IndRegua('SB1',cArqTmp,'B1_FILIAL+B1_TIPO+B1_CODITE',,cFiltro,STR0021)    //"Selecionando Registros"
		nIndr	:=	RetIndex("SB1")
		#IFNDEF TOP
			dbSetIndex(cArqTmp+OrdBagExt())
		#ENDIF
		dbSetOrder(nIndr + 1)
		dbSeek(xSB1 + mv_par03, .T.)
		cCondicao := "lContinua .And. !Eof() .And. B1_TIPO <= mv_par04"
	Else
		cFiltro   :=      "B1_CODITE>='" + mv_par01 + "'"
		cFiltro   += ".And.B1_CODITE<='" + mv_par02 + "'"
		cFiltro   += ".and.B1_FILIAL=='" + xSB1 + "'"
		cArqTmp   := CriaTrab('',.F.)
		IndRegua('SB1',cArqTmp,'B1_FILIAL+B1_CODITE',,cFiltro,STR0021)    //"Selecionando Registros"
		nIndr	:=	RetIndex("SB1")
		#IFNDEF TOP
			dbSetIndex(cArqTmp+OrdBagExt())
		#ENDIF
		dbSetOrder(nIndr + 1)
		dbSeek(xSB1 + mv_par01, .T.)
		cCondicao := "lContinua .And. !Eof() .And. B1_CODITE <= mv_par02"
	Endif
endif
Set SoftSeek Off

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Posiciona os indices dos arquivos consultados                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SC1")
dbSetOrder(2)

dbSelectArea("SC2")
dbSetOrder(2)

dbSelectArea("SC6")
dbSetOrder(2)

dbSelectArea("SC7")
dbSetOrder(2)

Store 0 To nSaldoTot,nEmpTot,nSCTot,nPCTot,nOPTot,nPVTot
lPassou1  := .F.
lContinua := .T.

If aReturn[8] == 5
	dbSelectArea("SB3")
	cCondicao := "lContinua .And. SB3->(!Eof())"
	cFiltro   :=      "B3_COD>='" + mv_par01 + "'"
	cFiltro   += ".And.B3_COD<='" + mv_par02 + "'"
	cFiltro   += ".and.B3_FILIAL=='" + xSB3 + "'"
	cArqTmp   := CriaTrab('',.F.)
	IndRegua('SB3',cArqTmp,'STR(B3_MEDIA)',,cFiltro,STR0021)    //"Selecionando Registros"
	nIndSb3:=RetIndex("SB3")
	#IFNDEF TOP
		dbSetIndex(cArqTmp+OrdBagExt())
	#ENDIF
	dbSetOrder(nIndSb3 + 1)
	dbGotop()
Else
	dbSelectArea("SB1")
	cCondicao += ".and.B1_FILIAL=='" + xSB1 + "'"
Endif
Do While &cCondicao
   cCondSec := "B1_FILIAL=='" + xSB1 + "' .And. "
	if ! lVEIC
		If aReturn[8] == 5
	  		dbSelectArea("SB1")
	  		dbSetOrder(1)
			dbSeek(xSB1 + SB3->B3_COD)
			cCondSec += "B1_COD == SB3->B3_COD"
		ElseIf aReturn[8] == 4
			cVar     := B1_GRUPO
			cCondSec += "B1_GRUPO == cVar"
		ElseIf aReturn[8] == 3
			cVar     := B1_DESC
			cCondSec += "B1_DESC == cVar"
		ElseIf aReturn[8] == 2
			cVar     := B1_TIPO
			cCondSec += "B1_TIPO == cVar"
		Else
			cVar     := B1_COD
			cCondSec += "B1_COD == cVar"
		Endif
	ELSE
		If aReturn[8] == 4
			cVar     := B1_GRUPO
			cCondSec += "B1_GRUPO == cVar"
		ElseIf aReturn[8] == 3
			cVar     := B1_DESC
			cCondSec += "B1_DESC == cVar"
		ElseIf aReturn[8] == 2
			cVar     := B1_TIPO
			cCondSec += "B1_TIPO == cVar"
		Else
			cVar     := B1_CODITE
			cCondSec += "B1_CODITE == cVar"
		Endif
	ENDIF
	Store 0 To nSaldoSub,nEmpSub,nSCSub,nPCSub,nOPSub,nPVSub
	lPassou2 := .F.
	DO While &cCondicao .And. &(cCondSec)
		
		If lEnd
			@ PROW()+1,001 PSay  STR0013		//"CANCELADO PELO OPERADOR"
			lContinua := .F.
			Exit
		EndIf
		
		IncRegua()
		
		lT := .F.
		if ! lVEIC
			If B1_COD < mv_par01 .Or. B1_COD > mv_par02
	   		lT := .T.
         endif
      else
			if B1_CODITE < mv_par01 .Or. B1_CODITE > mv_par02
	   		lT := .T.
			endif
		EndIf
		if lT
			dbSkip()
			Loop
		EndIf
		
		If B1_TIPO < mv_par03 .Or. B1_TIPO > mv_par04
			dbSkip()
			Loop
		EndIf
		
		If B1_GRUPO < mv_par05 .Or. B1_GRUPO > mv_par06
			dbSkip()
			Loop
		EndIf
		
		If B1_DESC < mv_par07 .Or. B1_DESC > mv_par08
			dbSkip()
			Loop
		EndIf
		
		Store 0 To nSaldoPro,nEmpPro,nSCPro,nPCPro,nOPPro,nPVPro
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Soma os saldos iniciais e os empenhos do SB2                 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea("SB2")
		Seek xSB2 + SB1->B1_COD
		dUsai := B2_USAI
		Do While !Eof() .And. B2_FILIAL + B2_COD == xSB2 + SB1->B1_COD
			
			If lEnd
				@ PROW()+1,001 PSay STR0013	//"CANCELADO PELO OPERADOR"
				lContinua := .F.
				Exit
			EndIf
			
			IF B2_LOCAL == cLocProc .and. mv_par13 == 2
				dbSkip()
				Loop
			Endif

			If B2_LOCAL < mv_par17 .Or. B2_LOCAL > mv_par18
				dbSkip()
				Loop
			EndIf

			nSaldoPro += B2_QATU
			nEmpPro   += If(mv_par16==1,B2_QEMP,If(mv_par16==2,B2_QEMPPRE,B2_QEMP+B2_QEMPPRE))
			nEmpPro   += If(mv_par20==1,B2_QEMPPRJ,0)
			nEmpPro   += B2_RESERVA
			If dUsai < B2_USAI
				dUsai := B2_USAI
			EndIf
			dbSkip()
			
		EndDo
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica se deve subtrair empenho do saldo atual             ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If mv_par10 == 1
			nSaldoPro -= nEmpPro
		EndIf
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica se deve listar apenas os obsoletos                  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If mv_par11 == 1 .And. (Empty(dUsai) .Or. dUsai > mv_par12)
			dbSelectArea("SB1")
			dbSkip()
			Loop
		EndIf
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica se o relatorio foi interrompido pelo usuario        ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !lContinua
			Exit
		EndIf
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Posiciona-se no arquivo de Demandas para pegar dados         ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If aReturn[8] # 5
			dbSelectArea("SB3")
			dbSeek(xSB3 + SB1->B1_COD)
		EndIf
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Aglutina as Solicitacos de Compra sem pedido colocado        ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea("SC1")
		Seek xSC1 + SB1->B1_COD
		Do While !Eof() .And. C1_FILIAL + C1_PRODUTO == xSC1 + SB1->B1_COD
			
			If lEnd
				@ PROW()+1,001 PSay STR0013		//"CANCELADO PELO OPERADOR"
				lContinua := .F.
				Exit
			EndIf
			
			If C1_LOCAL < mv_par17 .Or. C1_LOCAL > mv_par18
				dbSkip()
				Loop
			EndIf

			If C1_QUJE < C1_QUANT .And. MtrAvalOp(mv_par16,"SC1")
				nSCPro += (C1_QUANT - C1_QUJE)
			EndIf
			dbSkip()
			
		EndDo
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica se o relatorio foi interrompido pelo usuario        ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !lContinua
			Exit
		EndIf
		if ! lVEIC
		
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Aglutina as Ordens de Producao em aberto                     ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			dbSelectArea("SC2")
			dbSeek(xSC2 + SB1->B1_COD)
			
			Do While !Eof() .And. C2_FILIAL + C2_PRODUTO == xSC2 + SB1->B1_COD
				
				If lEnd
					@ PROW()+1,001 PSay STR0013		//"CANCELADO PELO OPERADOR"
					lContinua := .F.
					Exit
				EndIf
				
				If C2_LOCAL < mv_par17 .Or. C2_LOCAL > mv_par18
					dbSkip()
					Loop
				EndIf
	
				If Empty(C2_DATRF) .And. (aSC2Sld()) > 0 .And. MtrAvalOp(mv_par16)
					nOPPro += (aSC2Sld())
				EndIf
				dbSkip()
				
			EndDo
		endif
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica se o relatorio foi interrompido pelo usuario        ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !lContinua
			Exit
		EndIf
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Aglutina os Pedidos de Vendas ainda nao entregues            ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea("SC6")
		Seek xSC6 + SB1->B1_COD
		
		Do While !Eof() .And. C6_FILIAL + C6_PRODUTO == xSC6 + SB1->B1_COD
			
			If lEnd
				@ PROW()+1,001 PSay STR0013	//"CANCELADO PELO OPERADOR"
				lContinua := .F.
				Exit
			EndIf
			
			If C6_LOCAL < mv_par17 .Or. C6_LOCAL > mv_par18
				dbSkip()
				Loop
			EndIf

			If mv_par14 == 1
				dbSelectArea("SF4")
				dbSetOrder(1)
				dbSeek(xSF4 + SC6->C6_TES)
				dbSelectArea("SC6")
				If SF4->F4_ESTOQUE != "S"
					dbSkip()
					Loop
				End
			End
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Verifica se devera considerar ou nao os Residuos             ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If mv_par19 == 2 .And. AllTrim(C6_BLQ) == "R"
				dbSkip()
				Loop
			EndIf
			
			nPVPro += SC6->C6_QTDVEN
			dbSelectArea("SC9")
			dbSetOrder(1)
			cChave := xSC9 + SC6->C6_NUM + SC6->C6_ITEM
			If dbSeek(cChave)
				While ! SC9->(EOF()) .And. cChave = SC9->C9_FILIAL + SC9->C9_PEDIDO + SC9->C9_ITEM
					If C9_LOCAL < mv_par17 .Or. C9_LOCAL > mv_par18
						dbSkip()
						Loop
					EndIf
					If (Empty(SC9->C9_BLEST) .And. Empty(SC9->C9_BLCRED)) .And.+;
						SC9->C9_PRODUTO == SC6->C6_PRODUTO
						nPvPro -= SC9->C9_QTDLIB
					ElseIf SC9->C9_BLEST == "10" .AND. SC9->C9_PRODUTO == SC6->C6_PRODUTO
						nPvPro -= SC9->C9_QTDLIB
					EndIF
					dbskip()
				End
			Endif
			nPVPro := Max(0,nPVPro)			
			dbSelectArea("SC6")
			dbSkip()
			
		EndDo
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica se o relatorio foi interrompido pelo usuario        ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !lContinua
			Exit
		EndIf
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Aglutina os Pedidos de Compra ainda nao entregues            ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea("SC7")
		Seek xSC7 + SB1->B1_COD
		
		While !Eof() .And. C7_FILIAL + C7_PRODUTO == xSC7 + SB1->B1_COD
			
			If lEnd
				@ PROW()+1,001 PSay STR0013		//"CANCELADO PELO OPERADOR"
				lContinua := .F.
				Exit
			EndIf
			
			If C7_LOCAL < mv_par17 .Or. C7_LOCAL > mv_par18
				dbSkip()
				Loop
			EndIf

			If (C7_QUANT-C7_QUJE) > 0 .And. Empty(C7_RESIDUO) .And. MtrAvalOp(mv_par16,"SC7")
				nPCPro += (C7_QUANT-C7_QUJE)
			EndIf
			dbSkip()
			
		EndDo
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica se o relatorio foi interrompido pelo usuario        ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !lContinua
			Exit
		EndIf
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica deve listar apenas os itens a comprar               ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If mv_par09 == 2
			nSaldo := nSaldoPro+nSCPro+nPCPro+nOPPro
			If nSaldo > RetFldProd(SB1->B1_COD,"B1_EMIN") .Or. (RetFldProd(SB1->B1_COD,"B1_EMIN") == 0 .And. nSaldo == 0)
				dbSelectArea("SB1")
				dbSkip()
				Loop
			EndIf
		EndIf
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica deve listar saldo de produto zerado                 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If mv_par15 == 2
			If nSaldoPro == 0
				dbSelectArea("SB1")
				dbSkip()
				Loop
			Endif
		Endif                                                                        
		
		lPassou1 := .T.
		lPassou2 := .T.
		
		If li > 58
			Cabec(titulo,cabec1,cabec2,wnrel,Tamanho,nTipo)
		EndIf
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Adiciona 1 ao contador de registros impressos         ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		nCntImpr++
		
		dbSelectArea("SB1")
		if ! lVEIC
			@ li,000 PSay B1_COD
		else 
			@ li,000 PSay B1_CODITE
		endif
		@ li,016 + nCOL1 PSay Substr(B1_DESC,1,30)
		@ li,048 + nCOL1 PSay B1_TIPO
		@ li,051 + nCOL1 PSay B1_GRUPO
		@ li,056 + nCOL1 PSay B1_UM
	    @ li,059 + nCOL1 PSay nSaldoPro Picture PesqPictQt("B2_QATU"  ,14)
		@ li,074 + nCOL1 PSay nEmpPro   Picture PesqPictQt("B2_QEMP"  ,14)
		@ li,091 + nCOL1 PSay nSCPro    Picture PesqPictQt("C1_QUANT" ,14)
		@ li,106 + nCOL1 PSay nPCPro    Picture PesqPictQt("C7_QUANT" ,14)
		if !lVEIC
			@ li,121 + nCOL1 PSay nOPPro    Picture PesqPictQt("C2_QUANT" ,14)
		endif	
		@ li,136 + nCOL1 + nCOL2 PSay nPVPro    Picture PesqPictQt("C6_QTDVEN",14)
		@ li,151 + nCOL1 + nCOL2 PSay RetFldProd(SB1->B1_COD,"B1_EMIN")   Picture PesqPictQt("B1_EMIN"  ,12)
		@ li,164 + nCOL1 + nCOL2 PSay RetFldProd(SB1->B1_COD,"B1_LE") Picture PesqPictQt("B1_LE"    ,12)
		@ li,177 + nCOL1 + nCOL2 PSay CalcPrazo(B1_COD,RetFldProd(SB1->B1_COD,"B1_LE")) Picture "99999"
		@ li,184 + nCOL1 + nCOL2 PSay RetFldProd(SB1->B1_COD,"B1_TIPE")
		If SB3->B3_MEDIA != 0
			@ li,186 + nCOL1 + nCOL2 PSay nSaldoPro/SB3->B3_MEDIA Picture "99999"
		Else
			@ li,186 + nCOL1 + nCOL2 PSay STR0032 //"  N/D"
		EndIf
		@ li,192 + nCOL1 + nCOL2 PSay SB3->B3_MEDIA Picture PesqPictQt("B3_MEDIA",13)
		@ li,206 + nCOL1 + nCOL2 PSay dUsai
		@ li,216 + nCOL1 + nCOL2 PSay SB3->B3_CLASSE
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Soma o Total do Produto ao total da quebra                   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		nSaldoSub += nSaldoPro
		nEmpSub   += nEmpPro
		nSCSub    += nSCPro
		nPCSub    += nPCPro
		nOPSub    += nOPPro
		nPVSub    += nPVPro
		
		dbSelectArea("SB1")
		Skip
		li++
		
	EndDo
	
	If (aReturn[8] == 2 .Or. aReturn[8] == 4) .And. lPassou2
		If aReturn[8] == 2
			@ li,024 + nCOL1 PSay STR0014+cVar		//"T o t a l  d o   T i p o : "
		ElseIf aReturn[8] == 4
			@ li,023 + nCOL1 PSay STR0015+cVar		//"T o t a l   d o   G r u p o : "
		EndIf
		@ li,059 + nCOL1 PSay nSaldoSub Picture PesqPictQt("B2_QATU"  ,14)
		@ li,074 + nCOL1 PSay nEmpSub   Picture PesqPictQt("B2_QEMP"  ,14)
		@ li,091 + nCOL1 PSay nSCSub    Picture PesqPictQt("C1_QUANT" ,14)
		@ li,106 + nCOL1 PSay nPCSub    Picture PesqPictQt("C7_QUANT" ,14)
		if ! lVEIC
			@ li,121 + nCOL1 PSay nOPSub    Picture PesqPictQt("C2_QUANT" ,14)
		ENDIF
		@ li,136 + nCOL1 + nCOL2 PSay nPVSub    Picture PesqPictQt("C6_QTDVEN",14)
		li++
		@ li,   0 PSay __PrtThinLine()
		li++
	EndIf
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Soma o Total do Produto ao total da quebra                   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	nSaldoTot += nSaldoSub
	nEmpTot   += nEmpSub
	nSCTot    += nSCSub
	nPCTot    += nPCSub
	nOPTot    += nOPSub
	nPVTot    += nPVSub
	
	If aReturn[8] == 5
		dbSelectArea("SB3")
		dbSkip()
	Else
		dbSelectArea("SB1")
	EndIf
	
EndDo

If lPassou1
	If 	li > 56
		Cabec(titulo,cabec1,cabec2,wnrel,Tamanho,nTipo)
	EndIf
	li++
	@ li,024 + nCOL1 PSay STR0016		//"T o t a l   G e r a l :"
	@ li,059 + nCOL1 PSay nSaldoTot Picture PesqPictQt("B2_QATU"  ,14)
	@ li,074 + nCOL1 PSay nEmpTot   Picture PesqPictQt("B2_QEMP"  ,14)
	@ li,091 + nCOL1 PSay nSCTot    Picture PesqPictQt("C1_QUANT" ,14)
	@ li,106 + nCOL1 PSay nPCTot    Picture PesqPictQt("C7_QUANT" ,14)
	if ! lVEIC
		@ li,121 + nCOL1 PSay nOPTot        Picture PesqPictQt("C2_QUANT" ,14)
	endif	
	@ li,136 + nCOL1 + nCOL2 PSay nPVTot    Picture PesqPictQt("C6_QTDVEN",14)
EndIf

dbSelectArea("SC1")
dbSetOrder(1)
dbSelectArea("SC2")
dbSetOrder(1)
dbSelectArea("SC6")
dbSetOrder(1)
dbSelectArea("SC7")
dbSetOrder(1)

If li != 80
	Roda(nCntImpr,cRodaTxt,Tamanho)
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Devolve a condicao original do arquivo principal             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

dbSelectArea(cString)
Set Filter To
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Apaga Arquivos Temporarios especifico p/Gestao Concessionaria³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

if lVEIC
	if !empty(cArqTmp)
		RetIndex("SB1")
		dbsetorder(1)
		IF File( cArqTmp + OrdBagExt() )
			fErase( cArqTmp + OrdBagExt())
		EndIF
		cArqTmp := ""		
	endif
endif	
Set Order To 1

If aReturn[5] = 1
	Set Printer To
	dbCommitAll()
	OurSpool(wnrel)
Endif
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Apaga Arquivos Temporarios                                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If aReturn[8] == 5
	dbSelectArea("SB3")
	Ferase(cArqTmp+OrdBagExt())
	dbSetOrder(1)
EndIf

MS_FLUSH()
return    


/*           
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ AjustaSX1 ³ Autor ³ Ricardo Berti      ³ Data ³ 18/07/2006 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Ajustar o SX1 para SIGAVEI, SIGAPEC e SIGAOFI  	          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ AjustaSX1(ExpC1,ExpL1,ExpA1,ExpA2)  	                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Grupo do pergunte 		                          ³±±
±±³          ³ ExpL1 = Se .T. = gestao de Concessionarias(MV_VEICULO ="S")³±±
±±³          ³ ExpA1 = Array com TAMSX3 do B1_COD		                  ³±±
±±³          ³ ExpA2 = Array com TAMSX3 do B1_CODITE		              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ MATR290                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
*/
Static Function AjustaSX1(cPerg,lVeic,aSB1Cod,aSB1Ite)

Local aArea1	:= Getarea() 

DBSELECTAREA("SX1")
DBSETORDER(1)
DBSEEK(cPerg)
if lVEIC
   DBSEEK(cPerg)
   DO WHILE SX1->X1_GRUPO == cPerg .AND. !SX1->(EOF())
      IF "PRODU" $ UPPER(SX1->X1_PERGUNT) .AND. UPPER(SX1->X1_TIPO) == "C" .AND. ;
      (SX1->X1_TAMANHO <> aSB1Ite[1] .OR. UPPER(SX1->X1_F3) <> "VR4")

         RECLOCK("SX1",.F.)
         SX1->X1_TAMANHO := aSB1Ite[1]
         SX1->X1_F3 := "VR4"
         DBCOMMIT()
         MSUNLOCK()
         
      ENDIF
      DBSKIP()
   ENDDO
else
   DO WHILE SX1->X1_GRUPO == cPerg .AND. !SX1->(EOF())
      IF "PRODU" $ UPPER(SX1->X1_PERGUNT) .AND. UPPER(SX1->X1_TIPO) == "C" .AND. ;
      (SX1->X1_TAMANHO <> aSB1Cod[1] .OR. UPPER(SX1->X1_F3) <> "SB1")

         RECLOCK("SX1",.F.)
         SX1->X1_TAMANHO := aSB1Cod[1]
         SX1->X1_F3 := "SB1"
         DBCOMMIT()
         MSUNLOCK()
         
      ENDIF
      DBSKIP()
   ENDDO
endif
RESTAREA(aArea1)
Return Nil
