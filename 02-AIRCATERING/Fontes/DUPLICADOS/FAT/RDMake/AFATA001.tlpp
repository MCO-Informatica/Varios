#include "totvs.ch"

/*/
?????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????
???Programa          ? AFATA001             ? Analista    ? McInfotec                              ?  Data  ?     22/06/19    ???
?????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????
???Descricao         ? Ler arquivo .CSV e incluir Previsao de Vendas                                                          ???
???                  ?                                                                                                        ??? 
???                  ?                                                                                                        ???  
?????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????
???Uso               ? Exclusivo : Air Catering                                                                               ???  
???                  ?                                                                                                        ???  
?????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????
/*/

User Function AFATA001()

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Declaracao de Variaveis                                                                                                     //
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

Local c_Caminho := Space(100)
Local n_Radio   := 1
Local l_Retorno := .F.
Local a_Arquivo := {}
Local n_Opcao   := 0
Local c_DesFun	:= "" 

Private n_TotReg := 0
Private n_TotDeb := 0
Private n_TotCrd := 0

c_DesFun += "Este processamento tem o objetivo de ler um arquivo .CSV"+Chr(13)+Chr(10)
c_DesFun += "préformatado e proceder com a inclusão automática das"+Chr(13)+Chr(10)
c_DesFun += "Previsões de Vendas."+Chr(13)+Chr(10)

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Monta tela de parametros                                                                                                    //
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

While !l_Retorno
	n_Opcao := 0
	l_Retorno := .F.
	DEFINE MSDIALOG o_Dlg TITLE "Inclusao de Previsao de Vendas" From 0,0 To 22,50

	oSayLay := tSay():New(05,07,{|| c_DesFun},o_Dlg,,,,,,.T.,,,200,80)	
	oSayArq := tSay():New(60,07,{|| "Informe o arquivo para processamento :"},o_Dlg,,,,,,.T.,CLR_BLUE,,200,80)
	oGetArq := TGet():New(70,05,{|u| If(PCount()>0,c_Caminho:=u,c_Caminho)},o_Dlg,150,10,'@!',,,,,,,.T.,,,,,,,,,,'c_Caminho') 
	oBtnArq := tButton():New(70,160,"&Abrir...",o_Dlg,{|| c_Caminho := HABRPLA(c_Caminho)},30,12,,,,.T.)
	
	oBtnImp := tButton():New(150,050,"&Processar",o_Dlg,{|| n_Opcao:=1,o_Dlg:End()},40,12,,,,.T.)
	oBtnCan := tButton():New(150,110,"&Cancelar",o_Dlg,{|| n_Opcao:=0,o_Dlg:End()},40,12,,,,.T.)
	
	ACTIVATE MSDIALOG o_Dlg CENTERED

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Valida o arquivo                                                                                                            //
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

	If n_Opcao == 1
		If Empty(c_Caminho)
			MsgInfo("Informe o arquivo","Aviso")
			l_Retorno := .F.
		ElseIf !File(c_Caminho)
			MsgInfo("Arquivo não encontrado","Aviso")
			l_Retorno := .F.
		Else
			l_Retorno := .T.
		EndIf
	Else
		l_Retorno := .T.
	EndIf

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Chama a rotina de leitura do arquivo, havendo conteudo, chama rotina de processamento                                       //
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////// 

	If l_Retorno .And. n_Opcao == 1
		Processa({|lEnd| a_Arquivo := HLERPLA(c_Caminho,@lEnd)},"Aguarde ...","Processando a leitura do arquivo...")
		If Len(a_Arquivo) == 0
			MsgInfo("Atencão: Não há registros para inclusão","Aviso")
		ElseIf MsgYesNo("Serão processados "+AllTrim(Str(Len(a_Arquivo)))+" registro(s) de "+AllTrim(Str(n_TotReg))+" registro(s) lido(s). Confirma ?")
			Processa({|lEnd| a_Arquivo := HGRVPLA(a_Arquivo,@lEnd)},"Aguarde ...","Processando Inclusão de Previsão de Vendas ...")
		EndIf
	EndIf	
End

Return(l_Retorno)

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

/*/
?????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????
?Programa          ? HABRPLA              ? Analista    ? McInfotec                                  ?  Data  ?     22/06/19    ?
?????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????
?Descricao         ? Abre o arquivo .CSV                                                                                        ?
?????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????
?Uso               ? Exclusivo : AFATA001                                                                                       ?  
?????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????
/*/ 

Static Function HABRPLA(c_Arquivo)

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Faz a abertura do arquivo .CSV escolhido                                                                                    //
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////// 

c_Arquivo := cGetFile(" (*.csv) |*.csv|","Arquivos (CSV)")

If !Empty(c_Arquivo)
	c_Arquivo += Space(100-Len(c_Arquivo))
Else
	c_Arquivo := Space(100)
EndIf

Return(c_Arquivo)

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

/*/
?????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????
?Programa          ? HLERPLA              ? Analista    ? McInfotec                                  ?  Data  ?     22/06/19    ?
?????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????
?Descricao         ? Le o arquivo .CSV                                                                                          ?
?????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????
?Uso               ? Exclusivo : AFATA001                                                                                       ?  
?????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????
/*/ 

Static Function HLERPLA(c_Arquivo,l_End)

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Declaracao de Variaveis                                                                                                     //
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

Local c_Linha  := ""
Local c_Trecho := ""
Local n_Handle := 0
Local a_Arquivo:= {}
Local c_TipoDc := ""
Local l_Fim    := l_End
Local n_ColPos := 0
Local n_PsProd := 1
Local n_PsLoc  := 2
Local n_PsDoc  := 3
Local n_PsQtd  := 4
Local n_PsVal  := 5
Local n_PsData := 6
Local n_PsObs  := 7
Local cProduto := ""
Local cLocal   := ""
Local cDoc     := ""
Local nQtde    := 0
Local nValor   := 0
Local dData    := CtoD("  /  /    ")
Local cObs     := ""


/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Inicia a leitura do arquivo e armazena os valores no array                                                                  //
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

n_Handle := FT_FUSE(c_Arquivo)
n_TotReg := FT_FLASTREC()-1

FT_FGOTOP()
FT_FSKIP()
ProcRegua(n_TotReg)

While !FT_FEOF()
	If l_End
		Exit
	EndIf
	n_ColPos := 0
	c_Linha := FT_FREADLN()
	While !Empty(c_Linha)
		n_ColPos ++
		c_Trecho := If(At(";",c_Linha)>0,Substr(c_Linha,1,At(";",c_Linha)-1),c_Linha)
		c_Linha := If(At(";",c_Linha)>0,Substr(c_Linha,At(";",c_Linha)+1),"")
		Do Case
			Case n_PsProd == n_ColPos
				cProduto := Padr(c_Trecho,TamSX3("C4_PRODUTO")[1])
			Case n_PsLoc  == n_ColPos
				cLocal   := Padr(c_Trecho,TamSX3("C4_LOCAL")[1])
			Case n_PsDoc  == n_ColPos
				cDoc     := Padr(c_Trecho,TamSX3("C4_DOC")[1])
			Case n_PsQtd  == n_ColPos
				nQtde    := Val(StrTran(StrTran(c_Trecho,".",""),",","."))
			Case n_PsVal  == n_ColPos
				nValor   := Val(StrTran(StrTran(c_Trecho,".",""),",","."))
			Case n_PsData == n_ColPos
			    dData    := CtoD(c_Trecho)
			Case n_PsObs  == n_ColPos
				cObs     := Padr(c_Trecho,TamSX3("C4_OBS")[1])	
		EndCase
	End
    
	aAdd(a_Arquivo, {cProduto, cLocal, cDoc, nQtde, nValor, dData, cObs})
	
	IncProc()

	FT_FSKIP()
End
FT_FUSE()

Return(a_Arquivo)  

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

/*/
?????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????
?Programa          ? HGRVPLA              ? Analista    ? McInfotec                                  ?  Data  ?     22/06/19    ?
?????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????
?Descricao         ? Efetua a inclus?o da Previs?o                                                                              ?
?????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????
?Uso               ? Exclusivo : HCTBLAN                                                                                        ?  
?????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????
/*/

Static Function HGRVPLA(a_Arquivo,l_End)

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Declaracao de Variaveis                                                                                                     //
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////// 

Local a_Area   := GetArea()
Local n_Linha  := 1
Local i        := 0
Local aMata700 := {}
Local l_Erro   := .F.

Private lMsErroAuto := .F.
Private lMsHelpAuto := .T.
Private CTF_LOCK := 0

l_Erro := AFT001Erro(a_Arquivo) 
if !l_Erro

   ProcRegua(n_TotReg)

   Begin Transaction

   For I := 1 to n_TotReg

        If l_End
  		   Exit
	    EndIf

	    IncProc()
   
        aadd(aMata700,{"C4_FILIAL" , xFilial("SC4") , Nil})  // Filial da Previsao
        aadd(aMata700,{"C4_PRODUTO", a_Arquivo[I][1] , Nil})  // Produto
        aadd(aMata700,{"C4_LOCAL"  , a_Arquivo[I][2] , Nil})  // Armazem 
	    aadd(aMata700,{"C4_DOC"    , a_Arquivo[I][3] , Nil})  // Documento
        aadd(aMata700,{"C4_QUANT"  , a_Arquivo[I][4] , Nil})  // Quantidade Prevista do Produto
        aadd(aMata700,{"C4_VALOR"  , a_Arquivo[I][5] , Nil})  // Valor Total Previsto
        aadd(aMata700,{"C4_DATA"   , a_Arquivo[I][6] , Nil})  // Data da Previsao
        aadd(aMata700,{"C4_OBS"    , a_Arquivo[I][7] , Nil})  // Observacao da Previsao
 
        //MSExecAuto({|x,y|MATA700(x,y)}, aMata700, 3) 

        MATA700(aMata700,3)

	    If lMsErroAuto
 		    MostraErro()
		    DisarmTransaction()
	        l_End := .T.
            l_Erro:= .T.
	    Endif

	    n_Linha ++
 
   Next i

   End Transaction
   
Endif
                  
if !l_Erro
   MsgInfo("Previsao de Vendas incluida com sucesso !","Aviso")
else
   MsgInfo("Previsao de Vendas não foi importado devido ao erro informado !","Aviso")
endif

RestArea(a_Area)

Return()

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

Static Function AFT001Erro(aTabela)

Local lErro := .F.
Local nI    := 0
Local cMsg  := ""
Local aArea   := GetArea()

for nI := 1 to n_TotReg

    dbSelectArea("SB1")
    dbSetOrder(1)
    if !dbSeek(xFilial("SB1")+aTabela[nI][1])
       cMsg := "Atenção: Produto não cadastrado - Linha "+AllTrim(str(nI+1))+chr(13)+chr(10)
       cMsg += "Verificar e corrigir o conteúdo do arquivo CSV utilizado."
       MessageBox(cMsg,"Erro de Informação",16)
       lErro := .T.
    endif

    dbSelectArea("NNR")
    dbSetOrder(1)
    if !dbSeek(xFilial("NNR")+aTabela[nI][2])
       cMsg := "Atenção: Armazém não cadastrado - Linha "+AllTrim(str(nI+1))+chr(13)+chr(10)
       cMsg += "Verificar e corrigir o conteúdo do arquivo CSV utilizado."
       MessageBox(cMsg,"Erro de Informação",16)
       lErro := .T.
    endif

    dbSelectArea("SC4")
    dbSetOrder(1)
    if dbSeek(xFilial("SC4")+aTabela[nI][1]+DtoS(aTabela[nI][6]))
       dbSeek(xFilial("SC4")+aTabela[nI][1]+DtoS(aTabela[nI][6]))
       while .t.
           if aTabela[nI][1] = C4_PRODUTO .and. aTabela[nI][2] = C4_LOCAL .and. aTabela[nI][3] = C4_DOC .and. aTabela[nI][6] = C4_DATA
              cMsg := "Atenção: Previsão já existe - Linha "+AllTrim(str(nI+1))+chr(13)+chr(10)
              cMsg += "Verificar e corrigir o conteúdo do arquivo CSV utilizado."
              MessageBox(cMsg,"Erro de Informação",16)              
              lErro := .T.
              exit
           endif
           dbSkip()
           if aTabela[nI][1] <> C4_PRODUTO .or. aTabela[nI][6] = C4_DATA           
              exit           
           endif
       enddo
       if lErro
          exit
       endif
    endif
    
Next nI

RestArea(aArea)

Return(lErro)



     
