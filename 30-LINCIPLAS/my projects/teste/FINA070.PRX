#INCLUDE "PROTHEUS.CH"
#INCLUDE "ACADEF.CH"  
#INCLUDE "FWMVCDEF.CH"
#INCLUDE "FWADAPTEREAI.CH"
#INCLUDE "FINA070.CH"

Static lOpenCmc7
Static aPrefixo
Static lFWCodFil := FindFunction("FWCodFil") 
Static lFDataUse
Static __lF070AltV
Static __F070VDATA
Static lFinImp := FindFunction("FRaRtImp")       //Define se ha retencao de impostos PCC/IRPJ no R.A 
// 17/08/2009 - Compilacao para o campo filial de 4 posicoes
// 18/08/2009 - Compilacao para o campo filial de 4 posicoes
 

/*/PMC
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Nome         ³ Data   ³ PMCs ³  Detalhes                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Paulo Augusto ³06/12/05³ 7    ³Documentado o ponto de entrada  F070DCHQ ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³              ³        ³      ³                                         ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ FINA070  ³ Autor ³ Wagner Xavier         ³ Data ³ 26/05/92 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Programa de Baixa de Titulos a Receber                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ FINA070()                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Gen‚rico                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³			ATUALIZACOES SOFRIDAS                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Claudio    ³13/07/00³xxxxxx³ Retirar todas as chamadas a WriteSx2     ³±±
±±³ Claudio    ³25/07/00³XXXXXX³ Criado PE FA070BXL.                      ³±±
±±³ Fernando M.³09/01/01³XXXXXX³ Tratamento taxa da moeda do momento da   ³±±
±±³            ³        ³      ³ baixa do titulo                          ³±±
±±³ Norbert W. ³26/05/05³097731³ -Substituicao da funcao GDDeleted() por  ³±±
±±³            ³        ³      ³ aTail(aCols[?]), pois a GDDeleted() era  ³±±
±±³            ³        ³      ³ utilizada de forma incorreta (nAt da Get ³±±
±±³            ³        ³      ³ Dados diferente de N).                   ³±±
±±³            ³        ³      ³ -Totalizacao de valores apos processament³±±
±±³            ³        ³      ³ os (delecao, inclusao, abertura, etc.)   ³±±
±±³ Dolis      ³28/08/06³106091³ -Atribuicao do valor 0 para a variavel   ³±±
±±³            ³        ³      ³ nValorLiq na funcao FA070titw            ³±±
±±³Fernando A. ³03/07/07³128231³- Tratamento para verificar se o aluno    ³±±
±±³            ³        ³      ³está matriculado em outro curso antes de  ³±±
±±³            ³        ³      ³mudar o status de efetivo para provisorio.³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function FinA070( xAutoCab, nOpc, lNoMbrowse, nOpbaixa, cFiltro )
Local lPrjCni 		:= FindFunction("ValidaCNI") .And. ValidaCNI()
LOCAL aHelpPor		:= {}
LOCAL aHelpEng		:= {}
LOCAL aHelpSpa		:= {}
LOCAL aPergs		:= {}
LOCAL nPosDtCred	:= 0
LOCAL nPos			:= 0
LOCAL lResp			:= .T.
Local lPanelFin := If (FindFunction("IsPanelFin"),IsPanelFin(),.F.)
Local lRet         := .T.

PRIVATE lF070Auto	:= (xAutoCab <> NIL)
PRIVATE aAutoCab	:={}
PRIVATE cPortado	:= CriaVar("E1_PORTADO",.F.)
PRIVATE cBanco		:= CriaVar("E1_PORTADO",.F.)
PRIVATE cAgencia	:= CriaVar("E1_AGEDEP" ,.F.)
PRIVATE cConta		:= CriaVar("E1_CONTA"  ,.F.) 

PRIVATE cNatMov     := iif ( lPrjCni, CriaVar("E1_NATUREZ"  ,.F.)  , '')  

PRIVATE lValidou	:= .F. 
PRIVATE lOracle		:= "ORACLE"$Upper(TCGetDB())

PRIVATE nOldValRec	:= 0
PRIVATE aDadosRef	:= Array(7)
PRIVATE lFini055		:=IsInCallStack("FINI055")     
DEFAULT nOpc		:= 3
DEFAULT lNoMBrowse	:= .F.
DEFAULT nOpBaixa	:= 1 
DEFAULT cFiltro		:= NIL

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Restringe o uso do programa ao Financeiro e Sigaloja			  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !(AmIIn(5,6,12,11,14,41,97,33,49,59,72))		// S¢ Fat,Fin,Loja,Veiculos,Ofina, Pecas, Especiais e PLS, 49-GE, 59-GAC
	Return
Endif



























           





//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Vari veis                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
PRIVATE aRotina := MenuDef()
PRIVATE nValRec := 0
PRIVATE nValTot := 0
PRIVATE nJuros
PRIVATE nMulta
PRIVATE nPIS    	 := 0
PRIVATE nCOFINS    	 := 0
PRIVATE nCSLL    	 := 0
PRIVATE nCM
PRIVATE nDescont
PRIVATE nTotAGer     := 0
PRIVATE nTotADesp    := 0
PRIVATE nTotADesc    := 0
PRIVATE nTotAMul     := 0
PRIVATE nTotAJur     := 0
PRIVATE nValPadrao   := 0
PRIVATE nValEstrang  := 0
PRIVATE cMarca       := Get070Mark()
PRIVATE cLote
PRIVATE cLoteFin     := If(Type("cLoteFin") != "C", Space(TamSX3("E1_LOTE")[1]), cLoteFin)
PRIVATE cNaturLote   := Space (10)
PRIVATE nAcresc      := 0
PRIVATE nDecresc     := 0
PRIVATE aCaixaFin    := xCxFina() // Caixa Geral do Financeiro (MV_CXFIN)
PRIVATE aCols			:= {}
PRIVATE aHeader		:= {}
PRIVATE nMoedaBco	 := 1   
Private nCM1      := 0
Private nProRata  := 0   
Private cCodDiario	:= ""
PRIVATE nVlRetPis	:= 0
PRIVATE nVlRetCof	:= 0
PRIVATE nVlRetCsl	:= 0
PRIVATE aDadosRet := Array(7)
Private nIrrf := 0 
//Variaveis utilizada para acrescimo e decrescimo
Private aBxAcr	:= {} 
Private aBxDec	:= {} 
Private nDecrVlr	:= 0		//tratar visualizacao da varivel na tela de valores  

Private nTxMoeda

LoteCont( "FIN" )

PRIVATE oFontLbl, oFontAnt
PRIVATE lInverte := .F.  

VALOR := 0
        
AjustaSX1()
AjustaHlp()

If !lPanelFin
	SetKey (VK_F12,{|a,b| AcessaPerg("FIN070",.T.)})
Else
	SetKey (VK_F12,{|a,b| PergInPanel("FIN070",.T.)})
EndIf

If FunName() <> "FINA415"
	Pergunte("FIN070",.F.)
EndIf	

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define o cabe‡alho da tela de baixas                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
PRIVATE cCadastro := STR0006 // "Baixa de Titulos"

If lNoMBrowse
	dbSelectArea("SE1")
	If ( nOpc <> 0 ) .And. !Deleted()		
		bBlock := &( "{ |a,b,c,d,e| " + aRotina[ nOpc,2 ] + "(a,b,c,d,e) }" )
		Eval( bBlock, Alias(), (Alias())->(Recno()),nOpc)
	EndIf
Else


	If !lF070Auto
		DEFINE FONT oFontLbl NAME "Arial" SIZE 6, 15 BOLD
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Ponto de entrada para pre-validar os dados a serem  ³
		//³ exibidos.                                           ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		IF ExistBlock("F070BROW")
			ExecBlock("F070BROW",.f.,.f.)
		Endif
	
		If GetNewPar("MV_ACATIVO",.F.)
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Se o GE estiver ativo utilizar a ACmBrowse                     |
			//³ devido aos controles de filtro para restrição de visibilidade  ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			ACmBrowse( 6,1,22,75,"SE1",,,,,,Fa040Legenda("SE1"),,,,,,,,cFiltro)
		Else
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Endere‡a a fun‡„o de BROWSE                                  ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			mBrowse( 6,1,22,75,"SE1",,,,,,Fa040Legenda("SE1"))
		EndIf			
		                       
		If GetMv("MV_CMC7FIN") == "S"
			CMC7Fec(nHdlCMC7,GetMv("MV_CMC7PRT"))
		EndIf
		lOpenCmc7 := Nil
	
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Recupera a Integridade dos dados                             ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		SET KEY VK_F12 to
		RELEASE FONT oFontLbl
	Else  
		dbSelectArea('SE1')
		aAutoCab := SE1->(MSArrayXDB(xAutoCab,nil,4))
		If Len(aAutoCab) == 0
			Return
		EndIf   
		If (nPos := aScan(aAutoCab,{|x| x[1] == 'AUTMOTBX'})) > 0 .AND. (aAutoCab[nPos][2] == 'TRF')
			If (nPos := aScan(aAutoCab,{|x| x[1] == 'AUTVALREC'})) >0
				nPosDtCred := aScan(aAutoCab,{|x| x[1] == 'AUTDTBAIXA'}) 
				aAutoCab[nPos][2] := xMoeda(aAutoCab[nPos][2],SE1->E1_MOEDA,1,aAutoCab[nPosDtCred][2])
			EndIf
		EndIf
		If nOpc == 3
			lRet := fA070Tit("SE1",Recno(),4,,.T.)
		ElseIf nOpc == 5
			fA070Can("SE1",Recno(),5,,nOpbaixa)
		ElseIf nOpc == 6
			fA070Can("SE1",Recno(),6,,nOpbaixa)
		Endif
	EndIf	
Endif
Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ fA070Tit ³ Autor ³ Wagner Xavier         ³ Data ³ 26/05/92 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Fun‡„o utilizada para Baixa de Titulos                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ fA070Tit()                                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Gen‚rico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/        
Function fA070Tit(cAlias,nReg,nOpcx,aM,lAut)
Local lPrjCni := FindFunction("ValidaCNI") .And. ValidaCNI()
LOCAL oDlg
LOCAL oCbx
LOCAL oNumTit
LOCAL oCodCLi
LOCAL aDescMotbx := {}    
LOCAL oMulta
LOCAL oJuros
LOCAL oPIS
LOCAL oCOFINS
LOCAL oCSLL
LOCAL oOtrga
LOCAL oDifCambio
LOCAL nDecrescF
LOCAL nOpt
LOCAL nHdlPrv  :=0
LOCAL nTotal   :=0
LOCAL lPadrao
LOCAL cArquivo
LOCAL lRet     :=.T.
LOCAL nSalvRec := 0
LOCAL cParcela
LOCAL cNum := CRIAVAR ("E1_NUM",.F.)
LOCAL cPrefixo
LOCAL cMoeda
LOCAL nOrdem
LOCAL nT	   :=0
LOCAL nY	   :=0
LOCAL nErro    := 0
LOCAL lBaixou  :=.F.
LOCAL lJuros
LOCAL aCaixaLoja
LOCAL nTolerPg := GetMv("MV_TOLERPG")
LOCAL lContabiliza:=Iif(mv_par04==1,.T.,.F.)
Local lFa070Tit 	:= ExistBlock("FA070TIT")
Local lTFa070Tit 	:= ExistTemplate("FA070TIT")
Local lFa070MDB	:= ExistBlock("FA070MDB")
Local lMdbOk      := .F.
LOCAL aMotBx		:= ReadMotBx()
LOCAL nEstOriginal:= 0
Local cMoedaTx, nA := 0
LOCAL aModalSPB	:=  {"1=TED","2=CIP","3=COMP"}
LOCAL oModSpb
LOCAL lSpbInUse := SpbInUse()
Local oTxMoeda
Local nUltLin
Local bSetKey := {||}
Local oMultNat
Local lMultNat := .F.
Local lOk := .F. //Controla se foi confirmada a distribuicao 
Local aColsSEV := {}
Local NI
Local lFa070Bco 	:= ExistBlock("FA070BCO")    
Local lF070Bxpc     := ExistBlock("F070BXPC") 
Local aArea := GetArea()
Local nTotAdto   := 0
Local lBaixaAbat := .F.
Local nVlrBaixa  := 0
Local lBxCec     := .F.
Local lBxLiq	:= .F.
Local cTipo 
Local cCliente
Local cLoja            
Local aBaixa     := {}
Local x 
Local nLinha := 0
Local aButtons  := {}      
Local lImpBxCr := GetNewPar( "MV_IMPBXCR", "1" ) == "2" 
LOCAL oValorLiq
Local nLin2 := 0 
Local oCM1
Local oProRata
Local lGemInUse := .F.
Local aSeqSe5 := {} // Para gravar a sequencia no SEF com a mesma sequencia dos movimentos bancarios gerados
Local nVlMinImp := GetNewPar("MV_VL10925",5000)
Local aLocal := {} // array utilizado pelo GE para verificar se existe local de prova
Local lPanelFin := If (FindFunction("IsPanelFin"),IsPanelFin(),.F.)
LOCAL oNaturez 
LOCAL oTipo 
LOCAL aDiario := {}
Local aGrvLctPco := {	{"000004","09","FINA070"}, ;
						{"000004","10","FINA070"}  }
Local aFlagCTB := {}
Local lUsaFlag	:= SuperGetMV( "MV_CTBFLAG" , .T. /*lHelp*/, .F. /*cPadrao*/) 
Local lIntDL	:= SuperGetMv("MV_INTDL",.F.,"N") == "S" //-- integracao com Distribuicao e Logistica
Local lAcessMul	:= .T.
Local lAcessJur	:= .T.
Local lAcessDesc	:= .T.
Local lAcessdBaixa := .T.
Local lAcessDtCredito := .T.

Local oDtBaixa 
Local oDtCredito		
Local aCposDes		:= {}
Local nTotMult		:= 0

//Controla o Pis Cofins e Csll na baixa  (1-Retem PCC na Baixa ou 2-Retem PCC na Emissão(default) )
Local lPccBxCr := If (FindFunction("FPccBxCr"),FPccBxCr(),.F.)
Local lEECFAT  := SuperGetMv("MV_EECFAT",.F.,.F.) 
Local lEECFIN  := SuperGetMv("MV_AVG0131",.F.,.F.) //DFS - 17/02/11 - Parâmetro para verificar integração com financeiro.
Local aHdlPrv	:= {} 

Local lIrPjBxCr		:= If (FindFunction("FIrPjBxCr"),FIrPjBxCr(),.F.) //Controla IRPJ na baixa
Local oIrrf

Local lTipBxCP   := .F.
Local lSigaloja  := .F.

Local cMvJurTipo := SuperGetMv("MV_JURTIPO",,"")  // calculo de Multa do Loja , se JURTIPO == L
Local lLojxRMul  := FindFunction("LojxRMul") 
Local lMulLoj	  := SuperGetMv("MV_LJINTFS", ,.F.) //Calcula multa conforme regra do loja, se integração com financial estiver habilitada
Local lF070VLAD  := ExistBlock("F070VLAD")

Local lSubsPrv := IsInCallStack("FINA040")   
Local lFA070BLQ := ExistBlock("FA070BLQ")
Local lLibCm	:= .F.
Local lVlTitCR := GetNewPar("MV_VLTITCR",.F.)
Local lTpDesc		:= SE5->(FieldPos("E5_TPDESC"))	> 0 //Verifica campo TPDESC na tabela SE5 (<C>ondicional ou <I>ncondicional)
Local	lNatApura	:=	.F. //Natureza configurada para apurar impostos no SPED PIS/COFINS.                                    
Local lCposSped	:=	 (SED->(FieldPos("ED_APURCOF")) > 0 .And. SED->(FieldPos("ED_APURPIS")) > 0) //Campos que apuram impostos no SPED PIS/COFINS.
Local aAreaSED 	:= {}

PRIVATE lRaRtImp  := lFinImp .And.FRaRtImp()     //Define se ha retencao de impostos PCC/IRPJ no R.A 
PRIVATE nParciais := 0
PRIVATE aBaixaSE5 := {}
PRIVATE cMotBx
PRIVATE oVlEstrang
PRIVATE oValrec
PRIVATE oCM
PRIVATE oAgencia,oBanco,oConta
PRIVATE oDescont
PRIVATE nOtrga       := 0
PRIVATE nDifCambio   := 0
PRIVATE aTxMoedas	  := {}
PRIVATE cModSpb		:= "1"        
PRIVATE nAcrescF
//Variaveis PRIVATE utilizadas pela funcao FA040AxAlt()
PRIVATE nIndexSE1   := ""
PRIVATE cIndexSE1   := ""
PRIVATE lAltera     := .T.
PRIVATE nOldValor   := SE1->E1_VALOR
PRIVATE nOldIrrf    := SE1->E1_IRRF
PRIVATE nOldIss     := SE1->E1_ISS
PRIVATE nOldInss    := SE1->E1_INSS
PRIVATE nOldPis     := SE1->E1_PIS
PRIVATE nOldCofins  := SE1->E1_COFINS
PRIVATE nOldCsll    := SE1->E1_CSLL
PRIVATE nOldVlAcres := SE1->E1_ACRESC
PRIVATE nOldVlDecres:= SE1->E1_DECRESC                          
PRIVATE lAlterNat   := .F.
PRIVATE nOldVencto  := SE1->E1_VENCTO
PRIVATE nOldVenRea  := SE1->E1_VENCREA
PRIVATE cOldNatur	:= SE1->E1_NATUREZ
PRIVATE nOldVlCruz	:= SE1->E1_VLCRUZ
PRIVATE lAlterImp   := .F.
PRIVATE aDadosRet	  := {}
PRIVATE nSomaCheq := 0  
Private nIrrf := 0
PRIVATE nOldDescont := 0
PRIVATE nOldMulta := 0
PRIVATE nOldJuros := 0 
PRIVATE lTitLote  :=.F.
Private cTpDesc := "I"                
//Variaveis utilizada para acrescimo e decrescimo
aBxAcr	:= {} 
aBxDec	:= {} 
nDecrVlr	:= 0		//tratar visualizacao da varivel na tela de valores  

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Caso tenha seja um titulo gerado pelo SIGAEIC ou SIGAEEC não poderá sofrer baixa através desta rotina ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If GetMV("MV_EASYFIN") == "S" .And. UPPER(Alltrim(SE1->E1_ORIGEM)) $ "SIGAEIC"
	HELP(" ",1,"FAORIEIC")
	Return      
Endif

//If lEECFAT .And. UPPER(Alltrim(SE1->E1_ORIGEM)) $ "SIGAEEC" //DFS - A integração do financeiro independe da integração com o faturamento
// TDF - 26/12/11 - Acrescentado o módulo EFF para permitir liquidação
If lEECFIN .And. UPPER(Alltrim(SE1->E1_ORIGEM)) $ "SIGAEEC" .AND. !(cModulo $ "EEC/EDC/ECO/EFF") //DFS - 17/02/11 - Trava para outros módulos para títulos gerados no EEC 
   F070Help()
   HELP(" ",1,"FAORIEEC")                                                           
   Return
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Caso titulos originados pelo SIGALOJA estejam nas carteiras :  ³
//³I = Carteira Caixa Loja                                        ³
//³J = Carteira Caixa Geral                                       ³
//³Nao permitir esta operacao, pois ele precisa ser transferido   ³
//³antes pelas rotinas do SIGALOJA.                               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Upper(AllTrim(SE1->E1_SITUACA)) $ "I|J" .AND. Upper(AllTrim(SE1->E1_ORIGEM)) $ "LOJA010|LOJA701|FATA701"
	Help(" ",1,"NOUSACLJ")
	Return
Endif

//Caso a rotina esteja cadastrada no adapter, so pode ser enviada como 'Sincrona'. Uma baixa enviada como assincrona 
//sera concretizada mesmo que de erro no sistema integrado.
If !lFini055
	If !(FA070Integ(.F.))
		Return .F.
	Endif
Endif
// Zerar variaveis para contabilizar os impostos da lei 10925.
VALOR5 := 0
VALOR6 := 0
VALOR7 := 0                   

PIS			:= 0
COFINS		:= 0
CSLL		:= 0
IRRF		:= 0

cTpDesc	:= "I"	
lF415Auto := IIf(Type("lF415Auto")=="U",.F.,lF415Auto)		// Sergio Fuzinaka - 05.06.02
cPortado  := IIf(Type("cPortado")=="U",CriaVar("E1_PORTADO",.F.),cPortado)
cBanco 	 := IIf(Type("cBanco")=="U",CriaVar("E1_PORTADO",.F.), cBanco)
cAgencia  := IIf(Type("cAgencia")=="U",CriaVar("E1_AGEDEP" ,.F.),cAgencia)
cConta	 := IIf(Type("cConta")=="U",CriaVar("E1_CONTA"  ,.F.),cConta)

If mv_par10 == 1 .And. FunName() == "FINA740"
	cPortado	:= cPorta740
	cBanco 		:= cBanco740
	cAgencia	:= cAgenc740
	cConta		:= cConta740	
EndIf	

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se utiliza o modulo de Gerenciamento Academico.     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If GetNewPar("MV_ACATIVO", .F.)
	JA1->(dbSetOrder(8))

	If JA1->(MsSeek(xFilial("JA1") + SE1->E1_PREFIXO + SE1->E1_NUM))
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica se existe Local associado ao Processo Seletivo. ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		JA9->(dbSetOrder(1))
		JA9->(MsSeek(xFilial("JA9") + JA1->JA1_PROSEL))

		If JA9->(!Found())
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ O titulo somente podera ser baixado quando houver um local associado ao ³
			//³ Processo Seletivo.                                                      ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			Help(" ",1,"ACAA070_11")

			Return .F.
		EndIf
		
		If JA1->( FieldPos( "JA1_TIPREL" )) > 0
			aLocal := Aca070Lugar( JA1->JA1_CODINS,JA1->JA1_LOCAL,,,JA1->JA1_PROSEL,JA1->JA1_TPCAND,,,JA1->JA1_TIPDEF,JA1->JA1_TIPREL)
		Else
			aLocal := Aca070Lugar( JA1->JA1_CODINS,JA1->JA1_LOCAL,,,JA1->JA1_PROSEL,JA1->JA1_TPCAND)			
		Endif
        
		If Empty(aLocal)
			Return .F.
		EndIf
	EndIf
EndIf

nOpc1    := 0
If cPaisLoc <> "BRA"
   aAdd(aTxMoedas,{"",1,PesqPict("SM2","M2_MOEDA1")})
   For nA	:=	2 To MoedFin()
	  cMoedaTx	:=	Str(nA,IIf(nA <= 9,1,2))
	  If !Empty(GetMv("MV_MOEDA"+cMoedaTx))
	  	If lF070Auto .And. nA==SE1->E1_MOEDA
			 aAdd(aTxMoedas,{GetMv("MV_MOEDA"+cMoedaTx),SE1->E1_TXMOEDA,PesqPict("SM2","M2_MOEDA"+cMoedaTx) })
		Else
			 aAdd(aTxMoedas,{GetMv("MV_MOEDA"+cMoedaTx),RecMoeda(dDataBase,nA),PesqPict("SM2","M2_MOEDA"+cMoedaTx) })
		Endif
	  Else
		 Exit
	  Endif
   Next
   nTotAGer     := 0
   nTotADesp    := 0
   nTotADesc    := 0
   nTotAMul     := 0
   nTotAJur     := 0
   cMarca       := Get070Mark()
   cLoteFin     := Space(TamSX3("E1_LOTE")[1])
EndIf   
  			
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ PONTO DE ENTRADA 																	³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If (ExistBlock( "FA070CHK" ) )
	If !(ExecBlock("FA070CHK",.F.,.F.))
		Return .F.
	EndIf
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ PONTO DE ENTRADA TEMPLATE	  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If (ExistTemplate( "FA070CHK" ) )
	If !(ExecTemplate("FA070CHK",.F.,.F.))
	
		// Indica que houve um erro ao executar por rotina automatica, para tratamento externo
		If Type('lF070Auto') == 'L' .And. lF070Auto
			lMsErroAuto := .T.
		Endif	
		
		Return .F.
	EndIf
Endif

IF ExistBlock("F070MNAT")
	lMultNat := ExecBlock("F070MNAT",.F.,.F.)
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Monta Hist¢rico da Baixa para digita‡„o pelo usu rio                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private cHist070 := Criavar("E5_HISTOR")        //Inicilizador padrao

If Empty(cHist070)
	cHist070 := STR0007+Space(Len(cHist070)-24)  // "Valor recebido s/ T¡tulo"
Endif

cMotBx := criavar("E5_MOTBX")
IF lAut=NIL
	lAut:=.F.
EndIF

*ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
*³Salva ordem atual                                                     ³
*ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nOrdem:=IndexOrd()
dbSetOrder(1)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Cria as vari veis utilizadas para receber os dados do t¡tulo          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dDtCredito  := dDataBase
If Alltrim(SE1->E1_ORIGEM) == "LOJA010"
	aCaixaLoja  := xCxLoja()
	cPortado    := SE1->E1_PORTADO
	cBanco      := SE1->E1_PORTADO
	cAgencia    := SE1->E1_AGEDEP
	cConta      := SE1->E1_CONTA
Else
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³aCaixaFin conter  os dados Bco/Age/Cta do Caixa Geral, caso o titulo  ³
	//³esteja em carteira (SITUACA == 0).                                    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	// 0 = Carteira
	// F = Carteira Protesto
	// G = Carteira Acordo
	If mv_par10 == 2 .or. Empty(cBanco)
		cPortado    := IIF(SE1->E1_SITUACA $ "0FG", aCaixaFin[1] ,SE1->E1_PORTADO)
		cBanco      := IIF(SE1->E1_SITUACA $ "0FG", aCaixaFin[1] ,SE1->E1_PORTADO)
		cAgencia    := IIF(SE1->E1_SITUACA $ "0FG", aCaixaFin[2] ,SE1->E1_AGEDEP)
		cConta      := IIF(SE1->E1_SITUACA $ "0FG", aCaixaFin[3] ,SE1->E1_CONTA)
	EndIf	
EndIf

//Obtem a moeda do banco
nOrdSA6:=SA6->(IndexOrd())
DbSetOrder(1)
SA6->(MsSeek(xFilial("SA6")+cBanco+cAgencia+cConta))
nMoedaBco:= Max(SA6->A6_MOEDA,1)
SA6->(DbSetOrder(nOrdSA6))


//
// Eh um titulo gerado pelo template GEM ?
//
If HasTemplate("LOT") .and. ExistTemplate("GEMSE1LIX")
	lGemInUse := ExecTemplate("GEMSE1LIX",.F.,.F.)
EndIf 
If !SoftLock( "SE1" )
	Return
EndIf
// Verifica integracao com PMS e nao permite alteracao de titulos que tenham solicitacoes
// de transferencias em aberto.
If !( Alltrim(Upper(FunName())) == "FINA630" .or. (Type("lF630Auto")=="L" .and.  lF630Auto) ) .And. !Empty(SE1->E1_NUMSOL)
	HELP(" ",1,"FIN62003")
	Return
Endif

// Nao permitir baixar titulos de adiantamento relacionados a pedido
#IFDEF TOP
	If cPaisLoc == "BRA" .and. AliasInDic("FIE")
		If FinAdtSld( "R", SE1->( E1_CLIENTE + E1_LOJA + E1_PREFIXO + E1_NUM + E1_PARCELA + E1_TIPO ) )
			Help(" ",1,"ADTXPED",,STR0224,1,0) //"Adiantamento relacionado a um pedido. Somente poderá ser utilizado no relacionamento com pedidos."
			Return(.F.)
		Endif
	Endif
#ENDIF	

dBaixa      := CriaVar("E1_BAIXA")
nTxMoeda 	:= If(SE1->E1_MOEDA > 1, If(SE1->E1_TXMOEDA > 0, SE1->E1_TXMOEDA,RecMoeda(dBaixa,SE1->E1_MOEDA)),0)


// Se estiver utilizando CMC7, abre a porta para cadastro do cheque recebido.
If lOpenCmc7 == Nil .And. !lAut .And. GetMv("MV_CMC7FIN") == "S" 
	OpenCMC7()
	lOpenCmc7 := .T.
Endif

//Botao de recalculo dos impostos
If !lF070Auto .And. lImpBxCr .and. (SE1->E1_MULTNAT != "1" .or. (SE1->E1_MULTNAT == "1" .AND. F070RTMNBL()))
	AADD(aButtons, {"SIMULACAO", {|| FaCalcImp(.T.)}, STR0195 ,"Impostos" }) //"Recálculo dos Impostos"
EndIf

//Botao de Cheques no Painel Financeiro
AADD(aButtons, {"LIQCHECK", {|| CadCheqCR(cBanco,cAgencia,cConta,nValRec,dBaixa,1)}, STR0141 }) //"Cheques"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica os botoes de usuarios.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ExistBlock("FA070BTN")
	aButtons:= ExecBlock("FA070BTN",.F.,.F.,{aButtons})
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica os botoes de usuarios no Template.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If HasTemplate("LOT") .and. ExistTemplate("FA070BTN")
	aButtons := ExecTemplate("FA070BTN",.F.,.F.,{aButtons})
EndIf                                                                               

//Ponto de entrada para desabilitar campos de Multa, Juros ou Descontos, data de baixa, data de credito
If ExistBlock("F070DCNB")
	aCposDes:=ExecBlock("F070DCNB",.F.,.F.)
	If Len(aCposDes) > 0
		IF (nT := ascan(aCposDes,'MULTA')) > 0
			lAcessMul := .F.
		Endif
		IF (nT := ascan(aCposDes,'DESCONTO')) > 0
			lAcessDesc := .F.
		Endif
		IF (nT := ascan(aCposDes,'JUROS')) > 0
			lAcessJur := .F.
		Endif
		IF (nT := ascan(aCposDes,'DATABAIXA')) > 0	
			lAcessdBaixa := .F.
		Endif		
		IF (nT := ascan(aCposDes,'DATACREDITO')) > 0	
			lAcessDtCredito := .F.
		Endif		
	Endif
Endif

//verifica se o titulo é da integração Protheus X Tin, caso afirmativo, não e permitido alterar os valores
If AllTrim(SE1->E1_ORIGEM)=="FINI055"  .And. !lF070Auto .And. SuperGetMv("MV_ITLBCPO",,.F.) == .F.
	lAcessMul := .F.
	lAcessDesc := .F.
	lAcessJur := .F.
	lAcessdBaixa := .F.
	lAcessDtCredito := .F.
Elseif AllTrim(SE1->E1_ORIGEM)=="FINI055"  .And. !lF070Auto .And. SuperGetMv("MV_ITLBCPO",,.F.) == .T.
	lAcessMul := .T.
	lAcessDesc := .T.
	lAcessJur := .T.
	lAcessdBaixa := .T.
	lAcessDtCredito := .T. 
Endif

While .T.
   nOpc1			:= 0
	nJuros      := 0
	nPIS	    	:= 0
	nCOFINS    	:= 0	
	nCSLL    	:= 0	
	nVlRetPis	:= 0
	nVlRetCof	:= 0
	nVlRetCsl	:= 0	
	nMulta      := 0
	nCM         := 0
	nDescont    := 0
	nValRec     := 0
	nValEstrang := 0
	nParciais   := 0
	aBaixaSE5   :={}
	dBaixa      := CriaVar("E1_BAIXA")
	nAcrescF    := SE1->E1_SDACRES 
	nDeCrescF	:= SE1->E1_SDDECRE  + nDecrVlr 

	If ExistBlock("F070ACRE")
		ExecBlock("F070ACRE",.F.,.F.)
	EndIf

	lNatApura	:=	.F.	
	aAreaSED 	:= SED->(GetArea())
	DbSelectArea("SED")							
	DbSetOrder(1)			
	If DbSeek(xFilial("SED")+ SE1->E1_NATUREZ) .And. lCposSped
		If (!Empty(SED->ED_APURCOF) .Or. !Empty(SED->ED_APURPIS))
			lNatApura	:=	.T. //Natureza configurada para apurar impostos no SPED PIS/COFINS.
		Endif	
	Endif
   RestArea(aAreaSED)

	nAcresc     := Round(NoRound(xMoeda(nAcrescF,SE1->E1_MOEDA,nMoedaBco,dBaixa,3,nTxMoeda),3),2)
	nDecresc    := Round(NoRound(xMoeda(nDeCrescF,SE1->E1_MOEDA,nMoedaBco,dBaixa,3,nTxMoeda),3),2)
	nCM1        := 0
	nProRata    := 0

	*ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	*³Verifica se o T¡tulo j  foi Baixado Totalmente                      ³
	*ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	IF SE1->E1_SALDO = 0
		Help(" ",1,"TITBAIXADO")
		MsUnlock()
		Exit
	EndIF
	
	If lVlTitCR .And. !(SE1->E1_TIPO $ MVRECANT+"/"+MV_CRNEG) 
		aAreaSE1:=SE1->(GetArea())
		nBusca := F070BuscCR( "SE1", SE1->E1_CLIENTE, SE1->E1_LOJA )
		If nBusca <> 0
			lAD := .T.
			cMsg := STR0241 //"O Cliente deste titulo possui "
			Do Case 
				Case nBusca = 1 // Recebimento Antecipado
					cMsg += STR0242 //"Recebimento(s) Antecipado(s)."
				Case nBusca = 2 // NCC
					cMsg += STR0243 //"titulo(s) de credito."
			End Case
			cMsg += chr(13)+chr(10)
			cMsg += STR0244 //"Deseja mesmo assim baixa-lo ?"
			If isBlind()
				If lF070VLAD
					If !(ExecBlock("F070VLAD",.F.,.F.))
						Return .F.
					Endif
				Endif
			Else
				If !MsgYesNo( cMsg )
					Return .F. /*Function fA070Tit*/
				Endif
			Endif
		Endif
		RestArea(aAreaSE1)
	Endif
	
	
	*ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	*³Verifica se o tipo de calculo de juros é igual (L)loja ou Indicacao do calculo de Multa do Loja, calcula a multa      ³
	*ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If cMvJurTipo == "L" .OR. lMulLoj
       	

		*ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		*³ Calcula o valor da Multa  :funcao LojxRMul :fonte Lojxrec          ³
		*ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If lLojxRMul
		  nMulta := LojxRMul( , , ,SE1->E1_SALDO, SE1->E1_ACRESC, SE1->E1_VENCREA, dDtCredito , , SE1->E1_MULTA, ,;
		  					 SE1->E1_PREFIXO, SE1->E1_NUM, SE1->E1_PARCELA, SE1->E1_TIPO, SE1->E1_CLIENTE, SE1->E1_LOJA, "SE1" )   
		Else
		  nMulta := 0
		Endif


	Endif

	*ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	*³Verifica se ‚ um registro Principal                                 ³
	*ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	IF SE1->E1_TIPO $ MVABATIM+"/"+MVIRABT+"/"+MVINABT+"/"+MVFUABT //adicionado MVFUABT pois a variável MVABATIM não está retornando FU-
		Help(" ",1,"NAOPRINCIP")
		MsUnlock()
		Exit
	End

	*ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	*³Verifica se ‚ um t¡tulo provis¢rio                                    ³
	*ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	IF SE1->E1_TIPO $ MVPROVIS .AND. !lSubsPrv
		Help(" ",1,"TITULOPROV")
		MsUnlock()
		Exit
	EndIF

	nSalvRec  := RecNO()
	cNum      := SE1->E1_NUM
	cPrefixo  := SE1->E1_PREFIXO
	cParcela  := SE1->E1_PARCELA
	cTipo     := SE1->E1_TIPO
	cCliente  := SE1->E1_CLIENTE
	cLoja     := SE1->E1_LOJA
	nTotAbat  := 0
	nTotAbImp := 0
	nTotAbLiq := 0
	nValorLiq := 0     
	nValPadrao:= 0
	nTotAbat  := SumAbatRec(cPrefixo,cNum,cParcela,SE1->E1_MOEDA,"S",dBaixa,@nTotAbImp,,,,,,, nTxMoeda) 
	nTotAbLiq := nTotAbat - nTotAbImp
	dbGoto(nSalvRec)
	cMoeda := IIF(Empty(SE1->E1_MOEDA),"1",AllTrim(Str(SE1->E1_MOEDA,2)))

	*ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	*³Recebe os dados do t¡tulo a ser baixado                               ³
	*ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	SA1->(MsSeek(xFilial("SA1")+SE1->E1_CLIENTE+SE1->E1_LOJA))
	cTitulo 		:= SE1->E1_PREFIXO + " " + SE1->E1_NUM+ " " + SE1->E1_PARCELA
	cSituacao 	:= SE1->E1_SITUACA + " " + fa070situa()
	cDescMoeda 	:= SubStr(GetMV("MV_SIMB"+cMoeda),1,3)
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Para que o valor da baixa parcial nao fique negativo, verifico o saldo³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If (SE1->E1_VALOR > SE1->E1_SALDO) .And. Empty(SE1->E1_TIPOLIQ)
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Procura pelas baixas deste titulo ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		lTipBxCP:=lRaRtImp
		aBaixa := Sel070Baixa( "VL /V2 /BA /RA /CP /LJ /" + MV_CRNEG, cPrefixo, cNum, cParcela, cTipo,; 
			@nTotAdto, @lBaixaAbat, cCliente, cLoja, @nVlrBaixa, , @lBxCec, @lBxLiq ,@lSigaloja, @lTipBxCP)
		For x := 1 To Len(aBaixaSE5)
			nParciais += aBaixaSE5[x][8]
   		If lPccBxCR .And. lRaRtImp .And. nParciais > nVlMinImp
			   nParciais += aBaixaSE5[x][18]+aBaixaSE5[x][19]+aBaixaSE5[x][20]+aBaixaSE5[x][30]// somar impostos PCC 
			Elseif lIrPjBxCr .And. lRaRtImp  
		  		nParciais += aBaixaSE5[x][30]
			Endif   
			nTotMult	 += (aBaixaSE5[x][14]+aBaixaSE5[x][15])  // Soma Acrescimo mais Multa   
			If lRaRtImp
		 		nParciais += aBaixaSE5[x][32]+aBaixaSE5[x][33]
		 		nTotAbat  -= aBaixaSE5[x][32]+aBaixaSE5[x][33]
			Endif
		Next
		nParciais += nTotAdto
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Soma valor de decrescimo em baixas parciais, para evitar         ³
		//³ diferencas entre valor original e valor recebido                 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If SE1->E1_SDDECRE <> SE1->E1_DECRESC
			nParciais += ( SE1->E1_DECRESC - SE1->E1_SDDECRE )
		EndIf
	Else
		nParciais 	:= SE1->E1_VALOR-SE1->E1_SALDO
	Endif
	
	If "RA" $ SE1->E1_TIPO
		nParciais 	:= SE1->E1_VALOR-SE1->E1_SALDO
	Endif	

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Array aDescMotbx contendo apenas a descricao do motivo das Baixas. 	 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If Len( aDescMotbx ) == 0
		For nI := 1 to len( aMotBx )
			If SubStr(aMotBx[nI],34,01) == "A" .or. SubStr(aMotBx[nI],34,01) =="R"
				If !(substr(aMotBx[nI],01,03) $ "FAT|LOJ|LIQ|CEC|CMP|STP") 
					AADD( aDescMotbx,SubStr(aMotBx[nI],07,10))
				EndIf
			EndIf
		Next nI
	EndIf

	// Carrega varivael cmotbx para sua verifcacao na funcao fa080totmes()
	cMotBx		:= aDescMotBx[1] 	//NORMAL
	
	fA070Data(@nTxMoeda,.F.) // Calcula o desconto e o juros (se houver) e valida a data
	F070Ret()

	nOldValRec	:= nValRec	

 	If	lIrPjBxCr .OR. (lRaRtImp .and. SE1->E1_TIPO $ MVRECANT)
  		nParciais += nIrrf
		nValRec := SE1->E1_VALOR - (nParciais - nTotMult)
   		nIrrf:=FCaIrBxCR(nValRec,,(SE1->E1_VALOR <> SE1->E1_SALDO.AND. lRaRtImp))
	EndIf     	
	If lPccBxCR 
	    If SE1->E1_MOEDA > 1
			nValRec:=xMoeda(SE1->E1_VALOR,SE1->E1_MOEDA,1,dDatabase,3,nTxMoeda) 
			nValRec:=nValRec - (nParciais - nTotMult)
        Else
        	nValRec := SE1->E1_VALOR - (nParciais - nTotMult) // (nTotMult = pagamento de multas nas baixas efetuadas anteriormente). Se fizermos 2 baixas parciais com multa alta, o valor SE1->E1_VALOR - nParciais será negativo
        	nOldValRec	:= nValRec
        EndIf
        If Type("aAutoCab") == "A" .and. ((nPos := aScan(aAutoCab,{|x| x[1] == 'AUTMOTBX'})) > 0 .AND. (aAutoCab[nPos][2] == 'TRF'))
			aAdd(aDadosRet, 0)
		Else
			f070TotMes(dBaixa,.T.,,,,nTxMoeda)   
		EndIf
	Endif	

	*ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	*³Pr‚-inicializa o valor recebido.                                   ³
	*ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cMotBx		:= aDescMotBx[1] 	//NORMAL

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³PONTO DE ENTRADA FA070POS  ³
	//³                           ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Permite a altera‡„o de vari veis apos carga de dados do t¡tulo a ser ³
	//³ baixado, antes das informa‡äes serem mostradas na Tela.              ³
	//³ Vari veis dispon¡veis para serem alteradas :                         ³
	//³                                                                      ³
	//³ cBanco , cAgencia, cConta, cCheque                                   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	// Dourado 31/08/2000 criacao do ponto de entrada na mesma
	// posicao que foi criado no FINA080A.PRX (FA080POS), para a
	// inclusao da taxa do dolar do momento.
	// Carlao. 23/11/2005 - Inclusao de ponto de entrada de template

	// Template GEM
	If HasTemplate("LOT") .and. ExistTemplate("FA070POS")
		ExecTemplate("FA070POS",.F.,.F.)
	Endif
	If ExistBlock("FA070POS")
		ExecBlock("FA070POS",.F.,.F.)
	Endif
	
	aColsSEV := {}
   
	fa070val( nValrec, nTxMoeda )
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Pr‚-inicializa a modalidade de SPB                                 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lSpbInUse
		If !Empty(SE1->E1_MODSPB)
			cModSpb := SE1->E1_MODSPB
		Else
		   cModSpb := "1"
		Endif		
	Endif  
	
	If lFA070BLQ
	     lLibCm := ExecBlock("FA070BLQ",.F.,.F.)
    Endif    
    
   	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ SIGAPFS‚ A cotação para baixa dos títulos no modulo jurídico deve ser sempre a cotação diária.  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If UPPER(Alltrim(SE1->E1_ORIGEM)) $ "JURA203"  
  		nTxMoeda := If(SE1->E1_MOEDA > 1, RecMoeda(dBaixa,SE1->E1_MOEDA), 0)
	EndIf  
    

	If Type('lF070Auto') =='U' .or. ! lF070Auto
		bSetKey := SetKey(VK_F4,{|| If( !SE1->E1_TIPO $ MV_CRNEG,CadCheqCR(cBanco,cAgencia,cConta,nValRec,dBaixa,1),.F.)})
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Recebe os dados do t¡tulo a ser baixado                               ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If ( cPaisLoc=="CHI" )
			DEFINE FONT oFontLbl NAME "Arial" SIZE 6,15 BOLD
			DEFINE MSDIALOG oDlg FROM  69,33 TO 530,581 TITLE STR0103 PIXEL OF oMainWnd  //"Baixas a Receber"
		Else			
			DEFINE FONT oFontLbl NAME "Arial" SIZE 6, 15 BOLD
			nLin2 := If(cPaisLoc=="BRA",602,520)
			// Template GEM, nova linha do rodape para os campos especificos do template.
			If lGemInUse
				nLin2 += 24
			EndIf
			
		    DEFINE MSDIALOG oDlg FROM  69,33 TO nLin2,581 TITLE STR0103 PIXEL OF oMainWnd  //"Baixas a Receber"
		EndIf   
		oPanel1 := TPanel():New(0,0,'',oDlg,, .T., .T.,, ,45,45,.f.,.f. )
		oPanel1:Align := CONTROL_ALIGN_TOP

		oPanel2 := TPanel():New(0,0,'',oDlg,, .T., .T.,, ,30,30,.f.,.f. )
		oPanel2:Align := CONTROL_ALIGN_ALLCLIENT

		@ 001,002 GROUP oGrp1 TO 043, 272 LABEL STR0008 OF oPanel1 PIXEL //"Principal"
		@ 001,002 GROUP oGrp2 TO If(cPaisLoc=="BRA",205,165), 133 LABEL STR0009 OF oPanel2  PIXEL //"Dados Gerais"
		@ 001,139 GROUP oGrp3 TO If(cPaisLoc=="BRA",205,165), 272 LABEL STR0010 OF oPanel2  PIXEL //"Valores da Baixa"
		oGrp1:oFont := oFontLbl
		oGrp2:oFont := oFontLbl
		oGrp3:oFont := oFontLbl

		//////////////////////////
		//Dados do titulo
		@ 008,004 SAY STR0211				SIZE 31,07 OF oPanel1 PIXEL //"Prefixo"
		@ 008,027 MSGET SE1->E1_PREFIXO	SIZE 25,08 OF oPanel1 PIXEL When .F.
		@ 008,060 SAY STR0212 				SIZE 31,07 OF oPanel1 PIXEL //"Número"
		@ 008,085 MSGET SE1->E1_NUM		SIZE 70,08 OF oPanel1 PIXEL When .F.
		@ 008,165 SAY STR0213				SIZE 31,07 OF oPanel1 PIXEL //"Parcela"
		@ 008,188 MSGET SE1->E1_PARCELA	SIZE 25,08 OF oPanel1 PIXEL When .F.
		@ 008,220 SAY STR0214				SIZE 31,07 OF oPanel1 PIXEL //"Tipo"
		@ 008,238 MSGET oTipo VAR cTipo	F3 "SE1RDO" SIZE 30,08 OF oPanel1 PIXEL HASBUTTON
		oTipo:lReadOnly := .T.
		
		@ 019,004 SAY STR0014 SIZE 22, 07 OF oPanel1 PIXEL //"Cliente"
		@ 019,027 MSGET oCodCli VAR SE1->E1_CLIENTE F3 "SA1" SIZE 65,08 OF oPanel1 PIXEL HASBUTTON //READONLY 
		oCodCli:lReadOnly := .T.
		@ 019,105 MSGET SA1->A1_NOME SIZE 165,08 OF oPanel1 PIXEL When .F.

		@ 030,004 SAY STR0052 				SIZE 31,07 OF oPanel1 PIXEL //"Natureza"
		@ 030,027 MSGET oNaturez VAR SE1->E1_NATUREZ	F3 "SED" SIZE 70,08 OF oPanel1 PIXEL HASBUTTON
		oNaturez:lReadOnly := .T.
		@ 030,105 SAY STR0012 				SIZE 31,07 OF oPanel1 PIXEL //"Emiss„o"
		@ 030,133 MSGET SE1->E1_EMISSAO	SIZE 48,08 OF oPanel1 PIXEL When .F.
		@ 030,189 SAY STR0013 				SIZE 49,07 OF oPanel1 PIXEL //"Vencto.Atual"
		@ 030,222 MSGET SE1->E1_VENCREA	SIZE 48,08 OF oPanel1 PIXEL When .F.

      
		//////////////////////////
		//Dados Gerais		

		nUltLin := 10
		@ nUltLin,005 SAY STR0015 SIZE 39, 07 OF oPanel2 PIXEL //"Hist.Emiss„o"
		@ nUltLin,065 MSGET SE1->E1_HIST       SIZE 65, 08 OF oPanel2 PIXEL When .F.

		nUltLin += 12
		@ nUltLin,005 SAY STR0016 SIZE 35, 07 OF oPanel2 PIXEL //"Situa‡„o"
		@ nUltLin,065 MSGET cSituacao          SIZE 65, 08 OF oPanel2 PIXEL When .F.

		nUltLin += 12	
		@ nUltLin,005 SAY STR0023 SIZE 32, 07 OF oPanel2 PIXEL //"Mot.Baixa"        


		if lPrjCni
			@ nUltLin,065 MSCOMBOBOX oCbx VAR cMotBx ITEMS aDescMotBx SIZE 65, 47 OF oPanel2 PIXEL ;
															ON CHANGE oBanco:lReadOnly := SE1->E1_SITUACA$"27" .or. !MovBcobx(cMotBx, .T.) ;
															VALID fa070BDev(oJuros, oMulta, oDescont, oCm, nTxMoeda, oCbx)	.and. ;
															IIF(lFA070MDB,lMdbOk:=ExecBlock("FA070MDB",.F.,.F.),.T.) .and. FA070VlNat(cMotBx)

		Else
			@ nUltLin,065 MSCOMBOBOX oCbx VAR cMotBx ITEMS aDescMotBx SIZE 65, 47 OF oPanel2 PIXEL ;
															ON CHANGE oBanco:lReadOnly := SE1->E1_SITUACA$"27" .or. !MovBcobx(cMotBx, .T.) ;
															VALID fa070BDev(oJuros, oMulta, oDescont, oCm, nTxMoeda, oCbx)	.and. ;
															fA070Val(nValRec,nTxMoeda,(!Empty(oCbx) .AND. oCbx:lModified)) .And. ;
															IIF(lFA070MDB,lMdbOk:=ExecBlock("FA070MDB",.F.,.F.),.T.)
		EndIf
	
		nUltLin += 12
		@ nUltLin,005 SAY STR0017 SIZE 32, 07 OF oPanel2 PIXEL //"Banco"
		@ nUltLin,065 MSGET oBanco var cBanco  SIZE 65, 08 OF oPanel2 PIXEL F3 "SA6" ;
				Valid (AtulValidou() .And. !MovBcobx(cMotBx, .T.) .and. Empty(cBanco)) .or. ;
						(IiF(lFa070Bco,ExecBlock("FA070BCO",.F.,.F.),.T.) .And.;
							F070VldBco(cBanco,@cAgencia,@cConta,.T.,.T.) .And. f070AltBco(nTxMoeda,oJuros, oMulta, oDescont, oCm, oBanco,nValRec) .And. Iif( !Empty(oBanco) .and. oBanco:lModified, F070CnvPcc(nTxMoeda, SE1->E1_MOEDA), .T. ) , oPanel2:Refresh()   ) HASBUTTON
		oBanco:lReadOnly := (SE1->E1_SITUACA$"27" .OR. !MovBcobx(cMotBx, .T.))

		nUltLin += 12
		@ nUltLin,005 SAY STR0018 SIZE 32, 07 OF oPanel2 PIXEL //"Agˆncia"
		@ nUltLin,065 MSGET oAgencia var cAgencia  SIZE 65, 08 OF oPanel2 PIXEL Valid ;
							If(!lValidou,If(F070VldBco(cBanco,cAgencia,@cConta,.T.,.T.,cAgencia) .AND. ;
							f070AltBco(nTxMoeda,oJuros, oMulta, oDescont, oCm,oBanco,nValRec),.T.,oBanco:SetFocus()),.T.) ;
							WHEN ( If(SE1->E1_SITUACA$"27",.F.,.T.) .and. MovBcoBx(cMotBx, .T.))

		nUltLin += 12
		@ nUltLin,005 SAY STR0019 SIZE 28, 07 OF oPanel2 PIXEL //"Conta"
		@ nUltLin,065 MSGET oConta var cConta  SIZE 65, 08 OF oPanel2 PIXEL Valid ;
							If(!lValidou,If(F070VldBco(cBanco,cAgencia,cConta,.T.,.T.,cAgencia+cConta) .And. ;
							f070AltBco(nTxMoeda,oJuros, oMulta, oDescont, oCm,oBanco,nValRec),.T.,oBanco:SetFocus()),.T.);
							WHEN ( If(SE1->E1_SITUACA$"27",.F.,.T.) .and. MovBcoBx(cMotBx, .T.))

		nUltLin += 12		
		@ nUltLin,005 SAY STR0020 SIZE 39, 07 OF oPanel2 PIXEL//"Data Receb."
		@ nUltLin,065 MSGET oDtBaixa VAR dBaixa SIZE 65, 08 OF oPanel2 PIXEL HASBUTTON When F070DtRe() .and. !(AllTrim(SE1->E1_ORIGEM) =="FINI055") .and. lAcessdBaixa Valid ;
							( fA070Data(@nTxMoeda,,oDtBaixa,oJuros) .And. If(GetNewPar("MV_SIGAGSP")=="1" , GSPF250() , .t.) .And. ( nOldJuros := nJuros, .T. ).and. If (dBaixa <> dDataBase .and.;
							  SE1->E1_VALOR == nValRec+nPis+nCofins+nCsll+nIrrf,(nIrrf:=FCaIrBxCR(SE1->E1_VALOR),f070TotMes(dBaixa,.T.)), .T. ))  
		
		nUltLin += 12
		@ nUltLin,005 SAY STR0021 SIZE 32, 07 OF oPanel2 PIXEL //"Data Cr‚dito"
		@ nUltLin,065 MSGET oDtCredito VAR dDtCredito SIZE 65, 08 OF oPanel2 PIXEL HASBUTTON Valid (dDtCredito >= dBaixa  .and. Iif(SuperGetMv("MV_BXDTFIN",,"1") == "2", DtMovFin(dDtCredito), .T.) ) .or. GetMv("MV_ANTCRED")
		oDtCredito:SetEnable( lAcessDtCredito )
	
		nUltLin += 12
		@ nUltLin,005 SAY STR0022 SIZE 32, 07 OF oPanel2 PIXEL //"Hist.Baixa"
		@ nUltLin,065 MSGET cHist070           SIZE 65, 08 OF oPanel2 PIXEL HASBUTTON Picture "@!" VALID CheckSX3("E5_HISTOR") When VisualSX3("E5_HISTOR")

		If cPaisLoc == "BRA" .And. SE1->E1_MOEDA > 1
			nUltLin += 12
			@ nUltLin,005 SAY STR0142 	SIZE 53, 07 OF oPanel2 PIXEL //"Taxa contratada"
			@ nUltLin,065 MSGET oTxMoeda VAR nTxMoeda  SIZE 65, 08 OF oPanel2 PIXEL HASBUTTON Picture PesqPict( "SM2","M2_MOEDA"+AllTrim(Str(SE1->E1_MOEDA))) ;
						 			Valid (F070CnvPcc(nTxMoeda, SE1->E1_MOEDA), oPanel2:Refresh())
		Endif	

		If lSpbInUse
			nUltLin += 12
			@ nUltLin,005 SAY STR0140 SIZE 32, 07 OF oPanel2 PIXEL  //"Modalidade SPB"
			@ nUltLin,065 COMBOBOX oModSPB VAR cModSpb ITEMS aModalSpb SIZE 65, 47 OF oPanel2 PIXEL ;
								  When MovBcoBx(cMotBx,.T.)
		Endif

		If !__lPyme
			nUltLin += 12
			@ nUltLin,005 SAY STR0173 SIZE 100, 07 OF oPanel2 PIXEL	//"Rateio Mult.Naturezas"
			@ nUltLin,065 CHECKBOX oMultNat VAR lMultNat PROMPT "" SIZE 12,12 OF oPanel2 PIXEL
		EndIf

		//////////////////////////
		//Dados da Baixa		
		nLinha := 10
		If cPaisLoc <> "CHI"
		   @ nLinha,144 SAY STR0027 + cDescMoeda SIZE 53, 08 OF oPanel2 PIXEL COLOR CLR_HBLUE//"Valor Original "
		   @ nLinha,204 MSGET SE1->E1_VALOR  SIZE 66, 08 OF oPanel2 PIXEL COLOR CLR_HBLUE When .F. Picture PesqPict("SE1","E1_VALOR") //"@E 999,999,999,999.99"

		Else
		   @ nLinha,144 SAY STR0027 SIZE 53, 08 OF oPanel2 PIXEL COLOR CLR_HBLUE //"Valor Original "
		   @ nLinha,204 MSGET SE1->E1_VLCRUZ      SIZE 66, 08 OF oPanel2 PIXEL COLOR CLR_HBLUE When .F. Picture PesqPict("SE1","E1_VLCRUZ") //"@E 999,999,999,999.99"		
		EndIf

		nEstOriginal := nValEstrang-(xMoeda(nJuros+(nCm1+nProRata)+nMulta-nDescont-nOtrga+nAcresc-nDecresc - Iif(lPccBxCr,nPis+nCofins+nCsll,0)-Iif(lIrPjBxCr,nIrrf,0),nMoedaBco,SE1->E1_MOEDA,,,,nTxMoeda)) 

		FA070CORR(nEstOriginal,nTxMoeda)
	
		If cPaisLoc <> "CHI"
			nLinha +=12
			@ nLinha,144 SAY STR0028 SIZE 53, 07 OF oPanel2 PIXEL // "- Abatimentos"
			@ nLinha,204 MSGET nTotAbLiq         SIZE 66, 08 OF oPanel2 PIXEL When .F.  Picture PesqPict( "SE1","E1_VALOR" )  //"@E 999,999,999,999.99"

			If cPaisLoc == "BRA"		
				nLinha +=12
				@ nLinha,144 SAY STR0186 SIZE 53, 07 OF oPanel2 PIXEL // "- Impostos"
				@ nLinha,204 MSGET nTotAbImp     SIZE 66, 08 OF oPanel2 PIXEL When .F.  Picture PesqPict( "SE1","E1_VALOR" )  //"@E 999,999,999,999.99"      

				nValorLiq :=  (SE1->E1_VALOR - nTotAbLiq - nTotAbImp)
				nLinha +=12
				@ nLinha,144 SAY STR0187 SIZE 53, 07 OF oPanel2 PIXEL // "Valor Liquido"
				@ nLinha,204 MSGET oValorLiq VAR nValorLiq     SIZE 66, 08 OF oPanel2 PIXEL When .F. Picture PesqPict("SE1","E1_VLCRUZ") //"@E 999,999,999,999.99"		
			Endif
		Else 
			nLinha +=12
			@ nLinha,144 SAY STR0134 SIZE 53, 7 OF oPanel2 PIXEL // "+/- Dif. Cambio"
			@ nLinha,204 MSGET oDifCambio VAR nDifCambio SIZE 66, 08 OF oPanel2 PIXEL HASBUTTON Picture PesqPict("SE1","E1_VLCRUZ" /*"E1_CAMBIO"*/)  When .F. 
		EndIf
		nLinha +=12
		@ nLinha,144 SAY STR0029 SIZE 53, 07 OF oPanel2 PIXEL //"- Pagtos Parciais"
		@ nLinha,204 MSGET nParciais          SIZE 66, 08 OF oPanel2 PIXEL HASBUTTON When .F.  Picture PesqPict( "SE1","E1_VALOR" )  //"@E 999,999,999,999.99"

		nLinha +=12                                                                  
		@ nLinha,144 SAY  STR0136 SIZE 53, 07 OF oPanel2 PIXEL //"- Decrescimo"
		@ nLinha,204 MSGET if(nDecrescF > 0,nDecrescF,nDecrVlr)  SIZE 66, 08 OF oPanel2 PIXEL HASBUTTON     Picture PesqPict( "SE1","E1_DECRESC" )  When .f. 
                                       

		nLinha +=12                                                                  
		@ nLinha,144 SAY STR0135 SIZE 53, 07 OF oPanel2 PIXEL //"+ Acrescimo"
		@ nLinha,204 MSGET nAcrescF  SIZE 66, 08 OF oPanel2 PIXEL HASBUTTON     Picture PesqPict( "SE1","E1_ACRESC" )  When .F.
		
		// Template GEM, campos especifico do template.
		If lGemInUse
			nLinha +=12 
			@ nLinha,144 SAY STR0203 SIZE 53, 07 OF oPanel2 PIXEL // "+ C.M."
			@ nLinha,204 MSGET oCM1 VAR nCM1  SIZE 66, 08 OF oPanel2 PIXEL HASBUTTON When iIf(GMBLQCM() .Or. SE1->E1_TIPO $ MVRECANT+"/"+MV_CRNEG .And. !MovBcobx(cMotBx, .T.), .F., .T.)  Picture PesqPict( "SE1","E1_JUROS" ) ; //"@E 999,999,999,999.99" 
																Valid fa070CM1(oCM1,oJuros,oMulta) .AND. fa070Calc( nTxMoeda )
		EndIf
        
		nLinha +=12
		@ nLinha,144 SAY STR0030 SIZE 53, 07 OF oPanel2 PIXEL //"- Descontos"
		@ nLinha,204 MSGET oDescont VAR nDescont  SIZE 66, 08 OF oPanel2 PIXEL HASBUTTON When F070DSC() .And. If(SE1->E1_TIPO $ MVRECANT+"/"+MV_CRNEG .And. !MovBcobx(cMotBx, .T.), .F., .T.) .And. lAcessDesc Picture PesqPict( "SE1","E1_DESCONT" ) ; //"@E 999,999,999,999.99" 
															Valid Iif( nOldDescont # nDescont, ;
															( FA070DESC(oDescont) .and. fA070Val(nDescont,nTxMoeda) .and. ;
															nDescont <= xMoeda(SE1->E1_SALDO,SE1->E1_MOEDA,nMoedaBco,dBAIXA,,nTxMoeda),;
															nOldDescont := nDescont,Fa070DesCI(lTpDesc,lNatApura)),.T.) 
		oDescont:SetEnable( lAcessDesc )
		nOldDescont := nDescont
																	
		// Template GEM, campo especifico do template.
		If lGemInUse
			nLinha +=12
			@ nLinha,144 SAY STR0204 SIZE 53, 07 OF oPanel2 PIXEL // "+ Pro Rata"
			@ nLinha,204 MSGET oProRata VAR nProRata SIZE 66, 08 OF oPanel2 PIXEL HASBUTTON When iIf(SE1->E1_TIPO $ MVRECANT+"/"+MV_CRNEG .And. !MovBcobx(cMotBx, .T.), .F., .T.)  Picture PesqPict( "SE1","E1_JUROS" ) ; //"@E 999,999,999,999.99" 
																Valid fa070PRata( oProRata ,oJuros ,oMulta ) .AND. fa070Calc( nTxMoeda )
		EndIf

		nLinha +=12
		@ nLinha,144 SAY STR0101 SIZE 53, 07 OF oPanel2 PIXEL //"+ Multa"
		@ nLinha,204 MSGET oMulta VAR nMulta  SIZE 66, 08 OF oPanel2 PIXEL HASBUTTON When If(F070Mul(oMulta) .And. SE1->E1_TIPO $ MVRECANT+"/"+MV_CRNEG .And. !MovBcobx(cMotBx, .T.), .F., .T.)  .And. lAcessMul Picture PesqPict( "SE1","E1_MULTA" ) ; //"@E 999,999,999,999.99" 
															Valid (F070Mul(oMulta),Iif(nOldMulta # nMulta, (fA070Val(nMulta,nTxMoeda),nOldMulta := nMulta), .T.))
															
		oMulta:SetEnable( lAcessMul )
		nOldMulta := nMulta

	   If cPaisLoc <> "CHI"
			nLinha +=12
			@ nLinha,144 SAY STR0031 SIZE 53, 07 OF oPanel2 PIXEL //"+ Tx.Permanenc."
			@ nLinha,204 MSGET oJuros VAR nJuros   SIZE 66, 08 OF oPanel2 PIXEL HASBUTTON When F070Jrs() .And. If(SE1->E1_TIPO $ MVRECANT+"/"+MV_CRNEG .And. !MovBcobx(cMotBx, .T.), .F., .T.) .And. lAcessJur Picture PesqPict( "SE1","E1_JUROS" ) ; //"@E 999,999,999,999.99"
		   													Valid F070TxPer(oJuros) .AND. Iif(nOldJuros # nJuros, (fA070Val(nJuros,nTxMoeda),nOldJuros := nJuros),.T.) 
		   oJuros:SetEnable( lAcessJur )
			nOldJuros := nJuros
		Else 
		   nLinha +=12
		   @ nLinha,144 SAY STR0133 SIZE 53, 07 OF oPanel2 PIXEL //"- Outros Gastos"
		   @ nLinha,204 MSGET oOtrga VAR nOtrga  SIZE 66, 08 OF oPanel2 PIXEL HASBUTTON     Picture PesqPict( "SE1","E1_OTRGA" ) ; //"@E 999,999,999,999.99" 		   													
		   Valid fA070Val(nOtrga)
		EndIf     
		//Controla IRPJ na baixa
		If   cPaisLoc == "BRA" .And. lIrPjBxCr
			nLinha +=12	
			@ nLinha,144 SAY STR0228  SIZE 53, 07 OF oPanel2 PIXEL  // "- IRRF"
			@ nLinha,204 MSGET oIrrf VAR nIrrf SIZE 66, 08 OF oPanel2 PIXEL HASBUTTON Picture PesqPict( "SE1","E1_IRRF" ); //"@E 999,999,999,999.99"
															 Valid Iif( nOldIrrf # nIrrf, (fA070Val(nIrrf,nTxMoeda),nOldIrrf := nIrrf), .T.)//f070ValRec(@nValorRec,nTotAbLiq,nPis,nCofins,nCsll,nIrrf,nIss,nInss)
			nOldIrrf := nIrrf
		EndIf 
				
		If cPaisLoc == "BRA" .And. lPccBxCR //1-Retem PCC na Baixa ou 2-Retem PCC na Emissão(default)
		   nLinha +=12
		   @ nLinha,144 SAY STR0216 SIZE 53, 07 OF oPanel2 PIXEL //"- PIS"
		   @ nLinha,204 MSGET oPIS VAR nPIS   SIZE 66, 08 OF oPanel2 PIXEL HASBUTTON  Picture PesqPict( "SE1","E1_PIS" ) ; //"@E 999,999,999,999.99"
		   Valid Iif(nOldPis # nPis, (fA070Val(nPIS,nTxMoeda),nOldPis := nPis), .T.)
			nOldPis := nPis
		
		   nLinha +=12
		   @ nLinha,144 SAY STR0217 SIZE 53, 07 OF oPanel2 PIXEL //"- COFINS"
		   @ nLinha,204 MSGET oCOFINS VAR nCOFINS   SIZE 66, 08 OF oPanel2 PIXEL HASBUTTON Picture PesqPict( "SE1","E1_COFINS" ) ; //"@E 999,999,999,999.99"
		   Valid Iif(nOldCofins # nCofins, (fA070Val(nCOFINS,nTxMoeda),nOldCofins := nCofins), .T.)
			nOldCofins := nCofins			

		   nLinha +=12
		   @ nLinha,144 SAY STR0218 SIZE 53, 07 OF oPanel2 PIXEL //"- CSLL"
		   @ nLinha,204 MSGET oCSLL VAR nCSLL   SIZE 66, 08 OF oPanel2 PIXEL HASBUTTON Picture PesqPict( "SE1","E1_CSLL" ) ; //"@E 999,999,999,999.99"
		   Valid (oCSLL:Refresh(),Iif(nOldCsll # nCsll, (fA070Val(nCsll,nTxMoeda),nOldCsll := nCsll), .T.))
			nOldCsll := nCsll			


		EndIf              	
		  
		IF SE1->E1_MOEDA > 1 .Or. cPaisLoc<>"BRA"	
		   nLinha +=12
			@ nLinha,144 SAY STR0033 SIZE 53,07 OF oPanel2 PIXEL COLOR CLR_HBLUE //"= Valor Recebido"
			@ nLinha,204 MSGET oValRec VAR nValRec SIZE 66, 08 OF oPanel2 PIXEL HASBUTTON COLOR CLR_HBLUE Picture PesqPict( "SE1","E1_VALOR" )  ;//"@E 999,999,999,999.99" 
															Valid ( oValRec:Refresh(),;                                                                           
																		fa070Calc(nTxMoeda,.F.,.T.),;															
																		Fa070ValVR(nTxMoeda),;
																		FA070ValRec(oJuros,oMulta,oProRata,oDescont),;
																		oVlEstrang:Refresh(),;																		
																		oCM:Refresh() )
			nLinha +=12
			@ nLinha,144 SAY STR0034+SubStr(GetMV("MV_SIMB"+cMoeda),1,3) SIZE 53, 7 OF oPanel2 PIXEL // "Valor "
			@ nLinha,204 MSGET oVlEstrang VAR nValEstrang SIZE 66, 08 OF oPanel2 PIXEL HASBUTTON  ;
						 Picture PesqPict( "SE1","E1_VALOR" )  ;
						 VALID FA070Estrang(nTxMoeda) .And.;
						 		 Fa070ValEstrang(	nValEstrang,@nTxMoeda,@nValRec,dBaixa,oValRec,oTxMoeda,;
														nJuros+(nCm1+nProRata),nMulta,nDescont,nOtrga,nEstOriginal,oVlEstrang) .And.;
								FA070ValRec(oJuros,oMulta,oProRata,oDescont)
			nLinha +=12
			@ nLinha,144 SAY STR0032 SIZE 53,07 OF oPanel2 PIXEL // "+ Corr.Monet ria"
			@ nLinha,204 MSGET oCM     VAR nCM		SIZE 66, 08 OF oPanel2 PIXEL HASBUTTON     Picture PesqPict( "SE1","E1_CORREC" )  ; // "@E 999,999,999,999.99" 
															When (IIf(GetMv("MV_CALCCM") == "S",.T.,.F.).And. cPaisLoc<>"BRA") .OR. lLibCm
		

		Else
		   nLinha +=12
			@ nLinha,144 SAY STR0035 SIZE 53,07 OF oPanel2 PIXEL COLOR CLR_HBLUE //"= Valor Recebido"
			@ nLinha,204 MSGET oValRec VAR nValRec        SIZE 66, 08 OF oPanel2 ;
							PIXEL HASBUTTON COLOR CLR_HBLUE ;
							Valid ( oValRec:Refresh(), FA070ValRec(oJuros,oMulta,oProRata,oDescont) .and. Fa070Liq(oJuros,oValRec, oPanel2),  nValEstrang := nValRec ) ;
							Picture PesqPict( "SE1","E1_VALOR" )  //"@E 999,999,999,999.99"
		Endif
	
		If ( cPaisLoc <> "BRA" )
			AADD(aButtons, {"TABPRICE", {|| (nTxMoeda:=Fa070SetMd(),f070AltBco(nTxMoeda,oJuros, oMulta, oDescont, oCm))},"MOEDAS" }) //Troca de Taxas

		Endif  
		
		if lPrjCni
			If lPanelFin	
					ACTIVATE MSDIALOG oDlg ON INIT FaMyBar(oDlg,{|| IIf(FA070BtOK(),iIf( IIf( MovBcoBx(cMotBx, .T.),F070VldBco(cBanco,cAgencia,cConta,.T.,.F.), .T. ) .and. Iif( FA070VlNat(cMotBx),.T.,.F.);
		                                                  .and. If(SuperGetMv("MV_BXDTFIN",,"1") == "2",DtMovFin(dBaixa),.T.) .and. iIf(lFA070MDB .and. !lMdbOk,lMdbOk:=ExecBlock("FA070MDB",.F.,.F.) ,.T.) ,;
		                                                  (nOpc1 := 1,oDlg:End()),Nil),Nil)},;
		                                               {||(nOpc1 := 0,oDlg:End())},aButtons) CENTERED
			Else

					ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{|| iIf(FA070BtOK(),iIf( IIf( MovBcoBx(cMotBx, .T.),F070VldBco(cBanco,cAgencia,cConta,.T.,.F.), .T. ).and. Iif( FA070VlNat(cMotBx),.T.,.F.) ;
		                                                        .and. If(SuperGetMv("MV_BXDTFIN",,"1") == "2",DtMovFin(dBaixa),.T.) .and. iIf(lFA070MDB .and. !lMdbOk,lMdbOk:=ExecBlock("FA070MDB",.F.,.F.) ,.T.) ;
		                                                       ,IIf( ( FindFunction( "UsaSeqCor" ).And. UsaSeqCor() ), IIf( FA070Diario(), (nOpc1 := 1,oDlg:End()),Nil), (nOpc1 := 1,oDlg:End()) ) ;
		                                                    ,Nil),Nil)};
		                                               ,{||(nOpc1 := 0,oDlg:End())},,aButtons) CENTERED
		   	Endif
		Else
			If lPanelFin	
				ACTIVATE MSDIALOG oDlg ON INIT FaMyBar(oDlg,{|| IIf(FA070BtOK(),iIf( IIf( MovBcoBx(cMotBx, .T.),F070VldBco(cBanco,cAgencia,cConta,.T.,.F.), .T. ) ;
			                                                  .and. If(SuperGetMv("MV_BXDTFIN",,"1") == "2",DtMovFin(dBaixa),.T.) .and. PcoVldLan("000004","01","FINA070") .and. iIf(lFA070MDB .and. !lMdbOk,lMdbOk:=ExecBlock("FA070MDB",.F.,.F.) ,.T.) ,;
		    	                                              (nOpc1 := 1,oDlg:End()),Nil),Nil)},;
			        	                                       {||(nOpc1 := 0,oDlg:End())},aButtons) CENTERED
			Else
        	
				ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{|| iIf(FA070BtOK(),iIf( IIf( MovBcoBx(cMotBx, .T.),F070VldBco(cBanco,cAgencia,cConta,.T.,.F.), .T. ) ;
		        	                                                .and. If(SuperGetMv("MV_BXDTFIN",,"1") == "2",DtMovFin(dBaixa),.T.) .and. 	PcoVldLan("000004","01","FINA070") .and. iIf(lFA070MDB .and. !lMdbOk,lMdbOk:=ExecBlock("FA070MDB",.F.,.F.) ,.T.) ;
		            	                                           ,IIf( ( FindFunction( "UsaSeqCor" ) .And. UsaSeqCor() ), IIf( FA070Diario(), (nOpc1 := 1,oDlg:End()),Nil), (nOpc1 := 1,oDlg:End()) ) ;
		                	                                    ,Nil),Nil)};
		                    		                           ,{||(nOpc1 := 0,oDlg:End())},,aButtons) CENTERED
			 EndIf
   		Endif
   		
  		SetKey(VK_F4,bSetKey)
    Else                                     
		aValidGet:= {}
		If (nT := ascan(aAutoCab,{|x| x[1]='AUTMOTBX'})) > 0
			cMotBx	:=	aAutoCab[nT,2]	// Sergio Fuzinaka - 28.05.02
			If Len(AllTrim(cMotBx)) == 3
				If (nY := ascan(aMotBx,{|x| SubStr(x,1,3) == AllTrim(cMotBx)})) > 0
					aAutoCab[nT,2] := SubStr(aMotBx[nY],07,10)
					cMotBx := aAutoCab[nT,2]
				EndIf
			EndIf
			If ! lFA070MDB
	 	 		Aadd(aValidGet,{'cMotBx' ,aAutoCab[nT,2],"fa070BDev()",.t.})
	 	 	Else
 	 			Aadd(aValidGet,{'cMotBx' ,aAutoCab[nT,2],"fa070BDev()	.and. ExecBlock('FA070MDB',.F.,.F.)",.t.})
 	 		EndIf	
 	 	EndIf	
		If (! SE1->E1_SITUACA$"27") .and. MovBcobx(cMotBx, .T.)
			If (nT := ascan(aAutoCab,{|x| x[1]='AUTBANCO'})) > 0
				Aadd(aValidGet,{'cBanco' ,aAutoCab[nT,2],"CarregaSa6(@cBanco,,,.T.)",.t.})		
			Endif	
			If (nT := ascan(aAutoCab,{|x| x[1]='AUTAGENCIA'}) ) > 0
				Aadd(aValidGet,{'cAgencia' ,aAutoCab[nT,2],"CarregaSa6(@cBanco,@cAgencia,,.T.)",.t.})		
			EndIf	
			If (nT := ascan(aAutoCab,{|x| x[1]='AUTCONTA'}) ) > 0
				Aadd(aValidGet,{'cConta' ,aAutoCab[nT,2],"CarregaSa6(@cBanco,@cAgencia,@cConta,.T.,,.T.)",.t.})		
			EndIf	
		EndIF
		If (nT := ascan(aAutoCab,{|x| x[1]='AUTDTBAIXA'}) ) > 0
			Aadd(aValidGet,{'dBaixa' ,aAutoCab[nT,2],"fA070Data(,.F.)",.t.})
		EndIf	
		If (nT := ascan(aAutoCab,{|x| x[1]='AUTDTCREDITO'}) ) > 0
		   Aadd(aValidGet,{'dDTCredito' ,aAutoCab[nT,2],"(dDtCredito >= dBaixa  .and. DtMovFin(dDtCredito)) .or. GetMv('MV_ANTCRED')",.t.})
		EndIf	
		If VisualSX3("E5_HISTOR") .AND. (nT := ascan(aAutoCab,{|x| x[1]='AUTHIST'}) ) > 0
			Aadd(aValidGet,{'cHist070' ,aAutoCab[nT,2],"CheckSX3('E5_HISTOR')",.t.})		
		EndIf	
		nEstOriginal := nValEstrang-(xMoeda(nJuros+(nCm1+nProRata)+nMulta-nDescont-nOtrga+nAcresc-nDecresc - Iif(lPccBxCr,nPis+nCofins+nCsll,0)-Iif(lIrPjBxCr,nIrrf,0),nMoedaBco,SE1->E1_MOEDA,,,,nTxMoeda))   		
		FA070CORR(nEstOriginal)

		If (nT := ascan(aAutoCab,{|x| x[1]='AUTACRESC'}) ) > 0
			Aadd(aValidGet,{'nAcresc' ,aAutoCab[nT,2],"fA070Val(nAcresc)",.t.})		
		EndIf	
		
		If (nT := ascan(aAutoCab,{|x| x[1]='AUTMULTA'}) ) > 0
			Aadd(aValidGet,{'nMulta' ,aAutoCab[nT,2],"fA070Val(nMulta)",.t.})		
		EndIf	
		If (nT := ascan(aAutoCab,{|x| x[1]='AUTJUROS'}) ) > 0
			Aadd(aValidGet,{'nJuros' ,aAutoCab[nT,2],"fA070Val(nJuros)",.t.})		
		EndIf	
		
		// Template GEM, validacao dos campos especificos do template.
		If lGemInUse
			If (nT := ascan(aAutoCab,{|x| x[1]='AUTCM1'}) ) > 0
				Aadd(aValidGet,{'nCM1' ,aAutoCab[nT,2],"fa070Calc()",.t.})
			EndIf
			If (nT := ascan(aAutoCab,{|x| x[1]='AUTPRORATA'}) ) > 0
				Aadd(aValidGet,{'nProRata' ,aAutoCab[nT,2],"fa070Calc()",.t.})
			EndIf
		EndIf
		
		If (nT := ascan(aAutoCab,{|x| x[1]='AUTDESCONT'}) ) > 0
			If lGemInUse
				Aadd(aValidGet,{'nDescont' ,aAutoCab[nT,2],"FA070DESC(oDescont) .and. fA070Val(nDescont) .and. (nDescont <= (Round(nCM1+nMulta+nJuros+nProRata,2)+xMoeda(SE1->E1_SALDO,SE1->E1_MOEDA,nMoedaBco,dBAIXA)))",.t.})
			Else
				Aadd(aValidGet,{'nDescont' ,aAutoCab[nT,2],"FA070DESC(oDescont) .and. fA070Val(nDescont) .and. (nDescont <= Round(nMulta+nJuros,2)+xMoeda(SE1->E1_SALDO,SE1->E1_MOEDA,nMoedaBco,dBAIXA))",.t.})		
			Endif
			//Aadd(aValidGet,{'nDescont' ,aAutoCab[nT,2],"FA070DESC() .and. fA070Val(nDescont) .and. nDescont <= xMoeda(SE1->E1_SALDO,SE1->E1_MOEDA,1,dBAIXA)",.t.})
		EndIf	

		If (nT := ascan(aAutoCab,{|x| x[1]='AUTDECRESC'}) ) > 0
		 	Aadd(aValidGet,{'nDecresc' ,aAutoCab[nT,2],"fA070Val(nDecresc)",.t.})		
		EndIf
				
		If SE1->E1_MOEDA > 1  .Or. cPaisLoc<>"BRA"
			If cPaisLoc == "BRA"
				If (nT := ascan(aAutoCab,{|x| x[1]='AUTTXMOEDA'}) ) > 0
					nTxMoeda	:=	aAutoCab[nT,2]
					Aadd(aValidGet,{'nTxMoeda' ,aAutoCab[nT,2],"Fa070Val(0,"+STR(nTxMoeda,17,TamSx3("E2_TXMOEDA")[2])+")",.t.})		
				EndIf	                            
			Endif
			If (nT := ascan(aAutoCab,{|x| x[1]='AUTVALREC'}) ) > 0
				Aadd(aValidGet,{'nValRec' ,aAutoCab[nT,2],"Fa070ValVR("+Alltrim(Str(nTxMoeda))+")",.t.})
			EndIf	
		Else
			If (nT := ascan(aAutoCab,{|x| x[1]='AUTVALREC'}) ) > 0
				Aadd(aValidGet,{'nValRec' ,aAutoCab[nT,2],".T.",.t.})		
			EndIf	
		Endif
		If ! SE1->(MsVldGAuto(aValidGet)) // consiste os gets
			Return .f.
		EndIf     
	   
		nOpc1 := 1 
		
		// Se o conteudo do campo estiver vazio(zero), se existir o 4o. elemento no array de campos
		// e o mesmo retornar .T., assume os valores que o usuario enviou no array da rotina automatica
		If (nT := ascan(aAutoCab,{|x| x[1]='AUTJUROS'}) ) > 0
			If Empty(aAutoCab[nT,2]) .And. Len(aAutoCab[nT]) >= 4 .And. aAutoCab[nT][4]
				nJuros := aAutoCab[nT,2]
				fa070val(nJuros,,.F.)
			Endif	
		EndIf	
		If (nT := ascan(aAutoCab,{|x| x[1]='AUTMULTA'}) ) > 0
			If Empty(aAutoCab[nT,2]) .And. Len(aAutoCab[nT]) >= 4 .And. aAutoCab[nT][4]
				nMulta := aAutoCab[nT,2]
				fa070val(nMulta,,.F.)
			Endif	
		EndIf	
		If (nT := ascan(aAutoCab,{|x| x[1]='AUTDESCONT'}) ) > 0
			If Empty(aAutoCab[nT,2]) .And. Len(aAutoCab[nT]) >= 4 .And. aAutoCab[nT][4]
				nDescont := aAutoCab[nT,2]
				fa070val(nDescont,,.F.)
			Endif	
		EndIf
		
		// Template GEM, validacao dos campos especificos do template.
		If lGemInUse
			If (nT := ascan(aAutoCab,{|x| x[1]='AUTCM1'}) ) > 0
				If Empty(aAutoCab[nT,2]) .And. Len(aAutoCab[nT]) >= 4 .And. aAutoCab[nT][4]
					nCM1 := aAutoCab[nT,2]
					fa070val(nCM1)
				EndIf
			EndIf
			If (nT := ascan(aAutoCab,{|x| x[1]='AUTPRORATA'}) ) > 0
				If Empty(aAutoCab[nT,2]) .And. Len(aAutoCab[nT]) >= 4 .And. aAutoCab[nT][4]
					nProRata := aAutoCab[nT,2]
					fa070val(nProRata)
				EndIf
			EndIf
		EndIf		
	EndIf  
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Ponto de Entrada para permitir um controle do total de ³
	//³ cheques informados com o total a ser baixado           ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ExistBlock("F070TCTR") .And. nOpc1 > 0 .And. nSomaCheq > 0
	   nOpc1 := ExecBlock("F070TCTR",.F.,.F.,{nOpc1,nSomaCheq,nValRec})
	EndIf
	
	If nOpc1 == 0
		nErro ++
	EndIF

	If nErro > 2
		nErro :=0
		If Abandona()
			MsUnlock()
			Return Nil
		Endif
	Endif
	If nOpc1 == 1
	    
		If nCM1 > 0
			nJuros += nCM1
		Else
			nDescont -= nCM1
		EndIf
		
		If nProRata > 0
			nJuros += nProRata
		Else
			nDescont -= nProRata
		EndIf
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Verifica se dados bancários estão OK                                  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If MovBcobx(cMotBx, .T.) .and. !CarregaSA6(@cBanco,@cAgencia,@cConta,.T.,,.T.)
			// Se nao for baixa por rotina automatica, volta para o usuario corrigir os
			// dados, senao abandona a baixa.
			If Type('lF070Auto') =='U' .or. ! lF070Auto
				loop
			Else
				lRet := .F.
				Exit
			Endif
		Endif

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Verifica se nao foi alterado o banco quando for tit. em desconto.     ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If ( SE1->E1_SITUACA $ "27" .And.;
			cBanco+cAgencia+cConta!=SE1->E1_PORTADO+SE1->E1_AGEDEP+SE1->E1_CONTA )
			Help(" ",1,"FINA070BCO")
			// Se nao for baixa por rotina automatica, volta para o usuario corrigir os
			// dados, senao abandona a baixa.
			If Type('lF070Auto') =='U' .or. ! lF070Auto
				loop
			Else
				lRet := .F.
				Exit
			Endif
		EndIf

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Verifica se valor da baixa ‚ maior que o valor m ximo a receber       ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If cPaisLoc<>"BRA"     
			nTxMoeda:=Iif(nMoedaBco>0,aTxMoedas[nMoedaBco][2],1)
			nTxMdaOr:=aTxMoedas[SE1->E1_MOEDA][2]
			nValRecL := Round(XMoeda(nValRec,nTxMoeda,SE1->E1_MOEDA,dBaixa,7,,nTxMdaOr),2)
		EndIf
		IF Iif(cPaisLoc<>"BRA",nValRecL,nValRec) > (Round(xMoeda(SE1->E1_SALDO-nTotAbat+nTotMult,SE1->E1_MOEDA,nMoedaBco,dBaixa,7,nTxMoeda),2)+Round(Iif(Alltrim(SE1->E1_ORIGEM) == "FINA074",0,nJuros+nMulta-nDescont-nOtrga+nDifCambio+nTolerPg+nAcresc-nDecresc),2))
			Help(" ",1,"ValorMaior")
			If ( SE1->E1_MOEDA == 1 )
				// Se nao for baixa por rotina automatica, volta para o usuario corrigir os
				// dados, senao abandona a baixa.
				If Type('lF070Auto') =='U' .or. ! lF070Auto
					loop
				Else
					lRet := .F.
					Exit
				Endif
			Else
				loop
			EndIf
		Endif

		// Se controla saldo na compensacao do cheque
		// A primeira baixa tem que ter no mínimo o valor dos cheques, pois esses são completamente baixados 
		// pelo sistema.
		If GetMv("MV_SLDBXCR") == "C"
			// Soma o total recebido em cheque
			nSomaCheq := SomaCheqCr(.F.,SE1->E1_PREFIXO, SE1->E1_NUM, SE1->E1_PARCELA, SE1->E1_TIPO, SE1->E1_CLIENTE)
			If nValRec < nSomaCheq
		   		Aviso(STR0219,STR0222 + STR0223 ,{STR0221}) //"Não é possível realizar baixa de valor inferior aos cheques amarrados quando MV_SLDBXCR = 'C'.""Nessa configuração, os cheques serão sempre baixados primeiro."
		   		lRet := .F.
		   		exit		
			EndIf
		Endif
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Valida se e baixa parcial, quando e titulo do BIBLIOS³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
        if alltrim(upper(SE1->E1_ORIGEM)) = 'L' .and. GetNewPar('MV_RMBIBLI',.F.)
	        If nValRec < (SE1->E1_VALOR - SE1->E1_DECRESC + E1_ACRESC - nDescont + nMulta + nJuros )
		   		Aviso(STR0219,STR0220,{STR0221}) //"Não é possivel realizar a baixa parcial de um título nativo do RM Biblios"
		   		lRet := .F.
		   		exit
			endif			
        endif
	
	   //Baixa de titulo em moeda forte com a cotacao da moeda igual a zero !!
		If SE1->E1_MOEDA > 1 .and. RECMOEDA(dBaixa,cMoeda) == 0 .and. nTxMoeda == 0 .and. ;
				nValRec == 0 .and. nValEstrang == 0
			Help(" ",1,"TX_MOEDA",, STR0168,1,0)	//"Nao sera possivel baixar este titulo pois a cotacao da moeda do titulo na data da baixa é igual a zero."
			// Se nao for baixa por rotina automatica, volta para o usuario corrigir os
			// dados, senao abandona a baixa.
			If Type('lF070Auto') =='U' .or. ! lF070Auto
				loop
			Else
				lRet := .F.			
				Exit
			Endif
		Endif 
		If nValRec < (nJuros+nAcresc)                                                               
			nValPadrao := nValRec-(nJuros+Iif(SE1->E1_MOEDA<=1,nCM,0)+nMulta-nDescont-nDecresc)	
		Else
			nValPadrao := nValRec-(nJuros+Iif(SE1->E1_MOEDA<=1,nCM,0)+nMulta-nDescont+nAcresc-nDecresc)	
		Endif
	
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Verifica se saldo estava em outra moeda, caso estiver, converte valor ³
		//³recebido pela taxa diaria da moeda                                    ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		nOpt  := IIF(Str(nValPadrao,14,2)=Str(xMoeda(SE1->E1_SALDO-nTotAbat,SE1->E1_MOEDA,nMoedaBco,dBaixa,,nTxMoeda),14,2),1,2)
	Else
		lRet := .F.
		MsUnlock()
		//Se os valores dos impostos de abatimentos foram recalculados e se a baixa
		//do titulo nao for confirmada, restauro os valores calculados pelo sistema
		If lAlterImp
         
			//Guardo os valores "nOld" para retornar apos calculos
			nAntPis    := nOldPis    
			nAntCofins := nOldCofins
			nAntCsll   := nOldCsll
			nAntIrrf   := nOldIrrf
			nAntIss	   := nOldIss
			nAntInss   := nOldInss	  

			nPisAlter := SE1->E1_PIS
			nCofAlter := SE1->E1_COFINS
			nCslAlter := SE1->E1_CSLL
			nIrfAlter := SE1->E1_IRRF
			nIssAlter := SE1->E1_ISS
			nInsAlter := SE1->E1_INSS

			//Altero os valores somente para a funcao considerar o calculo
			//pois os valores de SE1-> e nOld precisam ser diferentes
			RecLock("SE1")
			SE1->E1_PIS    := nOldPis
			SE1->E1_COFINS := nOldCofins
			SE1->E1_CSLL   := nOldCsll
			SE1->E1_IRRF   := nOldIrrf
			SE1->E1_ISS		:= nOldIss
			SE1->E1_INSS   := nOldInss
			MsUnlock()
	      
			nOldPis    := nPisAlter
			nOldCofins := nCofAlter
			nOldCsll   := nCslAlter		
			nOldIrrf		:= nIrfAlter
			nOldIss		:= nIssAlter
			nOldInss		:= nInsAlter		
	
			//Faz a alteração dos valores
			FA040AxAlt(cAlias,lAlterImp)          
                                    
			//Restauro os valores para nao gerar problemas em novos calculos
			nOldPis    := nAntPis
			nOldCofins := nAntCofins
			nOldCsll   := nAntCsll		
			nOldIrrf		:= nAntIrrf
			nOldIss		:= nAntIss
			nOldInss		:= nAntInss

			//Se o valor total for menor que o valor minimo de retenção
			If (aDadosRet[1] + nValRec) <= nVlMinImp
				RecLock("SE1")
				SE1->E1_SABTPIS	+= If(SE1->E1_SABTPIS >= 0 ,nOldPis,0)
				SE1->E1_SABTCOF	+= If(SE1->E1_SABTCOF >= 0 ,nOldCofins,0)
				SE1->E1_SABTCSL	+= If(SE1->E1_SABTCSL >= 0 ,nOldCsll,0)
				SE1->E1_PIS			:= SE1->E1_SABTPIS
				SE1->E1_COFINS		:= SE1->E1_SABTCOF
				SE1->E1_CSLL		:= SE1->E1_SABTCSL
				MsUnlock()
			Endif
		EndIf
		Exit
	Endif
	If Empty( cMotBx )
		cMotBx		:= aDescMotBx[1] 	//NORMAL
	Endif
	IF nOpc1 == 1 
         
		 If ExistBlock("FA070ACR") 
		 	nAux := ExecBlock("FA070ACR",.F.,.F.,{nDecrVlr}) 
		 	If Valtype(nAux) == "N"
            	nDecresc := nAux
    		EndIf 
     	 Endif

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Verifica se data da baixa e valida                                    ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		IF (dBaixa < SE1->E1_EMISSAO .OR. dBaixa > dDataBase) .and. !GetMv("MV_ANTCRED")
			Help( " ", 1, "DATAERR" )
			// Se nao for baixa por rotina automatica, volta para o usuario corrigir os
			// dados, senao abandona a baixa.
			If Type('lF070Auto') =='U' .or. ! lF070Auto
				loop
			Else
				lRet := .F.
				Exit
			Endif
		Endif
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Verifica se modalidade do SPB é valida.									    ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If lSpbInUse
			cModSpb := Substr(cModSpb,1,1)
			IF !(SpbTipo("SE1",cModSpb,SE1->E1_TIPO))
				// Se nao for baixa por rotina automatica, volta para o usuario corrigir os
				// dados, senao abandona a baixa.
				If Type('lF070Auto') =='U' .or. ! lF070Auto
					loop
				Else
					lRet := .F.
					Exit
				Endif
			Endif
		Endif                    
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Verifica se permite ou nao baixar o titulo com o valor recebido menor ³
		//³que a soma dos valores de juros, multa e desconto                     ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !F070VldRec()
			If Type('lF070Auto') =='U' .or. !lF070Auto
				loop
			Else
				lRet := .F.			
				Exit
			Endif
		Endif

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Ponto de Entrada de Template para Confirmacao da Baixa       ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If lTFa070Tit
			lRet := ExecTemplate("FA070TIT",.F.,.F.,{nParciais})
		End
	
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Ponto de Entrada para Confirmacao da Baixa                   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If lFa070Tit
			lRet := ExecBlock("FA070TIT",.F.,.F.,{nParciais})
		End

		If !lRet
			Return lRet
		EndIf

		dbSelectArea("SE1")
		IF Empty(dBaixa) .or. (nValRec < 0 ) .or. Empty(cMotBx)
			Help(" ",1,"FA070INV")
			// Se nao for baixa por rotina automatica, volta para o usuario corrigir os
			// dados, senao abandona a baixa.
			If Type('lF070Auto') =='U' .or. ! lF070Auto
				loop
			Else
				lRet := .F.
				Exit
			Endif
		EndIF

      // Aqui neste ponto a variavel nJuros esta somada com o valor de nCM1
      // e da Prorata se esta for positiva, se for negativa
      // a prorata eh somada a nDescont
       
        IF nModulo == 43 //TMS
			If nDescont != Round(nMulta+nJuros+xMoeda((SE1->E1_SALDO+SE1->E1_ACRESC-SE1->E1_DECRESC),SE1->E1_MOEDA,1,dBaixa,2,nTxMoeda),2)
				If (nTotAbat=0.and.nValRec=0.and.nDescont==0).or.;
					(nValRec=0.and.nTotAbat!=SE1->E1_SALDO .and.;
					 nDescont!=Round(nMulta+nJuros+xMoeda(SE1->E1_SALDO-nTotAbat,SE1->E1_MOEDA,1,dBaixa,3,nTxMoeda),2)+nAcresc-nDecresc)
					Help(" ",1,"FA070INV")
					// Se nao for baixa por rotina automatica, volta para o usuario corrigir os
					// dados, senao abandona a baixa.
					If Type('lF070Auto') =='U' .or. ! lF070Auto
						loop
					Else
						lRet := .F.
						Exit
					Endif
				EndIf
			EndIF                            
	    Else
			If nDescont != Round(nMulta+nJuros+xMoeda((SE1->E1_SALDO+SE1->E1_ACRESC-(SE1->E1_DECRESC+nDecrescF+nDecrVlr)),SE1->E1_MOEDA,1,dBaixa,2,nTxMoeda),2)
				If (nTotAbat=0.and.nValRec=0.and.nDescont==0).and.;
					(nValRec=0.and.nTotAbat!=SE1->E1_SALDO .and.nPIS==0.and.nCOFINS==0.and.nCSLL==0.and.;
					nDescont!=Round(nMulta+nJuros+xMoeda(SE1->E1_SALDO-nTotAbat,SE1->E1_MOEDA,1,dBaixa,3,nTxMoeda),2)+nAcresc-nDecresc)
					Help(" ",1,"FA070INV")
					// Se nao for baixa por rotina automatica, volta para o usuario corrigir os
					// dados, senao abandona a baixa.
					If Type('lF070Auto') =='U' .or. ! lF070Auto
						loop
					Else
						lRet := .F.
						Exit
					Endif
				EndIf
			EndIF
	    EndIf

		If !FA070ValMo()
			// Se nao for baixa por rotina automatica, volta para o usuario corrigir os
			// dados, senao abandona a baixa.
			If Type('lF070Auto') =='U' .or. ! lF070Auto
				loop
			Else
				lRet := .F.
				Exit
			Endif
		EndIF

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Soma nos totalizadores, exceto se a situa‡„o do t¡tulo for:     ³
		//³2 - Cobran‡a Descontada   ou   7 - Cobranca Cau‡„o Descontada   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		IF !(SE1->E1_SITUACA $ "2/7")
			nTotAGer  += nValRec
			nTotADesc += nDescont+nDecresc
			nTotAMul  += nMulta
			nTotAJur  += nJuros+nAcresc 
			nTotADesp += Iif(SE1->E1_MOEDA<=1,nCM,0)
		Endif 
		// verifica se nao esta utilizando rotina automatica para poder gerar os lanctos contabeis
		cPadrao   := fA070Pad()
		lPadrao   := VerPadrao(cPadrao)

	  	IF Type('lF070Auto') =='U' .or. ! lF070Auto
			// Verifica se esta utilizando multiplas naturezas
			// E chama a rotina para distribuir o valor entre as naturezas
			If MV_MULNATR .and. lMultNat
				MultNatB("SE1",.F.,STR(mv_par07,1),@lOk,@aColsSEV,@lMultNat)
			Endif
		Endif		
        
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Inicializa a gravacao dos lancamentos do SIGAPCO          ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		PcoIniLan("000004")

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Inicio da prote‡„o via TTS                                   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		Begin Transaction
			lJuros  := IIF( mv_par05 == 1, .T., .F. )
			
			Aadd(aHdlPrv,{nHdlPrv,cPadrao,aFlagCTB,cArquivo}) 

			lBaixou := fA070Grv(lPadrao,Nil,NIl,Nil,Nil,dDtCredito,lJuros,Nil,Nil,nTxMoeda,mv_par08==1,aSeqSe5,aHdlPrv)		//Nil=Arquivo Cnab
			nHdlPrv	:= aHdlPrv[1][1]
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Grava os lancamentos nas contas orcamentarias SIGAPCO    ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If SE1->E1_SITUACA == "0"	// Carteira
				PcoDetLan("000004","01","FINA070")
			ElseIf SE1->E1_SITUACA == "1"	// Simples
				PcoDetLan("000004","02","FINA070")
			ElseIf SE1->E1_SITUACA == "2"	// Descontada
				PcoDetLan("000004","03","FINA070")
			ElseIf SE1->E1_SITUACA == "4"	// Vinculada
				PcoDetLan("000004","04","FINA070")
			ElseIf SE1->E1_SITUACA == "5"	// c/Advogado
				PcoDetLan("000004","05","FINA070")
			ElseIf SE1->E1_SITUACA == "6"	// Judicial
				PcoDetLan("000004","06","FINA070")
			ElseIf SE1->E1_SITUACA == "7"	// Caucionada Descontada
				PcoDetLan("000004","08","FINA070")
			EndIf

		
			// Se nao for baixa por rotina automatica, chama a rotina de contabilizacao
		  	// caso contrario nao deve haver contabilizacao devido a necessidade de
		  	// interacao do usuario nesta rotina (contabilizacao)
		  	IF Type('lF070Auto') =='U' .or. ! lF070Auto
				// Verifica se esta utilizando multiplas naturezas
				If MV_MULNATR .and. lMultNat .and. lOk
					MultNatC("SE1",@nHdlPrv,@nTotal,@cArquivo,lContabiliza,.F.,STR(mv_par07,1),,lOk,aColsSEV,lBaixou,aGrvLctPco)
				Endif

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Ponto de entrada antes da contabilizacao.			  ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If ExistBlock("F070ACONT")
					ExecBlock("F070ACONT",.F.,.F.)
				EndIf
	
				If lBaixou
		  			GravaChqCR(SE5->E5_SEQ,"FINA070",,aSeqSe5,lBaixou) // Grava os cheques no SEF
		  		Endif
			Endif
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Finaliza a gravacao dos lancamentos do SIGAPCO ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			PcoFinLan("000004")
			IF lPadrao .and. lContabiliza .and. lBaixou 
				If nHdlPrv <= 0			
					nHdlPrv		:= HeadProva(cLote,"FINA070",Substr(cUsuario,7,6),@cArquivo)
				EndIf

				VALOR			:= SE1->E1_VALLIQ
				ABATIMENTO	:= Round(NoRound(xMoeda(nTotAbat,SE1->E1_MOEDA,nMoedaBco,dBaixa,3,nTxMoeda),3),2)

				If lUsaFlag  // Armazena em aFlagCTB para atualizar no modulo Contabil 
					aAdd( aFlagCTB, {"E5_LA", "S", "SE5", SE5->( Recno() ), 0, 0, 0} )
				Endif
				nTotal += DetProva( nHdlPrv, cPadrao, "FINA070", cLote, /*nLinha*/, /*lExecuta*/,;
				                    /*cCriterio*/, /*lRateio*/, /*cChaveBusca*/, /*aCT5*/,;
				                    /*lPosiciona*/, @aFlagCTB, /*aTabRecOri*/, /*aDadosProva*/ )

				If ExistBlock("F070CTB")
					nTotal += ExecBlock("F070CTB",.F.,.F.,{cPadrao,nHdlPrv})
				Endif
			Endif
			IF lPadrao .and. lContabiliza .and. lBaixou
				//-- Se for rotina automatica força exibir mensagens na tela, pois mesmo quando não exibe os lançametnos, a tela 
				//-- sera exibida caso ocorram erros nos lançamentos padronizados
				If lF070Auto
					lSetAuto := _SetAutoMode(.F.)
					lSetHelp := HelpInDark(.F.)
					If Type('lMSHelpAuto') == 'L'
						lMSHelpAuto := !lMSHelpAuto
					EndIf						
				EndIf

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Localizacao Portugal - Gera dados para diario contabil ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If ( FindFunction( "UsaSeqCor" ) .And. UsaSeqCor() )
					AAdd( aDiario, {"SE5",SE5->(Recno()),cCodDiario,"E5_NODIA","E5_DIACTB"} )
				Else
					aDiario := {} 
				EndIf      
				
				RodaProva(nHdlPrv,nTotal)                               

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Envia para Lan‡amento Cont bil                         ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				cA100Incl( cArquivo, nHdlPrv, 3 /*nOpcx*/, cLote, Iif(mv_par01==1,.T.,.F.) /*lDigita*/,;
				           Iif(mv_par02==1,.T.,.F.) /*lAglut*/,;
				           /*cOnLine*/, /*dData*/, /*dReproc*/, @aFlagCTB, /*aDadosProva*/, aDiario )
				aFlagCTB := {}  // Limpa o coteudo apos a efetivacao do lancamento
				
				If lF070Auto
					HelpInDark(lSetHelp)
					_SetAutoMode(lSetAuto)
					If Type('lMSHelpAuto') == 'L'
						lMSHelpAuto := !lMSHelpAuto
					EndIf
				EndIf					
			EndIf          

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Integracao Protheus X RM Classis e RM Biblios ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ				
			if (GetNewPar("MV_RMCLASS", .F.) .or. GetNewPar('MV_RMBIBLI',.F.)) .and. lBaixou
				if alltrim(SE1->E1_ORIGEM) == 'L' .or. alltrim(upper(SE1->E1_ORIGEM)) == 'S' .or. SE1->E1_IDLAN > 0
					//Executa a funcao para efetuar manutencao nas tabelas do CorporeRM e Replicar a baixa
					ClsProcBx(SE1->(Recno()),'1',"FIN070")
				Elseif TcCanOpen(RetSqlName('FIB')) .and. ClsIsCheq(SE1->(Recno()))
					//Titulo de cheque nativo do FINA220 (Caixa-Tesouraria)
					//Elimina as ocorrencias de devolução (se houver)
					ClsEliOco(SE1->(Recno()))
				Endif
			endif
		   
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Trecho incluido para integração e-commerce          ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If lBaixou .And. FindFunction("LJ861EC02") .And. FindFunction("LJ861EC01")
				If  LJ861EC01(SE1->E1_NUM, SE1->E1_PREFIXO, .T./*PrecisaTerPedido*/)
					LJ861EC02(SE1->E1_NUM, SE1->E1_PREFIXO)
				EndIf		
			EndIf

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Final  da prote‡„o via TTS                                   ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If FindFunction( "GETROTINTEG" ) .AND. !lFini055 .and. FindFunction("FwHasEAI") .and. FWHasEAI("FINA070",.T.,,.T.)
				FwIntegDef( 'FINA070' )
			Endif
		End Transaction

		//..... 
		//    Conforme situacao do parametro abaixo, integra com o SIGAGSP ³
		//    MV_SIGAGSP - 0-Nao / 1-Integra                               ³                
		//    Gera os Lancamentos de Orcamentos
		//    ........
		If GetNewPar("MV_SIGAGSP","0") == "1" 
			GSPF210()
		EndIf

		//Ponto de entrada do Template.
		If ExistTemplate("SACI008")
			ExecTemplate("SACI008",.F.,.F.)
		EndIf

		If ExistBlock("SACI008")
			ExecBlock("SACI008",.F.,.F.)
		EndIf 
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
		//³Integracao protheus X tin	³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
		
	EndIF

	If lF070Bxpc .And. ( SE1->E1_SALDO > 0 )
	   	ExecBlock( "F070BXPC", .F., .F. )         
	Else
		Exit
	EndIf	
End     

If "CX" $ cBanco
	aCaixaFin[1] := cBanco
	aCaixaFin[2] := cAgencia
	aCaixaFin[3] := cConta
Endif

If cAlias != NIL
	dbSelectArea(cAlias)
	dbSetOrder(nOrdem)
	dbGoTo( nReg )
EndIf

RestArea (aArea)

If FunName() == "FINA740"
	cPorta740 := cPortado	
	cBanco740 := cBanco 		
	cAgenc740 := cAgencia	
	cConta740 := cConta		 
Endif

Return lRet


































































































































































































































































































































































































































































































































































































/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ fA070Lot ³ Autor ³ Wagner Xavier         ³ Data ³ 03/08/92 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Seleciona titulos a serem baixados                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ fA070Lot()                                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ FINA070                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Fa070Lot( cAlias,nReg,nOpcx )
LOCAL oDlg
LOCAL oCredConta

LOCAL lGrava:=.F.
LOCAL nOrdem
LOCAL cIndex:=""
LOCAL oLoteFin
Local lPreMark := .T.
Local lF070Mark := ExistBlock("F070MARK")
Local oBancoLt
Local aButton := { { "PESQUISA", { || Fa070Pesq(oMark, "SE1")  }, STR0127,STR0001 } } //"Pesquisar..(CTRL-P)"###"Pesquisar"
Local bSet16 := SetKey(16,{||Fa070Pesq(oMark,"SE1")})
Local lFa070Bco	:= ExistBlock("FA070BCO")    
Local cChaveLbn	:= ""
Local cChaveLbnOld := ""
Local aSize := {}
Local oPanel
Local nOrder	:= 1             
Local nA		:= 0
Local cMoedaTx	:= ""
Local lEECFAT := SuperGetMv("MV_EECFAT",.F.,.F.) 
Local lEECFIN := SuperGetMv("MV_AVG0131",.F.,.F.) //DFS - 17/02/11 - Parâmetro para verificar integração com financeiro.
Local lF070BxLt := ExistBlock("F070BxLt")
Local lRetBlot := .T.

//294 - Natureza sintetica/Analitica
Local lNatSa := If (FindFunction("FNatSAIsOn"),FNatSAIsOn(),.F.)  

PRIVATE dVencDe   := Ctod(Space(8))
PRIVATE cNatDe    := CriaVar("E1_NATUREZ")
PRIVATE dVencATe  := Ctod(Space(8))
PRIVATE cNatAte   := CriaVar("E1_NATUREZ")
PRIVATE cBancoLt  := CriaVar("E1_PORTADO")
PRIVATE cAgenciaLt:= CriaVar("E1_AGEDEP")
PRIVATE cContaLt  := CriaVar("E1_CONTA")
PRIVATE nNroTit   := 0
PRIVATE nTotDesp  := 0
PRIVATE nTotJur   := 0
PRIVATE nTotMul   := 0
PRIVATE nCredConta:= 0
PRIVATE nTotDesc  := 0
PRIVATE nTotGer   := 0
PRIVATE nValor    := 0
PRIVATE cLoteFin  := Space(TamSX3("E1_LOTE")[1])
PRIVATE cNaturLote:= Space(10)
PRIVATE nTotAGer  := 0
PRIVATE nTotADesp := 0
PRIVATE nTotADesc := 0
PRIVATE nTotAMul  := 0
PRIVATE nTotAJur  := 0
PRIVATE nQtdTit   := 0
PRIVATE nOtrga    := 0
PRIVATE nDifCambio:= 0
PRIVATE aTxMoedas := {}
Private nMoedaBco := 1
Private lTitLote  := .T.
If cPaisLoc <> "BRA"
	aAdd(aTxMoedas,{"",1,PesqPict("SM2","M2_MOEDA1")})
	For nA := 2 To MoedFin()
		cMoedaTx := Str(nA,IIf(nA <= 9,1,2))
		If !Empty(GetMv("MV_MOEDA"+cMoedaTx))
			If lF070Auto .And. nA == SE1->E1_MOEDA
				aAdd(aTxMoedas,{GetMv("MV_MOEDA"+cMoedaTx),SE1->E1_TXMOEDA,PesqPict("SM2","M2_MOEDA"+cMoedaTx) })
			Else
				aAdd(aTxMoedas,{GetMv("MV_MOEDA"+cMoedaTx),RecMoeda(dDataBase,nA),PesqPict("SM2","M2_MOEDA"+cMoedaTx) })
			Endif
		Else
			Exit
		Endif
	Next
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Caso tenha seja um titulo gerado pelo SIGAEIC ou SIGAEEC não poderá sofrer baixa através desta rotina ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If GetMV("MV_EASYFIN") == "S" .And. UPPER(Alltrim(SE1->E1_ORIGEM)) $ "SIGAEIC"
	HELP(" ",1,"FAORIEIC")
	Return
Endif       

//If lEECFAT .And. UPPER(Alltrim(SE1->E1_ORIGEM)) $ "SIGAEEC" //DFS - A integração do financeiro independe da integração com o faturamento
// TDF - 26/12/11 - Acrescentado o módulo EFF para permitir liquidação
If lEECFIN .And. UPPER(Alltrim(SE1->E1_ORIGEM)) $ "SIGAEEC" .and. !(cModulo $ "EEC/EDC/ECO/EFF") //DFS - 17/02/11 - Trava para outros módulos para títulos gerados no EEC  
   F070Help()
   HELP(" ",1,"FAORIEEC") 
   Return
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Caso titulos originados pelo SIGALOJA estejam nas carteiras :  ³
//³I = Carteira Caixa Loja                                        ³
//³J = Carteira Caixa Geral                                       ³
//³Nao permitir esta operacao, pois ele precisa ser transferido   ³
//³antes pelas rotinas do SIGALOJA.                               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Upper(AllTrim(SE1->E1_SITUACA)) $ "I|J" .AND. Upper(AllTrim(SE1->E1_ORIGEM)) $ "LOJA010|LOJA701|FATA701"
	Help(" ",1,"NOUSACLJ")
	Return
Endif
While .T.
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Zera Acumuladores                                                 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	nNroTit   := 0
	nTotDesp  := 0
	nTotJur   := 0
	nTotMul   := 0
	nCredConta:= 0
	nTotDesc  := 0
	nTotGer   := 0
	nValor    := 0
	nTotAGer  := 0
	nTotADesp := 0
	nTotADesc := 0
	nTotAMul  := 0
	nTotAJur  := 0
	nQtdTit   := 0
	cMarca    := Get070Mark()

	If lF070Mark
		lPreMark := ExecBlock("F070MARK",.F.,.F.)
	Endif
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Salva ordem atual                                                 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea ( cAlias )
	nOrdem:=IndexOrd()
	nSalvRec:=Recno()
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Desenha tela padr„o do browse                                ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	nOpc1    := 0
	nValor   := 0
	nQtdTit  := 0

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se data do movimento ‚ menor que data limite de     ³
	//³ movimentacao no financeiro                                   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If SuperGetMv("MV_BXDTFIN",,"1") == "2" .and.!DtMovFin()
		Return
	Endif

	UltiLote()

	If FindFunction("IsPanelFin") .and. IsPanelFin()  //Chamado pelo Painel Financeiro			
		dbSelectArea(cAlias)
		oPanelDados := FinWindow:GetVisPanel()
		oPanelDados:FreeChildren()
		aDim := DLGinPANEL(oPanelDados)
		DEFINE MSDIALOG oDlg OF oPanelDados:oWnd FROM 0,0 To 0,0 PIXEL STYLE nOR( WS_VISIBLE, WS_POPUP )	
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Observacao Importante quanto as coordenadas calculadas abaixo: ³ 
		//³ -------------------------------------------------------------- ³ 		
		//³ a funcao DlgWidthPanel() retorna o dobro do valor da area do	 ³
		//³ painel, sendo assim este deve ser dividido por 2 antes da sub- ³
		//³ tracao e redivisao por 2 para a centralizacao. 					 ³		
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ		
		nEspLarg := (((DlgWidthPanel(oPanelDados)/2) - 266) /2) -5		 					
		nEspLin  := 0						
	
	Else	
		DEFINE MSDIALOG oDlg FROM	102,5 TO 301,552 TITLE OemToAnsi(STR0058)	PIXEL //"Baixa a Pagar em Lote"
		nEspLarg := 0 
	  	nEspLin  := 0
		
	Endif	
	
	oDlg:lMaximized := .F.
	oPanel := TPanel():New(0,0,'',oDlg,, .T., .T.,, ,20,20)
	oPanel:Align := CONTROL_ALIGN_ALLCLIENT    
		
	@ 02+nEspLin, 004+nEspLarg TO 98+nEspLin, 77+nEspLarg  LABEL OemToAnsi(STR0044) 	 		PIXEL OF oPanel   //"Dados Banc rios"
	@ 02+nEspLin, 079+nEspLarg TO 98+nEspLin, 180+nEspLarg LABEL OemToAnsi(STR0045) 			PIXEL OF oPanel //"Valores"
	@ 02+nEspLin, 182+nEspLarg TO 73+nEspLin, 270+nEspLarg LABEL OemToAnsi(STR0046)			PIXEL OF oPanel //"Filtragem"

	
	@ 15+nEspLin, 009+nEspLarg SAY STR0047 SIZE 30,07 OF oPanel PIXEL //"Banco"
	@ 12+nEspLin, 035+nEspLarg MSGET oBancolt VAR cBancolt SIZE 20,10 OF oPanel PIXEL F3 "SA6" HASBUTTON ;
					Valid IIF(nOpc1 ==2, .T.,AtulValidou() .And. F070VldBco(cBancolt,@cAgencialt,@cContalt,.T.,.T.,,.T.) .And. IiF(lFa070Bco,ExecBlock("FA070BCO",.F.,.F.),.T.))

	@ 29+nEspLin ,009+nEspLarg SAY STR0048 SIZE 30,07 OF oPanel PIXEL //"Agˆncia"
	@ 26+nEspLin ,035+nEspLarg MSGET cAgencialt            SIZE 25,10 OF oPanel PIXEL;
					Valid IIF(nOpc1 ==2, .T.,If(!lValidou,If(F070VldBco(cBancolt,@cAgencialt,@cContalt,.T.,.T.,cAgencialt,.T.),.T.,oBancolt:SetFocus()),.T.))

	@ 42+nEspLin ,009+nEspLarg SAY STR0049 SIZE 29,07 OF oPanel PIXEL //"Conta"
	@ 40+nEspLin ,035+nEspLarg MSGET cContalt              SIZE 38,10 OF oPanel PIXEL ;
					Valid IIF(nOpc1 ==2, .T.,If(!lValidou,If(F070VldBco(cBancolt,@cAgencialt,@cContalt,.T.,.T.,cAgencialt+cContalt,.T.),.T.,oBancolt:SetFocus()),.T.))

	@ 56+nEspLin ,009+nEspLarg SAY STR0050 SIZE 30,07 OF oPanel PIXEL //"N.Titulos"
	@ 54+nEspLin ,035+nEspLarg MSGET nNroTit               SIZE 13,10 OF oPanel PIXEL Picture "999" Valid nNroTit > 0

	@ 70+nEspLin ,009+nEspLarg SAY STR0051 SIZE 29,07 OF oPanel PIXEL //"Lote"
	@ 69+nEspLin ,035+nEspLarg MSGET oLoteFin VAR cLoteFin SIZE 24,10 OF oPanel PIXEL Picture "@!" ;
					 Valid IIF(nOpc1 ==2,.T.,(cChaveLbnOld := cChaveLbn, CheckLote("R")) .And. Fa070LoteFin(cLoteFin, @cChaveLbn)) ON CHANGE UnLockByName(cChaveLbn,.T.,.F.)  // Libera Lock

	@ 84+nEspLin ,009+nEspLarg SAY STR0052 SIZE 29,07 OF oPanel PIXEL //"Natureza"
	@ 82+nEspLin ,035+nEspLarg MSGET cNaturLote            SIZE 37,10 OF oPanel PIXEL Picture "@!"  F3 "SED" Valid ExistCpo("SED") .and. If(lNatSA, FinVldNat(.F.,cNaturLote,1),.T.) HASBUTTON

	//Coluna 2
	@ 14+nEspLin ,084+nEspLarg SAY STR0053 SIZE 53,07 OF oPanel PIXEL //"Valor T¡tulos"
	@ 11+nEspLin ,125+nEspLarg MSGET nTotGer               SIZE 50,10 OF oPanel PIXEL Picture PesqPict("SE1","E1_VALOR") Valid MontaTotal(oCredConta) HASBUTTON

	@ 27+nEspLin ,084+nEspLarg SAY STR0054 SIZE 53,07 OF oPanel PIXEL //"Total Despesas"
	@ 25+nEspLin ,125+nEspLarg MSGET nTotDesp              SIZE 50,10 OF oPanel PIXEL Picture PesqPict("SE1","E1_VALOR") Valid MontaTotal(oCredConta) HASBUTTON

	@ 42+nEspLin ,084+nEspLarg SAY STR0055 SIZE 53,07 OF oPanel PIXEL //"Total Descontos"
	@ 39+nEspLin ,125+nEspLarg MSGET nTotDesc              SIZE 50,10 OF oPanel PIXEL Picture PesqPict("SE1","E1_DESCONT") Valid MontaTotal(oCredConta) HASBUTTON

	@ 56+nEspLin ,084+nEspLarg SAY STR0056 SIZE 53,07 OF oPanel PIXEL //"Total Multas"
	@ 54+nEspLin ,125+nEspLarg MSGET nTotMul               SIZE 50,10 OF oPanel PIXEL Picture PesqPict("SE1","E1_MULTA") Valid MontaTotal(oCredConta) HASBUTTON

	@ 70+nEspLin ,084+nEspLarg SAY STR0057 SIZE 53,07 OF oPanel PIXEL //"Total Juros"
	@ 69+nEspLin ,125+nEspLarg MSGET nTotJur               SIZE 50,10 OF oPanel PIXEL Picture PesqPict("SE1","E1_JUROS") Valid MontaTotal(oCredConta) HASBUTTON

	@ 85+nEspLin ,084+nEspLarg SAY STR0058 SIZE 53,07 OF oPanel PIXEL //"Cr‚dito em C/C"
	@ 83+nEspLin ,125+nEspLarg MSGET oCredConta VAR nCredConta  SIZE 50,10 OF oPanel PIXEL Picture PesqPict("SE1","E1_VALOR") HASBUTTON


	//Coluna 3
	@ 16+nEspLin ,187+nEspLarg SAY STR0059 SIZE 44,07 OF oPanel PIXEL //"Do Vencto."
	@ 14+nEspLin ,227+nEspLarg MSGET dVencDe               SIZE 37,10 OF oPanel PIXEL Valid IIF(nOpc1 ==2, .T.,! Empty(dVencDe)) HASBUTTON

	@ 30+nEspLin ,187+nEspLarg SAY STR0060 SIZE 42,07 OF oPanel PIXEL //"At‚ o Vencto."
	@ 28+nEspLin ,227+nEspLarg MSGET dVencAte              SIZE 37,10 OF oPanel PIXEL Valid IIF(nOpc1 ==2, .T.,! Empty(dVencAte)) HASBUTTON

	@ 45+nEspLin ,187+nEspLarg SAY STR0061 SIZE 39,07 OF oPanel PIXEL //"Da Natureza"
	@ 42+nEspLin ,227+nEspLarg MSGET cNatDe                SIZE 37,10 OF oPanel PIXEL Picture "@!" F3 "SED" HASBUTTON

	@ 59+nEspLin ,187+nEspLarg SAY STR0062 SIZE 45,07 OF oPanel PIXEL //"Até a Natureza"
	@ 57+nEspLin ,227+nEspLarg MSGET cNatAte               SIZE 37,10 OF oPanel PIXEL Picture "@!" F3 "SED" HASBUTTON Valid cNatAte>=cNatDe


	If FindFunction("IsPanelFin") .and. IsPanelFin()  //Chamado pelo Painel Financeiro			
		oDlg:Move(aDim[1],aDim[2],aDim[4]-aDim[2], aDim[3]-aDim[1])			
		ACTIVATE MSDIALOG oDlg ON INIT FaMyBar(oDlg,;
		{||(nOpc1 := 1, IIF(F070OkLote(cBancolt,cAgencialt,cContalt,cLoteFin),oDlg:End(),nOpc1 := 0))},;
		{||(nOpc1 := 2,oDlg:End())})
	 		
		FinVisual(cAlias,FinWindow,(cAlias)->(Recno()),.T.)
	Else
		DEFINE SBUTTON FROM 80, 214 TYPE 1 ENABLE OF oDlg ACTION (nOpc1 := 1,IIF(Empty(cLoteFin),(oLoteFin:SetFocus(),nOpc1 := 0),oDlg:End()))
		DEFINE SBUTTON FROM 80, 244 TYPE 2 ENABLE OF oDlg ACTION (nOpc1 := 2,oDlg:End())
		ACTIVATE MSDIALOG oDlg VALID (iif(nOpc1==1,( CarregaSA6(cBancolt,cAgencialt,cContalt,.T.,,.T.).and. ValidaTotal(cLoteFin) ),.T.)) CENTERED
	Endif

	If nOpc1 # 1
		UnLockByName(cChaveLbn,.T.,.F.)  // Libera Lock
		If __lSX8
			RollBackSX8()
		Endif
		Exit
	Endif

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Chama a SumAbatRec antes da IndRegua para abrir alias auxiliar __SE1 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	SumAbatRec( "", "", "", 1, "")

	nMoedaBco := Max(MoedaBco(cBancoLt,cAgenciaLt,cContaLt),1)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Filtra o arquivo para t¡tulos em abertos                   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea( cAlias )
	cIndex := CriaTrab(nil,.f.)
	cChave  := IndexKey()
    nOrder:=IndexOrd()
	IndRegua("SE1",cIndex,cChave,,;
		IIF(cBancolt="CX" .or. cBancolt$GetMV("MV_CARTEIR"),;
			FA070Chec0(),;
			FA070ChecF()),;
		STR0063) //"Selecionando Registros..."

	nIndex := RetIndex("SE1")
	dbSelectArea("SE1")
	#IFNDEF TOP
		dbSetIndex(cIndex+OrdBagExt())
	#ENDIF
	dbSetOrder(nIndex+1)
	dbGoTop()
	If BOF() .and. EOF()
		Help(" ",1,"RECNO")
		If __lSX8
			RollBackSX8()
		Endif
		UnLockByName(cChaveLbn,.T.,.F.)  // Libera Lock
		Exit
	EndIf
	// Se estiver utilizando CMC7, abre a porta para cadastro do cheque recebido.
	If lOpenCmc7 == Nil .And. GetMv("MV_CMC7FIN") == "S" 
		OpenCMC7()
		lOpenCmc7 := .T.
	Endif

	nValor   := 0    // valor total dos T¡tulos,mostrado no rodape do browse
	nQtdTit  := 0    // quantidade de T¡tulos,mostrado no rodape do browse
	nOpca    := 0

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Faz o calculo automatico de dimensoes de objetos     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aSize := MSADVSIZE()	

	DEFINE MSDIALOG oDlg1 TITLE STR0064 From aSize[7],0 To aSize[6],aSize[5] OF oMainWnd PIXEL //"Baixa em Lote"
	oDlg1:lMaximized := .T.

	If lPreMark
		Dbeval( { |a| FA070Dbeva() } )
	Endif

	oPanel := TPanel():New(0,0,'',oDlg1,, .T., .T.,, ,15,15,.T.,.T. )
	oPanel:Align := CONTROL_ALIGN_TOP

	@003,005 Say STR0065 PIXEL Of oPanel // "Valor Total:"
	@003,060 Say oValor VAR nValor Picture PesqPict("SE1","E1_VALOR") PIXEL Of oPanel
	@003,120 Say STR0066 PIXEL Of oPanel// "Quantidade:"
	@003,150 Say oQtda VAR nQtdTit Picture "@E 99999" SIZE 50,10 PIXEL of oPanel

	oMark := MsSelect():New(cAlias,"E1_OK","!E1_SALDO",,@lInverte,@cMarca,{35,oDlg1:nLeft,oDlg1:nBottom,oDlg1:nRight})
	oMark:bMark := {| | finaDisplay(cMarca,lInverte,oValor,oQtda,"R")}
	oMark:bAval	:= {|| Fa070bAval(cAlias,cMarca,oValor,oQtda)}
	oMark:oBrowse:lhasMark = .t.
	oMark:oBrowse:lCanAllmark := .t.
	oMark:oBrowse:bAllMark := { || FA070Inverte(cMarca,oValor,oQtda) }
	oMark:oBrowse:Align := CONTROL_ALIGN_ALLCLIENT
	
	If FindFunction("IsPanelFin") .and. IsPanelFin()  //Chamado pelo Painel Financeiro			
	   ACTIVATE MSDIALOG oDlg1 ON INIT FaMyBar     (oDlg1,{||nOpca := 1, If(F070VlLt(nValor) .and. IIF (lF070BxLt,lRetBlot := ExecBlock("F070BxLt",.f.,.f.),.T.), oDlg1:End(),nOpca := 0)},{|| nOpca := 2,oDlg1:End()},aButton)
	Else
		ACTIVATE MSDIALOG oDlg1 ON INIT EnchoiceBar(	oDlg1,{|| nOpca := 1, If(F070VlLt(nValor) .and. IIF (lF070BxLt,lRetBlot := ExecBlock("F070BxLt",.f.,.f.),.T.) , oDlg1:End(),nOpca := 0)},{|| nOpca := 2,oDlg1:End()},,aButton) CENTERED
	Endif
	SetKey(16,bSet16)																

	IF nOpcA == 1
		lGrava :=fA070Grava()
		If __lSX8 .And. lGrava
			ConfirmSX8()
		Endif
	Elseif nOpcA == 2
		If __lSX8
			RollBackSX8()
		Endif	
	EndIf
	UnLockByName(cChaveLbn,.T.,.F.)  // Libera Lock
	Exit
End

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Restaura os ¡ndices                                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SE1")
#IFDEF TOP
	DbClearFilter()
#ELSE
	RetIndex("SE1")
	IF cIndex != ""
		FErase (cIndex+OrdBagExt())
	Endif
#ENDIF

DbSetorder(nOrder)
dbGoTo(nSalvRec)
cLoteFin := SPACE(TamSX3("E1_LOTE")[1])
Return

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³fa070Dbeva³ Autor ³ Wagner Xavier         ³ Data ³ 20/05/92 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Trata o dbeval para marcar e desmarcar item                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³Fa070dbeva()                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ FINA070                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function fA070Dbeva()
Local lSavTTS

IF nValor <= nTotGer .And. nValor+Moeda(E1_SALDO,1,"R") <= nTotGer .And. ;
	nQtdTit <= nNroTit .And. !E1_TIPO $ MVABATIM+"/"+MVPROVIS+"/"+MVIRABT+"/"+MVINABT+"/"+MVFUABT //adicionado MVFUABT pois a variável MVABATIM não está retornando FU-
	lSavTTS := __TTSInUse
	__TTSInUse := .f.

	RecLock("SE1",.f.)
	Replace E1_OK with cMarca
	MsUnlock()
	__TTSInUse := lSavTTS

	If E1_TIPO $ MVABATIM+"/"+MVFUABT //adicionado MVFUABT pois a variável MVABATIM não está retornando FU-
		nValor-=Moeda(E1_SALDO,1,"R")
	Else
		nValor+=Moeda(E1_SALDO,1,"R")
	Endif
	nQtdTit++
Else
	lSavTTS := __TTSInUse
	__TTSInUse := .f.

	RecLock("SE1",.f.)
	Replace E1_OK with "  "
	MsUnlock()
	__TTSInUse := lSavTTS

EndIF
Return .T.


























/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ fa070Can ³ Autor ³ Wagner Xavier         ³ Data ³ 06/05/92 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Rotina de Cancelamento de Baixa a receber                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ fa070can(ExpC1,ExpN1,ExpN1)                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ FINA070                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function fa070can(	cAlias	, nReg	, nOpcx, aEncho, nOpbaixa )

LOCAL lCanc070Bx	:= ExistBlock("CANC070J")
LOCAL lACAtivo		:= GetNewPar("MV_ACATIVO", .F.)
LOCAL lBaixaAbat	:= .F.
LOCAL nHdlPrv		:= 0
LOCAL nTotal		:= 0
LOCAL nSalvRec		:= 0
LOCAL cTitAnt		:= ""
LOCAL cDescrMo		:=" "
LOCAL aBaixa		:= {}
LOCAL nJuros		:= nMulta := nCorrec := nDescont := 0
LOCAL nTotAdto		:= 0
LOCAL aAux			:= {}
LOCAL nI			:= 0
LOCAL cHist070		:= ""
LOCAL cMot070		:= ""
LOCAL cLoteFin		:= Space(TamSX3("E1_LOTE")[1])
LOCAL nOrdem
LOCAL lPadrao
LOCAL cBanco, cAgencia, cConta
LOCAL cArquivo
LOCAL cSequencia
LOCAL cLA
LOCAL dDataAnt
LOCAL nValor
LOCAL nSe1ValLiq, nSe1Descont, nSe1Multa, nSe1Juros, nSe1Correc
LOCAL cChave
LOCAL dDataCrd
LOCAL cParcela
LOCAL cNum			:= CRIAVAR("E1_NUM")
LOCAL cPrefixo
LOCAL dBaixa
LOCAL cMoeda
LOCAL cCliente,cLoja
LOCAL lCtbBaixa		:= .F.
LOCAL nValorM1		:= nValorM2  := 0
LOCAL aBaixaSE3		:= {}
LOCAL nVlrBaixa		:= 0
LOCAL cTipoBaixa
LOCAL nRecSE5		:=0
LOCAL lRet			:= .T.
LOCAL lFa070Ca4		:= ExistBlock("FA070CA4")
LOCAL lFa070Ca4T	:= ExistTemplate("FA070CA4")
LOCAL aMotBx		:= ReadMotBx()
LOCAL nDifCambio	:= 0
LOCAL dDtdispo		:= dDataBase
LOCAL cModSPB		:= "1"	// Modalidade de SPB
LOCAL aModalSpb		:= {"TED","CIP","COMP"}
LOCAL cDescSpb		:= "TED"
LOCAL lSpbInUse		:= SpbInUse()
LOCAL x
LOCAL lBxCEC		:= .F.  //Verificador de existencia de baixa por compensacao entre carteiras
LOCAL lBxLiq		:= .F.
LOCAL cHistCan070	:= " "
LOCAL lChqDev		:= .F.
LOCAL nLin			:= 0
LOCAL cSeqSE5		:= Space(TamSX3("E5_SEQ")[1])
LOCAL cMultNat		:= "2"
LOCAL nValSE5:=0
LOCAL lAcreDecre	:= .F.
LOCAL nAcresc		:= 0
LOCAL nDecresc		:= 0
LOCAL nMoedaBco		:= 0
LOCAL nRecDelSef	:= 0
LOCAL cSefOrigem	:= ""
LOCAL cEfImpress	:= ""
LOCAL lCheque		:= .F.
LOCAL cNumCheq		:= ""
LOCAL lComp			:=.F.
LOCAL aAreaSE5		:={}
LOCAL aAreaAtu		:={}
LOCAL lBxConc		:= GetNewPar("MV_BXCONC","2") == "1"
LOCAL lGemInUse		:= .F.
LOCAL nLin2			:= 0
LOCAL oNumTit
LOCAL oCodCli
LOCAL oNumBco
LOCAL lBxCnab		:= GetMv("MV_BXCNAB") == "S"
LOCAL nTamTit		:= TamSX3("E2_PREFIXO")[1]+TamSX3("E2_NUM")[1]
LOCAL nTamTit2		:= TamSX3("E2_PREFIXO")[1]+TamSX3("E2_NUM")[1]+TamSX3("E2_PARCELA")[1]+TamSX3("E2_TIPO")[1]
LOCAL cLoteBaixa	:= ""
LOCAL cSeqCanSe5	:= "  "
LOCAL cKeyLote		:= ""
LOCAL lExclusao		:= .F. //Para tratar a exclusao de baixa
LOCAL lF070CCAN		:= ExistBlock("F070CCAN")
LOCAL lChqAdt		:= .F.	//Cheque para adiantamento
LOCAL lPanelFin		:= If (FindFunction("IsPanelFin"),IsPanelFin(),.F.)
LOCAL oNaturez
LOCAL oTipo
LOCAL aDiario		:= {}
LOCAL cLoteBx		:= ""
LOCAL lSaldoChq		:= (GetMv("MV_SLDBXCR") == "C")



LOCAL lContinua		:= .T.


// Indica se existe integracao com Distribuicao e Logistica
LOCAL lIntDL		:= SuperGetMv("MV_INTDL",.F.,"N") == "S"

//Controla o Pis Cofins e Csll na baixa  (1-Retem PCC na Baixa ou 2-Retem PCC na Emissão(default) )
LOCAL lPccBxCr		:= If (FindFunction("FPccBxCr"),FPccBxCr(),.F.)

//Controla IRPJ na baixa
Local lIrPjBxCr		:= If (FindFunction("FIrPjBxCr"),FIrPjBxCr(),.F.)
Local oIrrf
LOCAL aFlagCTB		:= {}
LOCAL lUsaFlag		:= SuperGetMV( "MV_CTBFLAG" , .T. /*lHelp*/, .F. /*cPadrao*/)
LOCAL nTamE1Parc	:= TamSx3("E1_PARCELA")[1]	// Tamanho do campo parcela

//Variaveis para tratamento de campos especiais passados na array do MSExecAuto (aAutoCab)
LOCAL aValidGet		:= {}
LOCAL nx			:= {}

// Total do titulo recebido em cheque
LOCAL nSomaCheq		:= 0

//Indica se existe um abatimento baixado relacionado com o titulo, mesmo que seja um titulo baixado parcialmente
//Para casos onde o titulo eh baixado parcialmente por compensacao e o saldo eh baixado pela baixa manual, onde o abatimento eh baixado
LOCAL lExBxAbat		:=.F.
Local lAtuSldNat := FindFunction("AtuSldNat") .AND. AliasInDic("FIV") .AND. AliasInDic("FIW")

Local lF070Cancel := ExistBlock("F070CANCEL")
Local aCancel := {}
Local lEECFAT := SuperGetMv("MV_EECFAT",.F.,.F.) 
Local lEECFIN := SuperGetMv("MV_AVG0131",.F.,.F.) //DFS - 17/02/11 - Parâmetro para verificar integração com financeiro.
Local lF070BAUT := ExistBlock("F070BAUT") 
Local lF070CANABT := EXISTBLOCK("F070CANABT")
Local nValabt := 0
Local lF70ALTABAT	:= ExistBlock("F70ALTABAT")
Local lFinSalTit := ExistBlock("FinSalTit")     
Local lF070CHDV	 := ExistBlock("F070CHDV")
Local nValRetIR	 := 0      
Local lAtuSldbco := .T.

Local nFlxTit := 0 
Local FlxAbat := 0
Local lFlxUltBaixa := .F.
Local aOldBaixaSE5
Local aBxSE5:= {}
//Controle de abatimento
Local lTitpaiSE1 := (SE1->(FieldPos("E1_TITPAI")) > 0)
Local nOrdTitPai:=0
Local bWhile := {|| !Eof() .and. cTitAnt == (SE1->E1_FILIAL+SE1->E1_CLIENTE+SE1->E1_LOJA+SE1->E1_PREFIXO+SE1->E1_NUM+SE1->E1_PARCELA) }
Local nMoedaSE5 := 1
Local lE5CM1 		:= SE5->(FieldPos("E5_CM1")>0)
Local nOldCM1		:= 0

PRIVATE aBaixaSE5	:= {}     
PRIVATE lRaRtImp  	:= lFinImp .And.FRaRtImp()     //Define se ha retencao de impostos PCC/IRPJ no R.A 
PRIVATE nCM1			:= nProRata := 0
//variavel incluida para evitar não conformidade quando a rotina é chamado pelo FINA223
PRIVATE lFini055		:=IsInCallStack("FINI055")

// Zerar variaveis para contabilizar os impostos da lei 10925.
VALOR5	:= 0
VALOR6	:= 0
VALOR7	:= 0
nPis 	:=	0
nCofins	:= 0
nCsll	:= 0
nIrrf	:= 0
PIS		:= 0
COFINS	:= 0
CSLL	:= 0
IRRF	:= 0

DEFAULT nOpBaixa := 1

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Caso tenha seja um titulo gerado pelo SIGAEIC ou SIGAEEC não poderá sofrer baixa através desta rotina ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ	ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If GetMV("MV_EASYFIN") == "S" .And. Upper(Alltrim(SE1->E1_ORIGEM)) $ "SIGAEIC"
	HELP(" ",1,"FAORIEIC")
	Return
Endif
//Caso a rotina esteja cadastrada no adapter, so pode ser enviada como 'Sincrona'. Uma baixa enviada como assincrona 
//sera concretizada mesmo que de erro no sistema integrado.
If !lFini055
	If !(Fa070Integ(.T.))
		Return
	Endif
Endif
//If lEECFAT .And. Upper(Alltrim(SE1->E1_ORIGEM)) $ "SIGAEEC" //DFS - A integração do financeiro independe da integração com o faturamento
// TDF - 26/12/11 - Acrescentado o módulo EFF para permitir liquidação
If lEECFIN .And. Upper(Alltrim(SE1->E1_ORIGEM)) $ "SIGAEEC" .and. !(cModulo $ "EEC/EDC/ECO/EFF") //DFS - 17/02/11 - Trava para outros módulos para títulos gerados no EEC 
   F070Help()
   HELP(" ",1,"FAORIEEC")
   Return
Endif

//Verifica permissao do usuario
If nOpcx == 6 .and. !VerSenha(52)
	Return
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Barra o Cancelamento de Baixas realizadas atraves³
//³da Rotina Recebimentos Diversos.                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If SE1->E1_RECIBO <> "      " .AND. AllTrim(Upper(SE1->E1_ORIGEM)) <> "FINA087A"
	MsgAlert(STR0206+Chr(13)+Chr(10)+;
	STR0210+SE1->E1_RECIBO+'.')
	Return Nil
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ PONTO DE ENTRADA para verificar se a baixa  ³
//³ pode ou nao ser cancelada.                  ³
//³ Retorna .T. ou .F. 					         	³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ExistBlock("FA070CA3")
	IF !ExecBlock('FA070CA3',.F.,.F.,nOpcx)
		Return
	EndIf
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica se o T¡tulo n„o sofreu nenhuma baixa                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
IF Empty(SE1->E1_BAIXA)
	Help(" ",1,"TITNAOXADO")
	Return
EndIF

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Executa funcao do PLS usado na Integração com Drogaria  		   		       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If HasTemplate("DRO") .AND. SuperGetMv("MV_PLSATIV",.F.,.F.) .AND. Alltrim(SE1->E1_TIPO) =="PS";
	.AND. Substr(SE1->E1_ORIGEM,1,3) == "LOJ" .AND. FindFunction("PLSBXDRO")
	If ! PLSBXDRO()
		Return
	Endif
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica se ‚ um registro Principal                                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
IF SE1->E1_TIPO $ MVABATIM+"/"+MVIRABT+"/"+MVINABT+"/"+MVFUABT //adicionado MVFUABT pois a variável MVABATIM não está retornando FU-
	Help(" ",1,"NAOPRINCIP")
	Return
EndIF

//
// Eh um titulo gerado pelo template GEM ?
//
If HasTemplate("LOT") .and. ExistTemplate("GEMSE1LIX")
	lGemInUse := ExecTemplate("GEMSE1LIX",.F.,.F.)
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se utiliza o modulo de Gerenciamento Academico.     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lACAtivo .And. cPaisLoc=="BRA"
	//verifica se pode realizar o cancelamento da baixa do titulo no GE
	If !AcFinCanc1()
		Return .F.
	EndIf
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Trecho incluido para integração e-commerce          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If  FindFunction("LJ861EC03") .And. !( LJ861EC03(SE1->E1_NUM, SE1->E1_PREFIXO) )
	Return .F.		
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Salva ordem atual                                                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nOrdem   := IndexOrd()
dbSetOrder(1)
cMoeda   := IIF(Empty(SE1->E1_MOEDA),"1",AllTrim(Str(SE1->E1_MOEDA,2)))
nSalvRec := Recno()
















































































































cNum     := SE1->E1_NUM
cPrefixo := SE1->E1_PREFIXO
cParcela := SE1->E1_PARCELA
cTipo    := SE1->E1_TIPO
cCliente := SE1->E1_CLIENTE
cLoja    := SE1->E1_LOJA
nOtrGa     := IIf (SE1->(FieldPos("E1_OTRGA"))> 0,SE1->E1_OTRGA,0)
nDifCambio := IIf (SE1->(FieldPos("E1_CAMBIO"))> 0,SE1->E1_CAMBIO,0)
nTotAbImp := 0
nTotAbLiq := 0
nTotAbat := 0






//Se nao for adiantamento, verifico os abatimentos
If !SE1->E1_TIPO $ MVRECANT + "/"+MV_CRNEG
	nTotAbat := SumAbatRec( cPrefixo, cNum, cParcela, SE1->E1_MOEDA,"V",,@nTotAbImp)
Endif

nTotAbLiq := nTotAbat - nTotAbImp

dbGoTo(nSalvRec)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Procura pelas baixas deste titulo                                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aBaixa := Sel070Baixa( "VL /V2 /BA /RA /CP /LJ /"+MV_CRNEG,cPrefixo, cNum, cParcela,cTipo,@nTotAdto,@lBaixaAbat,cCliente,cLoja,@nVlrBaixa,,@lBxCEC,@lBxLiq)
//Exibe na tela de Cancelamento as baixas na ordem que foram feitas
aSort(aBaixa,,,{|x,y| SubStr(x,Len(x)-1,Len(x)) < SubStr(y,Len(y)-1,Len(y))})
aSort(aBaixaSE5,,, {|x,y| x[9] < y[9] } )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Escolhe Baixa a ser cancelada                                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

If ! lF070Auto
	If Len(aBaixa) > 1
		cListBox := aBaixa[1]
		nOpbaixa := 0
		
		DEFINE MSDIALOG oDlg FROM 5, 5 TO 14, 56 TITLE STR0070 // "Escolha A Baixa"
		
		@  .5, 2 LISTBOX cListBox ITEMS aBaixa SIZE 171 , 40 Font oDlg:oFont
		DEFINE SBUTTON FROM 055,137 TYPE 1 ACTION (nOpbaixa := 1,oDlg:End()) ENABLE OF oDlg
		DEFINE SBUTTON FROM 055,164 TYPE 2 ACTION oDlg:End() ENABLE OF oDlg
		
		ACTIVATE MSDIALOG oDlg CENTERED
		If nOpBaixa == 1
			nOpBaixa := Ascan(aBaixa,cListBox)
		Endif
		
		If nOpBaixa == 0 .or. !F070VenBx(aBaixa,nOpBaixa) .or. !F070ImpBx(aBaixa,nOpBaixa)
			Return
		Endif
	Else
		nOpbaixa := 1
	EndIF
Else
	IF lF070BAUT 
		nOpBaixa :=	ExecBlock("F070BAUT",.F.,.F.,{aBaixa,nOpBaixa})
	EndIf   

	if nOpBaixa > Len(aBaixa)
		nOpBaixa := 1
	Endif

Endif












If Len(aBaixa) == 0
	
	If HasTemplate("LOT") .and. ExistTemplate("GEMREGSE5")
		If ExecTemplate("GEMREGSE5",.F.,.F.,{cPrefixo,cNum,cParcela,cTipo})
			dbSelectArea("SE1")
			Return
		EndIf
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Procura pelas compensa‡”es                                            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lBxCEC  // Se houver baixa por Compensacao entre Carteiras (CEC)
		Help(" ",1,"BX_CEC")
	ElseIf SE5->(MsSeek(xFilial("SE5")+"CP"+cPrefixo+cNum+cParcela+cTipo))
		Help(" ",1,"TITULOADT")
	ElseIf (SE1->E1_TIPO $ MV_CRNEG+"/"+MVRECANT) .and. !lBaixaAbat
		Help(" ",1,"TITULOADT")
	ElseIF !Empty(SE1->E1_NUMLIQ) .and. ;
		!Empty(SE1->E1_TIPOLIQ) .and. ;
		Empty(SE1->E1_BCOCHQ+SE1->E1_CTACHQ+SE1->E1_AGECHQ);
		.Or. !Empty(SE1->E1_TIPOLIQ)
		Help(" ",1,"BXLIQUIDAC")
	Elseif (SE1->E1_TIPO $ MVPROVIS .and. FunName() <> "FINA040" .and. !lF070Auto) 
        Help(" ",1,"BXPROVIS",,STR0226,1)//"Titulo Provisorio não pode ser estornado diretamente, exclua o título efetivo gerado na substituiçao!"
	Elseif Empty( SE1 -> E1_FATURA )
		Help(" ",1,"BAIXTITINC")
	ElseIf lBxLiq   // Se houver baixa por Liquidacao
		Help(" ",1,"BXLIQUIDAC")
	Else
		Help(" ",1,"TITFATURAD")
	EndIF
	dbSelectArea("SE1")
	Return
EndIF    


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Pega os Valores da Baixa Escolhida                                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dBaixa     := aBaixaSE5[nOpBaixa,07]
dDataCrd   := aBaixaSE5[nOpBaixa,10]
cSequencia := aBaixaSE5[nOpBaixa,09]
cBanco     := aBaixaSE5[nOpBaixa,11]
cAgencia   := aBaixaSE5[nOpBaixa,12]
cConta     := aBaixaSE5[nOpBaixa,13]
cChave     := SE1->E1_PREFIXO+SE1->E1_NUM+ ( PADR(SE1->E1_PARCELA , nTamE1Parc) ) +SE1->E1_TIPO+;
										Dtos(dBaixa)+SE1->E1_CLIENTE+SE1->E1_LOJA+cSequencia
cHist070   := ""
cMot070    := ""
lCtbBaixa  := .F.

// fluxo por natureza
lFlxUltBaixa := nOpBaixa == Len(aBaixaSE5)

//verifica se existem os campos de valores de acrescimo e decrescimo no SE5
If SE5->(FieldPos("E5_VLACRES")) > 0 .and. SE5->(FieldPos("E5_VLDECRE")) > 0
	lAcreDecre := .T.
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se data do movimento ‚ menor que data limite de     ³
//³ movimentacao no financeiro                                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
// Depois de conversar com a liderança da inovação, ficou claro que o parametro MV_DATAFIN
// foi criado para definir uma data para impedir a ATUALIZAÇÃO DE SALDO de um banco
// Logo, caso seja um cancelamento, a atualização de saldo é na data base e
// caso uma exclusão, será exclusão do movimento na data da movimentação (tem que impedir baseando-se na data do movimento)

If nOpcx == 5 // cancelamento
	If SuperGetMv("MV_BXDTFIN",,"1") == "2" .and.!DtMovFin()
		Return
	Endif
Elseif nOpcx==6 //exclusao
	If SuperGetMv("MV_BXDTFIN",,"1") == "2" .and.!DtMovFin(dBaixa)
		Return
	Endif
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se data do cancelamento ‚ menor que a data da baixa ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If dBaixa > dDataBase .And. !GetMv("MV_ANTCRED")
	Help(" ",1,"DTBAIXA")
	Return
Endif

dbSelectArea("SE5")
SE5->(dbSetOrder(2))

cTipoBaixa := "CM/C2/CX/DC/D2/MT/M2/JR/J2/TL/V2/BA/VL/LJ"

For nI := 1 to Len(cTipoBaixa) Step 3
	
	cType:=Substr(ctipoBaixa,nI,2)
	
	If SE5->( MsSeek(xFilial("SE5")+cType+cChave))
		
		If SE5->E5_SITUACA == "C"
			Loop
		EndIf
		If lPccBxCR
			If Empty(SE5->E5_PRETPIS)
				nPis 	:= SE5->E5_VRETPIS
				PIS		:=nPis
			Endif
			If Empty(SE5->E5_PRETCOF)
				nCofins := SE5->E5_VRETCOF  
				COFINS	:=nCofins
			Endif
			If Empty(SE5->E5_PRETCSL)
				nCsll 	:= SE5->E5_VRETCSL
				CSLL	:= nCsll
			Endif
		Endif
		If lIrPjBxCr
			If Empty(SE5->E5_PRETIRF)
				nIrrf 	:= SE5->E5_VRETIRF
				IRRF    := nIrrf
			Endif
		EndIf
		If cType $ "CM/C2/CX"
			nCorrec := SE5->E5_VALOR
		ElseIf cType $ "DC/D2"
			If ExistBlock("F070DCDesc")
				nAux := ExecBlock("F070DCDesc",.F.,.F.,{cType,cChave,cSequencia,dBaixa})
				If Valtype(nAux) == "N"
					nDescont := nAux
				EndIf
			Else
				If ( cPaisLoc <> "BRA" )
					nDescont := SE5->E5_VLMOED2
				Else
					nDescont := SE5->E5_VALOR
				Endif
			EndIf
		ElseIf cType $ "MT/M2"
			If ( cPaisLoc <> "BRA" )
				nMulta := SE5->E5_VLMOED2
			Else
				nMulta := SE5->E5_VALOR
			Endif
		ElseIf cType $ "JR/J2/TL"
			If ExistBlock("F070JRVlr")
				nAux := ExecBlock("F070JRVlr",.F.,.F.,{cType,cChave,cSequencia,dBaixa})
				If Valtype(nAux) == "N"
					nJuros := nAux
				EndIf
			Else
				If ( cPaisLoc == "CHI" )
					nJuros := 0
				ElseIf ( cPaisLoc <> "BRA" )
					nJuros := SE5->E5_VLMOED2
				Else
					nJuros := SE5->E5_VALOR
				Endif
			Endif
		ElseIf cType $ "V2"
			nValRec := SE5->E5_VALOR
			cHist070 := SE5->E5_HISTOR
			cHistCan070:= STR0069 // Cancelamento de baixa
			cMot070 := SE5->E5_MOTBX
			nRecSE5  := SE5->( recno() )
		Elseif cType $ "VL/LJ"
			If cPaisLoc<>"BRA"
				nValRec := SE5->E5_VLMOED2
				nValSE5 := SE5->E5_VALOR
			Else
				nValRec := SE5->E5_VALOR
			Endif
			cHist070 := SE5->E5_HISTOR
			cHistCan070:= STR0069 // Cancelamento de baixa
			cMot070 := SE5->E5_MOTBX
			nValorM2 := SE5->E5_VLMOED2
			nRecSE5  := SE5->( recno() )
			dDtDispo := SE5->E5_DTDISPO
			cMultNat	:= SE5->E5_MULTNAT
			cSeqSE5	:= SE5->E5_SEQ
			If lAcreDecre
				nAcresc := SE5->E5_VLACRES
				nDecresc := SE5->E5_VLDECRE
			Endif
			
			If SE5->(FieldPos("E5_PRORATA")) >0
				nProRata := SE5->E5_PRORATA
			EndIf
			
			If SE5->(FieldPos("E5_CM1")) >0
				nCM1 := SE5->E5_CM1
			EndIf
			
		Elseif cType $ "BA"
			If cPaisLoc<>"BRA"
				nValRec := SE5->E5_VLMOED2
				nValSE5 := SE5->E5_VALOR
			Else
				nValRec  := SE5->E5_VALOR
			Endif
			cHist070 := SE5->E5_HISTOR
			cHistCan070:= IIF(Empty(SE5->E5_LOTE),STR0069,STR0116+SE5->E5_LOTE) // Cancelamento de baixa###"Canc Baixa Lote "
			cMot070  := SE5->E5_MOTBX
			cLoteFin := SE5->E5_LOTE
			cLA      := SE5->E5_LA
			nValorM2 := SE5->E5_VLMOED2
			nRecSE5  := SE5->( recno() )
			dDtDispo := SE5->E5_DTDISPO
			cMultNat	:= SE5->E5_MULTNAT
			nDescont := SE5->E5_VLDESCO
			cSeqSE5	:= SE5->E5_SEQ
			If lAcreDecre
				nAcresc := SE5->E5_VLACRES
				nDecresc := SE5->E5_VLDECRE

				//baixa que foi feita total precisa ter o juros retomado na cencelamento de baixa 	
				// caso contrario dara diferenca no saldo do titulo							
				If (IsInCallStack("LjRecCancBx")) .AND. SE1->E1_VALOR == (SE5->E5_VALOR -SE5->E5_VLJUROS) 
					nJuros := SE5->E5_VLJUROS
				EndIf	
				
			Endif
		EndIf
		nMoedaSE5 := Val(SE5->E5_MOEDA) 
		
		lCtbBaixa := If("S"$SE5->E5_LA,.T.,lCtbBaixa)
		aadd(aBaixaSE3,{ SE5->E5_MOTBX , SE5->E5_SEQ , SE5->(Recno()) })
	EndIf
	
Next nI

If nCM1 > 0
	nJuros -= nCM1
Else
	nDescont += nCM1
EndIf

If nProRata > 0
	nJuros -= nProRata
Else
	nDescont += nProRata
EndIf

cHistCan070:= PadR(cHistCan070,TamSx3("E5_HISTOR")[1])
SE5->( dbGoTo( nRecSE5 ) )

//Nao permitir o cancelamento de titulos cuja baixa foi feita pelos motivos Distrato e Aditivos - Integracao Tin x Protheus
If Upper(AllTrim(SE5->E5_MOTBX))== "TIN"  .And. AllTrim(SE1->E1_ORIGEM)== "FINI055"
	HELP(" ",1,"ProtheusXTIN" ,,STR0229,2,0)//"Títulos baixados por DISTRATO ou ADITIVO na Integração Protheus X TIN não podem ter a baixa cancelada!"
	Return 
Endif

//Nao permito cancelamento de baixa se a mesma foi conciliada e se
//o parametro MV_BXCONC estiver como 2(Padrao) - Nao permite
If !lBxConc
	
	lConciliado := .F.
	
	If !Empty(SE5->E5_RECONC)
		lConciliado := .T.
	Endif
	
	//Titulo baixado em bordero com movimento bancario totalizador
	If !lConciliado .and. !Empty(E5_DOCUMEN) .and. !Empty(E5_LOTE) .and. lBxCnab
		dbSelectArea("SE5")
		SE5->(dbSetOrder(5))
		cLoteBx := SE5->E5_LOTE
		If SE5->(MsSeek(xFilial("SE5")+cLoteBx+Space(nTamTit)))
			// Percorre movimentacoes de baixa, pelo lote e mesmo titulo do bordero e checa se existe reconciliacao bancaria
			Do While SE5->( !EoF() .And. E5_FILIAL+E5_LOTE+E5_PREFIXO+E5_NUMERO == xFilial("SE5")+cLoteBx+Space(nTamTit) )
				If SE5->E5_RECPAG == "R" .And. !Empty(SE5->E5_RECONC)
					lConciliado := .T.
					Exit
				EndIf
				SE5->(dbSkip())
			EndDo
		Endif
	Endif
	
	//Titulo baixado por lote
	If !lConciliado .and. Empty(E5_DOCUMEN) .and. !Empty(E5_LOTE) .and. lBxCnab
		dbSelectArea("SE5")
		SE5->(dbSetOrder(2))
		If SE5->(MsSeek(xFilial("SE5")+"BL"+Space(nTamTit2)+DTOS(SE5->E5_DATA)))
			cKeyLote := xFilial("SE5")+"BL"+Space(nTamTit2)+DTOS(SE5->E5_DATA)
			While SE5->(!Eof()) .and. cKeylote == xFilial("SE5")+"BL"+Space(nTamTit2)+DTOS(SE5->E5_DATA)
				If RIGHT(Alltrim(SE5->E5_HISTOR),4) == cLoteFin .and. !Empty(SE5->E5_RECONC)
					lConciliado := .T.
					Exit
				Else
					dbSkip()
				Endif
			Enddo
		Endif
	Endif
	
	If lConciliado
		Help(" ",1,"BXCONCIL")
		dbSelectArea("SE1")
		Return
	Endif 
Endif



//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Caso o motivo de baixa seja "CNF"- Cancelamento de Nota Fatura ³
//³ e o moduto for diferente de 65 (SIGAGAV - Gestao Advocaticia), ³
//³ nao permito o cancelamento da baixa.                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Alltrim(Upper(cMot070)) == "CNF" .And. nModulo <> 65
	Help(" ",1,"FA070GAV",,STR0188+Chr(13)+Chr(10)+Chr(13)+Chr(10)+STR0189, 1, 0 )
	Return
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica se foi utilizada taxa contratada para moeda > 1          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If cPaisLoc<>"BRA"
	nTxMoeda:= If( SE5->(FieldPos("E5_TXMOEDA"))>0, SE5->E5_TXMOEDA , (SE5->E5_VALOR / SE5->E5_VLMOED2) )
Else
	If SE1->E1_MOEDA > 1 .and. Round(NoRound(xMoeda(nValRec,nMoedaBco,SE1->E1_MOEDA,dBaixa,3),3),2) != SE5->E5_VLMOED2
		nTxMoeda := SE5->E5_VALOR / SE5->E5_VLMOED2
	Else
		nTxMoeda := RecMoeda(dBaixa,SE1->E1_MOEDA)
	Endif
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Caso moeda == 1 a funcao RecMoeda iguala nTxMoeda = 0. Iguala-se   ³
//³nTxMoeda = 1 p/ evitar problema c/ calculos de abatimento e outros.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nTxMoeda := IIF(nTxMoeda == 0 , 1 , nTxMoeda)

//ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica se foi ja foi gerado cheque para esta baixa; se o cheque ³
//³ ja foi gerado nao permite o cancelamento								  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

If (SE5->E5_TIPO $ MV_CRNEG+"/"+MVRECANT)
	dbSelectArea("SEF")
	dbSetOrder(3)
	If SEF->(MsSeek(xFilial()+SE5->E5_PREFIXO+SE5->E5_NUMERO+SE5->E5_PARCELA+SE5->E5_TIPO))
		cSeqSe5 := SE5->E5_SEQ
		lCheque := .F.
		cEfImpress := ""
		While SEF->( !Eof()) .and. EF_FILIAL == xFilial() 		.and. ;
				EF_TITULO == SE5->E5_NUMERO	.and. ;
				EF_PARCELA== SE5->E5_PARCELA .and. ;
				EF_PREFIXO== SE5->E5_PREFIXO .and. ;
				EF_TIPO   == SE5->E5_TIPO		
			If Str(SE5->E5_VALOR,17,2)	 == Str(SEF->EF_VALOR,17,2)	.And.;
				SEF->EF_SEQUENC == cSeqSe5			.And.;
				SE5->E5_CLIFOR  == SEF->EF_FORNECE
				cEfImpress	:= SEF->EF_IMPRESS
				// Permite a baixa se houver cheque cancelado.
				IF SEF->EF_IMPRESS # "C"
					lCheque		:= .T.
					cNumCheq		:= SEF->EF_NUM
					Exit
				Endif
			Endif
			SEF->( dbSkip() )
		End
	Endif

	dbSelectArea("SEF")
	dbSetOrder(1)

	If (lCheque .and. !Empty(cNumcheq)) .Or. (!Empty(SE1->E1_NUMBCO) .and. !Empty(cEfImpress))
		Help(" " , 1 , "FA080TEMCH")
		dbSelectArea("SE1")
		Return
	Endif
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se foi gerado cheque para esta baixa. Se o cheque já		³
//³ foi compensado, ele nao permitirá o cancelamento até que seja    ³
//³ feito o estorno pela rotina de cheques recebidos.	   			   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If GetMv("MV_SLDBXCR") == "C"		// Controla saldo na compensacao do cheque
	dbSelectArea("SEF")
	SEF->(dbSetOrder(3))
	If SEF->(MsSeek(xFilial("SEF")+SE5->E5_PREFIXO+SE5->E5_NUMERO+SE5->E5_PARCELA+SE5->E5_TIPO))
		cSeqSe5 := SE5->E5_SEQ
		lCheque := .F.
		While SEF->(!Eof()) .and. SEF->EF_FILIAL == xFilial() .And. ;
				SEF->EF_TITULO == SE5->E5_NUMERO	.And. ;
				SEF->EF_PARCELA== SE5->E5_PARCELA .And. ;
				SEF->EF_PREFIXO== SE5->E5_PREFIXO .And. ;
				SEF->EF_TIPO   == SE5->E5_TIPO
			If Str(SE5->E5_VALOR,17,2)	 == Str(SEF->EF_VALOR,17,2)	.And.;
				SEF->EF_SEQUENC == cSeqSe5			.And.;
				SEF->EF_CLIENTE == SE5->E5_CLIFOR
				If !Empty(SEF->EF_DTCOMP)
           		lCheque := .T.		
					Exit
				EndIf
			Endif
			SEF->(dbSkip())
		EndDo
	Endif
	dbSelectArea("SEF")
	dbSetOrder(1)
	If lCheque
		HELP(' ',1,"F070CHCOMP",,	STR0206+Chr(13)+Chr(10)+;		//"Esta baixa não poderá ser cancelada ou"
											STR0207+Chr(13)+Chr(10)+;		//"excluída, pois possui um cheque vínculado"
											STR0208+Chr(13)+Chr(10)+;		//"que já foi compensado. Será necessário"
											STR0209,1,0)						//"fazer o estorno da compensação."
		dbSelectArea("SE1")
		Return
	EndIf
EndIf

SE5->( dbGoTo( nRecSE5 ) )

dbSelectArea("SE1")

nI :=  Ascan(aMotBx, {|x| Substr(x,1,3) == Upper(cMot070) })
cDescrMo := if( nI > 0,Substr(aMotBx[nI],07,10),"" )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Mostra dados cadastrais,e referentes a baixa                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nOpc1 := 0
SA1->(MsSeek(xFilial("SA1")+SE1->E1_CLIENTE+SE1->E1_LOJA))
cMoeda      := IIF(Empty(SE1->E1_MOEDA),"1",AllTrim(Str(SE1->E1_MOEDA,2)))
cNomeCli    := SE1->E1_CLIENTE + " - " + Subst(SA1->A1_NOME,1,40)
cTitulo 		:= SE1->E1_PREFIXO + " " + SE1->E1_NUM+ " " + SE1->E1_PARCELA
cSituacao   := SE1->E1_SITUACA + " " + fa070situa()
cDescMoeda  := SubStr(GetMV("MV_SIMB"+cMoeda),1,3)

If lSpbInUse
	cModSpb	:= IIF(!Empty(SE5->E5_MODSPB),SE5->E5_MODSPB,"1")
	cDescSpb := aModalSpb[Val(cModSpb)]
Endif
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Recebe os dados do t¡tulo a ser baixado                              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

IF lF070Cancel 
	aCancel :=  ExecBlock("F070CANCEL",.f.,.f.,{nTotAbLiq,nTotAbImp,nValorM1,nCM1,nDescont,nMulta,nJuros})
	nTotAbLiq := aCancel[1][1]
	nTotAbImp:= aCancel[1][2]
	nValorM1 := aCancel[1][3]
	nCM1 := aCancel[1][4]
	nDescont := aCancel[1][5]
	nMulta := aCancel[1][6]
	nJuros := aCancel[1][7]  
EndIf

//Se nao for adiantamento, verifico os abatimentos
If !SE1->E1_TIPO $ MVRECANT + "/"+MV_CRNEG .AND. lF70ALTABAT
//	nTotAbImp := ExecBlock("F70ALTABAT",.F.,.F., nTotAbImp)

	nTotAbat := nTotAbImp
Endif

If ! lF070Auto

	DEFINE FONT oFontLbl NAME "Arial" SIZE 6, 15 BOLD

	nLin2 := If(cPaisLoc=="BRA",552,520)
	// Template GEM, nova linha do rodape para os campos especificos do template.
	If lGemInUse .and. (SE5->E5_MOTBX $ "CSS;DIS")
		MsgAlert("Este título foi baixado por uma rotina automatica(Cessao de Direito ou Distrato) do Template GEM e não poderá ser cancelada sua baixa.")
		Return
	EndIf

	nLin2 += 24
	
	DEFINE MSDIALOG oDlg FROM  69,33 TO nLin2,581 TITLE STR0074 PIXEL OF oMainWnd  //"Cancelamento da Baixa a Receber"

	oPanel1 := TPanel():New(0,0,'',oDlg,, .T., .T.,, ,45,45,.f.,.f. )
	oPanel1:Align := CONTROL_ALIGN_TOP

	oPanel2 := TPanel():New(0,0,'',oDlg,, .T., .T.,, ,30,30,.f.,.f. )
	oPanel2:Align := CONTROL_ALIGN_ALLCLIENT

	@ 001,002 GROUP oGrp1 TO 043, 272 LABEL STR0008 OF oPanel1 PIXEL //"Principal"
	@ 001,002 GROUP oGrp2 TO If(cPaisLoc=="BRA",170,165), 133 LABEL STR0009 OF oPanel2  PIXEL //"Dados Gerais"
	@ 001,139 GROUP oGrp3 TO If(cPaisLoc=="BRA",170,165), 272 LABEL STR0010 OF oPanel2  PIXEL //"Valores da Baixa"
	oGrp1:oFont := oFontLbl
	oGrp2:oFont := oFontLbl
	oGrp3:oFont := oFontLbl

	//////////////////////////
	//Dados do titulo
	@ 008,004 SAY STR0211				SIZE 31,07 OF oPanel1 PIXEL //"Prefixo"
	@ 008,027 MSGET SE1->E1_PREFIXO	SIZE 25,08 OF oPanel1 PIXEL When .F.
	@ 008,060 SAY STR0212 				SIZE 31,07 OF oPanel1 PIXEL //"Número"
	@ 008,085 MSGET SE1->E1_NUM		SIZE 70,08 OF oPanel1 PIXEL When .F.
	@ 008,165 SAY STR0213				SIZE 31,07 OF oPanel1 PIXEL //"Parcela"
	@ 008,188 MSGET SE1->E1_PARCELA	SIZE 25,08 OF oPanel1 PIXEL When .F.
	@ 008,220 SAY STR0214				SIZE 31,07 OF oPanel1 PIXEL //"Tipo"
	@ 008,238 MSGET oTipo VAR cTipo	F3 "SE1RDO" SIZE 30,08 OF oPanel1 PIXEL HASBUTTON
	oTipo:lReadOnly := .T.
	
	@ 019,004 SAY STR0014 SIZE 22, 07 OF oPanel1 PIXEL //"Cliente"
	@ 019,027 MSGET oCodCli VAR SE1->E1_CLIENTE F3 "SA1" SIZE 70,08 OF oPanel1 PIXEL HASBUTTON //READONLY 
	oCodCli:lReadOnly := .T.
	@ 019,105 MSGET SA1->A1_NOME SIZE 165,08 OF oPanel1 PIXEL When .F.

	@ 030,004 SAY STR0052 				SIZE 31,07 OF oPanel1 PIXEL //"Natureza"
	@ 030,027 MSGET oNaturez VAR SE1->E1_NATUREZ	F3 "SED" SIZE 70,08 OF oPanel1 PIXEL HASBUTTON
	oNaturez:lReadOnly := .T.
	@ 030,105 SAY STR0012 				SIZE 31,07 OF oPanel1 PIXEL //"Emiss„o"
	@ 030,133 MSGET SE1->E1_EMISSAO	SIZE 48,08 OF oPanel1 PIXEL When .F.
	@ 030,189 SAY STR0013 				SIZE 49,07 OF oPanel1 PIXEL //"Vencto.Atual"
	@ 030,222 MSGET SE1->E1_VENCREA	SIZE 48,08 OF oPanel1 PIXEL When .F.

	//////////////////////////
	//Dados Gerais		
	nUltLin := 10
	@ nUltLin,005 SAY STR0015 					SIZE 35, 07 OF oPanel2 PIXEL //"Hist.Emiss„o"
	@ nUltLin,065 MSGET SE1->E1_HIST  		SIZE 65, 08 OF oPanel2 PIXEL When .F.

	nUltLin += 12
	@ nUltLin,005 SAY STR0016					SIZE 35, 07 OF oPanel2 PIXEL //"Situa‡„o"
	@ nUltLin,065 MSGET cSituacao				SIZE 65, 08 OF oPanel2 PIXEL When .F.

	nUltLin += 12	
	@ nUltLin,005 SAY STR0023					SIZE 35, 07 OF oPanel2 PIXEL //"Mot.Baixa"
	@ nUltLin,065 MSGET cDescrMo				SIZE 65, 08 OF oPanel2 PIXEL When .F.

	nUltLin += 12
	@ nUltLin,005 SAY STR0017 					SIZE 35, 07 OF oPanel2 PIXEL //"Banco"
	@ nUltLin,065 MSGET oNumBco VAR cBanco	SIZE 65, 08 OF oPanel2 PIXEL F3 "SA6" HASBUTTON
	oNumBco:lReadOnly := .T.

	nUltLin += 12
	@ nUltLin,005 SAY STR0018					SIZE 35, 07 OF oPanel2 PIXEL //"Agˆncia"
	@ nUltLin,065 MSGET cAgencia				SIZE 65, 08 OF oPanel2 PIXEL  When .F. 

	nUltLin += 12
	@ nUltLin,005 SAY STR0019					SIZE 35, 07 OF oPanel2 PIXEL //"Conta"
	@ nUltLin,065 MSGET cConta					SIZE 65, 08 OF oPanel2 PIXEL  When .F.

	nUltLin += 12		
	@ nUltLin,005 SAY STR0020					SIZE 35, 07 OF oPanel2 PIXEL//"Data Receb."
	@ nUltLin,065 MSGET dBaixa					SIZE 65, 08 OF oPanel2 PIXEL When .F.
		
	nUltLin += 12
	@ nUltLin,005 SAY STR0022					SIZE 35, 07 OF oPanel2 PIXEL //"Hist.Baixa"
	@ nUltLin,065 MSGET cHist070				SIZE 65, 08 OF oPanel2 PIXEL When .F.

	nUltLin += 12
	@ nUltLin,005 SAY STR0171					SIZE 35, 07 OF oPanel2 PIXEL //"Hist.Cancel."
	@ nUltLin,065 MSGET cHistCan070			SIZE 65, 08 OF oPanel2 PIXEL

	nUltLin += 12
	@ nUltLin,005 SAY STR0172					SIZE 100, 07 OF oPanel2 PIXEL //"Cancelamento por Dev.Cheque" 
	IF lF070CHDV
		@ nUltLin,090 CHECKBOX oChqCan VAR lChqDev := IIF(lF070CHDV, ExecBlock("F070CHDV",.F.,.F.),.F.) PROMPT "" SIZE 11,11 OF oPanel2 PIXEL
	ELSE 
		@ nUltLin,090 CHECKBOX oChqCan VAR lChqDev PROMPT "" SIZE 11,11 OF oPanel2 PIXEL	
	ENDIF
	If lSpbInUse
		nUltLin += 12
		@ nUltLin,005 SAY STR0140				SIZE 35, 07 OF oPanel2 PIXEL  //"Modalidade SPB"
		@ nUltLin,065 MSGET cDescSpb 			SIZE 65, 08 OF oPanel2 PIXEL When .F.
	Endif

	//////////////////////////
	//Dados da Baixa		
	nLinha := 10
	@ nLinha,144 SAY STR0027 + cDescMoeda	SIZE 53, 08 OF oPanel2 PIXEL //"Valor Original"
	@ nLinha,204 MSGET SE1->E1_VALOR			SIZE 65, 08 OF oPanel2 PIXEL When .F. Picture PesqPict("SE1","E1_VALOR") //"@E 999,999,999,999.99"

	If cPaisLoc <> "CHI"

		nLinha +=12
		@ nLinha, 144 SAY STR0091				SIZE 53, 08 OF oPanel2 PIXEL //"- Abatimentos"
		@ nLinha, 204 MSGET nTotAbLiq			SIZE 65, 08 OF oPanel2 PIXEL When .F.    Picture PesqPict( "SE1","E1_VALOR" )  //"@E 999,999,999,999.99"

   	If cPaisLoc == "BRA"	
   		nLinha +=12
   		@ nLinha,144 SAY STR0186 			SIZE 53, 08 OF oPanel2 PIXEL // "- Impostos"
   		@ nLinha,204 MSGET nTotAbImp		SIZE 65, 08 OF oPanel2 PIXEL When .F.  Picture PesqPict( "SE1","E1_VALOR" )  //"@E 999,999,999,999.99"      

   		nLinha +=12
			nValorLiq :=  (SE1->E1_VALOR - nTotAbLiq - nTotAbImp) 	
			@ nLinha,144 SAY STR0187		SIZE 53, 08 OF oPanel2 PIXEL // "Valor Liquido"
			@ nLinha,204 MSGET nValorLiq	SIZE 65, 08 OF oPanel2 PIXEL When .F. Picture PesqPict("SE1","E1_VLCRUZ") //"@E 999,999,999,999.99"		
		EndIf 
	EndIf        

	nLinha +=12
	IF lF070Cancel = .F.
		nValorM1 := SE1->E1_VALOR-SE1->E1_SALDO
	EndIf
	@ nLinha,144 SAY STR0029					SIZE 53, 08 OF oPanel2 PIXEL //"- Pagtos Parciais"
	@ nLinha,204 MSGET nValorM1				SIZE 65, 08 OF oPanel2 PIXEL When .F.    PicTure PesqPict( "SE1","E1_VALOR" )  //"@E 999,999,999,999.99"

	// Template GEM, campo especifico do template.
	If lGemInUse
		nLinha +=12
		@ nLinha,144 SAY STR0203				SIZE 53, 08 OF oPanel2 PIXEL // "+ C.M."
		@ nLinha,204 MSGET nCM1 				SIZE 65, 08 OF oPanel2 PIXEL When .F. Picture PesqPict( "SE1","E1_JUROS" ) //"@E 999,999,999,999.99" 
	EndIf
	
	nLinha +=12
	@ nLinha, 144 SAY STR0093 					SIZE 53, 08 OF oPanel2 PIXEL //"- Descontos"
	@ nLinha, 204 MSGET nDescont				SIZE 65, 08 OF oPanel2 PIXEL When .F.    Picture PesqPict( "SE1","E1_DESCONT" )  //"@E 999,999,999,999.99"
														
	// Template GEM, campo especifico do template.
	If lGemInUse
		nLinha +=12
		@ nLinha,144 SAY STR0204 				SIZE 53, 08 OF oPanel2 PIXEL // "+ Pro Rata" 
		@ nLinha,204 MSGET nProRata 			SIZE 65, 08 OF oPanel2 PIXEL When .F. Picture PesqPict( "SE1","E1_JUROS" ) //"@E 999,999,999,999.99" 
	EndIf

	nLinha +=12
	@ nLinha, 144 SAY STR0094 					SIZE 53, 08 OF oPanel2 PIXEL //"+ Multa"
	@ nLinha, 204 MSGET nMulta					SIZE 65, 08 OF oPanel2 PIXEL When .F.    Picture PesqPict( "SE1","E1_MULTA" )  //"@E 999,999,999,999.99"

	nLinha += 12
	If cPaisLoc <> "CHI"
	   @ nLinha, 144 SAY STR0095 				SIZE 53, 08 OF oPanel2 PIXEL //"+ Tx.Permanenc."
	   @ nLinha, 204 MSGET nJuros				SIZE 65, 08 OF oPanel2 PIXEL When .F.    Picture PesqPict( "SE1","E1_JUROS" )  //"@E 999,999,999,999.99"
	Else 
	   @ nLinha, 144 SAY STR0133 				SIZE 53, 08 OF oPanel2 PIXEL //"- Otros Gastos"
	   @ nLinha, 204 MSGET nOtrga				SIZE 65, 08 OF oPanel2 PIXEL When .F.    Picture PesqPict( "SE1","E1_OTRGA" )  //"@E 999,999,999,999.99"	
	EndIf  
	//Controla IRPJ na baixa
	If   cPaisLoc == "BRA" .And. lIrPjBxCr
		nLinha +=12	
		@ nLinha,144 SAY STR0228  SIZE 53, 07 OF oPanel2 PIXEL  // "- IRRF"
		@ nLinha,204 MSGET oIrrf VAR nIrrf SIZE 65, 08 OF oPanel2 PIXEL When .F. Picture PesqPict( "SE1","E1_IRRF" ); //"@E 999,999,999,999.99"
														 Valid fA070Val(nIrrf,nTxMoeda)

	EndIf
	
	If cPaisLoc == "BRA" .And. lPccBxCR //1-Retem PCC na Baixa ou 2-Retem PCC na Emissão(default)
	   nLinha +=12
	   @ nLinha,144 SAY STR0216 SIZE 53, 07 OF oPanel2 PIXEL //"- PIS"
	   @ nLinha,204 MSGET oPIS VAR nPIS   SIZE 65, 08 OF oPanel2 PIXEL When .F.  Picture PesqPict( "SE1","E1_PIS" ) ; //"@E 999,999,999,999.99"
 	   Valid fA070Val(nPIS,nTxMoeda)
	
	   nLinha +=12
	   @ nLinha,144 SAY STR0217 SIZE 53, 07 OF oPanel2 PIXEL //"- COFINS"
	   @ nLinha,204 MSGET oCOFINS VAR nCOFINS   SIZE 65, 08 OF oPanel2 PIXEL When .F. Picture PesqPict( "SE1","E1_COFINS" ) ; //"@E 999,999,999,999.99"
 	   Valid fA070Val(nCOFINS,nTxMoeda)
	
	   nLinha +=12
	   @ nLinha,144 SAY STR0218 SIZE 53, 07 OF oPanel2 PIXEL //"- CSLL"
	   @ nLinha,204 MSGET oCSLL VAR nCSLL   SIZE 65, 08 OF oPanel2 PIXEL When .F. Picture PesqPict( "SE1","E1_CSLL" ) ; //"@E 999,999,999,999.99"
 	   Valid fA070Val(nJuros,nTxMoeda)
	EndIf  

	nLinha += 12
	@ nLinha, 144 SAY STR0097 					SIZE 53, 08 OF oPanel2 PIXEL //"= Valor Recebido"
	@ nLinha, 204 MSGET iif(cPaisLoc<>"BRA",nValSE5,nValRec)				SIZE 65, 08 OF oPanel2 PIXEL  When .F.   Picture PesqPict( "SE1","E1_VALOR" )  //"@E 999,999,999,999.99"

	If SE1->E1_MOEDA > 1
	   If cPaisLoc <> "CHI"
			nLinha += 12        
			@ nLinha, 144 SAY STR0098 + SubStr(GetMV("MV_SIMB"+cMoeda),1,3) SIZE 53,08 OF oPanel2 PIXEL //"Valor "
			@ nLinha, 204 MSGET  nValorM2          SIZE 65,08 OF oPanel2 PIXEL  When .F. Picture PesqPict( "SE1","E1_VALOR" )  //"@E 999,999,999,999.99"
	   Else 
			nLinha += 12        
		  	@ nLinha, 144 SAY STR0134 				SIZE 53,08 OF oPanel2 PIXEL //"+/- Dif. Cambio"
		  	@ nLinha, 204 MSGET  nDifCambio		SIZE 65,08 OF oPanel2 PIXEL  When .F. Picture PesqPict( "SE1","E1_CAMBIO" )  //"@E 999,999,999,999.99"	   
	   EndIf			  
		nLinha += 12
		@ nLinha, 144 SAY STR0096 					SIZE 53,08 OF oPanel2 PIXEL //"+ Corr.Monet ria"
		@ nLinha, 204 MSGET  nCorrec				SIZE 65,08 OF oPanel2 PIXEL  When .F.   Picture PesqPict( "SE1","E1_CORREC" )  //"@E 999,999,999,999.99"
	EndIf

	If lPanelFin	
		ACTIVATE MSDIALOG oDlg ON INIT FaMyBar(oDlg,	{|| nOpc1 := 1,oDlg:End()},;
		                                            	{||Iif( ExistBlock("F070CCAN"),;
		                                            	ExecBlock('F070CCAN',.F.,.F.,oDlg:End()),.F.),;
		                                            	 oDlg:End()}) CENTERED
	Else
		ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{|| nOpc1 := 1,oDlg:End()},;
		                                               	{||Iif( ExistBlock("F070CCAN"), ;
		                                               	ExecBlock('F070CCAN',.F.,.F.,oDlg:End()),.F.), ;
		                                               	oDlg:End()}) CENTERED
  	Endif

Else
	If VisualSX3("E5_HISTOR") .AND. (nx := aScan(aAutoCab,{|x|Upper(x[1])=="AUTHIST"})) > 0
    	aAdd(aValidGet,{"cHist070",aAutoCab[nx][2],"CheckSX3('E5_HISTOR')",.T.})
 	Endif                                  
    If Len(aValidGet) > 0 .AND. SE1->(MsVldGAuto(aValidGet))
    	If (nx := aScan(aValidGet,{|x|Upper(x[1])=="CHIST070"})) > 0
	    	If !Empty(aValidGet[nx][2])
	        	cHistCan070 := aValidGet[nx][2]
			Endif
		Endif
	Endif    
	nOpc1 := 1
Endif 
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se n„o ‚ a ultima baixa efetuada                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lCanc070Bx
	If nOpBaixa != Len(aBaixa)
		Help(" ",1,"A070NAOPODE")
		Return
	Endif
Endif

// Baixas por transferencia nao podem ser canceladas.
If Alltrim(Upper(cMot070)) == "TRF" .and. Alltrim(Upper(FunName())) != "FINA630" .And. !Empty(SE1->E1_NUMSOL)
	Help(" ",1,"A070NAOPODE",,STR0169, 4, 0 ) // "Baixas efetuadas por transferência"+CHR(13)+	"não podem ser canceladas"
	Return
Endif	  

SE5->( dbGoTo( nRecSE5 ) )
IF nOpc1 == 1

	If nCM1 > 0
		nJuros += nCM1
	Else
		nDescont -= nCM1
	EndIf

	If nProRata > 0
		nJuros += nProRata
	Else
		nDescont -= nProRata
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ PONTO DE ENTRADA - Confirmacao Exclusao     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lFa070Ca4T
		lRet := ExecTemplate('FA070CA4',.F.,.F.,nOpcx)
	Endif

	If lFa070Ca4
		lRet := ExecBlock('FA070CA4',.F.,.F.,nOpcx)
	Endif       

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Consiste MV_DATAFIN antes de cancelar baixa ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lRet	.and. SuperGetMv("MV_BXDTFIN",,"1") == "2"
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ A data da baixa deve ser passada como parametro, pois tanto ³
		//³ o cancelamento como a exclusao devem ser validados pela     ³
		//³ data de baixa e nao pela data base do sistema.              ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		// Depois de conversar com a liderança da inovação, ficou claro que o parametro MV_DATAFIN
		// foi criado para definir uma data para impedir a ATUALIZAÇÃO DE SALDO de um banco
		// Logo, caso seja um cancelamento, a atualização de saldo é na data base e
		// caso uma exclusão, será exclusão do movimento na data da movimentação (tem que impedir baseando-se na data do movimento)
		
		If nOpcx == 5 // cancelamento
			lRet := DtMovFin()
		Elseif nOpcx==6 //exclusao
			lRet := DtMovFin(dBaixa)			
		Endif

	Endif
	If !lRet
		Return
	EndIf
	If cPaisLoc<>"BRA"	
		nValPadrao := nValRec-(nJuros+nMulta+Iif(SE1->E1_MOEDA<=1,nCorrec,0)-nDescont-nOtrga)
	Else
		nTotAbat := NoRound(nTotAbat * nTxMoeda,3)
		nValPadrao:=nValRec-(nJuros+nMulta+Iif(SE1->E1_MOEDA<=1,nCorrec,0)-nDescont ) + SE1->E1_PIS + SE1->E1_COFINS + SE1->E1_CSLL + SE1->E1_IRRF		
		If SE1->E1_MOEDA > 1 
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Caso a Moeda seja > 1, converte o valor para atualiza‡„o do  ³
			//³ cadastro do Cliente a partir do valor da moeda estrangeira   ³
			//³ convertida p/ moeda 1 na Data de Emiss„o do t¡tulo, pois pode³
			//³ ser efetuada uma baixa informando taxa contratada.           ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			nValPadrao := nValorM2-(Round(NoRound(xMoeda(nJuros+nMulta-nDescont-nOtrga,nMoedaBco,SE1->E1_MOEDA,dBaixa,3),3),2))
			nValPadrao := Round(NoRound(xMoeda(nValPadrao,SE1->E1_MOEDA,nMoedaBco,SE1->E1_EMISSAO,3),3),2)
		Endif
    Endif

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Inicia a gravacao dos lancamentos - SIGAPCO ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	PcoIniLan("000004")

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Inicio da prote‡„o via TTS                                   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Begin Transaction
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Posiciona no registro de natureza                            ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea("SED")
		MsSeek(xFilial("SED")+SE1->E1_NATUREZ)
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Posiciona no registro do cliente e Estorna Atraso Medio.    ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If SA1->( MsSeek(xFilial("SA1")+SE1->E1_CLIENTE+SE1->E1_LOJA) ) .And. FunName() <> "FINA074"
			If !(SE1->E1_TIPO $ MVRECANT+"/"+MV_CRNEG)
				RecLock("SA1",.F.)
				If ( SE1->E1_BAIXA - SE1->E1_VENCREA) > 0
					SA1->A1_PAGATR := IiF(SA1->A1_PAGATR ==0,0,SA1->A1_PAGATR-SE1->E1_VALLIQ)   // Pagamentos Atrasados
				EndIF
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Atualiza o Cadastro de Clientes                             ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				SA1->A1_NROPAG := SA1->A1_NROPAG-1  //Numero de Duplicatas
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Atualiza media de atrasos                                   ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				SA1->A1_METR   :=  (SA1->A1_METR * (SA1->A1_NROPAG+1) - (SE1->E1_BAIXA - SE1->E1_VENCREA)) / SA1->A1_NROPAG
				MsUnLock()
				AtuSalDup("+",(nValPadrao),1,SE1->E1_TIPO,,SE1->E1_EMISSAO)
			Else
				AtuSalDup("-",(nValPadrao),1,SE1->E1_TIPO,,SE1->E1_EMISSAO)
			Endif
		Endif
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Gera backup dos valores da baixa (para cancelamento baixa parcial)³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		nSe1ValLiq  := SE1->E1_VALLIQ
		nSe1Descont := SE1->E1_DESCONT
		nSe1Multa   := SE1->E1_MULTA
		nSe1Juros   := SE1->E1_JUROS
		nSe1Correc  := SE1->E1_CORREC

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Grava novos valores do cancelamento da baixa parcial              ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		RecLock("SE1")
		If 	lPccBxCR  
			nValRec += nPis + nCsll + nCofins
		EndIf
		If lIrPjBxCr
			nValRec += nIrrf
		EndIf
		SE1->E1_VALLIQ  := nValRec 
		SE1->E1_DESCONT := nDescont
		SE1->E1_MULTA   := nMulta
		SE1->E1_JUROS   := nJuros-nAcresc
		SE1->E1_CORREC  := nCorrec

		If lChqDev
			SE1->E1_CHQDEV := "1"
        Else
			SE1->E1_CHQDEV := " "
		Endif
		
		MsUnlock()
		ABATIMENTO 		 := nTotAbat

       
		nSalvRec:=Recno()

		//Conciliacao SITEF
		FA070SITEF(SE1->E1_PREFIXO,SE1->E1_NUM,SE1->E1_PARCELA,SE1->E1_TIPO)

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Verifica se h  abatimentos para voltar a carteira                 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		SE1->(DbSetOrder(2)) // Filial + Cliente + Loja + Prefixo + Número + Parcela + Tipo
		
		If MsSeek(xFilial("SE1")+SE1->E1_CLIENTE+SE1->E1_LOJA+SE1->E1_PREFIXO+SE1->E1_NUM+SE1->E1_PARCELA)
			//
			//Função Específica do Modulo Sigapls para atualizar Status de Guias Compradas
			//
			If FindFunction("PL090TITCP").and. GetNewPar("MV_PLSATIV",.F.)   
				PL090TITCP(SE1->E1_PREFIXO,SE1->E1_NUM,SE1->E1_PARCELA,SE1->E1_TIPO,"5")
			EndIf
			dbSetOrder( 2 )
		   	dbSeek( xFilial("SE1")+cCliente+cLoja+cPrefixo+cNum+cParcela )     
		   	cTitAnt := (SE1->E1_FILIAL+SE1->E1_CLIENTE+SE1->E1_LOJA+SE1->E1_PREFIXO+SE1->E1_NUM+SE1->E1_PARCELA) 
			If lTitpaiSE1
		 		If FindFunction("OrdTitpai") .and. (nOrdTitPai:= OrdTitpai()) > 0
   				DbSetOrder(nOrdTitPai)
					If	DbSeek(xFilial("SE1")+cPrefixo+cNum+cParcela+cTipo+cCliente+cLoja)    
						bWhile := {|| !Eof() .And. Alltrim(cTitAnt) == Alltrim(xFilial("SE1")+cPrefixo+cNum+cParcela+cTipo+cCliente+cLoja) }   
						cTitAnt := (SE1->E1_FILIAL+SE1->E1_TITPAI) 
					Else
						dbSetOrder( 2 )
		   			dbSeek( xFilial("SE1")+cCliente+cLoja+cPrefixo+cNum+cParcela )     
					Endif
		   		Endif                                   
			Endif
			
			While Eval(bWhile)
				If !SE1->E1_TIPO $ MVABATIM+"/"+MVINABT+"/"+MVIRABT+"/"+MVFUABT //adicionado MVFUABT pois a variável MVABATIM não está retornando FU-
					dbSkip()
					Loop
				Endif
				If lTitpaiSE1
					If !Empty(E1_TITPAI).and.  (Alltrim(E1_TITPAI)!=Alltrim(cPrefixo+cNum+cParcela+cTipo+cCliente+cLoja )) 
						DbSkip()
						Loop
					EndIf
				EndIf
				//Abatimento baixado pela rotina de compensacao, desconsiderar
				If IIf(SE1->(FieldPos("E1_TITPAI"))#0,!Empty(SE1->E1_TITPAI),.F.) .AND. (MVRECANT $ SE1->E1_TITPAI .OR. MV_CRNEG $ SE1->E1_TITPAI)
					If nTotAbat > 0 
						If Round(NoRound(nTotAbat,3),2)  >= Round(NoRound(xMoeda(SE1->E1_VALOR,SE1->E1_MOEDA,nMoedaBco,,3,If(cPaisLoc=="BRA",SE1->E1_TXMOEDA,0)),3),2)
							//Abater do valor do abatimento um abatimento que nao foi baixado pela rotina
							//Operacao necessaria pois a variavel foi alimentada pela funcao SumAbatRec
							nTotAbat -= xMoeda(SE1->E1_VALOR,SE1->E1_MOEDA,nMoedaBco,,3,If(cPaisLoc=="BRA",SE1->E1_TXMOEDA,0))
						Endif
					Endif
					SE1->(dbSkip())
					Loop
				Endif    
				If !Empty(SE1->E1_BAIXA) .AND. SE1->E1_SALDO == 0	//Abatimento baixado
					lExBxAbat := .T.
				Endif
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Volta t¡tulo para carteira                                       ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If lExBxAbat .and. lF070CANABT .AND. SE1->E1_TIPO $ MVIRABT
					Execblock("F070BXABT",.F.,.F., {cDescrMo})
				Endif
				Reclock("SE1")
				SE1->E1_BAIXA   := Ctod("  /  /  ")
				SE1->E1_SALDO   := SE1->E1_VALOR
				SE1->E1_DESCONT := 0
				SE1->E1_JUROS   := 0
				SE1->E1_MULTA   := 0
				SE1->E1_CORREC  := 0
				SE1->E1_VARURV  := 0
				SE1->E1_VALLIQ  := 0
				SE1->E1_LOTE    := Space(Len(SE1->E1_LOTE))
				If ( cPaisLoc == "CHI" )
					SE1->E1_MOVIMEN := Ctod("  /  /  ")
				EndIf
				SE1->E1_STATUS  := "A"
				
				Msunlock()
				//saldo natureza - impostos
				If lAtuSldNat .AND. lExBxAbat
					AtuSldNat(E1_NATUREZ, dBaixa, E1_MOEDA, "3", "R", E1_VALOR, E1_VALOR, "+",,FunName(),"SE1",SE1->(Recno()),nOpcx)
				Endif			

				
				If lF070CANABT .and. SE1->E1_TIPO $ MVIRABT .and. !lExBxAbat
					nValabt := EXECBLOCK("F070CANABT",.F.,.F.)
					Reclock("SE1",.F.)
						E1_VALOR += nValabt
						E1_SALDO += nValabt
						E1_VLCRUZ += nValabt
					MsUnlock()
				ENDIF
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Carrega variaveis para contabilizacao dos    ³
				//³ abatimentos (impostos da lei 10925).         ³			
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If SE1->E1_TIPO == MVPIABT
					VALOR5 := SE1->E1_VALOR			
				ElseIf SE1->E1_TIPO == MVCFABT
					VALOR6 := SE1->E1_VALOR
				ElseIf SE1->E1_TIPO == MVCSABT
					VALOR7 := SE1->E1_VALOR						
				Endif			
				dbSkip()
			End
		Endif

		DbSetOrder(1)
		dbGoTo( nSalvRec )

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Posiciona no cadastro de bancos para contabiliza‡„o               ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea("SA6")
		MsSeek(xFilial()+cBanco+cAgencia+cConta)

		// Se estiver utilizando multiplas naturezas por titulo
		// nOpcx == 5 -> Cancelamento de baixa
		// nOpcx != 5 -> Exclusão de baixa
		If cMultNat == "1"
			DelMNatBx("SE1",@nHdlPrv,@nTotal,@cArquivo,nOpcx != 5,cSeqSE5,mv_par04 == 1) // Apaga as naturezas geradas para o titulo
		Endif	

		If lPccBxCR
  			If (!SE5->E5_PRETPIS $ "8_9" .And. !SE5->E5_PRETCOF $ "8_9" .And. !SE5->E5_PRETCSL $ "8_9") 	
	  			RecLock("SE5",.F.)
				SE5->E5_PRETPIS := "1"
				SE5->E5_PRETCOF := "1"
				SE5->E5_PRETCSL := "1"
				SE5->(MsUnlock())
			Endif
			
			nRecSe5  := SE5->(RecNo())
						
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Exclui os registros de relacionamentos do SFQ                                ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			aRecSE5 := FImpDelTit("E1B",SE5->E5_PREFIXO,SE5->E5_NUMERO,SE5->E5_PARCELA,SE5->E5_TIPO,SE5->E5_CLIFOR,SE5->E5_LOJA,SE5->E5_SEQ)
			For nI := 1 to Len(aRecSE5)
				SE5->(MSGoto(aRecSE5[nI]))	
				RecLock("SE5",.F.)          					

				If ((!SE5->E5_TIPO $ MVPAGANT) .Or. (SE5->E5_TIPO $ MVPAGANT .And.;
					!Empty(SE5->E5_PRETPIS) .And. !Empty(SE5->E5_PRETCOF) .And. !Empty(SE5->E5_PRETCSL)))					
		  			If (!SE5->E5_PRETPIS $ "8_9" .And. !SE5->E5_PRETCOF $ "8_9" .And. !SE5->E5_PRETCSL $ "8_9") 
		  				SE5->E5_PRETPIS := "1"
						SE5->E5_PRETCOF := "1"
						SE5->E5_PRETCSL := "1"
					Endif	 
				Endif	
				MsUnlock()
			Next 
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Exclui os registros de relacionamentos do SFQ                                ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			SE5->(dbGoto(nRecSe5))
			FImpDelSFQ("E1B",SE5->E5_PREFIXO,SE5->E5_NUMERO,SE5->E5_PARCELA,SE5->E5_TIPO,SE5->E5_CLIFOR,SE5->E5_LOJA,SE5->E5_SEQ)
				
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Apaga os registros de impostos gerados na Baixa                              ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			F070DelTxBx()			
			SE1->(dbGoTo(nSalvRec))
		EndIf
		If lIrPjBxCr
			F070DelTxBx()	
		EndIf					
		SE1->(dbGoTo(nSalvRec))

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Gera lan‡amento contabil de estorno                               ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		cPadrao:="527"    //cancelamento de baixa
		lPadrao:=VerPadrao(cPadrao)
		// Se Excluir(Cancelamento)- contabiliza ou 
		// Canc.Baixa(Estorno) e mv_par04 - contabiliza
		If (nOpcx == 6) .or. !MovBcoBx(cMot070) .or. (nOpcx == 5 .and. mv_par04==1)
			IF ((lPadrao .And. lCtbBaixa) .OR. nHdlPrv > 0) .and. cMultNat != "1"
				nHdlPrv:=HeadProva(cLote,"FINA070",Substr(cUsuario,7,6),@cArquivo)

				If lUsaFlag  // Armazena em aFlagCTB para atualizar no modulo Contabil 
					aAdd( aFlagCTB, {"E5_LA", "S", "SE5", SE5->( Recno() ), 0, 0, 0} )
				Endif
				nTotal += DetProva( nHdlPrv, cPadrao, "FINA070", cLote, /*nLinha*/, /*lExecuta*/,;
				                    /*cCriterio*/, /*lRateio*/, /*cChaveBusca*/, /*aCT5*/,;
				                    /*lPosiciona*/, @aFlagCTB, /*aTabRecOri*/, /*aDadosProva*/ )

				If ExistBlock("F070CTC")
					nTotal += ExecBlock("F070CTC",.F.,.F.,{cPadrao,nHdlPrv})
				Endif
			Endif
		EndIf
		
		dbSelectArea("SE1")
		DbSetOrder(1)
		dbGoTo( nSalvRec )
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Grava valores anteriores da contabilizacao do canc da baixa parcial³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		RecLock("SE1")
		SE1->E1_VALLIQ  := nSE1ValLiq
		SE1->E1_DESCONT := nSe1Descont
		SE1->E1_MULTA   := nSe1Multa
		SE1->E1_JUROS   := nSe1Juros
		SE1->E1_CORREC  := nSe1Correc
		MsUnlock()
		If lRaRtImp .and. (SE1->E1_VALOR > SE1->E1_SALDO) .And. Empty(SE1->E1_TIPOLIQ) 
			aOldBaixaSE5 := aClone(aBaixaSE5)     
			aBaixaSE5:= {}
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Soma a proporcionalização de iss e inss ao nValrec ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ 
			aBxSE5 := Sel070Baixa( "VL /V2 /BA /RA /CP /LJ /"+MV_CRNEG,cPrefixo, cNum, cParcela,cTipo,@nTotAdto,@lBaixaAbat,cCliente,;
									cLoja,@nVlrBaixa,,@lBxCEC,@lBxLiq,,.T.)
			For x := 1 To Len(aBaixaSE5)
		 		nTotAbat -= aBaixaSE5[x][32]+aBaixaSE5[x][33]
			Next
			aBaixaSE5 := aClone(aOldBaixaSE5)
		EndIf
		If lAtuSldNat                          
			// Esta cancelando um titulo com baixa total, a partir da ultima baixa
			If SE1->E1_SALDO == 0 
				nFlxTit := nValRec-nJuros-nMulta+nDescont+nOtrga+nTotAbat
			Else                                                
				nFlxTit := nValRec-nJuros-nMulta+nDescont+nOtrga
			EndIf
			AtuSldNat(SE1->E1_NATUREZ, dBaixa, SE1->E1_MOEDA, "3", "R", nFlxTit, nValorM2, If(SE1->E1_TIPO $ MVRECANT+"/"+MV_CRNEG,"+","-"),,FunName(),"SE1",SE1->(Recno()),nOpcx)
		EndIf

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Volta titulo para carteira                                 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		Reclock("SE1")
		If SE1->E1_SALDO # 0	//baixa parcial
			If !lExBxAbat .and. nValabt == 0		//existe abatimento baixado pelo titulo originador mesmo assim?
				nTotAbat := 0	//caso nao exista, zerar o valor
			Endif
		Endif

		dDataAnt := Iif(nOpBaixa==Len(aBaixa),Iif(Len(aBaixa)==1,CtoD(""),;
										aBaixaSE5[Len(aBaixa)-1,07]),E1_BAIXA)
		If cPaisLoc<>"BRA"
			nValor := SE1->E1_SALDO+(nValRec-nJuros-nMulta+nDescont+nTotAbat+nOtrga)
		Else
			IF SE1->E1_MOEDA == 1  
				nValor := SE1->E1_SALDO+(nValRec-nJuros-nMulta+nDescont+nTotAbat)			
			Else
				If cMot070 == "DEV"
					nValor:=SE1->E1_SALDO+((nValRec-nJuros-Iif(SE1->E1_MOEDA<=1,nCorrec,0)-nMulta+nDescont+nTotAbat) / RecMoeda(SE1->E1_EMISSAO,SE1->E1_MOEDA))				
				Else  
					nValor:=ROUND(SE1->E1_SALDO+((nValRec-nJuros-Iif(SE1->E1_MOEDA<=1,nCorrec,0)-nMulta+nDescont+nTotAbat) / NoRound(nTxMoeda,5)),2)									
				EndIf
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Volta valor original do titulo se cancelamento final das baixas ³
				//³ e n„o houverem compensa‡oes.                                   ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				
				IF Len(aBaixa) == 1 .and. nTotAdto == 0 
				   	If(SE1->E1_TIPO$MVRECANT+"/"+MV_CRNEG)
						aAreaAtu:=GetArea()
						aAreaSE5:=SE5->(GetArea())
						dbSelectArea("SE5")
						dbSetOrder(7)
						If MsSeek(xFilial("SE5")+SE1->E1_PREFIXO+SE1->E1_NUM+SE1->E1_PARCELA+SE1->E1_TIPO+SE1->E1_CLIENTE+SE1->E1_LOJA)
					    	While(!lComp .And. (xFilial("SE5")+SE1->E1_PREFIXO+SE1->E1_NUM+SE1->E1_PARCELA+SE1->E1_TIPO +SE1->E1_CLIENTE+SE1->E1_LOJA==;
					    	xFilial("SE5")+SE5->E5_PREFIXO+SE5->E5_NUMERO+SE5->E5_PARCELA+SE5->E5_TIPO+SE5->E5_CLIFOR+SE5->E5_LOJA))
								If SE5->E5_SITUACA <> "C" .And. !Empty(SE5->E5_DOCUMEN)
									If SE5->E5_TIPODOC =="BA"
										lComp:=.T.
									Else
										lComp:=.F.
									EndIf
								EndIf								
								DbSkip()	
							End
						EndIf	
						SE5->(RestArea(aAreaSE5))
						RestArea(aAreaAtu)
					EndIf
					If  !lComp
						nValor := SE1->E1_VALOR
					EndIf	
				Endif
			Endif
		Endif
		
		//Caso seja a ultima baixa a ser cancelada
		//Caso o titulo tenha sofrido baixa onde o valor recebido foi menor que os juros
		//Retorno o saldo total do titulo 
		If Empty(dDataAnt) .and. nValor > SE1->E1_VALOR
			nValor := SE1->E1_VALOR
		Endif						

		SE1->E1_BAIXA   := dDataAnt
		SE1->E1_SALDO   := Iif(nValor<0,0,nValor)

		// Caso tenha sido chamado do loja faz tratamento especifico
		If (FunName()$"LOJA701|LOJXREC|RPC")
			//salva o Array aBaixaSE5 para recompor se retornar vazio la do loja quando chamado diretamente do remote 
			aOldBaixaSE5 := aClone(aBaixaSE5)
			 
		    // Alimenta campo E1_VALLIQ com valor da penultima baixa(SE5) parcial caso exista
			SE1->E1_VALLIQ  := SE1ValLiq(E1_PREFIXO,E1_NUM,E1_PARCELA,E1_TIPO,E1_CLIENTE,E1_LOJA)   
			If FunName()=="RPC" .And. Empty(aBaixaSE5)  //se estiver vazio recompoe o array aBaixaSe5 salvo
				aBaixaSE5 := aClone(aOldBaixaSE5)
			EndIf
		Else
			SE1->E1_VALLIQ  := 0 // Padrao Financeiro
		EndIf
				
		SE1->E1_LOTE    := Space(Len(E1_LOTE))
		SE1->E1_DESCONT := 0
		SE1->E1_MULTA   := 0
		SE1->E1_JUROS   := 0
		SE1->E1_CORREC  := 0
		If (SE1->E1_SALDO == SE1->E1_VALOR .AND. EMPTY(SE1->E1_BAIXA))
		    If F070BXCOMP() == 0
				SE1->E1_SDACRES :=SE1->E1_ACRESC                     
				SE1->E1_SDDECRE :=SE1->E1_DECRESC 
			EndIf                                 
		ElseIf lAcreDecre
			SE1->E1_SDACRES := Round(NoRound(xMoeda(nAcresc,nMoedaBco,SE1->E1_MOEDA,dBaixa,3,,nTxMoeda),3),2)
			SE1->E1_SDDECRE := Round(NoRound(xMoeda(nDecresc,nMoedaBco,SE1->E1_MOEDA,dBaixa,3,,nTxMoeda),3),2)
		Endif
			
		If cPaisLoc == "CHI"
			SE1->E1_STATUS  := Iif(E1_SALDO>0,"A","B")
			SE1->E1_MOVIMEN := Ctod("  /  /  ")
			IIf (SE1->(FieldPos("E1_OTRGA"))  > 0,SE1->E1_OTRGA    := 0,.T.)   
			IIf (SE1->(FieldPos("E1_CAMBIO")) > 0,SE1->E1_CAMBIO   := 0,.T.)      
			IIf (SE1->(FieldPos("E1_TXMOEDA"))> 0,SE1->E1_TXMOEDA  := 0,.T.)              
		Else
			Replace E1_STATUS   With IIF(E1_STATUS != "R",Iif(E1_SALDO >= 0.01,"A","B"),"R")
		Endif

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Salva os campos na tabela SE1 especifico do Template GEM       ³
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If HasTemplate("LOT") .and. ExistTemplate("GEMSE1Grv")
			ExecTemplate("GEMSE1Grv",.F.,.F.)
		EndIf
		//.... 
		//   conforme situacao do parametro abaixo, integra com o SIGAGSP ³
		//   MV_SIGAGSP - 0-Nao / 1-Integra
		//   Estorna os lancamento de Orcamentacao
		//    ......
		If GetNewPar("MV_SIGAGSP","0") == "1" 
			GSPF220()
		EndIf

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Integracao Protheus X RM Classis e RM Biblios ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ				
		if (GetNewPar("MV_RMCLASS", .F.) .or. GetNewPar('MV_RMBIBLI',.F.))
			if alltrim(SE1->E1_ORIGEM) == 'L'  .or.  alltrim(upper(SE1->E1_ORIGEM)) == 'S' .or. SE1->E1_IDLAN > 0
				//Executa a funcao para efetuar manutencao nas tabelas do RM Biblios e Replicar a baixa
				ClsProcCan(SE1->(Recno()),'1',"FIN070")
			endif
		endif

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ PONTO DE ENTRADA                            ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If ExistBlock("FA070CAN")
			ExecBlock('FA070CAN',.F.,.F.,nValor)
		Endif
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Executa funcao do PLS para estornar cobranca de  ³
		//³juros no mes seguinte                            ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
        If  substr(SE1->E1_ORIGEM,1,3) == "PLS" .and. ;
            FindFunction("PLS510CJR")
            PLS510CJR()
        Endif
        
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Estorna Comissao                            ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		Fa440DeleB(aBaixaSE3,If(MV_PAR05==1,.T.,.F.),;
									If(MV_PAR03==1,.T.,.F.),;
													"FINA070")
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Localiza na movimenta‡„o banc ria, os registros referentes a baixa³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea("SE5")
		dbSetOrder(2)
		If lE5CM1
			nOldCM1	:= SE5->E5_CM1
		EndIf
		aAux := { "VL","CM","CX","DC","MT","JR","V2","C2","D2","M2","J2","BA","TL","LJ","RA" }
		
		For nI := 1 to len(aAux)
			If SE5->(MsSeek(xFilial("SE5")+aAux[ni]+cChave))
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ PONTO DE ENTRADA DE TEMPLATE                  ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If ExistTemplate("SE5FI70E")
					ExecTemplate('SE5FI70E',.F.,.F.,nOpcx)
				Endif
				
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ PONTO DE ENTRADA                              ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If ExistBlock("SE5FI70E")
					ExecBlock('SE5FI70E',.F.,.F.,nOpcx)
				Endif
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Se for baixa por lote, independente da rotina que est  se utilizando³
				//³(Cancelamento ou Estorno da baixa), gera-se estorno da mesma.	     ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If nOpcx == 5 .or. !Empty(cLoteFin)
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³Cancela as baixas gerando um lancamento de estorno no SE5         ³
					//³Se for baixa por lote, estorna saldo bancario                     ³
					//³S¢ faz estorno de baixa tipo BA se a mesma for gerada atraves da  ³
					//³baixa por lote( !empty(cLoteFin)), caso contrario ela ‚ apenas    ³
					//³cancelada.                                                        ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If aAux[ni] == "VL" .or. aAux[ni] == "LJ".or. (aAux[nI]=="BA" .and. !Empty(cLoteFin))
					
						If EXISTBLOCK("F070GerAb")
							nValRetIR := SE5->E5_VRETIRF
						Endif
                         
						RecLock("SE5",.T.)
						SE5->E5_FILIAL    := xFilial("SE5")
						SE5->E5_BANCO     := cBanco
						SE5->E5_AGENCIA   := cAgencia
						SE5->E5_CONTA     := cConta
						SE5->E5_DATA      := dDatabase  
						If FUNNAME() != "FINA060"
							SE5->E5_VALOR     := If(SE1->E1_SITUACA=="2",0,If(cPaisLoc<>"BRA",nValSE5,nValRec)) 
							If lPccBxCR
								SE5->E5_VALOR -= nPis + nCofins + nCsll
								SE5->E5_VRETPIS := nPis
								SE5->E5_VRETCOF := nCofins
								SE5->E5_VRETCSL := nCsll
							EndIf
							If lIrPjBxCr
								SE5->E5_VALOR -= nIrrf
								SE5->E5_VRETIRF := nIrrf
							EndIf
						Else	
							SE5->E5_VALOR   := 0
						EndIf
						SE5->E5_NATUREZ   := SE1->E1_NATUREZ
						SE5->E5_RECPAG    := Iif(SE1->E1_TIPO$MVRECANT+"/"+MV_CRNEG,"R","P")
						SE5->E5_TIPO      := cTipo
						SE5->E5_LA        := Iif(lPadrao .and. mv_par04 == 1,"S","N")
						SE5->E5_TIPODOC   := "ES"
						SE5->E5_LOTE      := cLoteFin
						SE5->E5_PREFIXO   := cPrefixo
						SE5->E5_NUMERO    := cNum
						SE5->E5_PARCELA   := cParcela
						SE5->E5_CLIFOR    := cCliente
						SE5->E5_CLIENTE   := cCliente
						SE5->E5_LOJA      := cLoja
						SE5->E5_BENEF     := SE1->E1_NOMCLI
						SE5->E5_DTDIGIT   := dDataBase
						SE5->E5_MOTBX     := cMot070
						SE5->E5_VLMOED2   := nValorM2
						SE5->E5_SEQ       := cSequencia
						SE5->E5_DTDISPO   := IIF (dDtdispo > dDatabase, dDtDispo, dDataBase)
						SE5->E5_FILORIG   := SE1->E1_FILORIG
						SE5->E5_HISTOR		:= cHistCan070
						SE5->E5_MULTNAT	:= cMultNat
						SE5->E5_VLJUROS	:= nJuros
						SE5->E5_VLDESCO	:= nDescont
						SE5->E5_VLMULTA	:= nMulta
						SE5->E5_VLCORRE	:= nCorrec
						If ( cPaisLoc <> "BRA" )
							IIf( SE5->(FieldPos("E5_TXMOEDA"))>0, SE5->E5_TXMOEDA:=nTxMoeda ,.T. )
							nMoedaBco := Max( MoedaBco(cBanco,cAgencia,cConta), 1)
							SE5->E5_MOEDA := StrZero(nMoedaBco,2)
						Else
							SE5->E5_MOEDA := StrZero(nMoedaSE5,2)
						Endif
						If lE5CM1
							SE5->E5_CM1 	:= nOldCM1
						EndIf				

						If lAcreDecre
							SE5->E5_VLACRES := nAcresc
							SE5->E5_VLDECRE := nDecresc
						Endif
						
						If lSpbInUse
							SE5->E5_MODSPB	 := cModSpb
						Endif
						// Conciliador SITEF
						If AliasInDic("FIF")
							DbselectArea("FIF")
							Dbsetorder(3)
							If dbseek(xFilial("FIF")+cPrefixo+cNum+cParcela+cTipo)
								SE5->E5_DATA    := dDtdispo
								SE5->E5_DTDISPO := dDtdispo
								SE5->E5_DTDIGIT := dDtdispo
							Endif
						Endif
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Salva os campos na tabela SE5 especifico do Template GEM       ³
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						If HasTemplate("LOT") .and. ExistTemplate("GEMSE5Grv")
							ExecTemplate("GEMSE5Grv",.F.,.F.)
						EndIf
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ PONTO DE ENTRADA F070EST                            ³
						//³ PE para grava‡äes complementares do cancelamento    ³
						//³ da baixa                                            ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						IF EXISTBLOCK("F070EST")
							ExecBlock("F070EST",.F.,.F.,{nValRetIR})
						Endif
						
						IF EXISTBLOCK("F070EST2")
							ExecBlock("F070EST2",.F.,.F.,{ aAux[ni], cChave })
						Endif
						
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Localizacao Portugal - Gera dados para diario contabil ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						If ( FindFunction( "UsaSeqCor" ) .And. UsaSeqCor() )
							AAdd( aDiario, {"SE5",SE5->(Recno()),cCodDiario,"E5_NODIA","E5_DIACTB"} )
						Else
							aDiario := {}
						EndIf
						
					Else
						If ExistBlock("F070HisCan")
							cSeqAux := ExecBlock("F070HisCan",.F.,.F.,{aAux[ni],cChave,cSequencia,cHistCan070,dBaixa,cPadrao,nHdlPrv,aFlagCtb,lPadrao,lCtbBaixa})
							If Valtype(cSeqAux) == "C"
								cSeqCanSe5 := cSeqAux
							Else
								cSeqCanSe5 := SE5->E5_SEQ
							EndIf
						Else
							If aAux[ni] == "V2"
								lAtuSldbco := .F.
							EndIF
							cSeqCanSe5 := SE5->E5_SEQ
							Reclock("SE5")
							SE5->E5_SITUACA := "C"
							If SE5->E5_TIPODOC <> "BA"
								SE5->E5_HISTOR	:= If(lF070Auto,SE5->E5_HISTOR,cHistCan070)
							Else
								If SE1->E1_TIPO <> "RA "
									SE5->E5_HISTOR	:= If(lF070Auto,SE5->E5_HISTOR,cHistCan070)
								Else
									//para tipodoc igual a "BA" referente a titulo tipo "RA" o deve gravar a data de cancelamento no historico 
									//pois relatorio finr130 ira considerar a data de cancelamento da baixa para compor saldo retroativo
									SE5->E5_HISTOR	:= PADR(If(lF070Auto,SE5->E5_HISTOR,cHistCan070),Len(SE5->E5_HISTOR)-16)+"###["+DTOS(dDatabase)+"]###"
								EndIf
							EndIf
							MsUnlock()
							//Cálculo de 4xmil colombia
							//--------------------------------------------------------------------------------------------------
							If cPaisLoc == "COL"
								If FindFunction("FinProcITF") .And. FinProcITF( SE5->( Recno() ),1 ) .and. SE1->E1_TIPO $ 'RA '
									FinProcITF( SE5->( Recno() ), 5, , .F.,, )
								EndIf
							End If
							//--------------------------------------------------------------------------------------------------
						EndIf
					Endif
					
					
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Grava os lancamentos das contas orcamentarias - SIGAPCO    ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					PcoDetLan("000004","07","FINA070")
					
				Else
					cSeqCanSe5 := SE5->E5_SEQ
					Reclock("SE5",.F.,.T.)
					dbDelete()
					MsUnlock()
					lExclusao := .T.
					
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Grava os lancamentos das contas orcamentarias - SIGAPCO    ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					PcoDetLan("000004","07","FINA070", .T. )
					PcoDetLan("000012","01","FINA110", .T. )	
				EndIf
			Endif
		Next


		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Finaliza a gravacao dos lancamentos do SIGAPCO ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		PcoFinLan("000004")

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Verifica se existe um cheque gerado para este adiantamento,  ³
		//³pois se tiver, dever  ser cancelado                          ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If SE1->E1_TIPO $ MVRECANT+"/"+MV_CRNEG
			dbSelectArea("SEF")
			SEF->( dbSetOrder(3) )
			If SEF->( MsSeek(xFilial()+Substr(cChave,1,TamSX3("EF_PREFIXO")[1]+TamSX3("EF_TITULO")[1]+TamSX3("EF_PARCELA")[1]+TamSX3("EF_TIPO")[1])) )
				While !Eof().and.SubStr(cChave,1,TamSX3("EF_PREFIXO")[1]+TamSX3("EF_TITULO")[1]+TamSX3("EF_PARCELA")[1]+TamSX3("EF_TIPO")[1])==;
					EF_PREFIXO+EF_TITULO+EF_PARCELA+EF_TIPO.AND.EF_FILIAL==xFilial("SEF")
					If SEF->EF_FORNECE == SE1->E1_CLIENTE
						//Verifico se foi gerado cheque para adiantamento na baixa, antes de ser deletado.
						If SEF->EF_TIPO $ MVRECANT+"/"+MV_CRNEG .And. AllTrim(SEF->EF_ORIGEM) == "FINA070"
							lChqAdt := .T.
						EndIf                   
						Reclock("SEF")
						SEF->( dbDelete() )
						Exit
					Endif
					SEF->( dbSkip())
				End
			Endif
			SEF->( dbSetOrder(1) )
		Endif

		// Limpa o identificador de que o cheque ja foi utilizado em uma baixa.
		// Deve ocorrer antes da atualizacao do saldo bancario, para buscar o 
		// valor correto do cheque na funcao "SomaCheqCr"
		If SEF->(FieldPos("EF_USADOBX")) > 0
			dbSelectArea("SEF")
			SEF->( dbSetOrder(3) )
			If SEF->( MsSeek(xFilial("SEF")+Substr(cChave,1,TamSX3("EF_PREFIXO")[1]+TamSX3("EF_TITULO")[1]+TamSX3("EF_PARCELA")[1]+TamSX3("EF_TIPO")[1])) )
				While !Eof().and.SubStr(cChave,1,TamSX3("EF_PREFIXO")[1]+TamSX3("EF_TITULO")[1]+TamSX3("EF_PARCELA")[1]+TamSX3("EF_TIPO")[1])==;
					SEF->EF_PREFIXO+SEF->EF_TITULO+SEF->EF_PARCELA+SEF->EF_TIPO.AND.SEF->EF_FILIAL==xFilial("SEF")
					If SEF->EF_CLIENTE == SE1->E1_CLIENTE .And. SEF->EF_USADOBX == "S" .And. SEF->EF_SEQUENC == cSeqCanSe5
							Reclock("SEF",.F.)
						SEF->EF_USADOBX := " "
						MsUnlock()
						Exit
					Endif
					SEF->(dbSkip())
				EndDo
			Endif
			SEF->(dbSetOrder(1))
		EndIf
      
		//Atualiza saldo Bancario apenas se o controle de movimento bancario na 
		//baixa a receber nao for na compensacao do cheque recebido
		If !(SE1->E1_SITUACA $ "2/7") .And. MovBcoBx(cMot070) .And. Empty( cLoteFin ) .And. lAtuSldbco
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Atualiza o saldo banc rio                                         ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If !(SE1->E1_TIPO $ MVRECANT+"/"+MV_CRNEG)
				// Controla saldo na compensacao do cheque
				If lSaldoChq
					// Soma o total recebido em cheque
					nSomaCheq := SomaCheqCr(.T.,SE1->E1_PREFIXO, SE1->E1_NUM, SE1->E1_PARCELA, SE1->E1_TIPO, SE1->E1_CLIENTE)
				EndIf
				AtuSalBco(cBanco,cAgencia,cConta,If(lExclusao,aBaixaSE5[nOpBaixa,7],dDataBase),nValRec-nSomaCheq,"-")
			Elseif (SE1->E1_TIPO $ MVRECANT+"/"+MV_CRNEG) .And. !lChqAdt
				AtuSalBco(cBanco,cAgencia,cConta,If(lExclusao,aBaixaSE5[nOpBaixa,7],dDataBase),nValRec,"+")
			Endif
		EndIF

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Se for baixa por lote, estorna saldo bancario                   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !Empty(cLoteFin)
			AtuSalBco(cBanco,cAgencia,cConta,If(lExclusao,aBaixaSE5[nOpBaixa,7],dDataBase),nValRec,"-")

			If cPaisLoc $ "ARG|POR|EUA|ANG|COL|MEX"
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Gerar a Movimentacao Bancaria na 2a. Moeda.         ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				SA6->( MsSeek(xFilial("SA6")+cBanco+cAgencia+cConta) )
				If !Empty(SA6->A6_MOEDA) .and. ( SA6->A6_MOEDA != SE1->E1_MOEDA )
					SE5->E5_FILIAL  := xFilial("SE5")
					SE5->E5_BANCO   := cBanco
					SE5->E5_AGENCIA := cAgencia
					SE5->E5_CONTA   := cConta
					SE5->E5_DATA    := dDataBase
					SE5->E5_MOEDA   := StrZero(SA6->A6_MOEDA,2)
					SE5->E5_VALOR   := xMoeda( nValRec,nMoedaBco,SA6->A6_MOEDA,dDataBase )
					SE5->E5_NATUREZ := SE1->E1_NATUREZ
					SE5->E5_RECPAG  := "P"
					SE5->E5_TIPO    := cTipo
					SE5->E5_LA      := cLA
					SE5->E5_TIPODOC := "ES"
					SE5->E5_LOTE    := cLoteFin
					SE5->E5_PREFIXO := cPrefixo
					SE5->E5_NUMERO  := cNum
					SE5->E5_PARCELA := cParcela
					SE5->E5_CLIFOR  := cCliente
					SE5->E5_CLIENTE := cCliente
					SE5->E5_LOJA    := cLoja
					SE5->E5_BENEF   := SE1->E1_NOMCLI
					SE5->E5_DTDIGIT := dDataBase
					SE5->E5_MOTBX   := cMot070
					SE5->E5_VLMOED2 := nValorM2
					SE5->E5_HISTOR  := STR0116+cLoteFin //"Canc Baixa Lote "
					SE5->E5_SEQ     := cSequencia
					SE5->E5_DTDISPO := IIF (dDtdispo > dDatabase, dDtDispo, dDataBase)
					SE5->E5_FILORIG := SE1->E1_FILORIG
					AtuSalBco(cBanco,cAgencia,cConta,If(lExclusao,aBaixaSE5[nOpBaixa,7],dDataBase),nValRec,"-" )
				EndIf
			EndIf

		EndIf
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Gera lan‡amento contabil de estorno                             ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		IF nHdlPrv > 0 .and. lPadrao .And. lCtbBaixa
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Envia para Lancamento Contabil                      ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			//-- Se for rotina automatica força exibir mensagens na tela, pois mesmo quando não exibe os lançametnos, a tela 
			//-- sera exibida caso ocorram erros nos lançamentos padronizados
			If lF070Auto
				lSetAuto := _SetAutoMode(.F.)
				lSetHelp := HelpInDark(.F.)
				If Type('lMSHelpAuto') == 'L'
					lMSHelpAuto := !lMSHelpAuto
				EndIf						
			EndIf

			RodaProva(nHdlPrv,nTotal)
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Envia para Lan‡amento Cont bil                      ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			cA100Incl( cArquivo, nHdlPrv, 3 /*nOpcx*/, cLote, Iif(mv_par01==1,.T.,.F.) /*lDigita*/,;
			           Iif(mv_par02==1,.T.,.F.) /*lAglut*/,;
			           /*cOnLine*/, /*dData*/, /*dReproc*/, @aFlagCTB, /*aDadosProva*/, aDiario )
			aFlagCTB := {}  // Limpa o coteudo apos a efetivacao do lancamento

			If lF070Auto
				HelpInDark(lSetHelp)
				_SetAutoMode(lSetAuto)
				If Type('lMSHelpAuto') == 'L'
					lMSHelpAuto := !lMSHelpAuto
				EndIf
			EndIf
		EndIF

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Final da prote‡„o via  TTS                                 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If FindFunction( "GETROTINTEG" ) .and. !lFini055 .and. FindFunction("FwHasEAI") .and. FWHasEAI("FINA070",.T.,,.T.)
			cIntegSeq:=cSequencia//utilizada na integdef. Nao transformar em local.
			FwIntegDef( 'FINA070' ) 
		Endif
	End Transaction
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Para utilizar no execblock abaixo                            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	VALOR := nMulta
	VALOR2:= nJuros
	VALOR3:= nDescont
	VALOR4:= nCorrec
	ABATIMENTO := nTotAbat
	
	If ExistTemplate("FA070CA2")
		ExecTemplate("FA070CA2",.F.,.F.)
	Endif
	
	If ExistBlock("FA070CA2")
		ExecBlock("FA070CA2",.F.,.F.,{nOpcx})
	Endif

	VALOR := 0
	VALOR2:= 0
	VALOR3:= 0
	VALOR4:= 0
	ABATIMENTO := 0

	If lACAtivo .And. cPaisLoc=="BRA"
		SE1->(dbGoTo(nSalvRec)) // Retorna o Posicionamento para a SE1
		//Realiza a consequencias do cancelamento da baixa do titulo no GE 
		AcFinCanc2()
	EndIF
	// Baixas geradas pela rotina de baixa de documentos (TMSA880) do TMS, devem cancelar a fatura
	// para que o usuario nao tenha que faze-lo manualmente, pois o titulo eh gerado automaticamente pelo sistema
	// no momento da baixa de um Ctrc, entao o cancelamento da baixa deste titulo deve tambem cancelar
	// a fatura (titulo).
	If Alltrim(SE1->E1_ORIGEM) == "TMSA880"
		Tmsa850Exc("SE1",/*cCampo*/,6,/*aM*/,/*aDocExclui*/,.F.)
	Endif
	
	//
	// Template GEM - Se todos os titulos foram pagos (E1_SALDO == 0 ) deve atualizar o STATUS do contrato.
	//
	If HasTemplate("LOT") .and. ExistTemplate("GMUpdStatContr")
		ExecTemplate("GMUpdStatContr",.F.,.F.,{SE1->E1_NUM ,SE1->E1_SERIE ,SE1->E1_PREFIXO } )
	EndIf             
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
	//³Integracao protheus X tin	³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
	

EndIF

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Trecho incluido para integração e-commerce          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If  FindFunction("LJ861EC04") .And. AliasInDic("MF5") 
    LJ861EC04(SE1->E1_NUM, SE1->E1_PREFIXO, nOpc1)
EndIf


dbSelectArea("SE5")
SE5->(dbSetOrder(1))
dbSelectArea(cAlias)
dbGoTo(nSalvRec)
dbSetOrder(nOrdem)
Return


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³F070BXCOMP³ Autor ³ TOTVS				    ³ Data ³ 11/06/10 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Retorna valor de titulos baixados por compensação          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ F070BXCOMP                                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Gen‚rico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function F070BXCOMP  
Local nValorBx   := 0
Local nValorCxBx := 0 
Local nValor     := 0   
Local aArea	     := GetArea()
Local aAreaSE5	 := SE5->(GetArea())

DbSelectArea("SE5")
DbSetOrder(10)
DbSeek(xFilial("SE5")+SE1->E1_PREFIXO+SE1->E1_NUM+SE1->E1_PARCELA+SE1->E1_TIPO+SE1->E1_LOJA)

Do While SE5->(!Eof() .And. TRIM(E5_FILIAL+E5_DOCUMEN) == TRIM(SE1->E1_FILIAL+SE1->E1_PREFIXO+SE1->E1_NUM+SE1->E1_PARCELA+SE1->E1_TIPO+SE1->E1_LOJA))
	If E5_TIPODOC == "BA"
	    nValorBx += E5_VALOR
	ElseIf E5_TIPODOC == "ES"
		nValorCxBx += E5_VALOR
	EndIf
	DbSkip()
EndDo

nValor :=nValorBx - nValorCxBx
RestArea(aAreaSE5)
RestArea(aArea)

Return (nValor)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³fA070TitW ³ Autor ³ Wagner Xavier         ³ Data ³ 26/05/92 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Fun‡„o utilizada para Baixa de Titulos - Windows           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ fA070Titw()                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Gen‚rico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function fA070TitW(cMarca,nTotal,nHdlPrv,lHdlPrv,lPadrao,cArquivo,cPadrao,nTotLtEZ,aDiario)
Local lPrjCni := FindFunction("ValidaCNI") .And. ValidaCNI()
Local nOpca
Local lJuros		:= IIF( mv_par05 == 1, .T., .F. )
LOCAL lMovBco  	:= .T.
LOCAL lBaixou  	:= .F.
LOCAL nTolerPg 	:= GetMv("MV_TOLERPG")

LOCAL oDlgLote
LOCAL oTitulo
LOCAL oEmissao
LOCAL oVencRea
LOCAL oNomeCli
LOCAL oHist
LOCAL oSituacao
LOCAL oBanco
LOCAL oAgencia
LOCAL oConta
LOCAL oBaixa
LOCAL oDtCredito
LOCAL oHist070
LOCAL oMotBx
LOCAL oValor
LOCAL oTotAbLiq 
Local oProRata
LOCAL oTotAbImp     
LOCAL oValorLiq
LOCAL oParciais
LOCAL oDescont
LOCAL oMulta
LOCAL oJuros
LOCAL oValRec
LOCAL oValEstr
LOCAL oAcresc
LOCAL oDecresc
LOCAL nDecrescF
LOCAL oCMonet
Local nSalvRec  := RecNO( )
LOCAL cParcela
LOCAL cMoeda
LOCAL aMotBx     := ReadMotBx()
LOCAL aDescMotBx := {}
Local lFa070MDB  := ExistBlock("FA070MDB")
Local lMdbOk     := .F.
Local oModSpb
Local aModalSpb := {"1=TED","2=CIP","3=COMP"}
Local lSpbInUse := SpbInUse()
Local nUltLin
Local bSetKey := {||}
Local oMultNat
Local lMultNat := .F.
Local NI
Local oPrefixo
Local oTipo
Local oParcela
Local lPanelFin := If (FindFunction("IsPanelFin"),IsPanelFin(),.F.)
Local aButtons := {}
Local nOldDescont := 0
Local nOldMulta := 0
Local nOldJuros := 0
Local nOldIrrf := 0 

//Controla o Pis Cofins e Csll na baixa  (1-Retem PCC na Baixa ou 2-Retem PCC na Emissão(default) )
Local lPccBxCr	:= If (FindFunction("FPccBxCr"),FPccBxCr(),.F.)
Local oPis
Local oCofins
Local oCsll
//Controla IRPJ na baixa
Local lIrPjBxCr		:= If (FindFunction("FIrPjBxCr"),FIrPjBxCr(),.F.)
Local oIRRF  
Local x         
LOCAL lBxLiq	:= .F.  
LOCAL lBxCEC	:= .F.  //Verificador de existencia de baixa por compensacao entre carteiras
Local aBaixa    := {}        
LOCAL nVlrBaixa := 0 
LOCAL lBaixaAbat:= .F.
LOCAL nTotAdto	:= 0    


PRIVATE aBaixaSE5	:= {}
Private nAcrescF
Private nIrrf := 0
Private nOldPis     := SE1->E1_PIS
Private nOldCofins  := SE1->E1_COFINS
Private nOldCsll    := SE1->E1_CSLL     
PRIVATE lRaRtImp  	:= lFinImp .And.FRaRtImp()     //Define se ha retencao de impostos PCC/IRPJ no R.A 

lF415Auto := IIf(Type("lF415Auto")=="U",.F.,lF415Auto)

IF ExistBlock("F070MNAT")
	lMultNat := ExecBlock("F070MNAT",.F.,.F.)
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Retorna o Array aDescMotbx contendo apenas a descricao do	³
//³ motivo das Baixas. 						  								³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If len( aDescMotbx ) ==0                           
	For NI := 1 to Len(aMotBx)
		If Substr(aMotBx[nI],34,01) == "A" .or. Substr(aMotBx[nI],34,01) =="R"
			AADD( aDescMotbx,Substr(aMotBx[nI],07,10))
		EndIf
	Next
EndIf

While SE1->(!Eof())
	If SE1->E1_OK != cMarca .or. SE1->E1_TIPO $ MVABATIM+"/"+MVFUABT //adicionado MVFUABT pois a variável MVABATIM não está retornando FU-
		SE1->(dbSKip())
		Loop
	Else
		Exit
	EndIf
End

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Posiciona Cliente no SA1 		                                       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SA1")
SA1->(MsSeek(xFilial("SA1")+SE1->E1_CLIENTE+SE1->E1_LOJA))
dbSelectArea("SE1")
lIrPjBxCr		:= If (FindFunction("FIrPjBxCr"),FIrPjBxCr(),.F.)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Carrega Variaveis da Baixa                                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nSalvRec := RecNo()
cTitulo 	:= SE1->E1_PREFIXO + " " + SE1->E1_NUM+ " " + SE1->E1_PARCELA
cParcela := SE1->E1_PARCELA
dEmissao	:= SE1->E1_EMISSAO
dVencRea	:= SE1->E1_VENCREA
cNomeCli := SE1->E1_CLIENTE + " - " + Subst(SA1->A1_NOME,1,40)
cHist		:= SE1->E1_HIST
cSituacao:= SE1->E1_SITUACA + " " + fa070situa()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Sustituidas as linhas acima pelas linhas abaixo para processamento   ³
//³da baixa por lote com os valores definidos na tela de lote.          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
// 0 = Carteira
// F = Carteira Protesto
// G = Carteira Acordo
cBanco	 := Iif( cLoteFin != Space( TamSX3("E1_LOTE")[1] ), cBancolt  , Iif( SE1->E1_SITUACA $ "0FG",aCaixaFin[1], SE1->E1_PORTADO ))
cAgencia := Iif( cLoteFin != Space( TamSX3("E1_LOTE")[1] ), cAgencialt, Iif( SE1->E1_SITUACA $ "0FG",aCaixaFin[2], SE1->E1_AGEDEP  ))
cConta	 := Iif( cLoteFin != Space( TamSX3("E1_LOTE")[1] ), cContalt  , Iif( SE1->E1_SITUACA $ "0FG",aCaixaFin[3], SE1->E1_CONTA   ))

nMoedaBco:= Max(MoedaBco(cBanco,cAgencia,cConta),1)

dBaixa      := CriaVar("E1_BAIXA")
If cPaisLoc == "BRA"
	nTxMoeda 	:= If(SE1->E1_MOEDA > 1, If(SE1->E1_TXMOEDA > 0, SE1->E1_TXMOEDA,RecMoeda(dBaixa,SE1->E1_MOEDA)),0)
Endif	
dDtCredito  := dDataBase
cHist070	:= Criavar("E5_HISTOR")		//Inicilizador padrao
If Empty(cHist070)
	cHist070 := STR0007+Space(Len(cHist070)-24)  // "Valor recebido s/ T¡tulo"
Endif
cMotBx		:= aDescMotBx[1] 	//NORMAL
nValor		:= SE1->E1_VALOR
nTotAbImp   := 0
nTotAbLiq   := 0
nTotAbat    := SumAbatRec(SE1->E1_PREFIXO,SE1->E1_NUM,SE1->E1_PARCELA,SE1->E1_MOEDA,"S",dBaixa,@nTotAbImp)
nValorLiq 	:= 0     
nTotAbLiq   := nTotAbat - nTotAbImp
dbGoto(nSalvRec)
nParciais	:= 0//SE1->E1_VALOR-SE1->E1_SALDO
nAcrescF		:= SE1->E1_SDACRES

If ExistBlock("F070ACRE")
	ExecBlock("F070ACRE",.F.,.F.)
Endif

nDecrescF	:= SE1->E1_SDDECRE
nAcresc     := Round(NoRound(xMoeda(nAcrescF,SE1->E1_MOEDA,nMoedaBco,dBaixa,3,nTxMoeda),3),2)
nDecresc    := Round(NoRound(xMoeda(SE1->E1_SDDECRE,SE1->E1_MOEDA,nMoedaBco,dBaixa,3,nTxMoeda),3),2)
nDescont    := 0
nMulta      := 0
nJuros      := 0
nCM         := 0
nValrec		:= Round(Noround(xMoeda(SE1->E1_SALDO-nTotAbat,SE1->E1_MOEDA,nMoedaBco,dBaixa,3,nTxMoeda),3),2)+nMulta+nJuros-nDescont+nAcresc-nDecresc
nPIS	    := 0
nCOFINS    	:= 0	
nCSLL    	:= 0	

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Para que o valor da baixa parcial nao fique negativo, verifico o saldo³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If (SE1->E1_VALOR > SE1->E1_SALDO) .And. Empty(SE1->E1_TIPOLIQ)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Procura pelas baixas deste titulo ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	lTipBxCP:=lRaRtImp
	aBaixa := Sel070Baixa( "VL /V2 /BA /RA /CP /LJ /" + MV_CRNEG, SE1->E1_PREFIXO, SE1->E1_NUM, SE1->E1_PARCELA, SE1->E1_TIPO,; 
					@nTotAdto, @lBaixaAbat, SE1->E1_CLIENTE, SE1->E1_LOJA, @nVlrBaixa, , @lBxCec, @lBxLiq,,.T.)
	For x := 1 To Len(aBaixaSE5)
		nParciais += aBaixaSE5[x][8]
       	If lPccBxCR .And. lRaRtImp .and. !(aBaixaSE5[x][21]$ "1|2")
		   nParciais += aBaixaSE5[x][18]+aBaixaSE5[x][19]+aBaixaSE5[x][20]+aBaixaSE5[x][30]// somar impostos PCC 
		Elseif lIrPjBxCr .And. lRaRtImp  
	  		nParciais += aBaixaSE5[x][30]
		Endif        
		If lRaRtImp
			nParciais += aBaixaSE5[x][32]+aBaixaSE5[x][33]
			nTotAbat  -= aBaixaSE5[x][32]+aBaixaSE5[x][33]
		Endif
	
	Next
	nParciais += nTotAdto
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Soma valor de decrescimo em baixas parciais, para evitar         ³
	//³ diferencas entre valor original e valor recebido                 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If SE1->E1_SDDECRE <> SE1->E1_DECRESC
		nParciais += ( SE1->E1_DECRESC - SE1->E1_SDDECRE )
	EndIf
Else
	nParciais 	:= SE1->E1_VALOR-SE1->E1_SALDO
Endif


If	lIrPjBxCr
	nParciais += nIrrf
	nValRec := SE1->E1_VALOR - nParciais
	nIrrf:=FCaIrBxCR(nValRec)
EndIf  

fA070Data(@nTxMoeda,.F.) // Calcula o desconto e o juros (se houver) e valida a data
//PCC Baixa CR
If lPccBxCR 
	nParciais += nPis + nCofins + nCsll
	nValRec := SE1->E1_VALOR - nParciais 
	nOldValRec	:= nValRec
	f070TotMes(dBaixa,.T.,,.F.)
	If Type("aDadosRet") = "A" .And. ValType(aDadosRet[1]) == "U"
		aDadosRet := Array(7)
		AFill( aDadosRet, 0 ) 
	Endif		
Endif	

fa070val( nValrec, nTxMoeda )
fA070Data(@nTxMoeda,.F.) // Calcula o desconto e o juros (se houver) e valida a data
cMoeda 		:= IIF(Empty(SE1->E1_MOEDA),"1",AllTrim(Str(SE1->E1_MOEDA,2)))
cDescMoeda 	:= SubStr(GetMV("MV_SIMB"+cMoeda),1,3)

nOldPis     := nPis
nOldCofins  := nCofins
nOldCsll    := nCsll

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Pr‚-inicializa a modalidade de SPB                                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lSpbInUse
	If !Empty(SE1->E1_MODSPB)
		cModSpb := SE1->E1_MODSPB
	Else
	   cModSpb := "1"
	Endif		
Endif

//Botao de Cheques no Painel Financeiro
AADD(aButtons, {"LIQCHECK", {|| CadCheqCR(cBanco,cAgencia,cConta,nValRec,dBaixa,1)}, STR0141 }) //"Cheques"

bSetKey := SetKey(VK_F4,{|| If( !SE1->E1_TIPO $ MV_CRNEG,CadCheqCR(cBanco,cAgencia,cConta,nValRec,dBaixa,1),.F.)})
DEFINE MSDIALOG oDlgLote FROM  69,33 TO If(cPaisLoc=="BRA",602,520),581 TITLE "BAIXAS A RECEBER" PIXEL OF oMainWnd  //"Baixas a Receber"

oPanel1 := TPanel():New(0,0,'',oDlgLote,, .T., .T.,, ,45,45,.f.,.f. )
oPanel1:Align := CONTROL_ALIGN_TOP

oPanel2 := TPanel():New(0,0,'',oDlgLote,, .T., .T.,, ,30,30,.f.,.f. )
oPanel2:Align := CONTROL_ALIGN_ALLCLIENT

@ 001,002 GROUP oGrp1 TO 043, 272 LABEL STR0008 OF oPanel1 PIXEL //"Principal"
@ 001,002 GROUP oGrp2 TO If(cPaisLoc=="BRA",205,165), 133 LABEL STR0009 OF oPanel2  PIXEL //"Dados Gerais"
@ 001,139 GROUP oGrp3 TO If(cPaisLoc=="BRA",205,165), 272 LABEL STR0010 OF oPanel2  PIXEL //"Valores da Baixa"
oGrp1:oFont := oFontLbl
oGrp2:oFont := oFontLbl
oGrp3:oFont := oFontLbl

//////////////////////////
//Dados do titulo
@ 008,004 SAY STR0211				SIZE 31,07 OF oPanel1 PIXEL //"Prefixo"
@ 008,027 MSGET oPrefixo VAR SE1->E1_PREFIXO	SIZE 25,08 OF oPanel1 PIXEL When .F.
@ 008,060 SAY STR0212 				SIZE 31,07 OF oPanel1 PIXEL //"Número"
@ 008,085 MSGET oTitulo VAR SE1->E1_NUM		SIZE 70,08 OF oPanel1 PIXEL When .F.
@ 008,165 SAY STR0213				SIZE 31,07 OF oPanel1 PIXEL //"Parcela"
@ 008,188 MSGET oParcela VAR SE1->E1_PARCELA	SIZE 25,08 OF oPanel1 PIXEL When .F.
@ 008,220 SAY STR0214				SIZE 31,07 OF oPanel1 PIXEL //"Tipo"
@ 008,238 MSGET oTipo VAR SE1->E1_TIPO	F3 "SE1RDO" SIZE 30,08 OF oPanel1 PIXEL HASBUTTON
oTipo:lReadOnly := .T.

@ 019,004 SAY STR0014 SIZE 22, 07 OF oPanel1 PIXEL //"Cliente"
@ 019,027 MSGET oNomeCli VAR SE1->E1_CLIENTE F3 "SA1" SIZE 70,08 OF oPanel1 PIXEL HASBUTTON
oNomeCli:lReadOnly := .T.
@ 019,105 MSGET SA1->A1_NOME SIZE 165,08 OF oPanel1 PIXEL When .F.

@ 030,004 SAY STR0052 				SIZE 31,07 OF oPanel1 PIXEL //"Natureza"
@ 030,027 MSGET oNaturez VAR SE1->E1_NATUREZ	F3 "SED" SIZE 70,08 OF oPanel1 PIXEL HASBUTTON
oNaturez:lReadOnly := .T.
@ 030,105 SAY STR0012 				SIZE 31,07 OF oPanel1 PIXEL //"Emiss„o"
@ 030,133 MSGET oEmissao VAR dEmissao	SIZE 48,08 OF oPanel1 PIXEL When .F.
@ 030,189 SAY STR0013 				SIZE 49,07 OF oPanel1 PIXEL //"Vencto.Atual"
@ 030,222 MSGET oVencRea VAR dVencRea SIZE 48,08 OF oPanel1 PIXEL When .F.


//////////////////////////
//Dados Gerais		

nUltLin := 10
@ nUltLin,005 SAY STR0015 SIZE 39, 07 OF oPanel2 PIXEL //"Hist.Emiss„o"
@ nUltLin,065 MSGET oHist		VAR cHist	SIZE 65, 08 OF oPanel2 PIXEL When .F.

nUltLin += 12
@ nUltLin,005 SAY STR0016 SIZE 35, 07 OF oPanel2 PIXEL //"Situa‡„o"
@ nUltLin,065 MSGET oSituacao	VAR cSituacao	SIZE 65, 08 OF oPanel2 PIXEL When .F.

nUltLin += 12		
@ nUltLin,005 SAY STR0023 SIZE 32, 07 OF oPanel2 PIXEL //"Mot.Baixa"

if lPrjCni
	@ nUltLin,065 MSCOMBOBOX oMotBX VAR cMotBx ITEMS aDescMotBx ;
		VALID IIF(lFA070MDB,lMdbOk:=ExecBlock("FA070MDB",.F.,.F.),.T.)  .and. FA070VlNat(cMotBx) ;
		SIZE 56, 47 OF oPanel2 PIXEL
Else
	@ nUltLin,065 MSCOMBOBOX oMotBX VAR cMotBx ITEMS aDescMotBx ;
		VALID IIF(lFA070MDB,lMdbOk:=ExecBlock("FA070MDB",.F.,.F.),.T.) ;
		SIZE 56, 47 OF oPanel2 PIXEL
EndIf

nUltLin += 12
@ nUltLin,005 SAY STR0017 SIZE 32, 07 OF oPanel2 PIXEL //"Banco"
@ nUltLin,065 MSGET oBanco		VAR cBanco		F3 "SA6" SIZE 22, 08 OF oPanel2 PIXEL READONLY Valid CarregaSa6(@cBanco,,,.T.) 

nUltLin += 12
@ nUltLin,005 SAY STR0018 SIZE 32, 07 OF oPanel2 PIXEL //"Agˆncia"
@ nUltLin,065 MSGET oAgencia 	VAR cAgencia	SIZE 35, 08 OF oPanel2 PIXEL Valid ;
											CarregaSa6(@cBanco,@cAgencia,,.T.) ;
											WHEN ( If(!(SE1->E1_SITUACA$"0FG"),.F.,.T.) .and. MovBcobx(cMotBx, .T.)) .and. ;
										 	If ( cLoteFin == Space(TamSX3("E1_LOTE")[1]), .t., .f. )

nUltLin += 12
@ nUltLin,005 SAY STR0019 SIZE 28, 07 OF oPanel2 PIXEL //"Conta"
@ nUltLin,065 MSGET oConta		VAR cConta		SIZE 65, 08 OF oPanel2 PIXEL Valid ;
											If(CarregaSa6(@cBanco,@cAgencia,@cConta,.T.,,.T.),.T.,oBanco:SetFocus()) ;
											WHEN ( If(!(SE1->E1_SITUACA$"0FG"),.F.,.T.) .and. MovBcobx(cMotBx, .T.)) .and. ;
										 	If ( cLoteFin == Space(TamSX3("E1_LOTE")[1]), .t., .f. )

nUltLin += 12
@ nUltLin, 005 SAY STR0020 SIZE 39, 07 OF oPanel2 PIXEL//"Data Receb."
@ nUltLin, 065 MSGET oBaixa		VAR dBaixa		SIZE 50, 08 OF oPanel2 PIXEL HASBUTTON When F070DtRe() .and. !(Alltrim(SE1->E1_ORIGEM)=="FINI055") Valid fA070Data(@nTxMoeda)

nUltLin += 12
@ nUltLin, 005 SAY STR0021 SIZE 32, 07 OF oPanel2 PIXEL //"Data Cr‚dito"
@ nUltLin, 065 MSGET oDtCredito	VAR dDtCredito	SIZE 50, 08 OF oPanel2 PIXEL HASBUTTON Valid (dDtCredito >= dBaixa  .and. Iif(SuperGetMv("MV_BXDTFIN",,"1") == "2", DtMovFin(dDtCredito), .T.)) .or. GetMv("MV_ANTCRED")

nUltLin += 12
@ nUltLin, 005 SAY STR0022 SIZE 32, 07 OF oPanel2 PIXEL //"Hist.Baixa"
@ nUltLin, 065 MSGET oHist070	VAR cHist070	SIZE 65, 08 OF oPanel2 PIXEL Picture "@!" VALID CheckSX3("E5_HISTOR") When VisualSX3("E5_HISTOR")

If cPaisLoc == "BRA"
	nUltLin += 12
	@ nUltLin,005 SAY STR0142 	SIZE 53, 07 OF oPanel2 PIXEL //"Taxa contratada"
	@ nUltLin,0065 MSGET nTxMoeda         SIZE 65, 08 OF oPanel2 PIXEL Picture PesqPict( "SM2","M2_MOEDA"+AllTrim(Str(SE1->E1_MOEDA))) ;
				 When SE1->E1_MOEDA > 1 Valid Fa070Val(0,nTxMoeda)
Endif	
If lSpbInUse
	nUltLin += 12
	@ nUltLin,005 SAY STR0140 SIZE 32, 07 OF oPanel2 PIXEL  //"Modalidade SPB"
	@ nUltLin,065 COMBOBOX oModSPB VAR cModSpb ITEMS aModalSpb SIZE 56, 47 OF oPanel2 PIXEL 
Endif
If !__lPyme
	nUltLin += 12
	@ nUltLin,005 SAY STR0173 SIZE 100, 07 OF oPanel2 PIXEL  //"Rateio Mult.Naturezas"
	@ nUltLin,65 CHECKBOX oMultNat VAR lMultNat PROMPT "" SIZE 11,11 OF oPanel2 PIXEL
EndIf


//////////////////////////
//Dados da Baixa		

nUltLin := 10
@ nUltLin,144 SAY STR0027 + cDescMoeda SIZE 53, 08 OF oPanel2 PIXEL //"Valor Original "
@ nUltLin,204 MSGET oValor 	VAR nValor	SIZE 65, 08 OF oPanel2 PIXEL When .F. Picture PesqPict( "SE1","E1_VALOR" )  //"@E 999,999,999,999.99"

If cPaisLoc <> "CHI"
	nUltLin += 12
	@ nUltLin,144 SAY STR0028 SIZE 53, 07 OF oPanel2 PIXEL // "- Abatimentos"
	@ nUltLin,204 MSGET oTotAbLiq	VAR nTotAbLiq  	SIZE 65, 08 OF oPanel2 PIXEL When .F.  Picture PesqPict( "SE1","E1_VALOR" )  //"@E 999,999,999,999.99"

	If cPaisLoc == "BRA"		
		nUltLin += 12
		@ nUltLin,144 SAY STR0186 SIZE 53, 07 OF oPanel2 PIXEL // "- Impostos"
		@ nUltLin,204 MSGET oTotAbImp  VAR nTotAbImp  SIZE 65, 08 OF oPanel2 PIXEL When .F.  Picture PesqPict( "SE1","E1_VALOR" )  //"@E 999,999,999,999.99"      

		nValorLiq :=  (SE1->E1_VALOR - nTotAbLiq - nTotAbImp) 	
		nUltLin += 12
		@ nUltLin,144 SAY STR0187 SIZE 53, 07 OF oPanel2 PIXEL // "Valor Liquido"
		@ nUltLin,204 MSGET oValorLiq VAR nValorLiq     SIZE 65, 08 OF oPanel2 PIXEL When .F. Picture PesqPict("SE1","E1_VLCRUZ") //"@E 999,999,999,999.99"		
	Endif
EndIf
nUltLin += 12
@ nUltLin,144 SAY STR0029 SIZE 53, 07 OF oPanel2 PIXEL //"- Pagtos Parciais"
@ nUltLin,204 MSGET oParciais	VAR nParciais	SIZE 65, 08 OF oPanel2 PIXEL When .F.  Picture PesqPict( "SE1","E1_VALOR" )  //"@E 999,999,999,999.99"

nUltLin += 12
@ nUltLin,144 SAY  STR0136 SIZE 53, 07 OF oPanel2 PIXEL //"- Decrescimo"
@ nUltLin,204 MSGET oDecresc VAR nDecrescF  SIZE 65, 08 OF oPanel2 PIXEL     Picture PesqPict( "SE1","E1_DECRESC" )  When .f.

nUltLin += 12
@ nUltLin,144 SAY  STR0135 SIZE 53, 07 OF oPanel2 PIXEL //"+ Acrescimo"
@ nUltLin,204 MSGET oAcresc VAR nAcrescF  SIZE 65, 08 OF oPanel2 PIXEL     Picture PesqPict( "SE1","E1_ACRESC" )  When .f. //"@E 999,999,999,999.99" 

nUltLin += 12
@ nUltLin,144 SAY STR0030 SIZE 53, 07 OF oPanel2 PIXEL //"- Descontos"
@ nUltLin,204 MSGET oDescont VAR nDescont  SIZE 65, 08 OF oPanel2 PIXEL HASBUTTON When F070DSC()  Picture PesqPict( "SE1","E1_DESCONT" ) ; //"@E 999,999,999,999.99" 
															Valid Iif( nOldDescont # nDescont, ;
															( FA070DESC(oDescont) .and. fA070Val(nDescont,nTxMoeda) .and. ;
															nDescont <= xMoeda(SE1->E1_SALDO,SE1->E1_MOEDA,nMoedaBco,dBAIXA ),;
															nOldDescont := nDescont ), .T.) 

nOldDescont := nDescont


nUltLin += 12
@ nUltLin,144 SAY STR0101 SIZE 53, 07 OF oPanel2 PIXEL //"+ Multa"
@ nUltLin,204 MSGET oMulta VAR nMulta  SIZE 65, 08 OF oPanel2 PIXEL HASBUTTON Picture PesqPict( "SE1","E1_MULTA" ) ; //"@E 999,999,999,999.99" 
															When F070Mul(oMulta) .And. If(SE1->E1_TIPO $ MVRECANT+"/"+MV_CRNEG .And. !MovBcobx(cMotBx, .T.), .F., .T.);
															Valid Iif(nOldMulta # nMulta, (fA070Val(nMulta,nTxMoeda),nOldMulta := nMulta), .T.)
nOldMulta := nMulta

If cPaisLoc <> "CHI"
	nUltLin += 12
   @ nUltLin,144 SAY STR0031 SIZE 53, 07 OF oPanel2 PIXEL //"+ Tx.Permanenc."
   @ nUltLin,204 MSGET oJuros VAR nJuros   SIZE 65, 08 OF oPanel2 PIXEL HASBUTTON When F070JRS() Picture PesqPict( "SE1","E1_JUROS" ) ; //"@E 999,999,999,999.99" 		   													
		   													Valid Iif(nOldJuros # nJuros, (fA070Val(nJuros,nTxMoeda),nOldJuros := nJuros), .T.)
	nOldJuros := nJuros
Else 
	nUltLin += 12
   @ nUltLin,144 SAY STR0133 SIZE 53, 07 OF oPanel2 PIXEL //"- Outros Gastos"
   @ nUltLin,204 MSGET oOtrga VAR nOtrga   SIZE 65, 08 OF oPanel2 PIXEL HASBUTTON Picture PesqPict( "SE1","E1_OTRGA" ) ; //"@E 999,999,999,999.99" 		   													
   Valid fA070Val(nOtrga)      				
EndIf   	
//Controla IRPJ na baixa	
If   cPaisLoc == "BRA" 
	nUltLin+=12	
	@ nUltLin,144 SAY STR0228  SIZE 53, 07 OF oPanel2 PIXEL  // "- IRRF"
	@ nUltLin,204 MSGET oIrrf VAR nIrrf SIZE 66, 08 OF oPanel2 PIXEL HASBUTTON Picture PesqPict( "SE1","E1_IRRF" ); //"@E 999,999,999,999.99"
													 Valid Iif( nOldIrrf # nIrrf, (fA070Val(nIrrf,nTxMoeda),nOldIrrf := nIrrf), .T.) When nIrrf > 0  //f070ValRec(@nValorRec,nTotAbLiq,nPis,nCofins,nCsll,nIrrf,nIss,nInss)
	nOldIrrf := nIrrf
EndIf

//Pcc Baixa CR
If cPaisLoc == "BRA" .And. lPccBxCR //1-Retem PCC na Baixa ou 2-Retem PCC na Emissão(default)
   nUltLin +=12
   @ nUltLin,144 SAY STR0216 SIZE 53, 07 OF oPanel2 PIXEL //"- PIS"
   @ nUltLin,204 MSGET oPIS VAR nPIS   SIZE 66, 08 OF oPanel2 PIXEL HASBUTTON  Picture PesqPict( "SE1","E1_PIS" ) ; //"@E 999,999,999,999.99"
		   Valid Iif(nOldPis # nPis, (fA070Val(nPIS,nTxMoeda),nOldPis := nPis), .T.)
			nOldPis := nPis

   nUltLin +=12
   @ nUltLin,144 SAY STR0217 SIZE 53, 07 OF oPanel2 PIXEL //"- COFINS"
   @ nUltLin,204 MSGET oCOFINS VAR nCOFINS   SIZE 66, 08 OF oPanel2 PIXEL HASBUTTON Picture PesqPict( "SE1","E1_COFINS" ) ; //"@E 999,999,999,999.99"
		   Valid Iif(nOldCofins # nCofins, (fA070Val(nCOFINS,nTxMoeda),nOldCofins := nCofins), .T.)
			nOldCofins := nCofins			

   nUltLin +=12
   @ nUltLin,144 SAY STR0218 SIZE 53, 07 OF oPanel2 PIXEL //"- CSLL"
   @ nUltLin,204 MSGET oCSLL VAR nCSLL   SIZE 66, 08 OF oPanel2 PIXEL HASBUTTON Picture PesqPict( "SE1","E1_CSLL" ) ; //"@E 999,999,999,999.99"
		   Valid Iif(nOldCsll # nCsll, (fA070Val(nCsll,nTxMoeda),nOldCsll := nCsll), .T.)
			nOldCsll := nCsll			
EndIf  

nUltLin += 12	
@ nUltLin,144 SAY STR0033 SIZE 53,07 OF oPanel2 PIXEL //"= Valor Recebido"
@ nUltLin,204 MSGET oValRec VAR nValRec SIZE 65, 08 OF oPanel2 PIXEL HASBUTTON Picture PesqPict( "SE1","E1_VALOR" )  ;//"@E 999,999,999,999.99" 
			Valid IIF( SE1->E1_MOEDA == 1,;
					( FA070ValRec(oJuros,oMulta,oProRata,oDescont) , nValEstrang := nValRec , oVlEstrang:Refresh()),;
					( oValRec:Refresh() , Fa070ValVR(nTxMoeda) , oVlEstrang:Refresh() , oCM:Refresh()) )

nUltLin += 12
@ nUltLin,144 SAY STR0034+cDescMoeda SIZE 53, 7 OF oPanel2 PIXEL // "Valor "
@ nUltLin,204 MSGET oVlEstrang VAR nValEstrang SIZE 65, 08 OF oPanel2 PIXEL  Picture PesqPict( "SE1","E1_VALOR" )  When SE1->E1_MOEDA > 1  //"@E 999,999,999,999.99"

nUltLin += 12
@ nUltLin,144 SAY STR0032 SIZE 53,07 OF oPanel2 PIXEL // "+ Corr.Monet ria"
@ nUltLin,204 MSGET oCM     VAR nCM		SIZE 65, 08 OF oPanel2 PIXEL     Picture PesqPict( "SE1","E1_CORREC" )  ;// "@E 999,999,999,999.99" 
												When SE1->E1_MOEDA > 1 .And. (IIf(GetMv("MV_CALCCM") == "S",.T.,.F.))

If lPanelFin	
	ACTIVATE MSDIALOG oDlgLote ON INIT FaMyBar(oDlgLote,{||( nOpca := 1,;
						If(lFA070MDB.and. !lMdbOk,lMdbOk:=ExecBlock("FA070MDB",.F.,.F.),.t.) .and. ;
						IIf(( FindFunction( "UsaSeqCor" ) .And. UsaSeqCor() ), FA070Diario(), .T. ) .and.;
						Fa070But(nOpca,nTolerPg,lMovBco,lJuros,@lBaixou,;
						@cTitulo,@oTitulo,@cParcela,@dEmissao,@oEmissao,;
						@dVencRea,@oVencRea,@cNomeCli,@oNomeCli,@cHist,@oHist,;
						@cSituacao,@oSituacao,@cBanco,@oBanco,@cAgencia,@oAgencia,;
						@cConta,@oConta,@oBaixa,@dDtCredito,@oDtCredito,;
						@cHist070,@oHist070,@cMotBx,@oMotBx,@nValor,@oValor,;
						@nTotAbLiq,@oTotAbLiq,@nTotAbImp,@oTotAbImp,@nParciais,@oParciais,@oDescont,;
						@oMulta,Iif(cPaisLoc <> "CHI",@oJuros,@oOtrga),@oValRec,@oValEstr,@oCMonet,@oDlgLote,;
						cMarca,@nTotal, @nHdlPrv, @lHdlPrv, @lPadrao,@cArquivo,;
						@cPadrao, aDescMotBx,@nAcresc,@oAcresc,@nDecresc,@oDecresc,@nAcrescF,@nDecrescF,;
						aModalSpb,@oModSpb,lSpbInUse,nTxMoeda,@oMultNat,@lMultNat,@nTotLtEZ,@nValorLiq,@oValorLiq,.T.,;
						@oPrefixo,@oParcela,@oTipo,@aDiario,lPccBxCr,@oPis,@oCofins,@oCsll,;
						@nOldDescont,@nOldMulta,@nOldJuros,@nOldPis,@nOldCofins,@nOldCsll, @lIrPjBxCr, @nIrrf,@oIrrf, @nOldIrrf))},;
                  {||( nOpca := 0 ,;
						Fa070But(nOpca,nTolerPg,lMovBco,lJuros,@lBaixou,;
						@cTitulo,@oTitulo,@cParcela,@dEmissao,@oEmissao,;
						@dVencRea,@oVencRea,@cNomeCli,@oNomeCli,@cHist,@oHist,;
						@cSituacao,@oSituacao,@cBanco,@oBanco,@cAgencia,@oAgencia,;
						@cConta,@oConta,@oBaixa,@dDtCredito,@oDtCredito,;
						@cHist070,@oHist070,@cMotBx,@oMotBx,@nValor,@oValor,;
						@nTotAbLiq,@oTotAbLiq,@nTotAbImp,@oTotAbImp,@nParciais,@oParciais,@oDescont,;
						@oMulta,Iif(cPaisLoc <> "CHI",@oJuros,@oOtrga),@oValRec,@oValEstr,@oCMonet,@oDlgLote,cMarca,;
						@nTotal, @nHdlPrv, @lHdlPrv, @lPadrao, @cArquivo,@cPadrao,;
						aDescmotBx,@nAcresc,@oAcresc,@nDecresc,@oDecresc,@nAcrescF,@nDecrescF,;
						aModalSpb,@oModSpb,lSpbInUse,nTxMoeda,@oMultNat,@lMultNat,@nTotLtEZ,@nValorLiq,@oValorLiq,.T.,;
						@oPrefixo,@oParcela,@oTipo,@aDiario,lPccBxCr,@oPis,@oCofins,@oCsll,;
						@nOldDescont,@nOldMulta,@nOldJuros,@nOldPis,@nOldCofins,@nOldCsll,@lIrPjBxCr, @nIrrf,@oIrrf, @nOldIrrf))},aButtons) CENTERED

Else

	ACTIVATE MSDIALOG oDlgLote ON INIT EnchoiceBar(oDlgLote,{|| ( nOpca := 1,;
						If(lFA070MDB.and. !lMdbOk,lMdbOk:=ExecBlock("FA070MDB",.F.,.F.),.t.) .and. ;
						IIf(( FindFunction( "UsaSeqCor" ) .And. UsaSeqCor() ), FA070Diario(), .T. ) .and.;						
						Fa070But(nOpca,nTolerPg,lMovBco,lJuros,@lBaixou,;
						@cTitulo,@oTitulo,@cParcela,@dEmissao,@oEmissao,;
						@dVencRea,@oVencRea,@cNomeCli,@oNomeCli,@cHist,@oHist,;
						@cSituacao,@oSituacao,@cBanco,@oBanco,@cAgencia,@oAgencia,;
						@cConta,@oConta,@oBaixa,@dDtCredito,@oDtCredito,;
						@cHist070,@oHist070,@cMotBx,@oMotBx,@nValor,@oValor,;
						@nTotAbLiq,@oTotAbLiq,@nTotAbImp,@oTotAbImp,@nParciais,@oParciais,@oDescont,;
						@oMulta,Iif(cPaisLoc <> "CHI",@oJuros,@oOtrga),@oValRec,@oValEstr,@oCMonet,@oDlgLote,;
						cMarca,@nTotal, @nHdlPrv, @lHdlPrv, @lPadrao,@cArquivo,;
						@cPadrao, aDescMotBx,@nAcresc,@oAcresc,@nDecresc,@oDecresc,@nAcrescF,@nDecrescF,;
						aModalSpb,@oModSpb,lSpbInUse,nTxMoeda,@oMultNat,@lMultNat,@nTotLtEZ,@nValorLiq,@oValorLiq,.T.,;
						@oPrefixo,@oParcela,@oTipo,@aDiario,lPccBxCr,@oPis,@oCofins,@oCsll,;
						@nOldDescont,@nOldMulta,@nOldJuros,@nOldPis,@nOldCofins,@nOldCsll, @lIrPjBxCr, @nIrrf,@oIrrf, @nOldIrrf))},;
                  {||( nOpca := 0 ,;
						Fa070But(nOpca,nTolerPg,lMovBco,lJuros,@lBaixou,;
						@cTitulo,@oTitulo,@cParcela,@dEmissao,@oEmissao,;
						@dVencRea,@oVencRea,@cNomeCli,@oNomeCli,@cHist,@oHist,;
						@cSituacao,@oSituacao,@cBanco,@oBanco,@cAgencia,@oAgencia,;
						@cConta,@oConta,@oBaixa,@dDtCredito,@oDtCredito,;
						@cHist070,@oHist070,@cMotBx,@oMotBx,@nValor,@oValor,;
						@nTotAbLiq,@oTotAbLiq,@nTotAbImp,@oTotAbImp,@nParciais,@oParciais,@oDescont,;
						@oMulta,Iif(cPaisLoc <> "CHI",@oJuros,@oOtrga),@oValRec,@oValEstr,@oCMonet,@oDlgLote,cMarca,;
						@nTotal, @nHdlPrv, @lHdlPrv, @lPadrao, @cArquivo,@cPadrao,;    
						aDescmotBx,@nAcresc,@oAcresc,@nDecresc,@oDecresc,@nAcrescF,@nDecrescF,;
						aModalSpb,@oModSpb,lSpbInUse,nTxMoeda,@oMultNat,@lMultNat,@nTotLtEZ,@nValorLiq,@oValorLiq,.T.,;
						@oPrefixo,@oParcela,@oTipo,@aDiario,lPccBxCr,@oPis,@oCofins,@oCsll,;
						@nOldDescont,@nOldMulta,@nOldJuros,@nOldPis,@nOldCofins,@nOldCsll, @lIrPjBxCr, @nIrrf,@oIrrf, @nOldIrrf))},,aButtons ) CENTERED
Endif

SetKey(VK_F4,bSetKey)
Return lBaixou

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³fA070OK   ³ Autor ³ Wagner Xavier         ³ Data ³ 26/05/92 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Verifica se dados digitados esta OK                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ fA070ok()                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Gen‚rico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Fa070OK(cBanco,cAgencia,cConta,nValRec,dBaixa,nJuros,nCM,nMulta,;
						nDescont,nTotAbat,nTolerPg,lMovBco,aDescMotBx,nAcresc,nDecresc,;
						lSpbInUse,nTxMoeda,nTotLtEZ)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica se nao foi alterado o banco quando for tit. em desconto.     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ( SE1->E1_SITUACA $ "27" .And.;
	 cBanco+cAgencia+cConta!=SE1->E1_PORTADO+SE1->E1_AGEDEP+SE1->E1_CONTA )
	Help(" ",1,"FINA070BCO")
	Return .F.
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica se valor da baixa ‚ maior que o valor m ximo a receber       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
IF Str(nValRec,17,2) > Str((xMoeda(SE1->E1_SALDO-nTotAbat,SE1->E1_MOEDA,nMoedaBco,dBaixa,7,nTxMoeda)+Iif(Alltrim(SE1->E1_ORIGEM) == "FINA074",0,nJuros+nMulta-nDescont-nOtrga+nDifCambio+nTolerPg+nAcresc-nDecresc)),17,2)
	Help(" ",1,"ValorMaior")
	If ( SE1->E1_MOEDA == 1 )
		Return .F.
	EndIf
//    Avisa o usu rio, por‚m deixa o usuario efetuar a baixa
//    pois ocorre de o cliente receber o mesmo numero de US$
//    com a cota‡„o acima a data cota‡„o di ria
Endif

dbSelectArea("SE1")
If Empty( cMotBx )
	cMotBx := aDescMotBx[1]  //"NORMAL"
Endif

IF Empty(dBaixa) .or. (nValRec < 0 ) .or. Empty(cMotBx)
	Help(" ",1,"FA070INV")
	Return .F.
EndIF

If nDescont != Round(xMoeda(SE1->E1_SALDO,SE1->E1_MOEDA,nMoedaBco,dBaixa,3,nTxMoeda),2)
	If (nTotAbat=0.and.nValRec=0).or.(nValRec=0.and.nTotAbat!=SE1->E1_SALDO)
		Help(" ",1,"FA070INV")
		Return .F.
	EndIf
EndIF

If !FA070ValMo(lMovBco)
	Return .F.
EndIF
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica se modalidade do SPB é valida.									    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lSpbInUse
	cModSpb := Substr(cModSpb,1,1)
	IF !(SpbTipo("SE1",cModSpb,SE1->E1_TIPO))
		Return .F.
	Endif
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica se data da baixa e valida                                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
IF (dBaixa < SE1->E1_EMISSAO .OR. dBaixa > dDataBase) .and. !GetMv("MV_ANTCRED")
	Help( " ", 1, "DATAERR" )
	Return .F.
Endif
Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³FA070But  ³ Autor ³ Wagner Xavier         ³ Data ³ 26/05/92 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Recarrega Variaveis                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Fa070But()                                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Gen‚rico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Fa070But(nOpca,nTolerPg,lMovBco,lJuros,lBaixou,;
						cTitulo,oTitulo,cParcela,dEmissao,oEmissao,;
						dVencRea,oVencRea,cNomeCli,oNomeCli,cHist,oHist,;
						cSituacao,oSituacao,cBanco,oBanco,cAgencia,oAgencia,;
						cConta,oConta,oBaixa,dDtCredito,oDtCredito,;
						cHist070,oHist070,cMotBx,oMotBx,nValor,oValor,;
						nTotAbLiq,oTotAbLiq,nTotAbImp,oTotAbImp,nParciais,oParciais,oDescont,;
						oMulta,oJuros,oValRec,oValEstr,oCMonet,oDlgLote,cMarca,;
						nTotal, nHdlPrv, lHdlPrv, lPadrao, cArquivo,cPadrao,aDescMotBx,;
						nAcresc,oAcresc,nDecresc,oDecresc,nAcrescF,nDecrescF,;
						aModalSpb,oModSpb,lSpbInUse,nTxMoeda,oMultNat,lMultNat,nTotLtEZ,nValorLiq,oValorLiq,lBxLote,;
						oPrefixo,oParcela,oTipo,aDiario,lPccBxCr,oPis,oCofins,oCsll,;
						nOldDescont,nOldMulta,nOldJuros,nOldPis,nOldCofins,nOldCsll, lIrPjBxCr, nIrrf,oIrrf, nOldIrrf)

Local nSalvRec
Local lRet := .T.
LOCAL lContabiliza:=Iif(mv_par04==1,.T.,.F.)
Local lOk := .F. //Controla se foi confirmada a distribuicao 
Local aColsSEV := {}
Local aFlagCTB := {}
Local lUsaFlag	:= SuperGetMV( "MV_CTBFLAG" , .T. /*lHelp*/, .F. /*cPadrao*/) 
Local x         
LOCAL lBxLiq	:= .F.  
LOCAL lBxCEC	:= .F.  //Verificador de existencia de baixa por compensacao entre carteiras
Local aBaixa    := {}        
LOCAL nVlrBaixa := 0 
LOCAL lBaixaAbat:= .F.
LOCAL nTotAdto	:= 0    


DEFAULT nTxMoeda := 0   
DEFAULT lBxLote := .F.   
DEFAULT lPccBxCr := .F.
DEFAULT lIrPjBxCr := .F.

// Zerar variaveis para contabilizar os impostos da lei 10925.
VALOR5 := 0
VALOR6 := 0
VALOR7 := 0  

If nOpca == 1
	//.....
	//    Conforme situacao do parametro abaixo, integra com o SIGAGSP ³
	//    MV_SIGAGSP - 0-Nao / 1-Integra                   ³                
	//    Validacao da Data de Baixa
	//    ......
	If GetNewPar("MV_SIGAGSP","0") == "1" 
	   lRet := GSPF040()
	   If !lRet
		  Return lRet
		EndIf  
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Ponto de Entrada de Template para Confirmacao da Baixa       ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ExistTemplate("FA070TIT")
		lRet := ExecTemplate("FA070TIT",.F.,.F.,{nParciais})
	Endif

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Ponto de Entrada para Confirmacao da Baixa                   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ExistBlock("FA070TIT")
		lRet := ExecBlock("FA070TIT",.F.,.F.,{nParciais})
	Endif

	If !lRet
		Return lRet
	Else
		oDescont:Refresh()
		oMulta:Refresh()
		oJuros:Refresh()
		oValRec:Refresh()
		oAcresc:Refresh()
		oDecresc:Refresh()
	EndIf

	If MovBcobx(cMotBx, .T.) .and.  !CarregaSA6(cBanco,cAgencia,cConta,.T.,,.T.)
		Return .F.
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se data do movimento ‚ menor que data limite de     ³
	//³ movimentacao no financeiro                                   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If SuperGetMv("MV_BXDTFIN",,"1") == "2" .and.!DtMovFin(dDtCredito)
		Return
	Endif

	If !Fa070OK(cBanco,cAgencia,cConta,nValRec,dBaixa,nJuros,;
			nCM,nMulta,nDescont,nTotAbat,nTolerPg,lMovBco,aDescMotBx,nAcresc,nDecresc,;
			lSpbInUse,nTxMoeda)
		Return .F.
	EndIf
	nValPadrao := nValRec-(nJuros+Iif(SE1->E1_MOEDA<=1,nCM,0)+nMulta-nDescont+nAcresc-nDecresc)
	If Empty( cMotBx )
		cMotBx := aDescMotBx[1]  //"NORMAL"
	Endif

	cPadrao   := fA070Pad()
	lPadrao   := VerPadrao(cPadrao)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Inicio da prote‡„o via TTS                                   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("SE1")
	nRegSE1 := Recno()

	If MV_MULNATR .and. lMultNat
		MultNatB("SE1",.F.,STR(mv_par07,1),@lOk,@aColsSEV,@lMultNat)
	Endif

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Inicializa a gravacao dos lancamentos do SIGAPCO          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	PcoIniLan("000004")

	Begin Transaction
		aHdlPrv	:= {} 
		Aadd(aHdlPrv,{nHdlPrv,cPadrao,aFlagCTB,cArquivo})

		lBaixou := fA070Grv(lPadrao,Nil,Nil,Nil,Nil,dDtCredito,lJuros,Nil,Nil,nTxMoeda,mv_par08==1,,aHdlPrv)	//Nil=Arquivo Cnab
		
		dbSelectArea("SE1")
		dbGoTo(nRegSE1)
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Soma nos totalizadores, exceto se a situa‡„o do t¡tulo for:     ³
		//³2 - Cobran‡a Descontada   ou   7 - Cobranca Cau‡„o Descontada   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		IF !(SE1->E1_SITUACA $ "2/7")
			nTotAGer  += nValRec
			nTotADesc += nDescont+nDecresc
			nTotAMul  += nMulta
			nTotAJur  += nJuros+nAcresc
			nTotADesp += Iif(SE1->E1_MOEDA<=1,nCM,0)
		Endif
		
		// Verifica se esta utilizando multiplas naturezas
		// Chama rotina de gravacao do SEV e SEZ
		If MV_MULNATR .and. lMultNat .and. lOk
			MultNatC("SE1",@nHdlPrv,@nTotal,@cArquivo,lContabiliza,.T.,STR(mv_par07,1),@nTotLtEZ,lOk,aColsSEV,lBaixou)
			lHdlPrv := nHdlPrv > 0
		Endif
	
		If lBaixou                            
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Integracao Protheus X RM Classis Net (RM Sistemas)³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			if GetNewPar("MV_RMCLASS", .F.) .or. GetNewPar("MV_RMBIBLI",.F.)
				if alltrim(upper(SE1->E1_ORIGEM)) == 'L' .or. alltrim(upper(SE1->E1_ORIGEM)) == 'S' .or. SE1->E1_IDLAN > 0
					//Executa a funcao para efetuar manutencao nas tabelas do RM Biblios e Replicar a baixa
					ClsProcBx(SE1->(Recno()),'1',"FIN070")
				endif
			endif

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Grava os cheques no SEF ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			GravaChqCR(SE5->E5_SEQ,"FINA070") 
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Grava os lancamentos nas contas orcamentarias SIGAPCO    ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If SE1->E1_SITUACA == "0"	// Carteira
				PcoDetLan("000004","01","FINA070")
			ElseIf SE1->E1_SITUACA == "1"	// Simples
				PcoDetLan("000004","02","FINA070")
			ElseIf SE1->E1_SITUACA == "2"	// Descontada
				PcoDetLan("000004","03","FINA070")
			ElseIf SE1->E1_SITUACA == "4"	// Vinculada
				PcoDetLan("000004","04","FINA070")
			ElseIf SE1->E1_SITUACA == "5"	// c/Advogado
				PcoDetLan("000004","05","FINA070")
			ElseIf SE1->E1_SITUACA == "6"	// Judicial
				PcoDetLan("000004","06","FINA070")
			ElseIf SE1->E1_SITUACA == "7"	// Caucionada Descontada
				PcoDetLan("000004","08","FINA070")
			EndIf

		Endif

		IF lBaixou .and. lContabiliza .and. !lMultNat

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Localizacao Portugal - Gera dados para diario contabil ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If ( FindFunction( "UsaSeqCor" ) .And. UsaSeqCor() )
				AAdd( aDiario, {"SE5",SE5->(Recno()),cCodDiario,"E5_NODIA","E5_DIACTB"} )
			Else
				aDiario := {} 
			EndIf

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Verifica qual o Lanc Padr„o que sera utilizado 	  ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			cPadrao := fa070Pad()
			lPadrao:=VerPadrao(cPadrao)
			IF !lHdlPrv .And. lPadrao
		 		nHdlPrv:=HeadProva(cLote,"FINA070",Substr(cUsuario,7,6),@cArquivo)
				lHdlPrv := .T.
			EndIF
			IF lPadrao .and. lHdlPrv
				VALOR := 0
				ABATIMENTO := Round(NoRound(xMoeda(nTotAbat,SE1->E1_MOEDA,nMoedaBco,dBaixa,3),3),2)

				If lUsaFlag  // Armazena em aFlagCTB para atualizar no modulo Contabil 
					aAdd( aFlagCTB, {"E5_LA", "S", "SE5", SE5->( Recno() ), 0, 0, 0} )
				Endif
				nTotal += DetProva( nHdlPrv, cPadrao, "FINA070", cLote, /*nLinha*/, /*lExecuta*/,;
				                    /*cCriterio*/, /*lRateio*/, /*cChaveBusca*/, /*aCT5*/,;
				                    /*lPosiciona*/, @aFlagCTB, /*aTabRecOri*/, /*aDadosProva*/ )

			EndIF     

		Endif
      
		If FindFunction( "GETROTINTEG" ) .and. !lFini055 .and. FindFunction("FwHasEAI") .and. FWHasEAI("FINA070",.T.,,.T.)
			FwIntegDef( 'FINA070' ) 
		Endif
	End Transaction

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Finaliza a gravacao dos lancamentos do SIGAPCO          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	PcoFinLan("000004")
	
	If ExistBlock ("F070BXLT")
		ExecBlock ("F070BXLT",.F.,.F.,{lBxLote})
	EndIF	

	dbSelectArea("SE1")
	dbGoTo(nRegSE1)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
	//³Integracao protheus X tin Baixa por Lote	³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ

Else
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Limpa marca para titulo com baixa abortada.                  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("SE1")
	Begin Transaction
		RecLock ("SE1",.F.)
		Replace E1_OK with ""
	End Transaction
Endif

nPis    := 0
nCsll   := 0
nCofins := 0                 

While !Eof()
	If SE1->E1_OK != cMarca .or. SE1->E1_TIPO $ MVABATIM+"/"+MVFUABT //adicionado MVFUABT pois a variável MVABATIM não está retornando FU-
		If SE1->E1_OK == "xx"
			nRecAtual := Recno()
			dbSKip()
			nProxRec := Recno()
			dbGoto(nRecAtual)	
			If !EOF()
				RecLock ("SE1",.F.)
				Replace E1_OK with ""
			Endif
			dbGoto(nProxRec)
			Loop
		Else
			dbSKip()
			Loop
		Endif
	Else
		Exit
	EndIf
End

If Eof()
	oDlgLote:End()
	Return lBaixou
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Posiciona Cliente  no SA1	                                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SA1")
SA1->(MsSeek(xFilial("SA1")+SE1->E1_CLIENTE+SE1->E1_LOJA))
dbSelectArea("SE1")
lIrPjBxCr	:= If (FindFunction("FIrPjBxCr"),FIrPjBxCr(),.F.)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Carrega Variaveis da Baixa                                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cTitulo 		:= SE1->E1_NUM
cParcela  	:= SE1->E1_PARCELA
dEmissao		:= SE1->E1_EMISSAO
dVencRea		:= SE1->E1_VENCREA
cNomeCli 	:= SE1->E1_CLIENTE + " - " + Subst(SA1->A1_NOME,1,40)
cHist			:= SE1->E1_HIST
cSituacao 	:= SE1->E1_SITUACA + " " + fa070situa()
dBaixa      := CriaVar("E1_BAIXA")
dDtCredito  := dDataBase
cHist070		:= Criavar("E5_HISTOR")		//Inicilizador padrao
If Empty(cHist070)
	cHist070 := STR0007+Space(Len(cHist070)-24)  // "Valor recebido s/ T¡tulo"
Endif
nValor		:= SE1->E1_VALOR
dbSelectArea("SE1")
nSalvRec 	:= Recno()
nTotAbImp   := 0
nTotAbat		:= SumAbatRec(SE1->E1_PREFIXO,SE1->E1_NUM,SE1->E1_PARCELA,SE1->E1_MOEDA,"S",dBaixa,@nTotAbImp)
dbSelectArea("SE1")
dbGoto(nSalvRec)
nDescont    := 0
nMulta      := 0
nJuros      := 0
nCM         := 0
nAcrescF		:= SE1->E1_SDACRES
If ExistBlock("F070ACRE")
	ExecBlock("F070ACRE",.F.,.F.)
Endif
nDeCrescF	:= SE1->E1_SDDECRE
nValorLiq 	:= SE1->E1_VALOR - nTotAbLiq - nTotAbImp
nAcresc     := Round(NoRound(xMoeda(nAcrescF,SE1->E1_MOEDA,nMoedaBco,dBaixa,3),3),2)
nDecresc    := Round(NoRound(xMoeda(SE1->E1_SDDECRE,SE1->E1_MOEDA,nMoedaBco,dBaixa,3),3),2)
nValrec		:= Round(Noround(xMoeda(SE1->E1_SALDO-nTotAbat,SE1->E1_MOEDA,nMoedaBco,dBaixa,3),3),2)+nMulta+nJuros-nDescont+nAcresc-nDecresc-nPis-nCofins-nCsll
nParciais	:=0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Para que o valor da baixa parcial nao fique negativo, verifico o saldo³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If (SE1->E1_VALOR > SE1->E1_SALDO) .And. Empty(SE1->E1_TIPOLIQ)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Procura pelas baixas deste titulo ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	lTipBxCP:=lRaRtImp
	aBaixa := Sel070Baixa( "VL /V2 /BA /RA /CP /LJ /" + MV_CRNEG, SE1->E1_PREFIXO, SE1->E1_NUM, SE1->E1_PARCELA, SE1->E1_TIPO,; 
					@nTotAdto, @lBaixaAbat, SE1->E1_CLIENTE, SE1->E1_LOJA, @nVlrBaixa, , @lBxCec, @lBxLiq,,.T.)
	For x := 1 To Len(aBaixaSE5)    
		If aBaixaSE5[x][1]+substr(aBaixaSE5[x][2],1,TamSX3("E1_NUM")[1])+aBaixaSE5[x][3]+aBaixaSE5[x][4]+aBaixaSE5[x][5]+aBaixaSE5[x][6]== SE1->E1_PREFIXO+ SE1->E1_NUM+SE1->E1_PARCELA+SE1->E1_TIPO+SE1->E1_CLIENTE+SE1->E1_LOJA
			nParciais += aBaixaSE5[x][8]
	        If lPccBxCR .And. lRaRtImp .and. !(aBaixaSE5[x][21]$ "1|2")
			   nParciais += aBaixaSE5[x][18]+aBaixaSE5[x][19]+aBaixaSE5[x][20]+aBaixaSE5[x][30]// somar impostos PCC 
			Elseif lIrPjBxCr .And. lRaRtImp  
		  		nParciais += aBaixaSE5[x][30]
			Endif        
			If lRaRtImp
		 		nParciais += aBaixaSE5[x][32]+aBaixaSE5[x][33]
		 		nTotAbat  -= aBaixaSE5[x][32]+aBaixaSE5[x][33]
			Endif
		Endif 
	
	Next
	nParciais += nTotAdto
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Soma valor de decrescimo em baixas parciais, para evitar         ³
	//³ diferencas entre valor original e valor recebido                 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If SE1->E1_SDDECRE <> SE1->E1_DECRESC
		nParciais += ( SE1->E1_DECRESC - SE1->E1_SDDECRE )
	EndIf
Else
	nParciais 	:= SE1->E1_VALOR-SE1->E1_SALDO
Endif


If	lIrPjBxCr
	nValRec := SE1->E1_VALOR - nParciais
	nIrrf:=FCaIrBxCR(nValRec)
EndIf
//PCC Baixa CR
If lPccBxCR 
	nParciais += nPis + nCofins + nCsll
	nValRec := SE1->E1_VALOR - nParciais 
	f070TotMes(dBaixa,.T.,,.F.)
Endif	

fa070val( nValrec, nTxMoeda )

cMoeda 		:= IIF(Empty(SE1->E1_MOEDA),"1",AllTrim(Str(SE1->E1_MOEDA,2)))
cDescMoeda 	:= SubStr(GetMV("MV_SIMB"+cMoeda),1,3)
lMultNat		:= .F.

IF ExistBlock("F070MNAT")
	lMultNat := ExecBlock("F070MNAT",.F.,.F.)
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Trecho incluido para integração e-commerce          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If  FindFunction("LJ861EC02") .And. FindFunction("LJ861EC01")
	If  LJ861EC01(SE1->E1_NUM, SE1->E1_PREFIXO, .T./*PrecisaTerPedido*/)
		LJ861EC02(SE1->E1_NUM, SE1->E1_PREFIXO)
	EndIf		
Endif

fA070Data(@nTxMoeda,.F.) // Calcula o desconto e o juros (se houver) e valida a data
oPrefixo:Refresh()
oTitulo:Refresh()
oParcela:Refresh()
oTipo:Refresh()
oEmissao:Refresh()
oVencRea:Refresh()
oNomeCli:Refresh()
oHist:Refresh()
oSituacao:Refresh()
oBanco:Refresh()
oBanco:SetFocus()
oAgencia:Refresh()
oConta:Refresh()
oBaixa:Refresh()
oDtCredito:Refresh()
oHist070:Refresh()
oMotBx:Refresh()
oMotBx:SetFocus()
oValor:Refresh()
IF cPaisLoc <> "CHI"
	oTotAbLiq:Refresh()
Endif
If cPaisLoc == "BRA"
	oTotAbImp:Refresh()
	oValorLiq:Refresh()
	If lPccBxCr
		nOldPis		:= nPis
		nOldCofins	:= nCofins
		nOldCsll		:= nCsll
		oPis:Refresh()
		oCofins:Refresh()
		oCsll:Refresh()	
	Endif		
	If lIrPjBxCr
 		oIrrf:Refresh()		
 	Endif   
Endif
oParciais:Refresh()
oDescont:Refresh()
oMulta:Refresh()
oJuros:Refresh()
oValRec:Refresh()
oAcresc:Refresh()
oDecresc:Refresh()

If !__lPyme
	oMultNat:Refresh()
EndIf

If SE1->E1_MOEDA > 1
	oVlEstrang:Refresh()
	oCM:Refresh()
Endif
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Pr‚-inicializa a modalidade de SPB                                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lSpbInUse
	If !Empty(SE1->E1_MODSPB)
		cModSpb := SE1->E1_MODSPB
	Else
	   cModSpb := "1"
	Endif		
	oModSpb:Refresh()
Endif

nOldDescont := nDescont
nOldMulta 	:= nMulta
nOldJuros	:= nJuros

Return lBaixou


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³FA070Grava³ Autor ³ Wagner Xavier 		  ³ Data ³ 06/05/92 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Grava as baixas 														  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ FA070Grava(ExpA1) 													  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ 																			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ FINA070																	  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function fA070Grava(dDtCredito)
LOCAL nx,cArquivo
LOCAL lDigita:=IIF(mv_par01==1,.T.,.F.),nTotal:=0,nHdlPrv:=0
LOCAL lAglut :=IIF(mv_par02==1,.T.,.F.),cPadrao,lPadrao := .F.
LOCAL nRecSe1:=0
LOCAL lContabiliza:=Iif(mv_par04==1,.T.,.F.)
Local cLoteOrig, lHdlPrv := .F.
Local dDataDisp, nRetencao, nCont
Local lSpbInUse := SpbInUse()
Local lf070DtCr := ExistBlock("F070DTCR")
Local nTotLtEZ := 0	//Totalizador da Bx Lote Mult Nat CC
Local nRecSeV	:= 0
Local nRecSeZ	:= 0
Local aDiario	:= {}
Local aFlagCTB := {}
Local lUsaFlag	:= SuperGetMV( "MV_CTBFLAG" , .T. /*lHelp*/, .F. /*cPadrao*/) 
Local lTpDesc		:= SE5->(FieldPos("E5_TPDESC"))	> 0 //Verifica campo TPDESC na tabela SE5 (<C>ondicional ou <I>ncondicional)


If Type("cTpDesc") == "U"
	cTpDesc:="I"
Endif  

// Zerar variaveis para contabilizar os impostos da lei 10925.
VALOR5 := 0
VALOR6 := 0
VALOR7 := 0                   

dDtCredito := Iif(dDtCredito==NIL,dDataBase,dDtCredito)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Caso o usu rio configure que deseja usar o lote financeiro, o   ³
//³ sistema becapeia o lote cont bil original e depois o restaura.  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cLoteOrig := cLote
If GetMv("MV_LOTEFIN") == "S" .and. !Empty( cLoteFin )
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Somente atualiza o lote contabil com o lote financeiro se o tamanho do ³
	//³ lote financeiro for menor ou igual ao do lote contabil.                ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If TamSX3("E1_LOTE")[1] > 4 .And. TamSX3("E1_LOTE")[1] <= TamSX3( "CT2_LOTE" )[1]
		cLote := cLoteFin
	EndIf	
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ O arquivo SE1 est  filtrado pela Indregua neste momento.        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SE1")
SE1->(dbGoTop( ))

While ! SE1->(Eof())

	IF SE1->E1_FILIAL != cFilial
		dbSkip()
		Loop
	End

	SE1->( dbSkip() )
	nX := SE1->( RecNo() )
	SE1->( dbSkip(-1) )

	Fa070TitW(cMarca,@nTotal,@nHdlPrv,@lHdlPrv,@lPadrao,@cArquivo,@cPadrao,@nTotLtEZ,@aDiario)

End

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Gera saldo banc rio totalizador, quando baixa p/lote³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If (nTotAGer > 0 )

	If lF070DtCr		// permite simular outra data base / data credito
		dDtCredito := Execblock("F070DTCR",.F.,.F.,dDtCredito)
	Endif

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Grava registro totalizador da movimenta‡„o banc ria ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Reclock( "SE5" , .T. )
	SE5->E5_FILIAL		:= xFilial()
	SE5->E5_BANCO 		:= cBancolt
	SE5->E5_AGENCIA	:= cAgencialt
	SE5->E5_CONTA		:= cContalt
	SE5->E5_VALOR		:= nTotAGer
	SE5->E5_RECPAG		:= "R"
	SE5->E5_HISTOR		:= STR0130+cLoteFin //BAIXA DE TITULOS P/LOTE 9999
	SE5->E5_DTDIGIT	:= dDtCredito
	SE5->E5_DATA		:= dDtCredito
	SE5->E5_NATUREZ	:= cNaturLote
	SE5->E5_TIPODOC	:= "BL"        // Baixa por Lote
	SE5->E5_LOTE		:= cLoteFin
	SE5->E5_MOEDA		:= StrZero(nMoedaBco,2)	   
	
	If lTpDesc
		SE5-> E5_TPDESC	:= cTpDesc			
	Endif
	
	If mv_par09 == 1
		If GetNewPar("MV_RETLTBX","1") == "1"		//1=Sim, 2=Nao
			//   Data de disponibilizacao -> considera dias de retencao
			nRetencao  := SA6->A6_RETENCAO
			if nRetencao > 0
				For nCont := 1 To nRetencao
					dDtCredito := DataValida(dDtCredito+1,.T.)
				Next nCont
			EndiF
		Endif
	Else
		nRetencao := 0
	EndIf
	
	Reclock("SE5",.F.)
	SE5->E5_DTDISPO := dDtCredito

	If lSpbInUse
		// Se houver retencao ModSpb = Comp, caso contrario, STR
		SE5->E5_MODSPB := IIF(SE5->E5_DTDISPO > dDataBase,"3","1")
	Endif		
	MsUnlock()
	AtuSalBco( cBancolt, cAgencialt, cContalt, dDtCredito, nTotAGer, "+" )

	If cPaisLoc $ "ARG|POR|EUA|ANG|COL|MEX"
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Gerar a Movimentacao Bancaria na 2a. Moeda.         ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		nRetencao := SA6->A6_RETENCAO
		If !Empty(SA6->A6_MOEDA) .and. ( SA6->A6_MOEDA != SE1->E1_MOEDA )
			AtuSalBco( cBancolt, cAgencialt, cContalt, dDataBase, nTotAGer, "+" )
			Reclock( "SE5" , .T. )
			SE5 -> E5_FILIAL       := cFilial
			SE5 -> E5_MOEDA     := StrZero(SA6->A6_MOEDA,2)
			SE5 -> E5_BANCO        := cBancolt
			SE5 -> E5_AGENCIA      := cAgencialt
			SE5 -> E5_CONTA        := cContalt
			SE5 -> E5_VALOR        := xMoeda( nTotAGer,nMoedaBco,SA6->A6_MOEDA,dDataBase )
			SE5 -> E5_RECPAG       := "R"
			SE5 -> E5_HISTOR       := STR0001+ cLoteFin //BAIXA DE TITULOS P/LOTE 9999
			SE5 -> E5_DTDIGIT      := dDataBase
			SE5 -> E5_DATA         := dDataBase
			SE5 -> E5_NATUREZ      := cNaturLote
			SE5 -> E5_TIPODOC   := "BL"        // Baixa por Lote
			If nRetencao > 0
				For nCont := 1 To nRetencao
					dDataDisp := DataValida(dDataDisp+1,.T.)
				Next nCont
			EndIf
			SE5->E5_DTDISPO := IIF(Empty(dDtCredito),dDataDisp,dDtCredito)
			MsUnlock()
		EndIf

	Endif

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Localizacao Portugal - Gera dados para diario contabil ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ( FindFunction( "UsaSeqCor" ) .And. UsaSeqCor() )
		AAdd( aDiario, {"SE5",SE5->(Recno()),cCodDiario,"E5_NODIA","E5_DIACTB"} )
	Else
		aDiario := {} 
	EndIf
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
    //³ PONTO DE ENTRADA PARA VALIDACAO APOS A GRAVACAO ³
    //³ DOS CAMPOS DA SE5 NA BAIXA POR LOTE             ³
    //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
    If ExistBlock("F070VSE5")
       ExecBlock("F070VSE5",.F.,.F.)		
    EndIf
		
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Avisa quando totais n„o baterem                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Str(nTotGer,16,2) != Str(nTotAGer,16,2)
	Help(" ",1,"TOTGERAL",, STR0137  + Space(1) +;
                            Trans(nTotGer, "@E 99,999,999,999.99") +;
                            Chr(13)+CHR(10) + STR0138 + Space(1) +;
                            Trans(nTotAGer, "@E 99,999,999,999.99"), 4, 0)
ElseIf Str(nTotDesp,16,2) != Str(nTotADesp,16,2)
	Help(" ",1,"TOTDESP" ,, STR0137  + Space(1) +;
   								 Trans(nTotDesp, "@E 99,999,999,999.99") +;
                            Chr(13)+CHR(10) + STR0138 + Space(1) +;
                            Trans(nTotADesp, "@E 99,999,999,999.99"), 4, 0)
ElseIf Str(nTotDesc,16,2) != Str(nTotADesc,16,2)
	Help(" ",1,"TOTDESC" ,, STR0137  + Space(1) +;
                            Trans(nTotDesc, "@E 99,999,999,999.99") +;
                            Chr(13)+CHR(10) + STR0138 + Space(1) +;
                            Trans(nTotADesc, "@E 99,999,999,999.99"), 4, 0)
ElseIf Str(nTotMul,16,2) != Str(nTotAMul,16,2)
	Help(" ",1,"TOTMULT" ,, STR0137  + Space(1) +;
                            Trans(nTotMul, "@E 99,999,999,999.99") +;
                            Chr(13)+CHR(10) + STR0138 + Space(1) +;
                            Trans(nTotAMul, "@E 99,999,999,999.99"), 4, 0)
ElseIf Str(nTotJur,16,2) != Str(nTotAJur,16,2)
	Help(" ",1,"TOTJUROS",, STR0137  + Space(1) +;
 									 Trans(nTotJur, "@E 99,999,999,999.99") +;
                            Chr(13)+CHR(10) + STR0138 + Space(1) +;
                            Trans(nTotAJur, "@E 99,999,999,999.99"), 4, 0)
EndIf

IF ExistBlock("FA070BXL")
   ExecBlock("FA070BXL",.F.,.F.)
ENDIF
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Envia para Lan‡amento Cont bil, se gerado arquivo   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
IF (lHdlPrv .And. lPadrao .and. lContabiliza) .OR. nHdlPrv > 0
	dbSelectArea( "SE1" )
	nRecSe1 := RecNo()
	dbGoBottom()
	dbSkip()

	dbSelectArea( "SEV" )
	nRecSeV := RecNo()
	dbGoBottom()
	dbSkip()

	dbSelectArea( "SEZ" )
	nRecSeZ := RecNo()
	dbGoBottom()
	dbSkip()

	If nTotAGer != 0
		//Contabilizar totalizador exceto o rateado por MultNat C.Custo
		VALOR := nTotaGer - nTotLtEZ
		ABATIMENTO := 0

		If lUsaFlag  // Armazena em aFlagCTB para atualizar no modulo Contabil 
			aAdd( aFlagCTB, {"E5_LA", "S", "SE5", SE5->( Recno() ), 0, 0, 0} )
		Endif
		nTotal += DetProva( nHdlPrv, cPadrao, "FINA070", cLote, /*nLinha*/, /*lExecuta*/,;
		                    /*cCriterio*/, /*lRateio*/, /*cChaveBusca*/, /*aCT5*/,;
		                    /*lPosiciona*/, @aFlagCTB, /*aTabRecOri*/, /*aDadosProva*/ )

		//Contabilizar totalizador dos rateios MultNat com C.Custo
		If nTotLtEZ > 0
			VALOR := nTotLtEZ   
			ABATIMENTO := 0

			If lUsaFlag  // Armazena em aFlagCTB para atualizar no modulo Contabil 
				aAdd( aFlagCTB, {"E5_LA", "S", "SE5", SE5->( Recno() ), 0, 0, 0} )
			Endif
			nTotal += DetProva( nHdlPrv, cPadrao, "FINA070", cLote, /*nLinha*/, /*lExecuta*/,;
			                    /*cCriterio*/, /*lRateio*/, /*cChaveBusca*/, /*aCT5*/,;
			                    /*lPosiciona*/, @aFlagCTB, /*aTabRecOri*/, /*aDadosProva*/ )

		Endif
	Endif

	RodaProva(nHdlPrv,nTotal)
	cA100Incl( cArquivo, nHdlPrv, 3 /*nOpcx*/, cLote, lDigita, lAglut,;
	           /*cOnLine*/, /*dData*/, /*dReproc*/, @aFlagCTB, /*aDadosProva*/, aDiario )
	aFlagCTB := {}  // Limpa o coteudo apos a efetivacao do lancamento

	dbSelectArea("SE1")
	SE1->(dbGoTo(nRecSe1))
	
	dbSelectArea("SEV")
	SEV->(dbGoTo(nRecSeV))

	dbSelectArea("SEZ")
	SEZ->(dbGoTo(nRecSeZ))

EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Restaura o lote cont bil original ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cLote := cLoteOrig
Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ fA070Data³ Autor ³ Wagner Xavier         ³ Data ³ 26/05/92 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Fun‡„o utilizada para consistir a Data                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ fA070Data()                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Gen‚rico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function fA070Data(nTxMoeda,lHelp,oDtBaixa,oJuros,oCbx)
LOCAL lRet := .T.    

Default lFDataUse := ExistBlock("FDataUse")
Default __lF070AltV := ExistBlock("F070AltV")

If lFDataUse  
	lRetPE := ExecBlock("FDataUse", .F. , .F. )
	If !lRetPE
		Return lRet
	EndIf
EndIf

DEFAULT oDtBaixa := ""

nCM1		:= If(Type("nCM1") != "N",0,nCM1)
nProRata	:= If(Type("nProRata") != "N",0,nProRata)

lHelp := IIf(lHelp == NIL, .T.,lHelp)

IF (dBaixa < SE1->E1_EMISSAO .OR. dBaixa > dDataBase) .and. !GetMv("MV_ANTCRED") .and. lHelp
	Help( " ", 1, "DATAERR" )
	lRet := .F.
Else
	
	If (dDtCredito < dBaixa) .And. ! GetMv("MV_ANTCRED")
		dDtCredito := dBaixa
	EndIf
	
	If GetNewPar("MV_BXDTFIN","1") == "2"	//nao permite data de baixa
		If !(DtMovFin(dBaixa,lHelp))					//menor que MV_DATAFIM
			Return .F.
		Endif
	Endif

	If __lF070AltV 
		ExecBlock("F070AltV",.F.,.F.,lHelp)
	Else
		dbSelectArea("SE1")
		If FunName() <>"FINA630"
		   IF (!Empty(oDtBaixa) .AND. oDtBaixa:lModified) .or. Empty(oDtBaixa)
				nDescont := FaDescFin("SE1",dBaixa,SE1->E1_SALDO-nTotAbat,nMoedaBco)
				nOldDescont := nDescont
			Endif
	  		IF (!Empty(oJuros) .AND. oJuros:lModified) .or. Empty(oJuros).or. oDtBaixa:lModified
				nCM1     := 0
				nProRata := 0

				fa070Juros(nMoedaBco)     

				If nCM1 > 0
					nJuros -= nCM1
				Else
					nDescont += nCM1
				EndIf
	
				If nProRata > 0
					nJuros -= nProRata
				Else
					nDescont += nProRata
				EndIf
			EndIf
		EndIf
	Endif

   	If cPaisLoc = "BRA" .And.  (!Empty(oDtBaixa) .AND. oDtBaixa:lModified)
		nTxMoeda := If(SE1->E1_MOEDA > 1, If(SE1->E1_TXMOEDA > 0, SE1->E1_TXMOEDA,RecMoeda(dBaixa,SE1->E1_MOEDA)),0)
	ElseIf Type("aTxMoedas") = "A" 
		Fa070SetMd(@nTxMoeda,.F.)
	EndIf 
	
   	If  TYPE('lTitLote')<> "U" .AND. lTitLote
   		fa070Val(0,nTxMoeda)
   	Else
   		fa070Val(0,nTxMoeda,(!Empty(oDtBaixa) .And. oDtBaixa:lModified .And. dBaixa <> dDataBase))
   	EndIf	
   	F070Ret()
	
Endif	
Return lRet


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³Fa070ValVR³ Autor ³ Antonio Maniero Jr.   ³ Data ³ 13/05/94 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Fun‡„o utilizada para consistir o valor digitado           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ fA070ValVR(Valor)                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Gen‚rico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function fa070ValVR(nTxMoeda)
Local nSaveVar
Local nEstOriginal := 0
Local nMoedaBx :=1     
//Controla o Pis Cofins e Csll na baixa  (1-Retem PCC na Baixa ou 2-Retem PCC na Emissão(default) )
LOCAL lPccBxCr		:= If (FindFunction("FPccBxCr"),FPccBxCr(),.F.)

//Controla IRPJ na baixa
Local lIrPjBxCr		:= If (FindFunction("FIrPjBxCr"),FIrPjBxCr(),.F.)   
 
nValTot     := SE1->E1_VLCRUZ
nValEstrang := nValOrig    := SE1->E1_SALDO-nTotAbat
nAcresc     := If(Type("nAcresc") != "N",SE1->E1_SDACRES,nAcresc)  
nAcrescf    := If(Type("nAcrescf") != "N",SE1->E1_SDACRES,nAcrescf)	
nDecresc    := If(Type("nDecresc") != "N",SE1->E1_SDDECRE,nDecresc)
nDecrescf   := If(Type("nDecrescf") != "N",SE1->E1_SDDECRE,nDecrescf)

nMoedaBx:=nMoedabco

If cPaisloc<>"BRA"
	nTxMoeda:=Iif(nMoedaBco>0,aTxMoedas[nMoedaBco][2],1)
	nTxMdaOr:=aTxMoedas[SE1->E1_MOEDA][2]
EndIf
If GetMv("MV_SIGAGSP") <> "1"  //Roberto - Integração com o SIGAGSP 0=Não integra 1=Integra
   nAcresc     := Round(NoRound(xMoeda(nAcrescF,SE1->E1_MOEDA,nMoedaBx,dBaixa,3,nTxMoeda),3),MsDecimais(nMoedaBx))
EndIf
nDecresc    := Round(NoRound(xMoeda(SE1->E1_SDDECRE,SE1->E1_MOEDA,nMoedaBx,dBaixa,3,nTxMoeda),3),MsDecimais(nMoedaBx))
DEFAULT nTxMoeda := 0
If Substr(cMotBx,1,3) != "DEV"                                              
	If cPaisLoc <> "BRA"
		nValMoeda1	:= xMoeda(SE1->E1_VALOR-nTotAbat,SE1->E1_MOEDA,nMoedaBx,dBaixa,3,Iif(nTxMdaOr>0,nTxMdaOr,nMoedaBx),nTxMoeda)
		nValMoeda	:= xMoeda(nValOrig,SE1->E1_MOEDA,nMoedaBx,dBaixa,3,Iif(nTxMdaOr>0,nTxMdaOr,nMoedaBx),nTxMoeda)
	Else
		nValMoeda1	:= xMoeda(SE1->E1_VALOR-nTotAbat,SE1->E1_MOEDA,nMoedaBx,dBaixa,3,nTxMoeda)
		nValMoeda	:= xMoeda(nValOrig,SE1->E1_MOEDA,nMoedaBx,dBaixa,3,nTxMoeda)
	EndIf	
	nValMoeda1	:= Round(NoRound(nValMoeda1,3),MsDecimais(nMoedaBx))
	nValMoeda	:= Round(NoRound(nValMoeda,3),MsDecimais(nMoedaBx))
Else                                                                 
	If cPaisLoc <> "BRA"
		nValMoeda1	:= xMoeda(SE1->E1_VALOR-nTotAbat,SE1->E1_MOEDA,nMoedaBx,SE1->E1_EMISSAO,3,,nTxMoeda)
		nValMoeda	:= xMoeda(nValOrig,SE1->E1_MOEDA,nMoedaBx,SE1->E1_EMISSAO,3,,nTxMoeda)
	Else
		nValMoeda1	:= xMoeda(SE1->E1_VALOR-nTotAbat,SE1->E1_MOEDA,nMoedaBx,SE1->E1_EMISSAO,3,nTxMoeda)
		nValMoeda	:= xMoeda(nValOrig,SE1->E1_MOEDA,nMoedaBx,SE1->E1_EMISSAO,3,nTxMoeda)
	EndIf
	nValMoeda1	:= Round(NoRound(nValMoeda1,3),MsDecimais(nMoedaBx))
	nValMoeda	:= Round(NoRound(nValMoeda,3),MsDecimais(nMoedaBx))
EndIf
If (nMulta+nJuros+nDescont+nAcresc+nDecresc+nPis+nCofins+nCsll) > 0 .or. Round(nValEstrang - xMoeda(nValRec,nMoedaBx,SE1->E1_MOEDA,dBaixa,,,nTxMoeda) ,MsDecimais(SE1->E1_MOEDA)) > 0.01
	If Substr(cMotBx,1,3) == "DEV"      
		nValEstrang := Round(NoRound(xMoeda(nValRec,nMoedaBx,SE1->E1_MOEDA,SE1->E1_EMISSAO,3,,nTxMoeda),3),MsDecimais(SE1->E1_MOEDA))
	Else
		nValEstrang := Round(NoRound(xMoeda(nValRec,nMoedaBx,SE1->E1_MOEDA,dBaixa,3,,nTxMoeda),3),MsDecimais(SE1->E1_MOEDA))
	EndIf
EndIf  
If cPaisLoc<>"BRA"
	nValEstrang := Round(NoRound(xMoeda(nValRec,nMoedaBx,SE1->E1_MOEDA,dBaixa,3,nTxMoeda,nTxMdaOr),3),MsDecimais(SE1->E1_MOEDA))
EndIf
nSaveVar := nValEstrang   // Pau de gerenciamento de Memoria (Wilson)
nValLiq     := nValRec-nMulta-nJuros-nPis-nCofins-nCsll+nDescont-nAcresc+nDecresc+Round(NoRound(xMoeda(nTotAbat,SE1->E1_MOEDA,nMoedaBx,dBaixa,3,nTxMoeda),3),MsDecimais(nMoedaBx)) 
nEstOriginal := nValEstrang-(xMoeda(nJuros+(nCm1+nProRata)+nMulta-nDescont-nOtrga+nAcresc-nDecresc - Iif(lPccBxCr,nPis+nCofins+nCsll,0)-Iif(lIrPjBxCr,nIrrf,0),nMoedaBx,SE1->E1_MOEDA,,,,nTxMoeda)) 
FA070CORR(nEstOriginal,nTxMoeda) // calcula Correc. Monetaria
nValEstrang := nSaveVar

If !lF415Auto .and. Type('lF070Auto') =='U' .and. !lF070Auto
	oVlEstrang:Refresh()
	oCM:Refresh()
Endif
Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ fA070Val ³ Autor ³ Wagner Xavier         ³ Data ³ 26/05/92 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Fun‡„o utilizada para consistir o valor digitado           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ fA070Val(Valor)                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Gen‚rico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function fA070Val(nVal,nTxMoeda,lRecalcVRec)
Local lRetorno := .T.

DEFAULT nTxMoeda := 0 
DEFAULT lRecalcVRec := .T.
Default __F070VDATA := ExistBlock("F070VDATA")

IF nVal<0
	Help(" ",1,"VALNEGAT")
	lRetorno := .F.
Else
	fa070Calc( nTxMoeda, lRecalcVRec )  
EndIF
// Utilizado na validacao dos dados
If __F070VDATA 
	lRetorno := ExecBlock("F070VDATA",.F.,.F., {nVal,nTxMoeda})
Endif

Return( lRetorno )

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ fA070Calc³ Autor ³ Reynaldo Miyashita    ³ Data ³ 15/06/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Recalcula os valores referentes a baixa do titulo a receber³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ fA070Calc(nTxMoeda)                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Gen‚rico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function fa070Calc(nTxMoeda, lRecalcVRec,lAltlVal)
Local nEstOriginal := 0	// Valor Estrangeiro Original para Correcao Monet.
Local nMoedaBx     := 1
Local x
Local nTxMdaOr := 0 
Local lPccBxCr := If (FindFunction("FPccBxCr"),FPccBxCr(),.F.)
//Controla IRPJ na baixa
Local lIrPjBxCr		:= If (FindFunction("FIrPjBxCr"),FIrPjBxCr(),.F.)

Local nTotMultas	:= 0

DEFAULT nTxMoeda := 0
DEFAULT lRecalcVRec := .T. //-- Adicionado argumento à função para tratamento de Bx Parcial em outra moeda
/*DEFAULT lAltlVal := .F.*/

nMoedaBx	:= If(Type("nMoedaBco") != "N",SE1->E1_MOEDA,nMoedaBco)

If cPaisLoc <> "BRA" 
	nTxMoeda := Iif(nMoedaBco > 0, aTxMoedas[nMoedaBco][2],1)
	nTxMdaOr := aTxMoedas[SE1->E1_MOEDA][2]
EndIf 

If !Empty( SE1->( FieldPos( "E1_APLVLMN" ) ) ) .and. SE1->E1_APLVLMN == "2"
	lAplVlMin := .F.
Endif

If FunName() == "FINA110"
	nParciais  := 0
	aBaixaSE5 := {}
		
	cPrefixo   := SE1->E1_PREFIXO
	cNum       := SE1->E1_NUM
	cParcela   := SE1->E1_PARCELA
	cTipo      := SE1->E1_TIPO
	nTotAdto   := 0
	lBaixaAbat := .F.
	cCliente   := SE1->E1_CLIENTE
	cLoja      := SE1->E1_LOJA
	nVlrBaixa  := 0
	lBxCec     := .F.
	lBxLiq     := .F.
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Para que o valor da baixa parcial nao fique negativo, verifico o saldo³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If (SE1->E1_VALOR > SE1->E1_SALDO)
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Procura pelas baixas deste titulo                                     ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		aBaixa := Sel070Baixa( "VL /V2 /BA /RA /CP /LJ /"+MV_CRNEG,cPrefixo,cNum,cParcela,cTipo,@nTotAdto,@lBaixaAbat,cCliente,cLoja,@nVlrBaixa,,@lBxCec,@lBxLiq)
		For x := 1 To Len(aBaixaSE5)
			nParciais += aBaixaSE5[x][8]
			nTotMultas += aBaixaSE5[x][15]
		Next
		nParciais += nTotAdto
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Soma valor de decrescimo em baixas parciais, para evitar         ³
		//³ diferencas entre valor original e valor recebido                 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If SE1->E1_SDDECRE <> SE1->E1_DECRESC
			nParciais += ( SE1->E1_DECRESC - SE1->E1_SDDECRE )
			nParciais -= 	nTotMultas
		EndIf
	Else
		nParciais 	:= SE1->E1_VALOR-SE1->E1_SALDO
	Endif
EndIf 


nOtrGa		:= If(Type("nOtrGa") != "N",0,nOtrGa)
nDifCambio	:= If(Type("nDifCambio") != "N",0,nDifCambio)
nAcresc		:= If(Type("nAcresc") != "N",SE1->E1_SDACRES,nAcresc)  
nAcrescf	:= If(Type("nAcrescf") != "N",SE1->E1_SDACRES,nAcrescf)	
nDecresc	:= If(Type("nDecresc") != "N",SE1->E1_SDDECRE,nDecresc)
nDecrescf	:= If(Type("nDecrescf") != "N",SE1->E1_SDDECRE,nDecrescf)

nValTot     := SE1->E1_VLCRUZ

nValEstrang := nValOrig := SE1->E1_SALDO-nTotAbat
nAcresc     := Round(NoRound(xMoeda(nAcrescF,SE1->E1_MOEDA,nMoedaBx,dBaixa,3,nTxMoeda),3),MsDecimais(nMoedaBx))
nDecresc    := Round(NoRound(xMoeda(SE1->E1_SDDECRE,SE1->E1_MOEDA,nMoedaBx,dBaixa,3,nTxMoeda),3),MsDecimais(nMoedaBx))
	
If TrazCodMot(cMotBx) == "DEV"             
	If cPaisLoc <> "BRA"
		nValMoeda1	:= xMoeda(SE1->E1_VALOR-nTotAbat,SE1->E1_MOEDA,nMoedaBx,SE1->E1_EMISSAO,3,nTxMdaOr,nTxMoeda)
		nValMoeda	:= xMoeda(nValOrig,SE1->E1_MOEDA,nMoedaBx,SE1->E1_EMISSAO,3,nTxMdaOr,nTxMoeda)
	Else	
		nValMoeda1	:= xMoeda(SE1->E1_VALOR-nTotAbat,SE1->E1_MOEDA,nMoedaBx,SE1->E1_EMISSAO,3,nTxMoeda)
		nValMoeda	:= xMoeda(nValOrig,SE1->E1_MOEDA,nMoedaBx,SE1->E1_EMISSAO,3,nTxMoeda)
	EndIf	
	nValMoeda1  := Round(NoRound(nValMoeda1,3),MsDecimais(nMoedaBx))
	nValMoeda   := Round(NoRound(nValMoeda ,3),MsDecimais(nMoedaBx))
Else
    If cPaisLoc <> "BRA"
		nValMoeda1	:= xMoeda(SE1->E1_VALOR-nTotAbat,SE1->E1_MOEDA,nMoedaBx,dBaixa,3,nTxMdaOr,nTxMoeda)
		nValMoeda	:= xMoeda(nValOrig,SE1->E1_MOEDA,nMoedaBx,dBaixa,5,nTxMdaOr,nTxMoeda)
		nValMoeda   := Round(NoRound(nValMoeda,5),4)
	Else
		nValMoeda1	:= xMoeda(SE1->E1_VALOR-nTotAbat,SE1->E1_MOEDA,nMoedaBx,dBaixa,3,nTxMoeda)
		nValMoeda	:= xMoeda(nValOrig,SE1->E1_MOEDA,nMoedaBx,dBaixa,3,nTxMoeda)
		nValMoeda   := Round(NoRound(nValMoeda,3),MsDecimais(nMoedaBx))
	EndIf
	nValMoeda1  := Round(NoRound(nValMoeda1,3),MsDecimais(nMoedaBx))
Endif

//-- Quando for chamada pela validação do campo nValRec, não recalcula o valor recebido (baixa parcial)

If lRecalcVRec .And. If(cPaisLoc == "BRA",.T.,nValRec==0 ) 
	nValRec     := Round(nValMoeda+nMulta+nJuros+(nCm1+nProRata)-nDescont-nOtrGa+nAcresc-nDecresc-Iif(lPccBxCr,nPis+nCofins+nCsll,0)-Iif(lIrPjBxCr,nIrrf,0),MsDecimais(SE1->E1_MOEDA))
EndIf

If (nMulta+nJuros+(nCm1+nProRata)+nDescont+nAcresc+nDecresc-nPis-nCofins-nCsll-nIrrf) > 0 .or. Round(nValEstrang - xMoeda(nValRec,nMoedaBx,SE1->E1_MOEDA,dBaixa,,,nTxMoeda),MsDecimais(SE1->E1_MOEDA) ) > 0.01 
	If Substr(cMotBx,1,3) == "DEV"      
		nValEstrang := Round(NoRound(xMoeda(nValRec,nMoedaBx,SE1->E1_MOEDA,SE1->E1_EMISSAO,3,,nTxMoeda),3),MsDecimais(SE1->E1_MOEDA))
	Else
		nValEstrang := Round(NoRound(xMoeda(nValRec,nMoedaBx,SE1->E1_MOEDA,dBaixa,3,,nTxMoeda),3),MsDecimais(SE1->E1_MOEDA))
	EndIf
EndIf
If cPaisLoc<>"BRA"
	nValEstrang := Round(NoRound(xMoeda(nValRec,nMoedaBx,SE1->E1_MOEDA,dBaixa,3,nTxMoeda,nTxMdaOr),3),MsDecimais(SE1->E1_MOEDA))
EndIf
nEstOriginal := nValEstrang-(xMoeda(nJuros+(nCm1+nProRata)+nMulta-nDescont-nOtrga+nAcresc-nDecresc - Iif(lPccBxCr,nPis+nCofins+nCsll,0)-Iif(lIrPjBxCr,nIrrf,0),nMoedaBx,SE1->E1_MOEDA,,,,nTxMoeda)) 
                                            
FA070CORR(nEstOriginal,nTxMoeda)


If !lAltlVal .OR. SE1->E1_MOEDA==1
	nOldValRec := nValRec
EndIf
Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³fa070Situa³ Autor ³ Vinicius Barreira     ³ Data ³ 13/12/94 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Displaya a descri‡„o da situa‡„o do titulo                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ fA070situa()                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ FINA070                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function fa070Situa()
Local Banco := Alias()
Local cVolta := ""

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Mostra a Situa‡„o do Titulo ser baixado                               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea( "SX5" )
SX5->( MsSeek(cFilial+"07") )
While SX5->X5_FILIAL+SX5->X5_TABELA == cFilial+"07"
	If SX5->X5_CHAVE = SE1->E1_SITUACA // Obs: Nao usar ==
		cVolta := Left( X5Descri(),20 )
		Exit
	EndIf
	SX5->(dbSkip())
End

If ! Empty( Banco )
	dbSelectArea( Banco )
Endif

Return cVolta

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ValidaTota³ Autor ³ Wagner Xavier         ³ Data ³ 06/05/92 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Verifica se Valor a ser Creditado ‚ valido                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ValidaTotal()                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Gen‚riCco                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function ValidaTotal()
LOCAL lRet := .T.
Do Case
Case Empty(cLote)
	lRet := .F. 
Case STR((nTotGer-nTotDesp-nTotDesc+nTotMul+nTotJur),17,MsDecimais(1)) != STR(nCredConta,17,MsDecimais(1))
	Help(" ",1,"VLRTOTINV")
	lRet := .F.
EndCase
Return lRet


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MontaTotal³ Autor ³ Valter G. Nogueira Jr.³ Data ³ 04/03/94 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Atualiza o Total geral                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³MontaTotal(oGet)                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³Gen‚rico                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function MontaTotal(oGet)
nCredConta := nTotGer-nTotDesp-nTotDesc+nTotMul+nTotJur     //subtrai despesa
oGet:refresh()
Return .T.


/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³FA070blank³ Autor ³ Wagner Xavier         ³ Data ³ 08/05/92 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Limpa as marcacoes do arquivo (E1_OK)                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ FA070blank()                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ FINA060                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function fa070Blank()
If !Empty(SE1->E1_NUM)
	Reclock("SE1")
	SE1->E1_OK := "  "
	MsUnlock()
Endif

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³Fa070ChecF³ Autor ³ Antonio Maniero Jr.   ³ Data ³ 17/03/94 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Retorna Expres„o para Indice Condicional ( Banco != "CX" )  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³Fa070ChecF()                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³Gen‚rico                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function FA070ChecF()
Local cFiltro := ""
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Processa um filtro pr¢prio caso exista o Ponto de Entrada     ³
//³Necessidade adaptda ao cliente CosWay                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
IF ExistBlock("F070OWN")
	cFiltro := Execblock("F070OWN",.f.,.f.)
Else
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Atencao para tamanho da String -> tamanho atual = 250 bytes   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cFiltro += 'E1_FILIAL+E1_PORTADO+E1_AGEDEP+E1_CONTA=="'+xFilial("SE1")+cBancoLt+cAgenciaLt+cContaLt+'".And.'
	cFiltro += 'DTOS(E1_VENCREA)>="'+DTOS(dVencDe) + '".And.'
	cFiltro += 'DTOS(E1_VENCREA)<="'+DTOS(dVencAte)+ '".And.'
	cFiltro += 'E1_NATUREZ>="'      +cNatDe       + '".And.'
	cFiltro += 'E1_NATUREZ<="'      +cNatAte      + '".and.'
	cFiltro += '!(E1_TIPO$"'+MVPROVIS+"/"+MVRECANT+"/"+MVIRABT+"/"+MVINABT+"/"+MV_CRNEG

	//Destarcar Abatimentos
	If mv_par06 == 2
		cFiltro += "/"+MVABATIM+"/"+MVFUABT +'")' //adicionado MVFUABT pois a variável MVABATIM não está retornando FU-
	Else
		cFiltro += '")'
	Endif

	// Verifica integracao com TMS e nao permite baixar titulos que tenham solicitacoes
	// de transferencias em aberto.
	cFiltro += ' .And. Empty(E1_NUMSOL)'
	cFiltro += ' .And. (E1_SALDO>0 .OR. E1_OK="xx")'
Endif
Return cFiltro  

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³Fa070Chec0³ Autor ³ Elaine F. T. Berldo   ³ Data ³ 16/06/94 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Retorna Expres„o para Indice Condicional  ( Banco == "CX" ) ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³Fa070Chec0()                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³Gen‚rico                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function FA070Chec0()
Local cFiltro := ""

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Processa um filtro pr¢prio caso exista o Ponto de Entrada     ³
//³Necessidade adaptda ao cliente CosWay                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
IF ExistBlock("F070OWN")
	cFiltro := Execblock("F070OWN",.f.,.f.)
Else
	cFiltro := 'E1_FILIAL=="' + xFilial("SE1") + '".And.'
	cFiltro += 'DTOS(E1_VENCREA)>="' + DTOS(dVencDe)  + '".And.'
	cFiltro += 'DTOS(E1_VENCREA)<="' + DTOS(dVencAte) + '".And.'
	cFiltro += 'E1_NATUREZ>="'       + cNatDe         + '".And.'
	cFiltro += 'E1_NATUREZ<="'       + cNatAte        + '".And.'
	cFiltro += '(E1_PORTADO="'       + cBancolt         + '".OR.'
	cFiltro += 'E1_PORTADO=="'+ space(Len(E1_PORTADO)) + '").AND.'
	cFiltro += '!(E1_TIPO$"'+MVPROVIS+"/"+MVRECANT+"/"+MVIRABT+"/"+MVINABT+"/"+MV_CRNEG

	//Destacar Abatimentos
	If mv_par06 == 2
		cFiltro += "/"+MVABATIM+"/"+MVFUABT +'")'//adicionado MVFUABT pois a variável MVABATIM não está retornando FU-
	Else
		cFiltro += '")'
	Endif

	// Verifica integracao com TMS e nao permite baixar titulos que tenham solicitacoes
	// de transferencias em aberto.
	cFiltro += ' .And. Empty(E1_NUMSOL)'
	cFiltro += ' .And. (E1_SALDO>0 .OR. E1_OK="xx")'
Endif
Return cFiltro


/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³FA070Inverte³ Autor ³ Wagner Xavier       ³ Data ³ 07/11/95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Marca e Desmarca Titulos, invertendo a marca‡†o existente  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ Fa070Inverte()                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ FINA070																	  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Fa070Inverte(cMarca,oValor,oQtda,lTodos)

LOCAL nReg := SE1->(Recno())
Local lSavTTS := __TTSInUse
Local nValorMarca := SE1->(E1_SALDO+E1_SDACRES-E1_SDDECRE)
Local cFieldMarca := "E1_OK"
Local lBxTitRec	  := .T.


DEFAULT lTodos:=.T.

__TTSInUse := .f.

dbSelectArea("SE1")
If lTodos
	MsSeek(cFilial)
EndIf
While !lTodos .or. (!Eof() .and. cFilial == E1_FILIAL)
	If (lTodos .And. SE1->(MsRLock())) .Or. !lTodos
		
		IF (ExistBlock("F110TIT")) .and. cFieldMarca == "E1_OK" .and. Empty(&cFieldMarca)
			// Bloqueio antes de chamar o ponto de entrada, pois no momento da chamada deste mesmo PE no 
			// FINA110, funcoes Fa110Marca e Fa110Inverte o registro da SE1 jah estah bloqueado.
			lBxTitRec := ExecBlock("F110TIT",.F.,.F., { nValorMarca })			
		Endif		
		
		IF E1_OK == cMarca
			SE1->E1_OK := "  "
			If SE1->E1_TIPO $ MVABATIM+"/"+MVFUABT //adicionado MVFUABT pois a variável MVABATIM não está retornando FU-
				nValor += xMoeda(E1_SALDO+E1_SDACRES-E1_SDDECRE,SE1->E1_MOEDA,nMoedaBco)
			Else
				nValor -= xMoeda(E1_SALDO+E1_SDACRES-E1_SDDECRE,SE1->E1_MOEDA,nMoedaBco)
			Endif
			nQtdtit--
			nQtdTit:= Iif(nQtdTit<0,0,nQtdTit)
		Else
			SE1->E1_OK := cMarca
			If lBxTitRec
				If SE1->E1_TIPO $ MVABATIM+"/"+MVFUABT //adicionado MVFUABT pois a variável MVABATIM não está retornando FU-
					nValor -= xMoeda(E1_SALDO+E1_SDACRES-E1_SDDECRE,SE1->E1_MOEDA,nMoedaBco)
				Else
					nValor += xMoeda(E1_SALDO+E1_SDACRES-E1_SDDECRE,SE1->E1_MOEDA,nMoedaBco)
				Endif
			Endif
			nQtdtit++
		Endif
	EndIf
	If lTodos
		dbSkip()
	Else
		Exit
	EndIf
End

If nValor < 0
	nValor := 0
Endif

__TTSInUse := lSavTTS

SE1->(dbGoto(nReg))
oValor:Refresh()
oQtda:Refresh()
oMark:oBrowse:Refresh(.t.)
Return Nil

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³ButtonoFF ³Autor  ³ Pilar S Albaladejo	  ³ Data ³12.03.97  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Desliga Botao da enchoice bar - WINDOWS						  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Generico 																  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function ButtonOff(bSet15,bSet24,lOk)
DEFAULT lOk := .t.
IF lOk
	 SetKey(15,bSet15)
	 SetKey(24,bSet24)
Endif
Return .T.


/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³Fa070Pesq ³ Autor ³ Mauricio Pequim Jr	  ³ Data ³23.11.98  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ tela de pesquisa - WINDOWS 										  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Generico 																  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Fa070Pesq(oMark, cAlias)

Local nRecno := Recno(), cChaBus // Guardo o RECNO e crio chave

AxPesqui()
dbSetOrder(nIndex+1)					// Busca a chave encontrada por AXPESQUI
cChaBus := &(IndexKey())			// No filtro IndRegua, caso nao encontre
If ! MsSeek(cChaBus)					// Posicione onde estava
	DbGoTo(nRecno)
Endif
oMark:oBrowse:Refresh(.T.)

Return

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³Fa070Acha ³ Autor ³ Mauricio Pequim Jr	  ³ Data ³23.11.98  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Funcao que realiza a pesquisa - WINDOWS						  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Generico 																  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Fa070Acha(cCampo,oMark)

dbSelectArea("SE1")
MsSeek(xFilial("SE1")+cCampo,.T.)
oMark:oBrowse:Refresh(.T.)
Return

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³Fa070BDev ³ Autor ³ Andreia Santos        ³ Data ³ 26/11/98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Limpa conteudo de Bco/Age/Cta/N.Chque se baixa DEVOLUCAO   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ Fa070bDev()																  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ FINA070																	  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function fa070BDev(oJuros, oMulta, oDescont, oCm, nTxMoeda, oCbx)
Local dBaixa

If ! MovBcoBx(cMotBx, .T.)
	nJuros		:= 0
	nDescont	:= 0
	nMulta		:= 0
	nCm			:= 0
	nValRec		:= Round(Noround(xMoeda(SE1->E1_SALDO-nTotAbat,SE1->E1_MOEDA,nMoedaBco,dBaixa,3,SE1->E1_TXMOEDA),3),2)+nMulta+nJuros-nDescont+nAcresc-nDecresc
	fA070Data(@nTxMoeda)
	cBanco 		:= Space(3)
	cAgencia	:= Space(5)
	cConta 		:= Space(10)
ElseIf Empty(cBanco)
	If Alltrim(SE1->E1_ORIGEM) == "LOJA010"
		aCaixaLoja  := xCxLoja()
		cPortado    := SE1->E1_PORTADO
		cBanco      := SE1->E1_PORTADO
		cAgencia    := SE1->E1_AGEDEP
		cConta      := SE1->E1_CONTA
	Else
		// 0 = Carteira
		// F = Carteira Protesto
		// G = Carteira Acordo
		cPortado    := IIF(SE1->E1_SITUACA $ "0FG" .and. Empty(cPortado), aCaixaFin[1] , SE1->E1_PORTADO)
		cBanco      := IIF(SE1->E1_SITUACA $ "0FG" .and. Empty(cBanco)  , aCaixaFin[1] , SE1->E1_PORTADO)
		cAgencia    := IIF(SE1->E1_SITUACA $ "0FG" .and. Empty(cAgencia), aCaixaFin[2] , SE1->E1_AGEDEP )
		cConta      := IIF(SE1->E1_SITUACA $ "0FG" .and. Empty(cConta)  , aCaixaFin[3] , SE1->E1_CONTA  )
	Endif
	fA070Data(@nTxMoeda,.F.) // Calcula o desconto e o juros (se houver) e valida a data
	F070Ret()
Endif

If (!Empty(oCbx) .AND. oCbx:lModified)
	dBaixa      := CriaVar("E1_BAIXA")
	nTxMoeda 	:= If(SE1->E1_MOEDA > 1, If(SE1->E1_TXMOEDA > 0, SE1->E1_TXMOEDA,RecMoeda(dBaixa,SE1->E1_MOEDA)),0)
	f070TotMes(dBaixa,.T.,,,,nTxMoeda)
	nIrrf 		:= FCaIrBxCR(SE1->E1_VALOR)
Endif	

If Type('lF070AUTO') =='U' .OR. ! lF070auto
	oBanco:Refresh()
	oAgencia:Refresh()
	oConta:Refresh()
	If ValType(oJuros) == "O"
		oJuros:Refresh()
	Endif	
	If ValType(oDescont) == "O"
		oDescont:Refresh()
	Endif	
	If ValType(oMulta) == "O"
		oMulta:Refresh()
	Endif	
	If ValType(oCm) == "O"
		oCm:Refresh()
	Endif	
Endif
Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³FA070Desc ³ Autor ³ Mauricio Pequim Jr	  ³ Data ³ 13/03/00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Alteracao de dados	da baixa											  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ F070Desc()			 													  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ FINA070																	  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function FA070Desc(oDescont)

Local lFa070Dsc	:= ExistBlock("F070DESC")
Local lRet 		:= .T.
Local dBaixa
Local cTxMod		:= If(SE1->E1_MOEDA > 1, If(SE1->E1_TXMOEDA > 0, SE1->E1_TXMOEDA,RecMoeda(dBaixa,SE1->E1_MOEDA)),0)

If lFa070Dsc
	lRet := ExecBlock("F070DESC",.F.,.F.,{nDescont})
	If ValType(lRet) <> "L"
		lRet := .T.
	EndIf
Endif      

If (!Empty(oDescont) .AND. oDescont:lModified)
	dBaixa      := CriaVar("E1_BAIXA")
	If !nTxMoeda <> cTxMod
		nTxMoeda 	:= If(SE1->E1_MOEDA > 1, If(SE1->E1_TXMOEDA > 0, SE1->E1_TXMOEDA,RecMoeda(dBaixa,SE1->E1_MOEDA)),0)
	EndIf	
	F070CnvPcc(nTxMoeda, SE1->E1_MOEDA)	
	f070TotMes(dBaixa,.T.,,,,nTxMoeda)
	nIrrf 		:= FCaIrBxCR(SE1->E1_VALOR)
Endif

Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³F070Jrs   ³ Autor ³ Mauricio Pequim Jr	  ³ Data ³ 13/03/00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Alteracao de dados	da baixa											  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ F070Jrs()			 													  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ FINA070																	  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function F070Jrs()

Local lRet	:= .T.

If	ExistBlock("F070JRS")
	lRet := ExecBlock("F070JRS",.F.,.F.)
Endif

Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³F070TxPer ³ Autora³ Andrea V. Santiago	  ³ Data ³ 06/06/11 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Permite alteracao ou validacao no valor dos juros na baixa  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ F070TxPer()			 													  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ FINA070																	  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function F070TxPer(oJuros)
Local lRet       	:= .T.
Local lF070TxPer 	:= Existblock("F070TXPER")     
Local dBaixa
Local cTxMod		:= If(SE1->E1_MOEDA > 1, If(SE1->E1_TXMOEDA > 0, SE1->E1_TXMOEDA,RecMoeda(dBaixa,SE1->E1_MOEDA)),0)

If	lF070TxPer
	lRet := ExecBlock("F070TXPER",.F.,.F.)
Endif    
                                     
If !Empty(oJuros) .and. oJuros:lModified
	dBaixa      := CriaVar("E1_BAIXA")
	If !nTxMoeda <> cTxMod
		nTxMoeda 	:= If(SE1->E1_MOEDA > 1, If(SE1->E1_TXMOEDA > 0, SE1->E1_TXMOEDA,RecMoeda(dBaixa,SE1->E1_MOEDA)),0)
	EndIf
	f070TotMes(dBaixa,.T.,,,,nTxMoeda)
	nIrrf 		:= FCaIrBxCR(SE1->E1_VALOR)
Endif

Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³FA070ValRec³ Autor ³ Adrianne Furtado  	 ³ Data ³ 05/06/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Alteracao de dados da baixa							   	   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ FA070ValRec()			 								   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ FINA070													   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function FA070ValRec(oJuros,oMulta,oProRata,oDescont)

Local lFa070VlRec	:= ExistBlock("F070VREC")
Local lRet			:= .T.
Local lAltValor		:= STR(nValRec,17,2) != STR(nOldValRec,17,2)
Local lPccBxCr		:= If (FindFunction("FPccBxCr"),FPccBxCr(),.F.)
Local lIrPjBxCr		:= If (FindFunction("FIrPjBxCr"),FIrPjBxCr(),.F.)
Local lProp			:= SuperGetMV("MV_DESCFIN",,"I")=="P" 
Local lAtOldRec		:= .T.
Local lBQ10925		:= SuperGetMV("MV_BQ10925",,"2") == "1" .And. !lRaRtImp

If lIrPjBxCr .And. lAltValor .And. !(lRaRtImp .And. SE1->E1_TIPO $ MVRECANT)
	nIrrf:=FCaIrBxCR(nValRec,,.T.,,lAltValor)
Endif

If  lAltValor .And. lPccBxCr
	f070TotMes(dBaixa,.T.)
	If lRaRtImp .And. lAltValor
		nOldValRec := nValRec
		nValRec := nValRec-nPis-nCoFins-nCsll-nIrrf
	
		If nValRec < 0 
			nValRec := nOldValRec
		Endif		
	
		If SE1->E1_MOEDA == 1
			nValEstrang := nValRec // nValEstrang = variavel que contem o valor do campo "valor pago" na baixa
		EndIf   
			
		If lAtOldRec
			nOldValRec	 := nValRec			
		EndIf
	ElseIf lAltValor .And. lPccBxCr
		nOldValRec := nValRec
		If lRaRtImp .And. ( lAltValor .And. lPccBxCr )
			nValRec := nValRec-nPis-nCoFins-nCsll-nIrrf		
		EndIF
		If nValRec < 0
			nValRec := nOldRec
		EndIf
		
		If SE1->E1_MOEDA == 1
			nValEstrang := nValRec // nValEstrang = variavel que contem o valor do campo "valor pago" na baixa
		EndIf
		
		//Se o parâmetro MV_BQ10925 for igual a 1, recomponho o valor da baixa parcial que será exibido com o valor dos impostos
		If lBQ10925
			nValRec += nPis + nCofins + nCsll + nIrrf
		EndIf
		
		If nValRec != nOldValRec
			nOldValRec := nValRec
		EndIf
	EndIf
Endif   

//Recalcula o desconto 
If lAltValor .And. lProp
	nDescont := FaDescFin("SE1",dBaixa,nValRec,nMoedaBco) 
	nValRec  -= nDescont
Endif

If lFa070VlRec
	lRet := ExecBlock("F070VREC",.F.,.F.,{nValRec})
	If ValType(lRet) <> "L"
		lRet := .T.
	EndIf
Endif

Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³F070DSC	³ Autor ³ Mauricio Pequim Jr	  ³ Data ³ 13/03/00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Alteracao de dados	da baixa											  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ F070DSC()			 													  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ FINA070																	  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function F070DSC()

Local lRet			:= .T.

If	ExistBlock("F070DSC")
	lRet := ExecBlock("F070DSC",.F.,.F.)
Endif

Return lRet



























































































































































































































































































































































































































































































































































































































































































































































































/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³Sel070baix³ Autor ³ Wagner Xavier         ³ Data ³ 23/10/98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Seleciona Baixas a serem canceladas                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³Sel070Baxia()                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³Gen‚rico                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Sel070Baixa(	cTipoDoc	,cPrefixo	,cNum		,cParcela,;
						cTipo		,nTotAdto	,lBaixaAbat	,cCliente,;
						cLoja		,nVlrBaixa	,lTransf	,lBxCec,;
						lBxLiq	,lSigaloja	, lTipBxCP)
LOCAL __k
LOCAL cTipoBaixa
LOCAL nRecAtu
LOCAL cSequencia
LOCAL lEstornada	:= .f.
LOCAL aBaixa		:= {}
LOCAL cNumero
Local cTipoEst      
Local aArea			:= GetArea()
Local nTamE1Parc	:= TamSx3("E1_PARCELA")[1]	// Tamanho do campo parcela
Local cOrdRec := ""
Local cSerRec := ""

If Type("lRaRtImp") == "U" //Forço a criação da vairável caso a mesma não exista
 Private	lRaRtImp  := lFinImp .And. FRaRtImp() //Define se ha retencao de impostos PCC/IRPJ no R.A 
EndIf

DEFAULT lTipBxCP	:= .F.
DEFAULT lSigaloja	:= .F.


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica se funcao foi chamada a partir da trasferˆncia banc ria FINA060 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
lTransf := IIF (lTransf == NIL, .F., lTransf)
lBxCec := IIF(lBxCec == NIL,.F.,lBxCec)
lBxLiq := IIF(lBxLiq == NIL,.F.,lBxLiq)

nTotAdto := if( nTotAdto == nil,0,nTotAdto )
lBaixaAbat := if( lBaixaAbat ==nil,.f.,lBaixaAbat)
nVlrBaixa := 0
cParcela	:= Padr(cParcela,nTamE1Parc)

FOR __k := 1 TO Len(cTipoDoc) Step 4

	cTipoBaixa := AllTrim(Substr(cTipodoc,__k,3 ) )
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Procura pela baixas dependendo do Tipodoc passado como parametro      ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("SE5")
	SE5->(dbSetOrder(2))
	If SE5->(DBSeek(xFilial("SE5")+cTipoBaixa+cPrefixo+cNum+cParcela+cTipo))
		While !SE5->(Eof()) .and. SE5->E5_FILIAL==xFilial("SE5") .and. ;
			SE5->E5_TIPODOC+SE5->E5_PREFIXO+SE5->E5_NUMERO+SE5->E5_PARCELA+SE5->E5_TIPO==cTipoBaixa+cPrefixo+cNum+cParcela+cTipo

		If SE5->E5_MOTBX $ "FAT#DSD" .or. (SE5->E5_MOTBX $ "STP" .and. FunName() <> "FINA040" .and. !lF070Auto) 
			dbSkip()
			Loop
		Endif
		
		If ("FINA061" $ Subst(SE5->E5_HISTOR,1,7)) .And. SE5->E5_MOTBX $ "PCC_IRF"
			dbSkip()
			Loop		
		Endif
		
		If Alltrim(SE5->E5_HISTOR) = "Bx. Bord. Ac."
			dbSkip()
			Loop		
		Endif
		If SE1->E1_TIPO $ MVRECANT+"/"+MV_CRNEG
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Verifica se as baixas sao de adiantamento (NORmal,DEVolucao,DACao)  ³
			//³Caso E5_DOCUMEN nao vazio e cTipoBaixa=="BA", ‚ compensacao e n„o   ³
			//³baixa de titulo de adiantamento.												  ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If cTipoBaixa =="VL" .or.( cTipoBaixa =="BA" .and. Empty(SE5->E5_DOCUMEN))
				lBaixaAbat := .t.
			EndIf
			IF SE5->E5_SITUACAO == "C" .or. SE5->E5_RECPAG != "P" .And. IIf(cpaisloc=="BRA",.T.,Empty(SE5->E5_ORDREC))
				SE5->(dbSkip())
				Loop
			EndIF
		Else
			IF SE5->E5_SITUACAO == "C" .or. SE5->E5_RECPAG != "R"  .And. IIf(cpaisloc=="BRA",.T.,Empty(SE5->E5_ORDREC))
				SE5->(dbSkip())
				Loop
			EndIF
		Endif

		IF SE5->E5_CLIFOR != cCliente .OR. SE5->E5_LOJA != cLoja
			SE5->(dbSkip())
			Loop
		EndIF
        IF SE5->E5_MOTBX == "LIQ" .and. !Empty(SE5->E5_DOCUMEN)
			lBxLiq	:= .T.
			SE5->(dbSkip())
			Loop
		EndIF

		If cTipoBaixa == "BA" .and. SE5->E5_TIPO $ MVRECANT .and. !Empty(SE5->E5_DOCUMEN) .And. IIf(cpaisloc=="BRA",.T.,Empty(SE5->E5_ORDREC))
			dbskip()
			loop
		EndIf
		
		If lSigaloja
			If TemBxCanc( SE5->E5_PREFIXO + SE5->E5_NUMERO + SE5->E5_PARCELA + SE5->E5_TIPO + SE5->E5_CLIFOR + SE5->E5_LOJA + SE5->E5_SEQ )
				dbSkip()
				Loop
			EndIf
		EndIf

		nRecAtu := SE5->(recno())
		cSequencia	:= SE5->E5_SEQ
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Verifica se existe uma baixa cancelada para esta baixa efetuada       ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If lTransf
				SE5->(DBSeek(xFilial("SE5")+"E2"+cPrefixo+cNum+cParcela+cTipo))
			cTipoEst:= "E2"
		Else
				SE5->(DBSeek(xFilial("SE5")+"ES"+cPrefixo+cNum+cParcela+cTipo))
			cTipoEst := "ES"
		Endif
			While !SE5->(Eof()) .and. SE5->E5_FILIAL== xFilial("SE5") .and. ;
				SE5->E5_TIPODOC+SE5->E5_PREFIXO+SE5->E5_NUMERO+SE5->E5_PARCELA+SE5->E5_TIPO==cTipoEst+cPrefixo+cNum+cParcela+cTipo

			IF SE5->E5_CLIFOR != cCliente .OR. SE5->E5_LOJA != cLoja
				SE5->(dbSkip())
				Loop
			EndIF

			IF SE5->E5_SEQ != cSequencia
				SE5->(dbSkip())
				Loop
			EndIF

			If SE5->E5_MOTBX == "FAT" .or. ( SE5->E5_MOTBX $ "STP" .and. FunName() <> "FINA040")
				dbSkip()
				Loop
			Endif

			//ÚBaixa NormalÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Sera estornado se for exatamente a mesma sequencia, carteira          ³
			//³contraria e nao for um adiantamento ou credito. (Titulo Normal)       ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If SE5->E5_SEQ == cSequencia .And. SE5->E5_RECPAG == "P" .and. !E5_TIPO $ MVRECANT+"/"+MV_CRNEG
				lEstornada := .T.
				Exit
			EndIf

			//ÚÄBaixa de AdiantamentoÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Sera estornado se for exatamente a mesma sequencia, carteira          ³
			//³contraria e for um adiantamento ou credito. (Titulo de Credito        ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If SE5->E5_SEQ == cSequencia .And. SE5->E5_RECPAG == "R" .and. E5_TIPO $ MVRECANT+"/"+MV_CRNEG
				lEstornada := .T.
				Exit
			EndIf
			SE5->( dbSkip() )
		End
		SE5->(dbGoTo(nRecAtu))
		If lEstornada
			lEstornada := .F.
			lBaixaAbat := .F.
			SE5->(dbSkip())
			Loop
		EndIf

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Aten‡„o, o array aBaixaSE5 ‚ o elemento fundamental para que ³
		//³a baixa funcione a contento. Se for necess rio alter -lo,deve³
		//³ser feito com a MAXIMA ATENCAO, pois as variaveis tratadas na³
		//³funcao est„o com posi‡”es fixas dentro do array              ³
		//³Mem¢ria de Calculo do array em quest„o.                      ³
		//³                                                             ³
		//³Informa‡„o        Posicao     Tamanho  					       ³
		//³                                                             ³
		//³Prefixo            01           03  					          ³
		//³Numero             02           12           					 ³
		//³Parcela            03           01						          ³
		//³Tipo               04           03					             ³
		//³Cliente/Fornec     05           06					             ³
		//³Loja               06           02					             ³
		//³Data da baixa      07           08					             ³
		//³Valor              08           15					             ³
		//³Sequencia          09           02					             ³
		//³Data Dispon.       10           08					             ³
		//³Banco              11           03					             ³
		//³Agencia            12           05					             ³
		//³Conta              13           10					             ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			
		If ! lTipBxCP .and. ( cTipoBAixa =="CP" .or. ( SE5->( E5_MOTBX == "CEC" .And. E5_TIPODOC == "BA" ) ) ) 
			If Alltrim(SE5->E5_MOEDA) == "01" //compensação E5_VALOR SEMPRE MOEDA FORTE E VLMOED2 EM MOEDA DO TITULO
				nTotAdto += SE5->E5_VALOR
			Else
				nTotAdto += SE5->E5_VLMOED2
			Endif
			
			If SE5->E5_MOTBX == "CEC"
				lBxCEC := .T.
			Endif
		Else
			nVlrBaixa += SE5->E5_VALOR
			cNumero := SE5->E5_NUMERO+Iif(Len(SE5->E5_NUMERO)==TamSX3("E5_NUMERO")[1],Space(TamSX3("E5_NUMERO")[1]),"")
			Aadd(aBaixa,SE5->E5_PREFIXO+" "+cNumero       +;
			" "+SE5->E5_PARCELA+" "+SE5->E5_TIPO+" "+SE5->E5_CLIFOR +;
			" "+SE5->E5_LOJA+" "+Dtoc(SE5->E5_DATA)       +;
			" "+Transf(SE5->E5_VALOR,"@E 9999,999,999.99")+"   "+SE5->E5_SEQ  )

			If cPaisLoc <> "BRA"
				cOrdRec := SE5->E5_ORDREC
				cSerRec := SE5->E5_SERREC
			Else
				cOrdRec := ""
				cSerRec := ""
			EndIf

			If ! (("FINA061" $ Subst(SE5->E5_HISTOR,1,7)) .And. SE5->E5_MOTBX $ "PCC_IRF" ) .And. !(Alltrim(SE5->E5_HISTOR) = "Bx. Bord. Ac.")	    
				Aadd( aBaixaSE5,{	SE5->E5_PREFIXO	, cNumero			, SE5->E5_PARCELA	, SE5->E5_TIPO		 ,;
										SE5->E5_CLIFOR	   , SE5->E5_LOJA		, SE5->E5_DATA		, SE5->E5_VALOR	 ,;
										SE5->E5_SEQ		   , SE5->E5_DTDISPO	, SE5->E5_BANCO	, SE5->E5_AGENCIA	 ,;
										SE5->E5_CONTA	   , SE5->E5_VLJUROS	, SE5->E5_VLMULTA	, SE5->E5_VLDESCO	 ,;
										SE5->E5_VLCORRE	, SE5->E5_VRETPIS	, SE5->E5_VRETCOF	, SE5->E5_VRETCSL	 ,;
										SE5->E5_PRETPIS	, SE5->E5_PRETCOF	, SE5->E5_PRETCSL	, SE5->E5_MOEDA	 ,;
										SE5->E5_TIPODOC	, IIf(SE5->(FieldPos("E5_FORMAPG")) > 0,AllTrim(SE5->E5_FORMAPG),NIL) ,;
										cOrdRec			   , cSerRec, SE5->E5_MOTBX,SE5->E5_VRETIRF,SE5->E5_PRETIRF, ;
										If(lRaRtImp, SE5->E5_PRISS,0), If(lRaRtImp, SE5->E5_PRINSS,0)} )
	      Endif
		EndIf
		SE5->(dbSkip())
	End
	EndIf
Next __k

RestArea( aArea )

Return( aBaixa )



Static Function Get070Mark()
Local cMarca

cMarca :=GetMark()
While cMarca == "xx"
  cMarca := Getmark()
End
Return cMarca 

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³F070SetMd ³ Autor ³ Fernando Machima      ³ Data ³ 09/01/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Mostra a tela de taxas de moeda    								  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³F070SetMd()                                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³FINA070                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function Fa070SetMd(nTxMoeda,lGetMd)
Local oDlg, nLenMoedas	:= Len(aTxMoedas)
Default lGetMd := .T.

If nLenMoedas > 1 .and. lGetMd
	DEFINE MSDIALOG oDlg From 200,0 TO 362,230 TITLE STR0132 PIXEL
	@ 005,005  To 062,110 OF oDlg PIXEL
	@ 012,010 SAY  aTxMoedas[2][1]  Of oDlg PIXEL
	@ 010,060 MSGET aTxMoedas[2][2] PICTURE aTxMoedas[1][3] Of oDlg PIXEL  HASBUTTON
	If nLenMoedas > 2
	   @ 024,010 SAY  aTxMoedas[3][1]  Of oDlg PIXEL
	   @ 022,060 MSGET aTxMoedas[3][2] PICTURE aTxMoedas[2][3] Of oDlg PIXEL  HASBUTTON
	   If nLenMoedas > 3
	      @ 036,010 SAY  aTxMoedas[4][1]  Of oDlg PIXEL
	      @ 034,060 MSGET aTxMoedas[4][2] PICTURE aTxMoedas[3][3] Of oDlg PIXEL  HASBUTTON
	      If nLenMoedas > 4
	         @ 048,010 SAY  aTxMoedas[5][1]  Of oDlg PIXEL
	         @ 046,060 MSGET aTxMoedas[5][2] PICTURE aTxMoedas[4][3] Of oDlg PIXEL  HASBUTTON
	      Endif
	   Endif
	Endif
	DEFINE SButton FROM 064,80 TYPE 1 Action (oDlg:End() ) ENABLE OF oDlg  PIXEL

   ACTIVATE MSDialog oDlg CENTERED
    
EndIf

If nLenMoedas > 1
	If SE1->E1_MOEDA > 1 .Or. cPaisLoc<>"BRA"  // somente se o titulo original for em moeda diferente de 1
		nTxMoeda := aTxMoedas[SE1->E1_MOEDA][2]
	EndIf
    nDifCambio := ((SE1->E1_VALOR * aTxMoedas[SE1->E1_MOEDA][2]) - SE1->E1_VLCRUZ)
        
Endif

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³CadCheqCR ºAutor  ³Claudio D. de Souza º Data ³  18/04/02   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Permite a digitacao dos cheques recebidos contas a receber º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Fina040/Fina070                                            º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function CadCheqCR(cBanco,cAgencia,cConta,nValRec,dData,nChamada,aColsSEF,lTela)
Local oDlg
Local aOldHead := If(Type("aHeader")=="A",aClone(aHeader),{})
Local aOldCols := {}
Local oGet
Local nOpca
Local aArea 	:= GetArea()
Local aAreaSef	:= SEF->(GetArea())
Local aOldRotina
Local aBut070	:=	If(GetMv("MV_CMC7FIN") == "S",;
							{{'COLINC',{||	F070CmC7(oGet,.T.,;
												aCols[oGet:nAt][1],aCols[oGet:nAt][2],aCols[oGet:nAt][3],;
											  	aCols[oGet:nAt][4],aCols[oGet:nAt][5],aCols[oGet:nAt][6],;
											  	aCols[oGet:nAt][7],aCols[oGet:nAt][8],aCols[oGet:nAt][9],;
											  	aCols[oGet:nAt][10],aCols[oGet:nAt][11],aCols[oGet:nAt][12])},STR0167,STR0180}},; //"Ler cheques" //"CMC7"
							Nil)			  	
Local oFnt
Local nX
Local lColsSEF := .F.
Local aCpoaCols:={}
Local lUSADOBX := SEF->( FieldPos("EF_USADOBX") ) > 0
Local lPanelFin := If (FindFunction("IsPanelFin"),IsPanelFin(),.F.)
Local lPaisBRA    := (cPaisLoc == "BRA")

If Type("aRotina")=="A"
	aOldRotina := aClone(aRotina)
Endif

DEFINE FONT oFnt	NAME "Arial" Size 10,15

DEFAULT nChamada := 2
DEFAULT cBanco   := Space(TamSx3("EF_BANCO")[1])
DEFAULT cAgencia := Space(TamSx3("EF_AGENCIA")[1])
DEFAULT cConta	  := Space(TamSx3("EF_CONTA")[1])
DEFAULT nValRec  := 0
DEFAULT dData	  := dDataBase
DEFAULT lTela	  := .T.


PRIVATE oQtdCheq
PRIVATE nQtdCheq 		:= 0
PRIVATE oVlrNomCheq
PRIVATE nVlrNomCheq  := 0
PRIVATE oVlRefBxChq
PRIVATE nVlRefBxChq  := 0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se o motivo de baixa permite informar cheque ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Type("cMotBx") == "C" .And. !ChqMotBx(cMotBx)
	Help( " ", 1, "CHQNOPER" )
	Return .F.
EndIf

If aColsSEF <> Nil
	lColsSEF := .T.
	aOldCols := If(Type("aCols")=="A",aClone(aCols),{})
	If ( Type("aColsSEF") == "A" .And. Len(aColsSEF) > 0 )
		aCols:= aClone(aColsSEF)
	Else
		aCols:= {}
	EndIf
EndIf

SetKey(VK_F4,{||})

#DEFINE USADO CHR(0)+CHR(0)+CHR(1)

aHeader := {}
aRotina := {{"","", 0 , 3}}
					
If nChamada == 3
	// Caso seja igual a 3 (Alteracao), monta o cabecalho sem a validacao de "aCols[n][13]!='Sim'" para edicao dos campos.
	Aadd( aHeader, { STR0145, "EF_BANCO"	, "@!", TamSx3("EF_BANCO")[1]	, 0, "aCols[n][13]!='Sim' .And. NaoVazio(M->EF_BANCO)"	 , USADO, "C",, "V" ,,,} )  
	Aadd( aHeader, { STR0146, "EF_AGENCIA"	, "@!", TamSx3("EF_AGENCIA")[1], 0, "aCols[n][13]!='Sim' .And. NaoVazio(M->EF_AGENCIA)", USADO, "C",, "V" ,,,} )  
	Aadd( aHeader, { STR0147, "EF_CONTA" 	, "@!", TamSx3("EF_CONTA")[1], 0, "aCols[n][13]!='Sim' .And. NaoVazio(M->EF_CONTA)"	 , USADO, "C",, "V" ,,,} )  
	Aadd( aHeader, { STR0148, "EF_NUM"		, "@!", TamSx3("EF_NUM")[1], 0, "aCols[n][13]!='Sim' .And. NaoVazio(M->EF_NUM)"		 , USADO, "C",, "V" ,,,} )
	Aadd( aHeader, { STR0163, "EF_VALOR"	, "@E 999,999,999.99"	, 15, 2, "Fa070TotCheq(1) .And. Positivo(M->EF_VALOR) .And. NaoVazio(M->EF_VALOR)", USADO, "N",, "V" ,,,} )
	If lPaisBRA
		Aadd( aHeader, { STR0149, "EF_VALORBX"	, "@E 999,999,999.99"	, 15, 2, "Fa070TotCheq(2) .And. Positivo(M->EF_VALORBX) .And. NaoVazio(M->EF_VALORBX)", USADO, "N",, "V" ,,,} )
 	Endif
	Aadd( aHeader, { STR0150, "EF_EMITENT"	, "@!", TamSx3("EF_EMITENT")[1], 0, "aCols[n][13]!='Sim' .And. NaoVazio(M->EF_EMITENT)"	, USADO, "C",, "V" ,,,} )
	Aadd( aHeader, { STR0151, "EF_DATA"		, "@!", 8, 0, "NaoVazio(M->EF_DATA) .And. M->EF_DATA <= aCols[n][9]"	, USADO, "D",, "V" ,,,} )
	
	If FunName() == "FINA220"
		Aadd( aHeader, { STR0152, "EF_VENCTO"	, "@!", 8, 0, "NaoVazio(M->EF_VENCTO) .And. M->EF_VENCTO >= aCols[n][8] .and. Fn220ChP()", USADO, "D",, "V" ,,,} )
	else
	Aadd( aHeader, { STR0152, "EF_VENCTO"	, "@!", 8, 0, "NaoVazio(M->EF_VENCTO) .And. M->EF_VENCTO >= aCols[n][8]", USADO, "D",, "V" ,,,} )
	endif
	
	Aadd( aHeader, { STR0153, "EF_CPFCNPJ"	, "@!", TamSx3("EF_CPFCNPJ")[1], 0, "aCols[n][13]!='Sim' .And. (Empty(M->EF_CPFCNPJ) .OR. Cgc(M->EF_CPFCNPJ))"	, USADO, "C",, "V" ,,,} )
	Aadd( aHeader, { STR0154, "EF_TEL"		, "@!", TamSx3("EF_TEL")[1] , 0, "aCols[n][13]!='Sim'"	, USADO, "C",, "V" ,,,} )
	Aadd( aHeader, { STR0155, "EF_HIST"		, "@!", TamSx3("EF_HIST")[1], 0,"aCols[n][13]!='Sim'", USADO, "C",, "V" ,,,} )
	Aadd( aHeader, { STR0157, "EF_CHCAD"	, "", 3, 0, ".F.", USADO, "C",, "V" ,,,} )
Else
	// Monta o cabecalho com validacao.
	Aadd( aHeader, { STR0145, "EF_BANCO"	, "@!", TamSx3("EF_BANCO")[1]	, 0, "aCols[n][13]!='Sim' .And. NaoVazio(M->EF_BANCO)"	 , USADO, "C",, "V" ,,, /*"Empty(aCols[n][1])"*/} )  
	Aadd( aHeader, { STR0146, "EF_AGENCIA"	, "@!", TamSx3("EF_AGENCIA")[1], 0, "aCols[n][13]!='Sim' .And. NaoVazio(M->EF_AGENCIA)", USADO, "C",, "V" ,,, /*"Empty(aCols[n][2])"*/} )  
	Aadd( aHeader, { STR0147, "EF_CONTA" 	, "@!", TamSx3("EF_CONTA")[1], 0, "aCols[n][13]!='Sim' .And. NaoVazio(M->EF_CONTA)"	 , USADO, "C",, "V" ,,, /*"Empty(aCols[n][3])"*/} )    
	Aadd( aHeader, { STR0148, "EF_NUM"		, "@!", TamSx3("EF_NUM")[1], 0, "aCols[n][13]!='Sim' .And. NaoVazio(M->EF_NUM)"		 , USADO, "C",, "V" ,,, } )
	Aadd( aHeader, { STR0163, "EF_VALOR"	, "@E 999,999,999.99"	, 15, 2, "aCols[n][13]!='Sim' .And. Fa070TotCheq(1) .And. Positivo(M->EF_VALOR) .And. NaoVazio(M->EF_VALOR)", USADO, "N",, "V" ,,,} )
	If lPaisBRA
		Aadd( aHeader, { STR0149, "EF_VALORBX"	, "@E 999,999,999.99"	, 15, 2, "Fa070TotCheq(2) .And. Positivo(M->EF_VALORBX) .And. NaoVazio(M->EF_VALORBX) .And. M->EF_VALORBX <= aCols[n][5]", USADO, "N",, "V" ,,,} )
	Endif
	Aadd( aHeader, { STR0150, "EF_EMITENT"	, "@!", TamSx3("EF_EMITENT")[1], 0, "aCols[n][13]!='Sim' .And. NaoVazio(M->EF_EMITENT)"	, USADO, "C",, "V" ,,,} )
	Aadd( aHeader, { STR0151, "EF_DATA"		, "@!", 8, 0, "aCols[n][13]!='Sim' .And. NaoVazio(M->EF_DATA) .And. M->EF_DATA <= aCols[n][9]"	, USADO, "D",, "V" ,,,} )
    
	If FunName() == "FINA220"
		Aadd( aHeader, { STR0152, "EF_VENCTO"	, "@!", 8, 0, "aCols[n][13]!='Sim' .And. NaoVazio(M->EF_VENCTO) .And. M->EF_VENCTO >= aCols[n][8] .and. Fn220ChP()", USADO, "D",, "V" ,,,} )
	else
	Aadd( aHeader, { STR0152, "EF_VENCTO"	, "@!", 8, 0, "aCols[n][13]!='Sim' .And. NaoVazio(M->EF_VENCTO) .And. M->EF_VENCTO >= aCols[n][8]", USADO, "D",, "V" ,,,} )
	endif

	Aadd( aHeader, { STR0153, "EF_CPFCNPJ"	, "@!", TamSx3("EF_CPFCNPJ")[1], 0, "aCols[n][13]!='Sim' .And. (Empty(M->EF_CPFCNPJ) .OR. Cgc(M->EF_CPFCNPJ))"	, USADO, "C",, "V" ,,,} )
	Aadd( aHeader, { STR0154, "EF_TEL"		, "@!", TamSx3("EF_TEL")[1] , 0, "aCols[n][13]!='Sim'"	, USADO, "C",, "V" ,,,} )
	Aadd( aHeader, { STR0155, "EF_HIST"		, "@!", TamSx3("EF_HIST")[1], 0, "aCols[n][13]!='Sim'"	, USADO, "C",, "V" ,,,} )
	Aadd( aHeader, { STR0157, "EF_CHCAD"	, "", 3, 0, ".F.", USADO, "C",, "V" } )
Endif            
	Aadd( aHeader, { "Sequencia",            "EF_SEQUENC"	, "", TamSx3("EF_SEQUENC")[1], 0, ".F.", USADO, "C",, "V" ,,,} )

If lUSADOBX
	Aadd( aHeader, { "Usado em outra baixa", "EF_USADOBX"	, "", 3, 0, ".F.", USADO, "C",, "V" ,,,} )
Endif	                                                                                           

If (Type("aCols") == "A" .And. Empty(aCols)) .Or. Type("aCols") != "A"
	aCols := {}
	Aadd(aCols,Array(Len(aHeader)+1))
	Aeval(aHeader, { |e,nX|	If(e[8] == "D", aCols[1][nX] := dData,;
									If(e[8] == "N", aCols[1][nX] := 0    ,;
									If(e[8] == "C" .And. !Empty(e[1])    ,;
								   	aCols[1][nX] := Space(e[4])       ,;
										aCols[1][nX] := "")))})
	If nChamada == 1 .Or. nChamada == 3 // 1 = Baixa de titulos ou 3 = Alteracao pelo FINA040
		dbSelectArea("SEF")
		DbSetOrder(3)
		MsSeek(xFilial("SEF")+SE1->(E1_PREFIXO+E1_NUM+E1_PARCELA+E1_TIPO))
	Endif	
	If (nChamada == 1 .Or. nChamada == 3) .And. !Eof()
		aCols := {}
			While SEF->(!Eof()) .And. xFilial("SEF")==SEF->EF_FILIAL .And. SEF->EF_CART == "R" .And.;
				SEF->(EF_PREFIXO+EF_TITULO+EF_PARCELA+EF_TIPO) == SE1->(E1_PREFIXO+E1_NUM+E1_PARCELA+E1_TIPO)
				
				If lPaisBRA
					Aadd(aCols, {	SEF->EF_BANCO,;
									 	SEF->EF_AGENCIA,;
									 	SEF->EF_CONTA,;
									 	SEF->EF_NUM,;
									 	SEF->EF_VALOR,;
									 	SEF->EF_VALORBX,;
									 	SEF->EF_EMITENT,;
									 	SEF->EF_DATA,;
									 	SEF->EF_VENCTO,;
									 	SEF->EF_CPFCNPJ,;
									 	SEF->EF_TEL,;
									 	SEF->EF_HIST,;  
									 	'Sim',;  
									 	SEF->EF_SEQUENC})

				Else
					Aadd(aCols, {	SEF->EF_BANCO,;
							 	SEF->EF_AGENCIA,;
							 	SEF->EF_CONTA,;
							 	SEF->EF_NUM,;
							 	SEF->EF_VALOR,;
							 	SEF->EF_EMITENT,;
							 	SEF->EF_DATA,;
							 	SEF->EF_VENCTO,;
							 	SEF->EF_CPFCNPJ,;
							 	SEF->EF_TEL,;
							 	SEF->EF_HIST,;
							 	'Sim',;  
							 	SEF->EF_SEQUENC})
										
				Endif

							 	
			// Totalizadores de cheques cadastrados
			nVlrNomCheq += SEF->EF_VALOR
			If lPaisBRA
				nVlRefBxChq += SEF->EF_VALORBX
			Endif
			nQtdCheq 	++
										 	
			// Grava o identificador de que o cheque ja foi utilizado na baixa, devido as
			// baixas parciais, pois nas baixas futuras esses cheques nao podem mais serem utilizados
			// na geracao do movimento bancario
			If lUSADOBX
				Aadd(aCols[Len(aCols)], If(SEF->EF_USADOBX == "S", STR0246, STR0245) ) //"sim","não"
			Endif
			Aadd(aCols[Len(aCols)], IF( lUSADOBX, ( SEF->EF_USADOBX == 'S' ), .f.) )
			SEF->(DbSkip())
		End	
	Else
		SA1->(dbSetOrder(1))
		SA1->(MsSeek(xFilial("SA1")+SE1->E1_CLIENTE+SE1->E1_LOJA))
		aCols[1][1]  := cBanco
		aCols[1][2]  := cAgencia
		aCols[1][3]  := cConta
		aCols[1][5]  := nValRec
      If cPaisLoc =="BRA"
			aCols[1][6]  := nValRec
			aCols[1][7]  := SA1->A1_NOME
			aCols[1][10] := SA1->A1_CGC
			aCols[1][11] := Pad(AllTrim(SA1->A1_DDD)+" "+AllTrim(SA1->A1_TEL),TamSx3("EF_TEL")[1])
		Else
			aCols[1][6]  := SA1->A1_NOME
			aCols[1][9] := SA1->A1_CGC
			aCols[1][11] := Pad(AllTrim(SA1->A1_DDD)+" "+AllTrim(SA1->A1_TEL),TamSx3("EF_TEL")[1])
		EndIf
		aCols[1][Len(aCols[1])] := .F. // Indica que a linha nao esta deletada
		IF ExistBlock("F070DCHQ")
			If lPaisBRA
				aCpoaCols:={	"EF_BANCO",;
								 	"EF_AGENCIA",;
								 	"EF_CONTA",;
								 	"EF_NUM",;
								 	"EF_VALOR",;
								 	"EF_VALORBX",;
								 	"EF_EMITENT",;
								 	"EF_DATA",;
								 	"EF_VENCTO",;
								 	"EF_CPFCNPJ",;
								 	"EF_TEL",;
								 	"EF_HIST"}
			Else
				aCpoaCols:={	"EF_BANCO",;
								 	"EF_AGENCIA",;
								 	"EF_CONTA",;
								 	"EF_NUM",;
								 	"EF_VALOR",;
								 	"EF_EMITENT",;
								 	"EF_DATA",;
								 	"EF_VENCTO",;
								 	"EF_CPFCNPJ",;
								 	"EF_TEL",;
								 	"EF_HIST"}
			Endif						 	
			aCols := ExecBlock("F070DCHQ",.f.,.f.,{aCpoaCols,aCols})
	    EndIf
	Endif
	SEF->(RestArea(aAreaSef))
	RestArea(aArea)
Endif	
If lTela

	aSize := MSADVSIZE()
	DEFINE MSDIALOG oDlg TITLE STR0156 From aSize[7],0 To aSize[6],aSize[5] OF oMainWnd PIXEL   //"Cheques recebidos"
	oDlg:lMaximized := .T.

	oPanel := TPanel():New(0,0,'',oDlg,, .T., .T.,, ,15,15,.T.,.T. )
	oPanel:Align := CONTROL_ALIGN_BOTTOM

	@ 002,  10 SAY STR0183 	OF oPanel PIXEL // "Qtde. Cheques "
	@ 002,  50 SAY oQtdCheq VAR nQtdCheq PICTURE "@E 999,999" PIXEL OF oPanel FONT oFnt COLOR CLR_HBLUE
	@ 002,  90 SAY STR0184 	OF oPanel 	PIXEL // "Valor nominal dos cheques "
	@ 002, 160 SAY oVlrNomCheq VAR nVlrNomCheq PICTURE  "@E 999,999,999,999.99" PIXEL OF oPanel FONT oFnt COLOR CLR_HBLUE
	@ 002, 230 SAY STR0185	OF oPanel PIXEL // "Valor ref. a baixa dos cheques "
	@ 002, 310 SAY oVlRefBxChq VAR nVlRefBxChq PICTURE  "@E 999,999,999,999.99" PIXEL OF oPanel FONT oFnt COLOR CLR_HBLUE	

	oGet := MsNewGetDados():New(16, 0, 080, 396,GD_INSERT + GD_UPDATE + GD_DELETE ,"CadCheqLOk(.T.)","CA070TudOk",,Iif(lPaisBRA,{"EF_BANCO","EF_AGENCIA","EF_CONTA","EF_NUM","EF_VALOR","EF_VALORBX","EF_EMITENT","EF_DATA","EF_TEL","EF_CPFCNPJ","EF_VENCTO","EF_HIST"},{"EF_BANCO","EF_AGENCIA","EF_CONTA","EF_NUM","EF_VALOR","EF_EMITENT","EF_DATA","EF_TEL","EF_CPFCNPJ","EF_VENCTO","EF_HIST"}),, 999,,,"Fa070ChqDel",,@aHeader,@aCols,, )

	oGet:oBrowse:Align := CONTROL_ALIGN_ALLCLIENT
	
	FA070AtuTT() //Atualiza totais

	If lPanelFin
		ACTIVATE MSDIALOG oDlg ON INIT FaMyBar(oDlg,;
															 {||nOpca:=1,If(oGet:TudoOk(),oDlg:End(),nOpca := 0)},;
															 {||nOpca:=0,(aCols := {},oDlg:End())},aBut070)
	Else	
		ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,;
															 {||nOpca:=1,If(oGet:TudoOk(),oDlg:End(),nOpca := 0)},;
															 {||nOpca:=0,(aCols := {},oDlg:End())},,aBut070)
	Endif
aCols      := aClone(oGet:aCols)	
EndIf

aHeader	:= aClone(aOldHead)

If lColsSEF
	aColsSEF:= aClone(aCols)
	aCols := aClone(aOldCols)
EndIf

If aOldRotina != Nil
	aRotina := aClone(aOldRotina)
Endif	
RestArea(aArea)

SetKey(VK_F4,{|| If( !SE1->E1_TIPO $ MV_CRNEG,CadCheqCR(cBanco,cAgencia,cConta,nValRec,dData,1),.F.)})

Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³CadCheqLOkºAutor  ³Claudio D. de Souza º Data ³  16/04/02   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Valida a linha da GetDados dos Cheques recebidos           º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Fina070                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function CadCheqLOk(lLin)
Local lRet := .T.
Local nX
Local aAreaSef
Local lResp := .T.
Local nPos := 0

nSomaCheq := 0     

Default lLin :=.T.
If !aTail(aCols[n])
	// Valida todas as colunas da GetDados
	For nX := 1 To Len(aCols[n])-1
		If Empty(aCols[n][nX]) .And. !Empty(aHeader[nX][1]) .And. !AllTrim(aHeader[nX][2])$"EF_HIST/EF_TEL/EF_CHCAD/EF_CPFCNPJ/EF_USADOBX/EF_SEQUENC"
			lRet := .F.
			HELP(" ",1,"NVAZIO",,aHeader[nX][1],4,0)
			Exit
		Endif
		If cPaisLoc=="BRA" .And. aCols[n][5] < aCols[n][6] // Valor nominal menor que o valor ref. a baixa
			IW_MsgBox(STR0170,STR0144,"STOP")  //"Valor ref. a baixa não pode ser maior que o valor nominal"
			lRet := .F.
			Exit
		Endif
	Next
Endif	
// Verifica se o cheque nao esta cadastrado tanto em aCols quanto no SEF
If lRet
	nInd := aScan(aCols, {|e| e[1]+e[2]+e[3]+e[4] == aCols[n][1]+aCols[n][2]+aCols[n][3]+aCols[n][4] })
	If nInd > 0 .And. nInd != n .And. !aTail(aCols[n])
		Help ( " ", 1, "CHEQJAEXIST" )
		lRet := .F.
	Endif
	DbSelectArea("SEF")
	aAreaSef := SEF->(GetArea())
	DbSetOrder(6)
	If lRet .And. MsSeek(xFilial("SEF")+aCols[n][1]+aCols[n][2]+aCols[n][3]+aCols[n][4]+SE1->(E1_PREFIXO+E1_NUM+E1_PARCELA+E1_TIPO))
		Help ( " ", 1, "CHEQJAEXIST" )
		lRet := .F.
	Endif
	If cPaisLoc=="BRA" .And. lRet .And.  aCols[n][5]< F070TOTCH(aCols[n][1],aCols[n][2],aCols[n][3],aCols[n][4],iif(cPaisLoc == "BRA",aCols[n][6],0), If(!Empty(SE1->E1_CLIENTE), SE1->E1_CLIENTE, M->E1_CLIENTE)) .and. "MV_CMC7FIN" == "S"       
		Help ( " ", 1, "CHEQJAEXIST" )
		lRet := .F.
	EndIf
	SEF->(RestArea(aAreaSef))
Endif
If lRet
	If Type("aCols") == "A" // Soma os cheques
		nSomaCheq := 0
		nVlrNomCheq := 0
		nQtdCheq		:= 0
		nVlRefBxChq	:= 0
		nPos := Ascan(aHeader, {|x| x[2] == "EF_USADOBX" })
		For nX := 1 To Len(aCols)
			if (Len(aCols[nX]) > 15) .and. (aCols[nX][nPos] = STR0246)
			  aCols[nX][Len(aHeader)+1] := .T. // Se o cheque foi usado mantem deletado.
			endif
			If !aTail(aCols[nX]) .And. (Len(aCols[nX]) < 16 .Or. aCols[nX][nPos] != STR0246)
				nVlrNomCheq += aCols[nX][5]
                If cPaisLoc == "BRA"
					nSomaCheq 	+= aCols[nX][6]
					nVlRefBxChq += aCols[nX][6]
				EndIf
				nQtdCheq 	++
			Endif	
		Next
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Ponto de entrada que permite ao usuario somente validar o total dos cheques no botao OK ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ	
	    If ExistBlock("F070VCHL") .and. lLin
	       lResp := ExecBlock("F070VCHL",.F.,.F.)
	    Endif   
	    If lResp
		   // Se o total dos cheques nao conferir com o total baixado, nao permite a inclusao
		   If !GdDeleted() .And. nSomaCheq > 0
		      If Type("nValRec")=="N" .And. Str(nSomaCheq,17,2) > Str(nValRec,17,2) 
			     Help ( " ", 1, "CHEQBXNBAT" )
				 lRet := .F.
			  Endif	
  		   Endif	
  		Endif
  				
  	Endif	
Endif
If lRet 
	If Type("oVlrNomCheq") == "O"
		oVlrNomCheq:Refresh()
	Endif	
	If Type("oQtdCheq") == "O"
		oQtdCheq:Refresh()
	Endif	
	If Type("oVlRefBxChq") == "O"
		oVlRefBxChq:Refresh()
	Endif
Endif

If  lRet .And. ExistBlock("F070CLOK")
	lRet := ExecBlock("F070CLOK",.F.,.F.)
Endif 

Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³GravaChqCRºAutor  ³Claudio D. de Souza º Data ³  18/04/02   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Grava cheque no SEF a partir de aCols na carteira a receberº±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Fina040/Fina070                                            º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function GravaChqCR(cSeqBaixa,cOrigem,aColsSEF,aSeqSe5,lBaixou)

Local nX			:= 0 
Local aArea 	:= GetArea()
Local aAreaSef := SEF->(GetArea())
Local aOldCols := {}
Local lPaisBRA := (cPaisLoc == "BRA")

DEFAULT cSeqBaixa := ""
DEFAULT cOrigem 	:= ""
DEFAULT aSeqSe5   := {}
DEFAULT lBaixou 	:= .F.

If aColsSEF <> Nil
	aOldCols := If(Type("aCols")=="A",aClone(aCols),{})
	If ( Type("aColsSEF") == "A" .And. Len(aColsSEF) > 0 )
		aCols:= aClone(aColsSEF)
	Else
		aCols:= {}
	EndIf
EndIf

//Se os cheques foram incluídos no FINA040 e o usuário não clicar no botão de cheque na hora da baixa,
//então será carregado o aCols com os cheques para fazer a gravação da sequencia da baixa, conforme os
//movimentos gerados no SE5.
If lBaixou .And. Empty(aColsSEF) .And. Type("cMotBx") == "C" .And. ChqMotBx(cMotBx)
	CadCheqCR(Nil,Nil,Nil,Nil,Nil,1,Nil,.F.)
EndIf

If Type("aCols") == "A"
	DbSelectArea("SEF")
	DbSetOrder(6)
	For nX := 1 To Len(aCols)
		// Se o cheque nao estiver deletado, os dados forem validos e nao existir no SEF		
		If(VALTYPE(aCols[nX][Len(aCols[1])]) == 'L') .and.	!aCols[nX][Len(aCols[1])] .And.;		
			!Empty(aCols[nX][1])			.And.;
			!Empty(aCols[nX][2])			.And.;
			!Empty(aCols[nX][3])			.And.;
			!Empty(aCols[nX][4])			.And.;
			!Empty(aCols[nX][5])			.And.;
			!Empty(aCols[nX][6])			.And.;
			!Empty(aCols[nX][7])			.And.;
			!Empty(aCols[nX][8])			.And.;
			!Empty(aCols[nX][9])
	  		If !MsSeek(xFilial("SEF")+"R"+aCols[nX][1]+aCols[nX][2]+aCols[nX][3]+aCols[nX][4]+SE1->(E1_PREFIXO+E1_NUM+E1_PARCELA+E1_TIPO))
		  		RecLock("SEF",.T.)
		  		SEF->EF_FILIAL		:= xFilial("SEF")
		  		SEF->EF_BANCO		:= aCols[nX][1]
		  		SEF->EF_AGENCIA	:= aCols[nX][2]
		  		SEF->EF_CONTA		:= aCols[nX][3]
		  		SEF->EF_NUM			:= aCols[nX][4]
		  		SEF->EF_VALOR		:= aCols[nX][5]
				If lPaisBRA
			  		SEF->EF_VALORBX	:= aCols[nX][6]
			  		SEF->EF_EMITENT	:= aCols[nX][7]
			  		SEF->EF_DATA		:= aCols[nX][8]
			  		SEF->EF_VENCTO		:= aCols[nX][9]
			  		SEF->EF_CPFCNPJ	:= aCols[nX][10]
			  		SEF->EF_TEL			:= aCols[nX][11]
			  		SEF->EF_HIST		:= aCols[nX][12]
				Else
			  		SEF->EF_EMITENT	:= aCols[nX][6]
			  		SEF->EF_DATA		:= aCols[nX][7]
			  		SEF->EF_VENCTO		:= aCols[nX][8]
			  		SEF->EF_CPFCNPJ	:= aCols[nX][9]
			  		SEF->EF_TEL			:= aCols[nX][10]
			  		SEF->EF_HIST		:= aCols[nX][11]

			 	Endif
				SEF->EF_CLIENTE	:= SE1->E1_CLIENTE
				SEF->EF_LOJACLI	:= SE1->E1_LOJA
		  		SEF->EF_PREFIXO	:= SE1->E1_PREFIXO
				SEF->EF_TITULO		:= SE1->E1_NUM
				SEF->EF_PARCELA	:= SE1->E1_PARCELA
				SEF->EF_TIPO		:= SE1->E1_TIPO
				If GetMv("MV_SLDBXCR")=="C" .And. Len(aSeqSe5) > 0
					SEF->EF_SEQUENC := aSeqSe5[nX]
				Else
					SEF->EF_SEQUENC := cSeqBaixa
				Endif	
				SEF->EF_CART		:= "R"
				SEF->EF_ORIGEM		:= cOrigem
				// Grava o identificador de que o cheque ja foi utilizado na baixa, devido as
				// baixas parciais, pois nas baixas futuras esses cheques nao podem mais serem utilizados
				// na geracao do movimento bancario
				If SEF->(FieldPos("EF_USADOBX")) > 0 .And. cOrigem != "FINA040"
					SEF->EF_USADOBX := "S"
				Endif	
				
				If GetNewPar("MV_RMCLASS",.F.) .and. FunName() == "FINA220"
					SEF->EF_LTCXA  := cLoteCx
					SEF->EF_NOPER  := nTrans
					SEF->EF_TITULO := SEF->EF_NUM
				EndIF
				SEF->(MsUnlock())
			Else
				RecLock("SEF",.F.)
				//Grava os valores alteraveis
				SEF->EF_VALOR	:= aCols[nX][5]
				If lPaisBRA
			  		SEF->EF_VALORBX	:= aCols[nX][6]
			  		SEF->EF_DATA	:= aCols[nX][8]
			  		SEF->EF_VENCTO	:= aCols[nX][9]
			  		SEF->EF_HIST	:= aCols[nX][12]
				Else
			  		SEF->EF_DATA	:= aCols[nX][7]
					SEF->EF_VENCTO	:= aCols[nX][8]
			  		SEF->EF_HIST	:= aCols[nX][11]
		
			 	Endif
		  		If GetMv("MV_SLDBXCR")=="C" .And. Len(aSeqSe5) > 0 .And. aSeqSe5[nX] != Replicate("0",TamSX3("E5_SEQ")[1])
					SEF->EF_SEQUENC := aSeqSe5[nX]
				Endif	
				// Grava o identificador de que o cheque ja foi utilizado na baixa, devido as
				// baixas parciais, pois nas baixas futuras esses cheques nao podem mais serem utilizados
				// na geracao do movimento bancario
				If SEF->(FieldPos("EF_USADOBX")) > 0 .And. cOrigem != "FINA040" .And. IIF(lPaisBRA,aCols[nX][15] == STR0245,aCols[nX][14] == STR0245)
					SEF->EF_USADOBX := "S"
				Endif	
				SEF->(MsUnlock())                                     	
			Endif
		EndIf
	Next
	SEF->(RestArea(aAreaSef))
	RestArea(aArea)
Endif

If aColsSEF <> Nil
	aCols    := aClone(aOldCols)
	aColsSEF := {}
Else
	aCols := {}
EndIf


Return Nil

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³ F070CMC7 ³ Autor ³ Mauricio Pequim Jr    ³ Data ³ 28/08/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Programa destinado a efetuar a leitura de cheques a partir ³±±
±±³          ³ do equipamento CMC7 e alimentar a rotina de baixa          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function F070CMC7(oGet,lGetDados,EF_BANCO,EF_AGENCIA,EF_CONTA,EF_NUM,EF_VALOR,;
						EF_VALORBX,EF_EMITENT,EF_DATA,EF_VENCTO,EF_CPFCNPJ,EF_TEL,EF_HIST)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Declaracao de Variaveis                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local nDiasVcto := 0
Local lContinua	:= .T.
Local nNumCH		:= 1			// Numeral do cheque (qual cheque esta sendo lido. Se primeiro, segundo etc)
Local aCMC7			:= {}			//Array que guardara os dados do cheque vindos da leitora 
Local aCmc7Tc 		:= {}			// Armazena o retorno da funcao F460Cmc7Tc
Local nX			:= 2            // Variavel para comparar com o tamanho do aCols
Local lContLeit		:= .T.			// Verifica se a leitura (teclado) acabou ou nao 
Local nPosBanco		:= aScan( aHeader,{|z| Alltrim(Upper(z[2]))=="EF_BANCO" })		// Captura no aHeader o campo do banco
Local nPosAgen		:= aScan( aHeader,{|z| Alltrim(Upper(z[2]))=="EF_AGENCIA" })	// Captura no aHeader o campo da agencia
Local nPosConta		:= aScan( aHeader,{|z| Alltrim(Upper(z[2]))=="EF_CONTA" }) 	// Captura no aHeader o campo da conta
Local nPosCheque	:= aScan( aHeader,{|z| Alltrim(Upper(z[2]))=="EF_NUM" })		// Captura no aHeader o campo do nro. do cheque	
Local nPosValor 	:= aScan( aHeader,{|z| Alltrim(Upper(z[2]))=="EF_VALOR" })	 	// Captura no aHeader o campo do valor
Local nPosValorBx 	:= aScan( aHeader,{|z| Alltrim(Upper(z[2]))=="EF_VALORBX" })	// Captura no aHeader o campo do valor da baixa
Local nPosEmitente	:= aScan( aHeader,{|z| Alltrim(Upper(z[2]))=="EF_EMITENT" })	// Captura no aHeader o campo do emitente	
Local nPosData		:= aScan( aHeader,{|z| Alltrim(Upper(z[2]))=="EF_DATA"	 })		// Captura no aHeader o campo da data
Local nPosVencto	:= aScan( aHeader,{|z| Alltrim(Upper(z[2]))=="EF_VENCTO" })	// Captura no aHeader o campo do vencimento		
Local nPosCpfCnpj 	:= aScan( aHeader,{|z| Alltrim(Upper(z[2]))=="EF_CPFCNPJ" })	// Captura no aHeader o campo do cpf/cnpj
Local nPosTel		:= aScan( aHeader,{|z| Alltrim(Upper(z[2]))=="EF_TEL" })		// Captura no aHeader o campo do telefone	
Local nPosHist	 	:= aScan( aHeader,{|z| Alltrim(Upper(z[2]))=="EF_HIST" })		// Captura no aHeader o campo do historico	
Local nPosChCad 	:= aScan( aHeader,{|z| Alltrim(Upper(z[2]))=="EF_CHCAD" })		// Captura no aHeader o campo do cheque
Local nPosUsado	:= aScan( aHeader,{|z| Alltrim(Upper(z[2]))=="EF_USADOBX" })		// Captura no aHeader o campo do Usado na baixa
Local nPosSeq		:= aScan( aHeader,{|z| Alltrim(Upper(z[2]))=="EF_SEQUENC" }) 
Local lPrimeiro 	:= .T.		// Controla se é o primeiro cheque
Local aCpaCols		
Local lPaisBRA    := (cPaisLoc == "BRA")

Private nHdlCMC7  := ReadCMC7()[1]		// Abertura da porta
Private lUsaCmC7	 := .T.		// Necessario para funcionamento das funcoes de leitura
DEFAULT lGetDados := .T.

M->EF_DATA 		:= If(EMPTY(EF_DATA)  ,ddatabase,EF_DATA)
M->EF_VENCTO	:= If(EMPTY(EF_VENCTO),ddatabase,EF_VENCTO)

If Len(aCols) > 1 .or. (Len(aCols)>0 .And. AllTrim(aCols[1,nPosCheque]) <> "")
	lPrimeiro := .F.      
EndIf

If nHdlCMC7 < 0 // Publica, alterada pela OpenCmc7
	lContinua := .F.   
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Leitura do cheque utilizando leitor via teclado.³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
    While lContLeit
		aCmc7Tc := {}
		aCmc7Tc	:= F460Cmc7Tc()
		If Len( aCmc7Tc ) > 0
			M->EF_BANCO		:= aCmc7Tc[1]
			M->EF_AGENCIA	:= aCmc7Tc[3]
			M->EF_CONTA		:= aCmc7Tc[4]
			M->EF_NUM		:= aCmc7Tc[2]
		
			If !lPrimeiro  
				Aadd( aCols, Array(Len(aHeader)+1) )  
				aCols[Len(aCols),nPosChCad] 	:= STR0245			// Cheque Cadastrado				
				aCols[Len(aCols),8] 			:= dDataBase			// 
				aCols[Len(aCols),9] 			:= dDataBase			// 
				n := Len(aCols)
			EndIf
			If lGetDados .AND. F070GetChq( aCmC7Tc,		@EF_BANCO,		@EF_AGENCIA,	@EF_CONTA,		@EF_NUM,	@M->EF_DATA,;
											@M->EF_VENCTO,	@M->EF_EMITENT,	@M->EF_VALOR,	@M->EF_CPFCNPJ,	@M->EF_TEL,	@M->EF_HIST,;
											lGetDados ) 
				//Pesquisa se cheque ja foi lido anteriormente (Banco/Agencia/Conta/Nro.Cheque)
				If aScan(aCols,{|x| AllTrim(x[1])+AllTrim(x[2])+AllTrim(x[3])+AllTrim(x[4])== ;
					AllTrim(aCmc7Tc[1])+AllTrim(aCmc7Tc[3])+AllTrim(aCmc7Tc[4])+AllTrim(aCmc7Tc[2]) }) = 0
					// Verifica se a primeira linha está preenchida (banco, agencia, conta) e sobrescreve
					If !Empty(aCols[1][nPosBanco]) .And. !Empty(aCols[1][nPosAgen]) .And. !Empty(aCols[1][nPosConta]) .AND. lPrimeiro			
						aCols[1,nPosBanco]		:= aCmc7Tc[1]		// Banco
						aCols[1,nPosAgen]		:= aCmc7Tc[3]		// Agencia
						aCols[1,nPosConta]		:= aCmc7Tc[4]		// Conta
						aCols[1,nPosCheque]		:= aCmc7Tc[2]		// Cheque   
						aCols[1,nPosValor]		:= M->EF_VALOR		// Valor
						If lPaisBRA
							aCols[1,nPosValorBx]		:= M->EF_VALOR		// Valor da Baixa
						Endif
						aCols[1,nPosEmitente]	:= M->EF_EMITENT	// Emitente
						aCols[1,nPosData]		:= M->EF_DATA		// Data
						aCols[1,nPosVencto]		:= M->EF_VENCTO		// Vencimento
						aCols[1,nPosCpfCnpj]	:= M->EF_CPFCNPJ	// CPF ou CNPJ
						aCols[1,nPosTel] 		:= M->EF_TEL		// Telefone
						aCols[1,nPosHist]		:= M->EF_HIST		// Historico
						aCols[1,nPosChCad] 		:= STR0245			// Cheque Cadastrado
						If SEF->(FieldPos("EF_USADOBX")) > 0
							aCols[1,nPosSeq]			:= Nil 				// 
							aCols[1,nPosUsado]			:= STR0245				// 			
						EndIf
						aCols[1,Len(aHeader)+1]	:= .F.				// Garante que a linha e' valida
						lPrimeiro 				:= .F.				// Verifica se o cheque e' o primeiro
					Elseif SEF->(FieldPos("EF_USADOBX")) > 0
						aCols[Len(aCols),nPosBanco]		:= aCmc7Tc[1]		// Banco
						aCols[Len(aCols),nPosAgen]		:= aCmc7Tc[3]		// Agencia
						aCols[Len(aCols),nPosConta]		:= aCmc7Tc[4]		// Conta
						aCols[Len(aCols),nPosCheque]	:= aCmc7Tc[2]		// Cheque   
						aCols[Len(aCols),nPosValor]		:= M->EF_VALOR		// Valor
						If lPaisBRA
							aCols[Len(aCols),nPosValorBx]	:= M->EF_VALOR		// Valor da Baixa
						Endif
						aCols[Len(aCols),nPosEmitente]	:= M->EF_EMITENT	// Emitente
						aCols[Len(aCols),nPosData]		:= M->EF_DATA		// Data
						aCols[Len(aCols),nPosVencto]	:= M->EF_VENCTO		// Vencimento
						aCols[Len(aCols),nPosCpfCnpj]	:= M->EF_CPFCNPJ	// CPF ou CNPJ
						aCols[Len(aCols),nPosTel] 		:= M->EF_TEL		// Telefone
						aCols[Len(aCols),nPosHist]		:= M->EF_HIST		// Historico
						aCols[Len(aCols),nPosChCad] 	:= STR0245			// Cheque Cadastrado
						If SEF->(FieldPos("EF_USADOBX")) > 0
							aCols[Len(aCols),nPosSeq]			:= Nil 				// 
							aCols[Len(aCols),nPosUsado]			:= STR0245				// 			
						EndIf
						
						aCols[Len(aCols),Len(aHeader)+1]			:= .F. 				// 
						lPrimeiro 				:= .F.
					Endif
				Else  
					aDel(aCols,Len(aCols))
        			aSize(aCols,Len(aCols)-1)
	        		IW_MsgBox(STR0158,STR0144,"STOP")	//""Cheque já Incluído ! Será Desprezado.""###"Atenção"	
	        		
	        	Endif	
	        Else 
				If !lPrimeiro 
					aCpaCols := aClone(aCols)  
					aCols	:= Array(Len(aCpaCols)-1)
					ACOPY( aCpaCols, aCols , 1, Len(aCpaCols)-1,1)				   
					n := Len(aCols)   			
				EndIf        	
	        Endif	
	    
	    	If !IW_MsgBox(STR0159,STR0156,"YESNO")	//"Deseja incluir mais cheques?"###"Cheques recebidos"
	    		lContLeit := .F.
	    	Endif   
	    	
	    	If ExistBlock("F070VENCTO")
				nDiasVcto := ExecBlock("F070VENCTO", .F., .F. )
			Else
				nDiasVcto := 30
			Endif
			M->EF_VENCTO += nDiasVcto

		ElseIf !IW_MsgBox(STR0159,STR0156,"YESNO")	//"Deseja incluir mais cheques?"###"Cheques recebidos"
    		lContLeit := .F.
    	Endif   
		Loop			
	Enddo	
Endif

If ExistBlock("F070CMC7") 	// Se existir o PE F070CMC7, a leitura dos dados
									// ficara sob responsabilidade deste PE.
	ExecBlock("F070CMC7", .F., .F. )
	Return
Endif

While lContinua
	aCmc7 		:= {}
	aCmc7 		:= LjLeCmc7(nNumCH) // Exemplo: { "745", "900201 ", "0030", "30049113", "018" }
	M->EF_VALOR	:= 0
	If Len(aCmc7) > 0
		M->EF_BANCO		:= aCmc7[1]
		M->EF_AGENCIA	:= aCmc7[3]
		M->EF_CONTA		:= aCmc7[4]
		M->EF_NUM		:= aCmc7[2]
		If lGetDados .And. F070GetChq(aCmC7,@EF_BANCO,@EF_AGENCIA,@EF_CONTA,@EF_NUM,@M->EF_DATA,@M->EF_VENCTO,@M->EF_EMITENT,@M->EF_VALOR,@M->EF_CPFCNPJ,@M->EF_TEL,@M->EF_HIST,lGetDados)
			//Pesquisa se cheque ja foi lido anteriormente (Banco/Agencia/Conta/Nro.Cheque)
			If aScan(aCols,{|x|	AllTrim(x[1])+AllTrim(x[2])+AllTrim(x[3])+AllTrim(x[4])== ;
										AllTrim(aCmc7[1])+AllTrim(aCmc7[3])+AllTrim(aCmc7[4])+AllTrim(aCmc7[2]) }) = 0 .And. !aTail(aCols[n])
				//Caso não esteja preenchido as informacoes de banco, agencia, conta e numero
				// do cheque, atualizar a linha do acols posicinado (N).
				If Empty(aCols[n][1]) .And. Empty(aCols[n][2]) .And. Empty(aCols[n][3]) .And. Empty(aCols[n][4])
				   aCols[n][01] := aCmc7[1]
				   aCols[n][02] := aCmc7[3]
				   aCols[n][03] := aCmc7[4]
				   aCols[n][04] := aCmc7[2]
				   aCols[n][05] := M->EF_VALOR
				   If cPaisLoc == "BRA"
					   aCols[n][06] := M->EF_VALOR
				   Endif 
				   aCols[n][07] := M->EF_EMITENT
				   aCols[n][08] := M->EF_DATA
				   aCols[n][09] := M->EF_VENCTO
				   aCols[n][10] := M->EF_CPFCNPJ
				   aCols[n][11] := M->EF_TEL
				   aCols[n][12] := M->EF_HIST
				   aCols[n][13] := STR0245
				Elseif SEF->(FieldPos("EF_USADOBX")) > 0
					Aadd(aCols,{aCmc7[1],		aCmc7[3],		aCmc7[4],	aCmc7[2],		M->EF_VALOR,;
								M->EF_VALOR,	M->EF_EMITENT,	M->EF_DATA,	M->EF_VENCTO,	M->EF_CPFCNPJ,;
								M->EF_TEL,		M->EF_HIST,		STR0245,		NIL})
					AADD(aCols[Len(aCols)],.F.)	// controle de delecao
				Else
					Aadd(aCols,{aCmc7[1],		aCmc7[3],		aCmc7[4],	aCmc7[2],		M->EF_VALOR,;
								M->EF_VALOR,	M->EF_EMITENT,	M->EF_DATA,	M->EF_VENCTO,	M->EF_CPFCNPJ,;
								M->EF_TEL,		M->EF_HIST,		STR0245})
					AADD(aCols[Len(aCols)],.F.)	// controle de delecao 					                                                                                                        				
				Endif
			Else
				IW_MsgBox(STR0158,STR0144,"STOP")  //"Cheque já Incluído ! Será Desprezado."###"Atenção"
			Endif
			If !IW_MsgBox(STR0159,STR0156,"YESNO")  //"Deseja incluir mais cheques?"###"Cheques recebidos"
				Exit
			EndIf
			If ExistBlock("F070VENCTO")
				nDiasVcto := ExecBlock("F070VENCTO", .F., .F. )
			Else
				nDiasVcto := 30
			Endif
			M->EF_VENCTO += nDiasVcto
			nNumCH++
		Endif
		lContinua := lGetDados
	Else
		// Pergunta se deseja sair da leitura somente se estiver na GetDados
		IF !lGetDados .Or. IW_Msgbox(STR0160,STR0144,"YESNO")  //"Encerra leitura de Cheques ?"###"Atenção" //"Encerra leitura de Cheques ?"###"Atenção"
			Exit
		Endif
	Endif
End
If lGetDados
	n:= Len(aCols)
Endif	
If ValType(oGet)=="O"
	oGet:ForceRefresh()
	oGet:lNewLine := .F.
Endif

FA070AtuTT() //Atualiza totais

Return

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³F070GetChq³ Autor ³ Claudio D. de Souza   ³ Data ³ 28/08/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Entrada de dados do cheque   										  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ F460GetChq(ExpA1,ExpD1,ExpN1)										  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ aCmc7=Array contendo dados do cheque (vindos da leitora)	  ³±±
±±³          ³ EF_BANCO=Codigo do banco, por referencia						  ³±±
±±³          ³ EF_AGENCIA=Codigo da agencia, por referencia					  ³±±
±±³          ³ EF_AGENCIA=Codigo da agencia, por referencia					  ³±±
±±³          ³ EF_CONTA=Codigo da conta, por referencia					     ³±±
±±³          ³ EF_NUM=Numero do Cheque, por referencia					     ³±±
±±³          ³ EF_DATA=Data do cheque, por referencia					  		  ³±±
±±³          ³ EF_VENCTO=Vencto. do cheque, por referencia					  ³±±
±±³          ³ EF_EMITENT=Emitente do cheque, por referencia				  ³±±
±±³          ³ EF_VALOR=Valor nominal, por referencia					  		  ³±±
±±³          ³ EF_CPFCNPJ=Cpf/Cnpj do cheque, por referencia				  ³±±
±±³          ³ EF_HIST=Historico do cheque, por referencia			  		  ³±±
±±³          ³ lGetDados=.T. se estiver chamando da GetDados				  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ FINA070, FINA040 e FINA191											  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function F070GetChq(aCmC7,EF_BANCO,EF_AGENCIA,EF_CONTA,EF_NUM,EF_DATA,;
									EF_VENCTO,EF_EMITENT,EF_VALOR,EF_CPFCNPJ,EF_TEL,EF_HIST,;
									lGetDados)
Local lCorrige := .F.
Local lRet := .F.
Local nOpca := 0
Local oDlg
Local aValid 	:= {}
Local aPicture := {}
Local oBanco
Local cValid

// Pequisa um determinados campo no SX3 e retorna o conteudo desejado do dicionario.
Local bSx3		:= { |cCampo,cCampoSx3|	SX3->(DbSetOrder(2)), SX3->(MsSeek(cCampo)),;
													SX3->(DbSetOrder(1)), SX3->&(cCampoSx3) }

If lGetDados .And. Type("aHeader")=="A"
	cValid := StrTran(aHeader[1][6],"aCols[n][12]!='Sim'",".T.")
	Aadd(aValid, {"Banco"	, "{||"+If(Empty(cValid),".T.",cValid)+"}"}) 
	Aadd(aPicture, {"Banco"	, aHeader[1][3]})
	cValid := StrTran(aHeader[2][6],"aCols[n][12]!='Sim'",".T.")
	Aadd(aValid, {"Agencia"	, "{||"+If(Empty(cValid),".T.",cValid)+"}"}) 
	Aadd(aPicture, {"Agencia"	, aHeader[2][3]})
	cValid := StrTran(aHeader[3][6],"aCols[n][12]!='Sim'",".T.")
	Aadd(aValid, {"Conta"	, "{||"+If(Empty(cValid),".T.",cValid)+"}"}) 
	Aadd(aPicture, {"Conta"	, aHeader[3][3]})
	cValid := StrTran(aHeader[4][6],"aCols[n][12]!='Sim'",".T.")
	Aadd(aValid, {"Numero"	, "{||"+If(Empty(cValid),".T.",cValid)+"}"}) 
	Aadd(aPicture, {"Numero"	, aHeader[4][3]})
	cValid := StrTran(aHeader[5][6],"aCols[n][13]!='Sim'",".T.")
	Aadd(aValid, {"Valor"	, "{||"+If(Empty(cValid),".T.",cValid)+"}"}) 
	Aadd(aPicture, {"Valor"	, aHeader[5][3]})
	cValid := StrTran(aHeader[8][6],"aCols[n][12]!='Sim'",".T.")
	Aadd(aValid, {"Emissao"	, "{||"+If(Empty(cValid),".T.",cValid)+"}"}) 
	Aadd(aPicture, {"Emissao"	, aHeader[8][3]})
	cValid := StrTran(aHeader[9][6],"aCols[n][13]!='Sim'",".T.")
	cValid := StrTran(cValid, ".And. aCols[n][9] >= aCols[n][8]","")
	Aadd(aValid, {"Vencto"	, "{||"+If(Empty(cValid),".T.",cValid)+"}"}) 
	Aadd(aPicture, {"Vencto"	, aHeader[9][3]})
	cValid := StrTran(aHeader[7][6],"aCols[n][13]!='Sim'",".T.")
	Aadd(aValid, {"Emitente", "{||"+If(Empty(cValid),".T.",cValid)+"}"}) 
	Aadd(aPicture, {"Emitente", aHeader[7][3]}) 
	cValid := StrTran(aHeader[10][6],"aCols[n][13]!='Sim'",".T.")
	Aadd(aValid, {"CPF/CNPJ", "{||"+If(Empty(cValid),".T.",cValid)+"}"}) 
	Aadd(aPicture, {"CPF/CNPJ", aHeader[10][3]})
	cValid := StrTran(aHeader[12][6],"aCols[n][13]!='Sim'",".T.")
	Aadd(aValid, {"OBS"		, "{||"+If(Empty(cValid),".T.",cValid)+"}"}) 
	Aadd(aPicture, {"OBS", aHeader[12][3]})
	cValid := StrTran(aHeader[11][6],"aCols[n][13]!='Sim'",".T.")
	Aadd(aValid, {"Telefone", "{||"+If(Empty(cValid),".T.",cValid)+"}"}) 
	Aadd(aPicture, {"Telefone", aHeader[11][3]})
Else
	cValid := Eval(bSx3,"EF_BANCO","X3_VALID")
	Aadd(aValid, {"Banco"	, "{||"+If(Empty(cValid),".T.",cValid)+"}"})
	Aadd(aPicture, {"Banco"	, Eval(bSx3,"EF_BANCO","X3_PICTURE")})
	cValid := Eval(bSx3,"EF_AGENCIA","X3_VALID")
	Aadd(aValid, {"Agencia"	, "{||"+If(Empty(cValid),".T.",cValid)+"}"})
	Aadd(aPicture, {"Agencia"	, Eval(bSx3,"EF_AGENCIA","X3_PICTURE")})	
	cValid := Eval(bSx3,"EF_CONTA","X3_VALID")
	Aadd(aValid, {"Conta"	, "{||"+If(Empty(cValid),".T.",cValid)+"}"})  
	Aadd(aPicture, {"Conta"	, Eval(bSx3,"EF_CONTA","X3_PICTURE")})  	
	cValid := Eval(bSx3,"EF_NUM","X3_VALID")
	Aadd(aValid, {"Numero"	, "{||"+If(Empty(cValid),".T.",cValid)+"}"})
	Aadd(aPicture, {"Numero"	, Eval(bSx3,"EF_NUM","X3_PICTURE")})
	cValid := Eval(bSx3,"EF_VALOR","X3_VALID")
	Aadd(aValid, {"Valor"	, "{||"+If(Empty(cValid),".T.",cValid)+"}"})
	Aadd(aPicture, {"Valor"	, Eval(bSx3,"EF_VALOR","X3_PICTURE")})
	cValid := Eval(bSx3,"EF_DATA","X3_VALID")
	Aadd(aValid, {"Emissao"	, "{||"+If(Empty(cValid),".T.",cValid)+"}"})
	Aadd(aPicture, {"Emissao"	, Eval(bSx3,"EF_DATA","X3_PICTURE")})
	cValid := Eval(bSx3,"EF_VENCTO","X3_VALID") 
	Aadd(aValid, {"Vencto"	, "{||"+If(Empty(cValid),".T.",cValid)+"}"})
	Aadd(aPicture, {"Vencto"	, Eval(bSx3,"EF_VENCTO","X3_PICTURE")})
	cValid := Eval(bSx3,"EF_EMITENT","X3_VALID")
	Aadd(aValid, {"Emitente", "{||"+If(Empty(cValid),".T.",cValid)+"}"})
	Aadd(aPicture, {"Emitente", Eval(bSx3,"EF_EMITENT","X3_PICTURE")})
	cValid := Eval(bSx3,"EF_CPFCNPJ","X3_VALID")
	Aadd(aValid, {"CPF/CNPJ", "{||"+If(Empty(cValid),".T.",cValid)+"}"})
	Aadd(aPicture, {"CPF/CNPJ", Eval(bSx3,"EF_CPFCNPJ","X3_PICTURE")})
	cValid := Eval(bSx3,"EF_HIST","X3_VALID")
	Aadd(aValid, {"OBS", "{||"+If(Empty(cValid),".T.",cValid)+"}"})
	Aadd(aPicture, {"OBS", Eval(bSx3,"EF_HIST","X3_PICTURE")})
	cValid := Eval(bSx3,"EF_TEL","X3_VALID")
	Aadd(aValid, {"Telefone", "{||"+If(Empty(cValid),".T.",cValid)+"}"})
	Aadd(aPicture, {"Telefone", Eval(bSx3,"EF_TEL","X3_PICTURE")})
Endif

While .T.
	nOpca := 0

	DEFINE MSDIALOG oDlg FROM	38,16 TO 347,550 TITLE  STR0161 PIXEL   //"Dados do cheque"
	@ 003, 004 TO 120, 265 OF oDlg  PIXEL
	@ 010, 011 SAY STR0145		OF oDlg PIXEL  //"Banco"
	@ 020, 011 MSGET oBanco VAR aCmc7[1]	WHEN lCorrige .Or. "?" $ aCmc7[1] ;
														VALID Eval(&(aValid[1][2])) PICTURE aPicture[1][2] SIZE 17, 11 OF oDlg PIXEL
	@ 010, 038 SAY STR0146	OF oDlg PIXEL  //"Agência"
	@ 020, 038 MSGET aCmc7[3]	WHEN lCorrige .Or. "?" $ aCmc7[3] ;
										VALID Eval(&(aValid[2][2])) PICTURE aPicture[2][2] SIZE 30, 11 OF oDlg PIXEL
	@ 010, 078 SAY STR0147		OF oDlg PIXEL   //"Conta"
	@ 020, 078 MSGET aCmc7[4]	WHEN lCorrige .Or. "?" $ aCmc7[4];
										VALID Eval(&(aValid[3][2])) PICTURE aPicture[3][2] SIZE 37, 11 OF oDlg PIXEL
	@ 010, 125 SAY STR0162	SIZE 46, 7 OF oDlg PIXEL  //"Número do Cheque"
	@ 020, 125 MSGET aCmc7[2]	WHEN lCorrige .Or. "?" $ aCmc7[2];
										VALID Eval(&(aValid[4][2])) PICTURE aPicture[4][2] SIZE 40, 11 OF oDlg PIXEL
	@ 010, 175 SAY STR0163	OF oDlg PIXEL	 //"Valor Nominal"
	@ 020, 175 MSGET M->EF_VALOR VALID Eval(&(aValid[4][2])) PICTURE aPicture[5][2] Size 60,11  OF oDlg PIXEL
	@ 035, 011 SAY STR0164 OF oDlg PIXEL	 //"Data de Emissão"
	@ 045, 011 MSGET M->EF_DATA VALID Eval(&(aValid[6][2])) PICTURE aPicture[6][2] SIZE 60,11 OF oDlg PIXEL	
	@ 035, 078 SAY STR0165 OF oDlg PIXEL	 //"Data de Vencto"
	@ 045, 078 MSGET M->EF_VENCTO  VALID Eval(&(aValid[7][2])) PICTURE aPicture[7][2] SIZE 60,11 OF oDlg PIXEL	

	@ 060, 011 SAY STR0150 OF oDlg PIXEL //"Emitente"
	@ 070, 011 MSGET M->EF_EMITENT SIZE 140,11 VALID Eval(&(aValid[8][2])) PICTURE aPicture[8][2] OF oDlg PIXEL
	
	@ 060, 175 SAY STR0166 OF oDlg PIXEL	 //"CNPJ/CPF"
	@ 070, 175 MSGET M->EF_CPFCNPJ VALID Eval(&(aValid[9][2])) PICTURE If(Empty(aPicture[9][2]),"99999999999999",aPicture[9][2])SIZE 50,11 OF oDlg PIXEL
		
	@ 085, 011 SAY STR0155 OF oDlg PIXEL	 //"Observações"
	@ 095, 011 MSGET M->EF_HIST VALID Eval(&(aValid[10][2])) PICTURE If(Empty(aPicture[10][2]),"@!",aPicture[10][2]) SIZE 140,11 OF oDlg PIXEL
	
	@ 085, 175 SAY STR0154 OF oDlg PIXEL	 //"Telefone"
	@ 095, 175 MSGET M->EF_TEL  VALID Eval(&(aValid[11][2])) PICTURE If(Empty(aPicture[11][2]),"@!",aPicture[11][2]) SIZE 50,11 OF oDlg PIXEL
			
	DEFINE SBUTTON FROM 130, 175 TYPE 1 ACTION (nOpca:=1,oDlg:End())ENABLE OF oDlg PIXEL
	DEFINE SBUTTON FROM 130, 205 TYPE 2 ACTION (nOpca:=2,oDlg:End())ENABLE OF oDlg PIXEL
	DEFINE SBUTTON FROM 130, 235 TYPE 5 ACTION (nOpca:=3,lCorrige:=.T.,oBanco:SetFocus()) ENABLE OF oDlg PIXEL
	
	ACTIVATE MSDIALOG oDlg CENTERED
	
	If nOpca == 1  // Confirma Dados do Cheque
		M->EF_BANCO		:= aCmc7[1]
		M->EF_AGENCIA	:= aCmc7[3]
		M->EF_CONTA		:= aCmc7[4]
		M->EF_NUM		:= aCmc7[2]
	   lRet := .T.
		lCorrige := .F.
		Exit
	ElseIf nOpca == 2 	// Finaliza inclusao de cheques
		lRet := .F.
		lCorrige := .F.
		Exit
	ElseIf nOpca == 3   // Edita dados do cheque
		aCmc7[1] := PADR(aCmc7[1],3," ")
		aCmc7[3] := PADR(aCmc7[3],4," ")
		aCmc7[4] := PADR(aCmc7[4],8," ")
		aCmc7[2] := PADR(aCmc7[2],6," ")
		lCorrige := .T.
	Endif	
End	
Return(lRet)   

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³F070Liq³ Autor ³ Wagner Xavier   ³          Data ³ 30/12/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Permite a manipulacao do juros apos o valor liquido		     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function fa070Liq(oJuros,oValRec,oPanel2)

LOCAL lfa070Liq := Existblock("fcalcjur")
LOCAL lF070IRBX := Existblock("F070IRBX")

If lF070IRBX
	nTotAbImp := Execblock("F070IRBX", .F., .F., {NVALREC, nTotAbImp})
	oPanel2:Refresh()
Endif

IF lfa070liq
	nJuros := Execblock("fcalcjur",.f.,.f.)
	oJuros:refresh()
	oValRec:refresh()
Endif
Return .T.

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³F070Ret   ³ Autor ³ Mauricio Pequim Jr    ³ Data ³ 10/02/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Tratamento de retencao na data de credito					  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ F070Ret(ExpC1,ExpD4) 						              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpD1 = Data de Credito Atual                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ FINA070 													  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function F070Ret()
Local j
Local cFunName	:= AllTrim( Upper( FunName() ) )	// Nome da Rotina
Local lConsRet	:= .T.								// Considera retancao bancaria

// Determina se a rotina considera retencao bancaria, se esta funcao foi
// chamada por outro programa sempre considera.
If	( cFunName == "FINA070" .AND. mv_par09 <> 1 ) .OR.;
	( cFunName == "FINA110" .AND. mv_par07 <> 1 )

	lConsRet	:= .F.
EndIf

If !( cFunName $ "FINA070*FINA740" ) .OR. lConsRet

	SA6->(MsSeek(xFilial("SA6")+cBanco+cAgencia+cConta))
	dDtCredito := dBaixa
	
	If SA6->(!Eof()) .AND. SA6->A6_RETENCA > 0 .AND. SE1->E1_SITUACA $ "12347"
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Atualiza data vencto real c/reten‡„o Banc ria³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		For j:=1 To SA6->A6_RETENCA
			dDtCredito := DataValida(dDtCredito+1,.T.)
		Next j
	Else
		dDtCredito := dDataBase
	Endif
Endif	
Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³FA070Pes  ³ Autor ³ Rafael Rodrigues      ³ Data ³ 04/09/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Cria uma pesquisa personalizada para usar campos virtuais.  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³Gestao Educacional                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function FA070Pes(cAlias, nReg, nOpc)
local nOpcA
local oDlg, oOrdem, oChave, oBtOk, oBtCan, oBtPar
local cExpChave := ""
local cChave	:= Space(255)
local aOrdens	:= {}
local aBlocks	:= {}
local nOrder	:= 1

static cOrdem

SIX->( dbSetOrder(1) )
SIX->( MsSeek(cAlias) )

while SIX->( !eof() .and. INDICE == cAlias )

	aAdd( aOrdens, Capital( SIXDescricao() ) )
	aAdd( aBlocks, &("{ || "+cAlias+"->(dbSetOrder("+Str(nOrder,2,0)+")), "+cAlias+"->(MsSeek(xFilial('"+cAlias+"')+Rtrim(cChave))) }") )
	
	nOrder++
	
	SIX->( dbSkip() )
end

aAdd( aOrdens, Capital( STR0177 ) )	// "Nome do Aluno"
aAdd( aBlocks, &("{ || "+cAlias+"->(dbSetOrder(2)), "+cAlias+"->(MsSeek(xFilial('"+cAlias+"')+Posicione('JA2',3,xFilial('JA2')+Rtrim(cChave),'JA2_CLIENT')))}") )

aAdd( aOrdens, Capital( STR0178 ) )	// "Incrição do Candidato"
aAdd( aBlocks, &("{ || "+cAlias+"->(dbSetOrder(1)), "+cAlias+"->(MsSeek(xFilial('"+cAlias+"')+RTrim(Posicione('JA1',1,xFilial('JA1')+Rtrim(cChave),'JA1_PRFTIT')+Posicione('JA1',1,xFilial('JA1')+Rtrim(cChave),'JA1_NUMTIT')+Posicione('JA1',1,xFilial('JA1')+Rtrim(cChave),'JA1_PARTIT')+Posicione('JA1',1,xFilial('JA1')+Rtrim(cChave),'JA1_TIPTIT'))))}") )

define msDialog oDlg title STR0001 from 00,00 TO 100,500 pixel

@ 005, 005 combobox oOrdem var cOrdem items aOrdens size 210,08 of oDlg pixel
@ 020, 005 msget oChave var cChave size 210,08 of oDlg pixel

define sButton oBtOk  from 05,218 type 1 action (nOpcA := 1, oDlg:End()) enable of oDlg pixel
define sButton oBtCan from 20,218 type 2 action (nOpcA := 0, oDlg:End()) enable of oDlg pixel
define sButton oBtPar from 35,218 type 5 when .F. of oDlg pixel

activate msdialog oDlg center

if nOpcA == 1

	for nOrder := 1 to len(aOrdens)
		if aOrdens[ nOrder ] == cOrdem
			cExpChave := (cAlias)->(IndexKey(nOrder))
			if "DTOS" $ cExpChave .Or. "DTOC" $ cExpChave
				cChave := ConvData( cExpChave, cChave )
			endif
			Eval( aBlocks[ nOrder ] )
		endif
	next i
	
endif

Return


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³FA070Aca  ³ Autor ³ Rafael Rodrigues      ³ Data ³ 07/11/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Realiza as consequencias de uma baixa de titulo com o modulo³±±
±±³          ³Gestao Educacional ativo.                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³FA070Aca( <cPrf>, <cNum>, <cParc>, <cTipo> )                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC01: Prefixo do Titulo Baixado (OPCIONAL)                ³±±
±±³          ³ExpC02: Numero do Titulo Baixado  (OPCIONAL)                ³±±
±±³          ³ExpC03: Parcela do Titulo Baixado (OPCIONAL)                ³±±
±±³          ³ExpC04: Tipo do Titulo Baixado    (OPCIONAL)                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³Generico                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function FA070Aca( cPrf, cNum, cParc, cTipo, lShowLog )


ACFina070( cPrf, cNum, cParc, cTipo, lShowLog )

Return
/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³F070VlLt  ³ Autor ³ Mauricio Pequim Jr    ³ Data ³ 09/10/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Verificação do valor de marcacao dos titulos para Bx. Lote  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³F070VlLt(ExpN1) 								                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpN1 = Valor total dos titulos selecionados               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ FINA070 																	  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function F070VlLt(nValor)
Local lRet := .T.

If nValor <= 0
	Help(" ",1,"VL_LOTE")
	lRet := .F.
Endif	

Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³CalcJrDev ºAutor  ³Nilton Pereira      º Data ³  04/12/04   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³Calcula os juros devido e nao pago de um titulo a receber   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºSintaxe   ³CalcJrDev ( <cPrefixo> , <cNum> , <cParcela>, <cTipo> )     º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ ExpC01: Prefixo do Titulo (OPCIONAL)                       º±±
±±º          ³ ExpC02: Numero do Titulo                                   º±±                                                            
±±º          ³ ExpC03: Parcela do Titulo                                  º±±                                                            
±±º          ³ ExpC04: Tipo do Titulo Baixado                             º±±                                                            
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Financeiro                                                 º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function CalcJrDev(cPrefixo,cNum,cParcela,cTipo)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Declaracao de variaveis                                       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local nVlrBaixa   := 0
Local lBxCEC      := .F.
Local lBaixaAbat  := .F.    
Local aBaixa      := {}
Local cCliente    := ""
Local cLoja       := ""
Local nTotAdto    := 0
Local nValPago    := 0
Local nJurTit     := 0
Local nMoeda      := 1
Local nValBas     := 0
Local nJurPago    := 0
Local x           := 0    
Local nTxMoeda	  := 0

Private aBaixaSE5 := {}
 
dbSelectArea("SE1")
dbSetOrder(1)
                    
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica a existencia ou nao do titulo a receber              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !MsSeek(xFilial()+cPrefixo+cNum+cParcela+cTipo) .And.;
	(SE1->E1_PORCJUR  == 0) .And. (SE1->E1_BAIXA < DataValida(SE1->E1_VENCTO,.T.))                 
	MsgAlert("Titulo a Receber nao encontrado ou sem juros !")
	Return 
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Carrega as informacoes do titulo posicionado                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cCliente    := SE1->E1_CLIENTE
cLoja       := SE1->E1_LOJA   
nDias       := SE1->E1_BAIXA - SE1->E1_VENCTO
nValor      := SE1->E1_VALOR                 
dVecto      := SE1->E1_VENCTO        
dVencRea    := SE1->E1_VENCREA        
nPerJur     := SE1->E1_PORCJUR
dEmissao    := SE1->E1_EMISSAO  
nTxMoeda 	:= If(SE1->E1_MOEDA > 1, If(SE1->E1_TXMOEDA > 0, SE1->E1_TXMOEDA,RecMoeda(dBaixa,SE1->E1_MOEDA)),0)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Seleciona as baixas do titulo para calculo dos juros          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aBaixa := Sel070Baixa( "VL /V2 /BA /RA /CP /LJ /"+MV_CRNEG,cPrefixo, cNum, cParcela,cTipo,@nTotAdto,@lBaixaAbat,cCliente,cLoja,@nVlrBaixa,,@lBxCEC)

For x:= 1 To Len(aBaixaSE5)
	nValBas  := SE1->E1_VALOR - nValPago 
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Calcula os juros para cada baixa encontrada para o titulo     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If x == 1
		nJurTit  += FaJuros(nValor,nValBas,dVecto,0,nPerJur,nMoeda,dEmissao,aBaixaSE5[x][7],nTxMoeda,,dVencRea, "SE1")
	Else
		nJurTit  += FaJuros(nValor,nValBas,dVecto,0,nPerJur,nMoeda,dEmissao,aBaixaSE5[x][7],nTxMoeda,aBaixaSE5[x-1][7],dVencRea, "SE1")
	Endif
	nValPago += aBaixaSE5[x][08] - aBaixaSE5[x][14] - aBaixaSE5[x][15] + aBaixaSE5[x][16]
	nJurPago += aBaixaSE5[x][14]
Next

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Faz calculo dos juros pagos/recebidos, informando a diferenca ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nJurDev := nJurTit - nJurPago

Return nJurDev 

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ f070AltBco   ³ Autor ³ Leonardo Ruben     	³ Data ³ 02/12/04 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Verifica moeda        para o banco escolhido/alterado          ³±±
±±³          ³ Somente paises localizados                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function f070AltBco(nTxMoeda,oJuros, oMulta, oDescont, oCm, oBanco, nValRec)
Local lRet := .T. 
Local lPccBxCr  := If (FindFunction("FPccBxCr"),FPccBxCr(),.F.)         
Local lIrPjBxCr := If (FindFunction("FIrPjBxCr"),FIrPjBxCr(),.F.)
                                  
DEFAULT oBanco := ""
If (cBanco+cAgencia+cConta == SA6->A6_COD+SA6->A6_AGENCIA+SA6->A6_NUMCON)
	nMoedaBco := Max( SA6->A6_MOEDA, 1)  

	If cPaisLoc <> "BRA"
		nTxMoeda:=Iif(nMoedaBco>0,aTxMoedas[nMoedaBco][2],1)
		nTxMdaOr:=aTxMoedas[SE1->E1_MOEDA][2]
	Else
		nTxMoeda := If(SE1->E1_MOEDA > 1, If(SE1->E1_TXMOEDA > 0, SE1->E1_TXMOEDA,RecMoeda(dBaixa,SE1->E1_MOEDA)),0)
		nTxMdaOr := If(SE1->E1_MOEDA > 1, If(SE1->E1_TXMOEDA > 0, SE1->E1_TXMOEDA,RecMoeda(dBaixa,SE1->E1_MOEDA)),0)		
	Endif

	nValRec	:= Round(NoRound(xMoeda(nValEstrang,SE1->E1_MOEDA,nMoedaBco,dBaixa,5,nTxMdaOr,nTxMoeda),5),3)
	nValEstrang:=  Round(NoRound(xMoeda(nValRec,nMoedaBco,SE1->E1_MOEDA,dBaixa,3,nTxMoeda,nTxMdaOr),3),MsDecimais(1))

	oValRec:Refresh()

	If Type("oValEstrang") == "O"
		oVlEstrang:Refresh() 
	Endif

	nEstOriginal := nValEstrang-(xMoeda(nJuros+(nCm1+nProRata)+nMulta-nDescont-nOtrga+nAcresc-nDecresc - Iif(lPccBxCr,nPis+nCofins+nCsll,0)-Iif(lIrPjBxCr,nIrrf,0),1,SE1->E1_MOEDA,,,,nTxMoeda)) 	
   If (!EMPTY(oDescont) .AND. oDescont:lModified) .OR. EMPTY(oDescont)
		nDescont := FaDescFin("SE1",dBaixa,SE1->E1_SALDO-nTotAbat,nMoedaBco)
	EndIf       
	IF (!Empty(oJuros) .AND. oJuros:lModified) .or. Empty(oJuros)
		fa070Juros(nMoedaBco)
	EndIf

	If (!Empty(oBanco) .AND. (oBanco:lModified))
		FA070CORR(nEstOriginal,nTxMoeda) 
    endIF

	If cPaisLoc == "BRA"	
		oJuros:Refresh()
		oMulta:Refresh()
		oDescont:Refresh()
		If Type("oCm") == "O"
			oCm:Refresh()
		Endif
	Endif
EndIf

//Ponto de entrada para validacao do Banco
If ExistBlock("F070KCO")
	lRet := ExecBlock("F070KCO",.F.,.F.,{cBanco,cAgencia,cConta})
Endif

Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³Fa080ValEs³ Autor ³ Claudio D. de Souza   ³ Data ³ 17.09.03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Validacao do valor recebido em moeda estrangeira			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ Fa080ValEstrang(	nValEstrang,nTxMoeda,nValRec,dBaixa, 	  ³±±
±±³			 ³ 						oValRec,oTxMoeda,nJuros,nMulta,nDescont, ³±±
±±³			 ³ 						nOtrga,nEstOriginal)			  				  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ FINA070  																  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Static Function Fa070ValEstrang(	nValEstrang,nTxMoeda,nValRec,dBaixa,oValRec,oTxMoeda,;
											nJuros,nMulta,nDescont,nOtrga,nEstOriginal,oVlEstrang)
Local nTxMdaOr := 0
Local lPccBxCr 	:= If (FindFunction("FPccBxCr"),FPccBxCr(),.F.)
Local lIrPjBxCr	:= If (FindFunction("FIrPjBxCr"),FIrPjBxCr(),.F.)   
If cPaisLoc<>"BRA"
	nTxMoeda:=Iif(nMoedaBco>0,aTxMoedas[nMoedaBco][2],1)
	nTxMdaOr:=aTxMoedas[SE1->E1_MOEDA][2]
EndIf

If nTxMoeda > 0 .And. oVlEstrang:lModified
	// Converte o valor em moeda estrangeira para identificar o valor total do pagto. 
	If cPaisLoc <> "BRA"		
		nValRec := xMoeda(nValEstrang,SE1->E1_MOEDA,nMoedaBco,dBaixa,3,nTxMdaOr,nTxMoeda)
	Else	
		nValRec := xMoeda(nValEstrang,SE1->E1_MOEDA,nMoedaBco,dBaixa,3,nTxMoeda)
	EndIf	
	// Verifica a taxa utilizada
	nTxMoeda := nValRec/nValEstrang
	// Atualiza os objetos
	If cPaisLoc=="BRA"
		oTxMoeda:Refresh()
	EndIf	
	oValRec:Refresh()
	// Calcula a correcao monetaria
	nEstOriginal := nValEstrang-(xMoeda(nJuros+(nCm1+nProRata)+nMulta-nDescont-nOtrga+nAcresc-nDecresc - Iif(lPccBxCr,nPis+nCofins+nCsll,0)-Iif(lIrPjBxCr,nIrrf,0),nMoedaBco,SE1->E1_MOEDA,,,,nTxMoeda))  	
	FA070CORR(nEstOriginal,nTxMoeda)
Endif	

Return .T.

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ Fa070LoteFin ³ Autor ³ Claudio Donizete   	³ Data ³ 27/04/05 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Cria trava para lote financeiro na baixa por lote, para        ³±±
±±³          ³ dois usuarios nao utilizem o mesmo numero de lote.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ FINA070                                                        ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function Fa070LoteFin(cLoteFin,cChaveLbn)
Local lRet := .T.
Local cChave

//-- Parametros da Funcao LockByName() :
//   1o - Nome da Trava
//   2o - usa informacoes da Empresa na chave
//   3o - usa informacoes da Filial na chave 
cChave := "CRBXLOTE"+cLoteFin
If !LockByName(cChave,.T.,.F.)
	//-- Se Ja estiver reservado retorna .F. pois nao pode executar a Rotina	
	MsgAlert(STR0181,STR0182) // "Existe outro usuário utilizando este mesmo número de lote. Não é permitida a baixa por lote com mesmo número por dois usuários" ## "Atenção"
	lRet := .F.
Else
	cChaveLbn := cChave
EndIf

Return lRet

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ Fa070TotCheq ³ Autor ³ Claudio Donizete   	³ Data ³ 31/08/05 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Totaliza valores dos cheques recebidos                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ FINA070 e FINA040                                              ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Fa070TotCheq(nCampo)
Local nX

nVlrNomCheq := 0
nQtdCheq		:= 0
nVlRefBxChq	:= 0

For nX := 1 To Len(aCols)
	If !aTail(aCols[nX])
		nVlrNomCheq += If(nCampo == 1 .And. nX == n,&(ReadVar()), aCols[nX][5]) // VALOR NOMINAL
		If cPaisLoc == "BRA"
			nVlRefBxChq += If(nCampo == 2 .And. nX == n,&(ReadVar()), aCols[nX][6]) // VALOR REF. BAIXA
		Endif 
		nQtdCheq 	++
	Endif	
Next
If Type("oVlrNomCheq") == "O"
	oVlrNomCheq:Refresh()
Endif	
If Type("oQtdCheq") == "O"
	oQtdCheq:Refresh()
Endif	
If Type("oVlRefBxChq") == "O"
	oVlRefBxChq:Refresh()
Endif		

Return .T.

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ Fa070ChqDel  ³ Autor ³ Claudio Donizete   	³ Data ³ 31/08/05 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Trata tecla DEL na inclusao de cheques recebidos				   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ FINA070 e FINA040                                              ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Fa070ChqDel
STATIC __nExeDel 

If __nExeDel == Nil
	__nExeDel := 0
Endif

__nExeDel++
// Como esta rotina eh chamada duas vezes pela exclusao na GetDados, controlar
// as chamadas para nao ocorrer erro nos calculos dos dados do rodape  
If (__nExeDel%2)==0
	Return .T.
Endif

If !aTail(aCols[n])	// Registro deletado -> eh o contrario pois pressionou <DEL> e o arquivo ainda esta com Flag trocado
	nVlrNomCheq -= aCols[n][5] // VALOR NOMINAL
	If cPaisLoc == "BRA"
		nVlRefBxChq -= aCols[n][6] // VALOR REF. BAIXA
	Endif		
	nQtdCheq 	--
Else
	nVlrNomCheq += aCols[n][5] // VALOR NOMINAL
	If cPaisLoc == "BRA"
		nVlRefBxChq += aCols[n][6] // VALOR REF. BAIXA
	Endif 
	nQtdCheq 	++
EndIf

If Type("oVlrNomCheq") == "O"
	oVlrNomCheq:Refresh()
Endif	
If Type("oQtdCheq") == "O"
	oQtdCheq:Refresh()
Endif	
If Type("oVlRefBxChq") == "O"
	oVlRefBxChq:Refresh()
Endif
	
Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³Fa070Visual³ Autor ³ Fernando Machima      ³ Data ³ 29/11/05 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Rotina de Visualização de Baixa a receber                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ fa070Visual()                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ FINA070                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function fa070Visual(cAlias,nReg,nOpcx)

AxVisual(cAlias,nReg,nOpcx)

Return .T.	          

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³F070Mul   ³ Autor ³ Ricardo A. Canteras	  ³ Data ³ 12/01/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Validacao do campo de Multa da baixa								  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ F070Mul()			 													  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ FINA070																	  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function F070Mul(oMulta)

Local lRet		:= .T.
Local dBaixa
Local cTxMod	:= If(SE1->E1_MOEDA > 1, If(SE1->E1_TXMOEDA > 0, SE1->E1_TXMOEDA,RecMoeda(dBaixa,SE1->E1_MOEDA)),0)
If	ExistBlock("F070MUL")
	lRet := ExecBlock("F070MUL",.F.,.F.)
Endif

If (!Empty(oMulta) .AND. oMulta:lModified)
	dBaixa      := CriaVar("E1_BAIXA")
	If !nTxMoeda <> cTxMod
		nTxMoeda 	:= If(SE1->E1_MOEDA > 1, If(SE1->E1_TXMOEDA > 0, SE1->E1_TXMOEDA,RecMoeda(dBaixa,SE1->E1_MOEDA)),0)
	EndIf
	f070TotMes(dBaixa,.T.,,,,nTxMoeda)
	nIrrf 		:= FCaIrBxCR(SE1->E1_VALOR)
Endif

Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³FaCalcImp ³ Autor ³ Ricardo A. Canteras   ³ Data ³ 13/02/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Funcao para recalcular os impostos e permitir ao usuario	  ³±±
±±³          ³altera-los na tela de baixas do contas a receber            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ FaCalcImp()                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ FINA070                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function FaCalcImp()   

Local aArea     := GetArea()
Local nVlMinImp := GetNewPar("MV_VL10925",5000)
Local nIrrf     := 0                                                                   
Local nIss	    := 0                                                                   
Local nInss	    := 0 
Local nOrigPis	  := 0 
Local nOrigCofins := 0
Local nOrigCsll	  := 0                                                                 
Local nValImp   := 0                       
Local cAlias    := "SE1"
Local cNum      := SE1->E1_NUM
Local cPrefixo  := SE1->E1_PREFIXO
Local cParcela  := SE1->E1_PARCELA			 
Local nImp10925 := 0
Local lRecIss	 := .F.      
Local lSC5RecIss := SC5->(FieldPos("C5_RECISS")) > 0
Local lPanelFin := If (FindFunction("IsPanelFin"),IsPanelFin(),.F.)
//Controla o Pis Cofins e Csll na baixa  (1-Retem PCC na Baixa ou 2-Retem PCC na Emissão(default) )
Local lPccBxCr := If (FindFunction("FPccBxCr"),FPccBxCr(),.F.)
//Controla IRPJ na baixa
Local lIrPjBxCr		:= If (FindFunction("FIrPjBxCr"),FIrPjBxCr(),.F.)
Local lF070Imp2	:= ExistBlock("F070Imp2")
Local lFa070Imp	:= ExistBlock("Fa070Imp")

//Verifico se a natureza e o cliente estão configurados para o cálculo de PCC
//Caso não estejam, aborto a rotina para que não sejam gerados impostos incorretamente
//Esses impostos gerados nesse cenário ficavam inconsistentes, por isso foi feito esse tratamento
If (SED->ED_CALCPIS != "S" .And. SED->ED_CALCCSL != "S" .And. SED->ED_CALCCOF != "S") .Or.;
	(SA1->A1_RECPIS  # "S#P" .And. SA1->A1_RECCSLL # "S#P" .And. SA1->A1_RECCOFI # "S#P")
	Help("",1,"F070IMPBTN")
	Return .T.
Endif

RegToMemory(cAlias, .F., .F. )

aDadosRet := F040TotMes(dBaixa,@nIndexSE1,@cIndexSE1)	

nImp10925 := ChkAbtImp(SE1->E1_PREFIXO,SE1->E1_NUM,SE1->E1_PARCELA,SE1->E1_MOEDA,"V",SE1->E1_BAIXA)

//Se o valor total for menor que o valor minimo de retenção
If (aDadosRet[1] + nValRec) <= nVlMinImp
	nPisCalc := NoRound((m->e1_valor * (Iif(SED->ED_PERCPIS>0,SED->ED_PERCPIS,GetMv("MV_TXPIS")) / 100)),2)
	nCofCalc := NoRound((m->e1_valor * (Iif(SED->ED_PERCCOF>0,SED->ED_PERCCOF,GetMv("MV_TXCOFIN")) / 100)),2)
	nCslCalc := NoRound((m->e1_valor * (SED->ED_PERCCSL / 100)),2)
	nPis     := 0
	nCofins  := 0
	nCsll    := 0     
	
	If nImp10925 > 0	
		nOrigPis	:= nOldPis
		nOrigCofins := nOldCofins
		nOrigCsll   := nOldCsll
	Endif
	
	nOldPis  	:= m->e1_pis
	nOldCofins  := m->e1_cofins
	nOldCsll  	:= m->e1_csll               	
Else
	nOrigPis	:= nOldPis
	nOrigCofins := nOldCofins
	nOrigCsll   := nOldCsll
	
	nPis 	  := m->e1_pis
	nCofins   := m->e1_cofins
	nCsll     := m->e1_csll   
	nValorRec := m->e1_valor
Endif

IF !("FINA040" $ SE1->E1_ORIGEM) .AND. lSC5RecIss
	SC5->(dbSetOrder(1))
	If SC5->(MsSeek(xfilial("SC5")+SE1->E1_PEDIDO))
		lRecIss := (SC5->C5_RECISS == "1" .And. GetNewPar("MV_DESCISS",.F.))
	Else	
		lRecIss := (SA1->A1_RECISS == "1" .And. GetNewPar("MV_DESCISS",.F.))
	Endif
Else	
	lRecIss := (SA1->A1_RECISS == "1" .And. GetNewPar("MV_DESCISS",.F.))
Endif

//Inicializo os demais impostos
nIrrf := SE1->E1_IRRF
nInss := SE1->E1_INSS	   
nIss  := If(lRecIss, SE1->E1_ISS	,0)

DEFINE MSDIALOG oDlg FROM  69,33 TO 475,525 TITLE STR0190 PIXEL OF oMainWnd  //"Recálculo dos Impostos (PIS/COFINS/CSLL)"
oPanel := TPanel():New(0,0,'',oDlg,, .T., .T.,, ,40,40,.T.,.T. )
oPanel:Align := CONTROL_ALIGN_ALLCLIENT

@ 001,002 GROUP oGrp1 TO 043, 245 LABEL STR0008 OF oPanel PIXEL //"Principal"
@ 050,002 GROUP oGrp2 TO 183, 125 LABEL STR0191 OF oPanel PIXEL //"Valores Originais" 
@ 050,130 GROUP oGrp3 TO 183, 245 LABEL STR0192 OF oPanel PIXEL //"Valores Recalculados na Data da Baixa"

oGrp1:oFont := oFontLbl
oGrp2:oFont := oFontLbl
oGrp3:oFont := oFontLbl                                                

//////////////////
//DADOS TO TITULO
@ 008,004 SAY STR0211				SIZE 31,07 OF oPanel PIXEL //"Prefixo"
@ 008,027 MSGET SE1->E1_PREFIXO	SIZE 25,08 OF oPanel PIXEL When .F.
@ 008,055 SAY STR0212 				SIZE 31,07 OF oPanel PIXEL //"Número"
@ 008,080 MSGET SE1->E1_NUM		SIZE 65,08 OF oPanel PIXEL When .F.
@ 008,150 SAY STR0213				SIZE 31,07 OF oPanel PIXEL //"Parcela"
@ 008,173 MSGET SE1->E1_PARCELA	SIZE 25,08 OF oPanel PIXEL When .F.
@ 008,200 SAY STR0214				SIZE 31,07 OF oPanel PIXEL //"Tipo"
@ 008,213 MSGET oTipo VAR SE1->E1_TIPO	F3 "SE1RDO" SIZE 30,08 OF oPanel PIXEL HASBUTTON
oTipo:lReadOnly := .T.

@ 019,004 SAY STR0014 SIZE 22, 07 OF oPanel PIXEL //"Cliente"
@ 019,027 MSGET oCodCli VAR SE1->E1_CLIENTE F3 "SA1" SIZE 65,08 OF oPanel PIXEL HASBUTTON //READONLY 
oCodCli:lReadOnly := .T.
@ 019,095 MSGET SA1->A1_NOME SIZE 148,08 OF oPanel PIXEL When .F.

@ 030,004 SAY STR0052 				SIZE 31,07 OF oPanel PIXEL //"Natureza"
@ 030,027 MSGET oNaturez VAR SE1->E1_NATUREZ	F3 "SED" SIZE 65,08 OF oPanel PIXEL HASBUTTON
oNaturez:lReadOnly := .T.
@ 030,095 SAY STR0012 				SIZE 31,07 OF oPanel PIXEL //"Emiss„o"
@ 030,118 MSGET SE1->E1_EMISSAO	SIZE 43,08 OF oPanel PIXEL When .F.
@ 030,169 SAY STR0013 				SIZE 49,07 OF oPanel PIXEL //"Vencto.Atual"
@ 030,200 MSGET SE1->E1_VENCREA	SIZE 43,08 OF oPanel PIXEL When .F.


//////////////////
//VALORES ORIGINAIS
nLinha := 60
@ nLinha,005 SAY STR0020 				SIZE 53, 07 OF oPanel PIXEL//"Data Receb."
@ nLinha,046 MSGET dBaixa				SIZE 65, 08 OF oPanel PIXEL HASBUTTON When .F.

nLinha +=12
@ nLinha,005 SAY STR0027 + cDescMoeda	SIZE 53, 08 OF oPanel PIXEL //"Valor Original "
@ nLinha,046 MSGET nOldValor   			SIZE 65, 08 OF oPanel PIXEL HASBUTTON When .F. Picture PesqPict("SE1","E1_VALOR") //"@E 999,999,999,999.99"

nLinha +=12	
@ nLinha,005 SAY STR0028 SIZE 53, 07 OF oPanel PIXEL // "- Abatimentos"
@ nLinha,046 MSGET nTotAbLiq         SIZE 65, 08 OF oPanel PIXEL HASBUTTON When .F.  Picture PesqPict( "SE1","E1_VALOR" )  //"@E 999,999,999,999.99"

nLinha +=12
@ nLinha,005 SAY "- PIS" SIZE 53, 07 OF oPanel PIXEL
@ nLinha,046 MSGET If(nImp10925 > 0, nOldPis, 0) SIZE 65, 08 OF oPanel PIXEL HASBUTTON When .F.  Picture PesqPict( "SE1","E1_VALOR" )  //"@E 999,999,999,999.99"

nLinha +=12
@ nLinha,005 SAY "- COFINS" SIZE 53, 07 OF oPanel PIXEL
@ nLinha,046 MSGET If(nImp10925 > 0, nOldCofins, 0) SIZE 65, 08 OF oPanel PIXEL HASBUTTON When .F.  Picture PesqPict( "SE1","E1_VALOR" )  //"@E 999,999,999,999.99"

nLinha +=12
@ nLinha,005 SAY "- CSLL" SIZE 53, 07 OF oPanel PIXEL
@ nLinha,046 MSGET If(nImp10925 > 0, nOldCsll, 0) SIZE 65, 08 OF oPanel PIXEL HASBUTTON When .F.  Picture PesqPict( "SE1","E1_VALOR" )  //"@E 999,999,999,999.99"

nValorLiq :=  (SE1->E1_VALOR - nTotAbat)

nLinha +=12
@ nLinha,005 SAY STR0187 SIZE 53, 07 OF oPanel PIXEL
@ nLinha,046 MSGET nValorLiq     SIZE 65, 08 OF oPanel PIXEL HASBUTTON When .F. Picture PesqPict("SE1","E1_VLCRUZ") //"@E 999,999,999,999.99"

If nImp10925 == 0
	nPis := 0
	nCofins := 0
	nCsll := 0
EndIf

If lF070Imp2
	nValorRec := nValRec
Endif
///////////////
//Valores atualizados
nLinha := 60
@ nLinha,134 SAY "PIS" SIZE 53, 07 OF oPanel PIXEL
@ nLinha,174 MSGET oPis VAR nPis SIZE 66, 08 OF oPanel PIXEL HASBUTTON Picture PesqPict( "SE1","E1_VALOR" );  //"@E 999,999,999,999.99"
													Valid f070ValRec(@nValorRec,nTotAbLiq,nPis,nCofins,nCsll,nIrrf,nIss,nInss)
nLinha +=12
@ nLinha,134 SAY "COFINS" SIZE 53, 07 OF oPanel PIXEL
@ nLinha,174 MSGET oCofins VAR nCofins SIZE 66, 08 OF oPanel PIXEL HASBUTTON Picture PesqPict( "SE1","E1_VALOR" );  //"@E 999,999,999,999.99"
												Valid f070ValRec(@nValorRec,nTotAbLiq,nPis,nCofins,nCsll,nIrrf,nIss,nInss)

nLinha +=12
@ nLinha,134 SAY "CSLL" SIZE 53, 07 OF oPanel PIXEL
@ nLinha,174 MSGET oCsll VAR nCsll SIZE 66, 08 OF oPanel PIXEL HASBUTTON Picture PesqPict( "SE1","E1_VALOR" ); //"@E 999,999,999,999.99"
													Valid f070ValRec(@nValorRec,nTotAbLiq,nPis,nCofins,nCsll,nIrrf,nIss,nInss)

//Criado para utilização GPS Microsiga
If lFa070Imp

	nLinha +=12	
	@ nLinha,134 SAY "IRRF" SIZE 53, 07 OF oPanel PIXEL
	@ nLinha,174 MSGET oIrrf VAR nIrrf SIZE 66, 08 OF oPanel PIXEL HASBUTTON Picture PesqPict( "SE1","E1_VALOR" ); //"@E 999,999,999,999.99"
														Valid f070ValRec(@nValorRec,nTotAbLiq,nPis,nCofins,nCsll,nIrrf,nIss,nInss)

	If lRecIss
		nLinha +=12
		@ nLinha,134 SAY "ISS" SIZE 53, 07 OF oPanel PIXEL
		@ nLinha,174 MSGET oIss VAR nIss SIZE 66, 08 OF oPanel PIXEL HASBUTTON Picture PesqPict( "SE1","E1_VALOR" ); //"@E 999,999,999,999.99"
															Valid f070ValRec(@nValorRec,nTotAbLiq,nPis,nCofins,nCsll,nIrrf,nIss,nInss)
	EndIf

	nLinha +=12
	@ nLinha,134 SAY "INSS" SIZE 53, 07 OF oPanel PIXEL
	@ nLinha,174 MSGET oInss VAR nInss SIZE 66, 08 OF oPanel PIXEL HASBUTTON Picture PesqPict( "SE1","E1_VALOR" ); //"@E 999,999,999,999.99"
														Valid f070ValRec(@nValorRec,nTotAbLiq,nPis,nCofins,nCsll,nIrrf,nIss,nInss)

EndIf
														
nValImp := nPis + nCofins + nCsll + nIrrf + nInss + nIss

If !lF070Imp2
	nValorRec := (SE1->E1_VLCRUZ - nTotAbLiq - nValImp)
Endif

nLinha +=12
@ nLinha,134 SAY STR0035 SIZE 53,07 OF oPanel PIXEL //"= Valor Recebido"
@ nLinha,174 MSGET oValorRec VAR nValorRec SIZE 66, 08 OF oPanel PIXEL HASBUTTON Valid ( oValRec:Refresh(), FA070ValRec() )Picture PesqPict( "SE1","E1_VALOR" )  //"@E 999,999,999,999.99"

If lPanelFin
	ACTIVATE MSDIALOG oDlg ON INIT FAMYBAR(oDlg,{|| nOpc1 := 1,oDlg:End()},{|| nOpc1 := 2,oDlg:End()}) CENTERED
Else
	ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{|| nOpc1 := 1,oDlg:End()},{|| nOpc1 := 2,oDlg:End()}) CENTERED
Endif

If nOpc1 == 1   
	If MsgYesNo(STR0193+CHR(10)+CHR(13)+STR0194)
		//Gravo novos valores
		RecLock("SE1")
		//Ponto de entrada para guardar os valores dos impostos
		//Criado para utilização GPS Microsiga
		If lFa070Imp 
			ExecBlock("FA070IMP",.F.,.F.,{nIrrf,nIss,nPis,nCofins,nCsll,nInss})
			SE1->E1_IRRF := nIrrf
			SE1->E1_ISS  := If (lRecIss, nIss, SE1->E1_ISS)			
			SE1->E1_INSS := nInss			
		Endif
		SE1->E1_PIS    := nPis
		SE1->E1_COFINS := nCofins
		SE1->E1_CSLL   := nCsll
		MsUnlock()
		
		//Volto o valor do ISS para, caso tenha alteracao neste valor, seja alterado no titulo IS-
		nIss := SE1->E1_ISS
		
		//Faz a alteração com os novos valores
		PcoIniLan("000001")
		FA040AxAlt(cAlias)          
		PcoFinLan("000001")
		
		//Recalculo os valores a serem apresentados na tela de baixa
		nTotAbImp := 0
		nTotAbLiq := 0
		nTotAbat	 := 0	
		nTotAbat  := SumAbatRec(cPrefixo,cNum,cParcela,SE1->E1_MOEDA,"S",dBaixa,@nTotAbImp) 
		nTotAbLiq := nTotAbat - nTotAbImp
		nValorLiq := (SE1->E1_VALOR - nTotAbat + nAcresc) 	
 	    nValRec := nValorRec + nAcresc                     
		lAlterImp := .T.
		
		//Recupero os valores originais dos impostos, caso precise cancelar a operação
		nOldPis    := nOrigPis
		nOldCofins := nOrigCofins
		nOldCsll   := nOrigCsll
		
	Else	
		nPis     := If(nImp10925 > 0 .Or. lPccBxCr, nOldPis, 0)
		nCofins  := If(nImp10925 > 0 .Or. lPccBxCr, nOldCofins, 0)
		nCsll    := If(nImp10925 > 0 .Or. lPccBxCr, nOldCsll, 0)
		lAlterImp := .T.
	EndIf
Else	
	nPis     := If(nImp10925 > 0 .Or. lPccBxCr, nOldPis, 0)
	nCofins  := If(nImp10925 > 0 .Or. lPccBxCr, nOldCofins, 0)
	nCsll    := If(nImp10925 > 0 .Or. lPccBxCr, nOldCsll, 0)
	lAlterImp := .T.
EndIf 

nOpc1 := 0
RestArea(aArea)

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³f070ValRec³ Autor ³ Ricardo A. Canteras   ³ Data ³ 13/02/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Funcao utilizada para dar refresh no valor recebido		  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³f070ValRec(nValorRec,nTotAbLiq,nPis,nCofins,nCsll,nIss,nInss³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function f070ValRec(nValorRec,nTotAbLiq,nPis,nCofins,nCsll,nIrrf,nIss,nInss)

Local nValImp := 0
Local lF070Imp2	:= ExistBlock("F070IMP2")

nValImp := nPis + nCofins + nCsll + nIrrf + nIss + nInss

If lF070Imp2
	nValorRec := (SE1->E1_SALDO - nTotAbLiq - nValImp)
Else
	nValorRec := (SE1->E1_VLCRUZ - nTotAbLiq - nValImp)
Endif

oValorRec:Refresh()

Return

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³F070VldRec³ Autor ³ Ricardo A. Canteras   ³ Data ³ 15/02/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Verifica se o valor recebido eh menor que o valor da soma   ³±±
±±³          ³de Juros, Multa e Desconto                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³F070VldRec(oValRec)							                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ FINA070 																	  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function F070VldRec()

Local lRet   := .T.
Local nSoma  := 0
Local cBxRec := GetNewPar("MV_VLBXREC","1")

If cBxRec == "1"
	lRet := .T.
Else
	nSoma := (nMulta + nJuros) - nDescont
	If nSoma > nValRec
		If cBxRec == "2"
			If MsgYesNo(STR0196+CHR(10)+CHR(13)+STR0197)//"Atenção ! O valor recebido é menor que a soma dos valores de juros, multa e desconto." ## "Deseja confirmar a baixa?"
				lRet := .T.
			Else
				lRet := .F.
			EndIf
		ElseIf cBxRec == "3"
			MsgAlert(STR0196+CHR(10)+CHR(13)+STR0198)//"Atenção ! O valor recebido é menor que a soma dos valores de juros, multa e desconto." ## "Favor modificar os valores."			
			lRet := .F.	
		EndIf
	Else
		lRet := .T.
	EndIf
Endif	

Return lRet           

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³F070ConfirºAutor  ³Mauro Sano          º Data ³  06/04/06   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Funcao para a validar o código capturado pelo leitor de    º±±
±±º          ³ CMC7.                                                      º±± 
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametr  ³ ExpC1 = Contem a string lida do cheque, antes do tratamentoº±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±± 
±±ºRetorno   ³ ExpL1 = Confirma se os dados lidos sao validos             º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ FINA070                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function F070Confirma( cCmc7 )
Local lRet := .F.		// Retorno da validacao da string

If Empty( cCmc7 ) // Caso nao leia nada
	MsgAlert( STR0201 )		// "Passe o cheque novamente no leitor." 
Elseif ( "?" $ cCmc7 ) .OR. Len( AllTrim( cCmc7 ) ) <> 34 // Se encontrar o caracter de erro (?) ou tamnaho menor que o correto (34)
	MsgAlert( STR0202 + " " + STR0201 )		// "Erro na leitura." ### "Passe o cheque novamente no leitor."  
Else
	lRet := .T.
Endif	

Return ( lRet )	    

                   
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³F070Cmc7TcºAutor  ³Mauro Sano          º Data ³  06/04/06   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Funcao para a captura do código CMC7 pelo leitor via       º±±
±±º          ³ teclado.                                                   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºRetorno   ³ ExpA1 = Array contendo os dados lidos do cheque:           º±±
±±º          ³ [1] - Banco | [2] - Agencia | [3] - Conta | [4] - Cheque   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ FINA070                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function F070Cmc7Tc()          
Local cCmc7 	:= Space(35)	// Recebera o conteudo lido do cheque 
Local oCmc7						// Objeto do get do CMC7
Local oDlg						// Monta a tela de captura do codigo  
Local aCmc7Tc	:= {}			// Armazena os dados do cheque; retorno da funcao
Local nOpcx 	:= 0


DEFINE MSDIALOG oDlg TITLE STR0199 FROM 200 , 001 TO 300 , 300 OF oMainWnd PIXEL	// "Leitura do código do cheque"
@ 010 , 018 Say STR0200 SIZE 050 , 050 OF oDlg PIXEL								// "Passe o cheque:"
@ 018 , 018 MSGET oCmc7 VAR cCmc7 PICTURE "@!" SIZE 120,009 OF oDlg PIXEL

DEFINE SBUTTON FROM 35 , 080 TYPE 1 ACTION (Iif (F070Confirma(cCmc7), (oDlg:End(), nOpcx := 1), oCmc7:SetFocus()) )  ENABLE OF oDlg
DEFINE SBUTTON FROM 35 , 110 TYPE 2 ACTION oDlg:End()  ENABLE OF oDlg

ACTIVATE MSDIALOG oDlg CENTERED	

If nOpcx == 1  //Confirmou cheque

	If ExistBlock("F460CMTC")
		aCmc7Tc := ExecBlock("F460CMTC",.F.,.F.,cCmC7)
	Else
		Aadd( aCmc7Tc, SubStr(cCmc7, 2, 3) )	//Banco
		Aadd( aCmc7Tc, SubStr(cCmc7, 14, 6) )	//Cheque
		Aadd( aCmc7Tc, SubStr(cCmc7, 5, 4) )	//Agencia  
		Aadd( aCmc7Tc, SubStr(cCmc7, 25, 8) )	//Conta
	Endif	

Endif	
//<34161168<0010002995>651020209808C 
//<23728016<0010002185>777500568207C

Return( aCmc7Tc )
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³FA070BtOK ³ Autor ³ Adrianne Furtado      ³ Data ³ 20/04/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Validacao no botao OK da tela de baixa					  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ FA070BtOK()			 									  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ FINA070													  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function FA070BtOK()
Local lFa070BtOk := ExistBlock("F070BtOK")
Local lRet := .T.

// Se existe cheque relacionado a baixa, entao veririca se o valor dos cheques utilizados para baixa eh maior que o
// valor recebido. Se verdadeiro, nao permite a baixa ateh que seja equalizado o valor dos cheques com o recebido.
If nSomaCheq > 0
	If Type("nValRec") == "N" .And. Str( nSomaCheq, 17, 2 ) > Str( nValRec, 17, 2 )
	     Help( " ", 1, "CHEQBXNBAT" )
		 lRet := .F.
	 EndIf
EndIf

If lFa070BtOk
	lRet := ExecBlock("F070BtOK",.F.,.F.)
Endif      

If	( SuperGetMv("MV_BXDTFIN",,"1") == "2" .And. !DtMovFin(dDtCredito) ) .Or.;
	( dDtCredito < dBaixa  .And. !GetMv("MV_ANTCRED") )
   lRet := .F.
Endif

Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³fa070Cm1  ³ Autor ³ Reynaldo Miyashita    ³ Data ³ 19/05/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Validacao do campo Correcao monetaria 1                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ fa070Cm1( oCM1 ,oJuros ,oMulta )                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ FINA070                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function fa070Cm1( oCM1 ,oJuros ,oMulta )
Local lOk     := .T.
Local aRet    := {}
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Template GEM - Calcula os valores de ProRata, Multa e Juros sobre o Titulo ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If HasTemplate("LOT") .and. ExistTemplate("GEMTitRec")
		aRet := ExecTemplate("GEMTitRec",.F.,.F.,{SE1->E1_PREFIXO,SE1->E1_NUM,SE1->E1_PARCELA ,nCM1 ,nProRata ,dBaixa })
		If Valtype(aRet) == "A"
			nJuros   := aRet[1]
			nMulta   := aRet[2]
		EndIf
	EndIf
	
	oCM1:refresh()
	oJuros:refresh()
	oMulta:refresh()

Return( lOk )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³fa070PRata³ Autor ³ Reynaldo Miyashita    ³ Data ³ 19/05/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Validacao do campo de Pro Rata Atraso diario                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ fa070PRata( oProRata ,oJuros ,oMulta )                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ FINA070                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function fa070PRata( oProRata ,oJuros ,oMulta )
Local lOk       := .T.
Local aRet      := {}

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Template GEM - Calcula os valores de Multa e Juros sobre o Titulo  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If HasTemplate("LOT") .and. ExistTemplate("GEMTitRec")
		aRet := ExecTemplate("GEMTitRec",.F.,.F.,{SE1->E1_PREFIXO,SE1->E1_NUM,SE1->E1_PARCELA ,nCM1 ,nProRata ,dBaixa })
		If Valtype(aRet) == "A"
			nJuros   := aRet[1]
			nMulta   := aRet[2]
		EndIf
	EndIf
	
If (!Empty(oProRata) .AND. oProRata:lModified)
	dBaixa      := CriaVar("E1_BAIXA")
	nTxMoeda 	:= If(SE1->E1_MOEDA > 1, If(SE1->E1_TXMOEDA > 0, SE1->E1_TXMOEDA,RecMoeda(dBaixa,SE1->E1_MOEDA)),0)
	f070TotMes(dBaixa,.T.,,,,nTxMoeda)
	nIrrf 		:= FCaIrBxCR(SE1->E1_VALOR)
Endif
	oProRata:refresh()
	oJuros:refresh()
	oMulta:refresh()

Return( lOk )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao	 ³FA070AtuTT³ Autor ³Norbert Waage Junior   ³ Data ³ 26/05/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Atualizacao dos totalizadores da tela                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³FA070AtuTT()                               					  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³FINA070				            									  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function FA070AtuTT()

Local nX 		:= 0
Local nTamCol	:= Len(aCols)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Zera acumuladores³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nVlrNomCheq := 0
nVlRefBxChq := 0
nQtdCheq	:= 0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Recalcula totais³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
For nX := 1 to nTamCol

	If !aTail(aCols[nX])
		nVlrNomCheq += aCols[nX][5] // VALOR NOMINAL
		If cPaisLoc == "BRA"
			nVlRefBxChq += aCols[nX][6] // VALOR REF. BAIXA
		EndIf
		nQtdCheq 	++
	EndIf

Next nX

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Atualiza objetos³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Type("oVlrNomCheq") == "O"
	oVlrNomCheq:Refresh()
Endif	
If Type("oQtdCheq") == "O"
	oQtdCheq:Refresh()
Endif	
If Type("oVlRefBxChq") == "O"
	oVlRefBxChq:Refresh()
Endif

Return Nil

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³MenuDef   ³ Autor ³ Ana Paula N. Silva     ³ Data ³21/11/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Utilizacao de menu Funcional                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Array com opcoes da rotina.                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Parametros do array a Rotina:                               ³±±
±±³          ³1. Nome a aparecer no cabecalho                             ³±±
±±³          ³2. Nome da Rotina associada                                 ³±±
±±³          ³3. Reservado                                                ³±±
±±³          ³4. Tipo de Transa‡„o a ser efetuada:                        ³±±
±±³          ³		1 - Pesquisa e Posiciona em um Banco de Dados     ³±±
±±³          ³    2 - Simplesmente Mostra os Campos                       ³±±
±±³          ³    3 - Inclui registros no Bancos de Dados                 ³±±
±±³          ³    4 - Altera o registro corrente                          ³±±
±±³          ³    5 - Remove o registro corrente do Banco de Dados        ³±±
±±³          ³5. Nivel de acesso                                          ³±±
±±³          ³6. Habilita Menu Funcional                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static function MenuDef()
Local aRotina := {}
Local lR5     := GetRpoRelease("R5")                       //Indica se o release e 11.5
                        
If GetNewPar("MV_ACATIVO",.F.)
	aAdd( aRotina,	{ STR0001, "FA070Pes" , 0 , 1,,.F. }) //"Pesquisar"
else
	aAdd( aRotina,	{ STR0001, "AxPesqui" , 0 , 1,,.F. }) //"Pesquisar"
endif

aAdd( aRotina,	{ STR0002, "fa070Visual" , 0 , 2 }) //"Visualizar"
aAdd( aRotina,	{ STR0003, "fA070Tit" , 0 , 4 }) //"Baixar"
aAdd( aRotina,	{ STR0004, "fA070Lot" , 0 , 4 }) //"Lote"
aAdd( aRotina,	{ STR0005, "fA070Can" , 0 , 5 })//"Canc Baixa"
aAdd( aRotina,	{ STR0131, "fA070CAN" , 0 , 5,52})	//"Excluir"
aAdd( aRotina,	{ STR0139, "FA040Legenda", 0 , 6, ,.F.}) //"Le&genda"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica no parametro se gera um Contas a Pagar ³
//³ quando existir taxa na admistradora do cartao,  ³
//³ para habilitar menu de baixa por adminstradora  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If(lR5 .AND. SuperGetMV("MV_LJGERTX",,.F.) .AND. FindFunction("LJXBxAdmFi"))
	aAdd( aRotina,	{ STR0225, "LJXBxAdmFi" , 0 , 4 }) //"Baixa Adm/Fin."
Endif	

Return(aRotina)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³SumAbatPCC³ Autor ³ Mauricio Pequim Jr    ³ Data ³ 05/02/07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Soma titulos de abatimento relacionado a um determinado titu³±±
±±³          ³lo a receber - Pis, Cofins e Csll	                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³SumAbatPcc()                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Nome do Imposto(PIS, COFINS,CSLL ou PCC) 	                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³FINA070                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function SumAbatPCC( cImposto)
Local aAreaPcc:=GetArea()
Local nTotAbtPCC := 0
Local cPrefixo := SE1->E1_PREFIXO
Local cNumero	:= SE1->E1_NUM
Local cParcela := SE1->E1_PARCELA
Local cTipoAtu := SE1->E1_TIPO
Local cCliente := SE1->E1_CLIENTE
Local cLoja 	:= SE1->E1_LOJA
Local cTipo		:= "PCC"
Local cSeq		:= SE5->E5_SEQ

DEFAULT cImposto := "PCC"       

//Se a sequencia de baixa posicionada nao for vazia
//Faz a busca dos valores de Pis e Cofins do titulo
If SE1->E1_SALDO == 0 .AND. !Empty(cSeq)

	//Verifico se eh a ultima sequencia das baixas do titulo
	//Caso nao seja, retorno zero ja que os impostos somente serao abatidos na ultima baixa do titulo
	If Select("__SE5") == 0
		ChkFile("SE5",.F.,"__SE5")
	Else
		dbSelectArea("__SE5")
	Endif
	dbSetOrder(7)
	MsSeek(xFilial("SE5")+cPrefixo+cNumero+cParcela+cTipoAtu+cCliente+cLoja+"zz",.T.)
	dbSkip(-1)
	If (xFilial("SE5")+cPrefixo+cNumero+cParcela+cTipoAtu+cCliente+cLoja+cSeq == ;
		__SE5->(E5_FILIAL+E5_PREFIXO+E5_NUMERO+E5_PARCELA+E5_TIPO+E5_CLIFOR+E5_LOJA+E5_SEQ))

		If Select("__SE1") == 0
			ChkFile("SE1",.F.,"__SE1")
		Else
			dbSelectArea("__SE1")
		Endif
		
		DO CASE
			CASE UPPER(cImposto) == "PIS"
				cTipo := MVPIABT
			CASE UPPER(cImposto) == "COFINS"
				cTipo := MVCFABT
			CASE UPPER(cImposto) == "CSLL"
				cTipo := MVCSABT
			OtherWise
				cTipo := "PCC"
		END CASE		
				
		dbSetOrder( 1 )
		dbSeek( xFilial("SE1")+cPrefixo+cNumero+cParcela+IIF(cTipo == "PCC","",cTipo))
		
		While !Eof() .And. E1_FILIAL == xFilial("SE1") .And. E1_PREFIXO == cPrefixo .And.;
				E1_NUM == cNumero .And. E1_PARCELA == cParcela .and. IIF(cTipo == "PCC",.T.,cTipo == E1_TIPO)
		
		  	If E1_TIPO $ MVCSABT+"/"+MVCFABT+"/"+MVPIABT
				nTotAbtPCC += E1_VLCRUZ
			Endif
		
			dbSkip()
		Enddo
	Endif	
Endif
	
RestArea(aAreaPcc)

Return (NoRound(nTotAbtPCC))


Static Function AjustaSX1()
Local aHelpSpa:={}
Local aHelpPor:={}
Local aHelpEng:={}
Local aArea:=GetArea()
Local cKey		:= ""
      
cKey := "P.FIN07010."

AADD(aHelpPor,'Informe "Sim" se deseja considerar o')
AADD(aHelpPor,'banco da ultima baixa ou "Nao" se deseja')
AADD(aHelpPor,"considerar o banco padrao") 

AADD(aHelpEng,'Select "Yes" if you want to consider the')
AADD(aHelpEng,'last posting bank, or "Nao" if you')
AADD(aHelpEng,"want the default one")

AADD(aHelpSpa,'Informe "Si" si desea considerar el')
AADD(aHelpSpa,'banco de la ultima baja o "No" si desea')
AADD(aHelpSpa,"considerar el banco estandar")             

  
PutSx1( PadR("FIN070",Len(SX1->X1_GRUPO)), "10","Utiliza banco anterior?","¿Utiliza banco anterior?","Uses previous bank?","mv_cha","N",1,0,2,"C","","","","",;
	"mv_par10","Sim","Si","Yes","","Nao","No","No","","","","","","","","","",,,)

PutSX1Help(cKey,aHelpPor,aHelpEng,aHelpSpa)

RestArea(aArea)
Return



/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³F070RTMNBL³ Autor ³ Mauricio Pequim Jr    ³ Data ³ 05/02/07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Informa se o rateio Multinat foi realizado pelo valor bruto ³±±
±±³          ³ou liquido								                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³F070RTMNBL()                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³FINA070                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function F070RTMNBL()

Local aArea		:=GetArea()
Local lRtMnBrt := .T.
Local cChaveSev := SE1->(E1_PREFIXO+E1_NUM+E1_PARCELA+E1_TIPO+E1_CLIENTE+E1_LOJA)+"1"
Local nTotRateio := 0

dbSelectArea("SEV")
dbSetOrder(2)

If SE1->E1_MULTNAT == "1" .And. MsSeek(xFilial("SEV")+cChaveSev)

	//Monto o total do rateio
	While !EOF() .AND. xFilial("SEV")+SEV->(EV_PREFIXO+EV_NUM+EV_PARCELA+EV_TIPO+EV_CLIFOR+;
		EV_LOJA+EV_IDENT) == xFilial("SEV")+cChaveSev

		//Apenas carteira a receber
		If SEV->EV_RECPAG == 'R'
			nTotRateio += SEV->EV_VALOR		
		Endif
		DbSkip()
	EndDo
	
	//Verifico se o rateio foi liquido ou bruto
	If STR(SE1->E1_VALOR,17,2) > STR(nTotRateio,17,2)
		lRtMnBrt := .F.
	Endif

Endif
	
RestArea(aArea)

Return (lRtMnBrt)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³F070VLDBCO³ Autor ³ Andre O Anjos         ³ Data ³ 02/10/07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Valida o cadastro do banco + agencia + conta informados na  ³±±
±±³          ³baixa do titulo (esta funcao foi criada pois a usada anteri ³±±
±±³          ³ormente era falha e permitia o erro do BOPS 133332)		  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³lVdlCnta : Indica se valida Bco+Ag+Cnta ou somente Bco+Ag   ³±±
±±³			 ³lHelp : Indica se exibe o help FA100BCO					  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³F070VLDBCO()                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³FINA070                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function F070VldBco(cBco,cAg,cCnta,lVldCnta,lHelp,cChaveBco,lTelaLote)

Local aArea		:= GetArea()
Local lRet 		:= .T.           

DEFAULT lVldCnta  := .T.  
DEFAULT cChaveBco := "" //Chave para considerar a agencia e/ou conta, quando necessario
DEFAULT lHelp := .F. 
DEFAULT lTelaLote := .F.

dbSelectArea("SA6")
SA6->(dbSetOrder(1))
If Empty(cBco) .OR. Empty(cAg) .OR. (lVldCnta .And. Empty(cCnta)) .OR. ;
	!SA6->(dbSeek(xFilial("SA6") + cBco + cAg + IIf(lVldCnta,cCnta,"")))
	If (!SA6->(Found()) .OR. SA6->A6_COD # cBco) .OR. Empty(cAg) .OR. (lVldCnta .And. Empty(cCnta))
		If SA6->(dbSeek(xFilial("SA6") + cBco + cChaveBco))
			cAg := SA6->A6_AGENCIA
			cCnta := SA6->A6_NUMCON
		Else
			If lHelp
				Help(" ",1,"FA100BCO")
				lValidou := .T.
			EndIf
			lRet := .F.                                         
		Endif
	Else
		If lHelp
			Help(" ",1,"FA100BCO")
			lValidou := .T.
		EndIf
		lRet := .F.                                         
	Endif
ElseIf !Empty(cBco) .AND. !Empty(cAg) .AND. (!lVldCnta .OR. Empty(cCnta))	//Conta
	If SA6->(Found()) .AND. SA6->(A6_FILIAL+A6_COD+A6_AGENCIA+A6_NUMCON) == xFilial("SA6")+cBco+cAg+cCnta
		If !Empty(SA6->A6_NUMCON)
			cCnta := SA6->A6_NUMCON
		Else
			If lHelp
				Help(" ",1,"FA100BCO")
				lValidou := .T.
			EndIf
			lRet := .F.		
		Endif
	Else
		If lHelp
			Help(" ",1,"FA100BCO")
			lValidou := .T.
		EndIf
		lRet := .F.		
	Endif
EndIf

// Verifica se o banco selecionado pode ser usado para baixa do titulo
If lRet .AND. !lTelaLote .and. !Empty( cCnta ) .AND. FindFunction( "FXMultSld" ) .AND. FXMultSld()
	lRet := FXVldBxBco( cBco, cAg, cCnta, SE1->E1_NATUREZ, SE1->E1_MOEDA )
	If !lRet
		lValidou := .T.
	EndIf
EndIf

RestArea(aArea)

Return (lRet)

Function AtulValidou() //Faz parte da F070VLDBCO

lValidou := .F.

Return .T.


/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³CA070TudOk³ Autor ³Fabio Rogerio Pereira  ³ Data ³ 02/05/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Critica se todo o GetDados esta OK                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Objeto a ser verificado.                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ CdaA090                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function CA070TudOk()
Local lRet := .T.
Local nX   := 0

For nX := 1 To Len(aCols)
	If ! CadCheqLOk(.F.)
		lRet := .F.
		Exit
	Endif
Next

Return lRet


/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³F070OkLote³ Autor ³Mauricio Pequim Jr     ³ Data ³ 12/05/08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Critica tela inicial da baixa por lote                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Banco do Lote.				                          ³±±
±±³          ³ ExpC2 = Agencia do Lote.			                          ³±±
±±³          ³ ExpC3 = Conta do Lote.				                          ³±±
±±³          ³ ExpC4 = Lote Financeiro.			                          ³±±
±±³          ³ ExpO1 = Objeto do Lote para foco se necessario             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ FINA070                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function F070OkLote(cBancolt,cAgencialt,cContalt,cLoteFin,oLoteFin)

Local lRet := .T.

IF Empty(cLoteFin)
	oLoteFin:SetFocus()
	lRet := .F.
Endif

If lRet .and. !(CarregaSA6(cBancolt,cAgencialt,cContalt,.T.,,.T.) .and. ValidaTotal(cLoteFin))
	lRet := .F.
Endif

Return lRet



/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³FinA070T   ³ Autor ³ Marcelo Celi Marques ³ Data ³ 27.03.08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Chamada semi-automatica utilizado pelo gestor financeiro   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ FINA070                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function FinA070T(aParam)

	cRotinaExec := "FINA070"
	ReCreateBrow("SE1",FinWindow)      	
	FinA070(,aParam[1],.T.)
	dbSelectArea("SE1")
	ReCreateBrow("SE1",FinWindow)      	

	dbSelectArea("SE1")
	
	INCLUI := .F.
	ALTERA := .F.

Return .T.




/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ FA070Diario º Autor ³ Gustavo Henrique º Data ³  31/05/08  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Seleciona codigo do diario contabil, utilizado na quando   º±±
±±º          ³ selecionada contabilizacao on-line.                        º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Financeiro - Localizacao Portugal                          º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/            
Static Function FA070Diario()

Local lRet := .T.

cCodDiario	:= CTBAVerDia()
lRet := !Empty( cCodDiario )

Return lRet

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³Fa070bAval ³ Autor ³ Marcelo Akama        ³ Data ³ 27/02/09 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Bloco de marcacao       			          				  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ Fa070bAval()		  										  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ FINA070													  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function Fa070bAval(cAliasSE1,cMarca,oValor,oQtda)
Local lRet	:= .T.
lRet:=FA070Integ(.F.)
If lRet
// Verifica se o registro nao esta sendo utilizado em outro terminal
	If (cAliasSE1)->(MsRLock())
		FA070Inverte(cMarca,oValor,oQtda,.F.) // Marca o registro e trava
		lRet := .T.
	Else
		IW_MsgBox(STR0215,STR0144,"STOP")  //"Este titulo está sendo utilizado em outro terminal, não pode ser utilizado na fatura"###"Atenção"
		lRet := .F.
	Endif	
Endif
Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³F070DtRe   ³ Autor ³ Clóvis Magenta	  ³ Data ³ 18/05/09   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Validacao do campo de Data de Recebimento da baixa		  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ F070DtRe()			 									  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ FINA070													  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function F070DtRe()

Local lRet	:= .T.

If	ExistBlock("F070DtRe")
	lRet := ExecBlock("F070DtRe",.F.,.F.)
Endif

Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³F070TotMes³ Autor ³Adrianne Furtado      ³ Data ³10/09/2009³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Verifica o total de notas do Cliente que vencem no mesmo ³±±
±±³          ³mes.                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpD1 - Data de referencia                                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function F070TotMes(dReferencia,lCalcRet,lCalcRA,lSE1,lAltData, nTxMoeda) 

Local aAreaSE1	:= SE1->( GetArea() ) 
Local aRecnos	:= {}      
Local dDataIni	:= FirstDay( dReferencia )   
Local dDataFim	:= LastDay( dReferencia ) 
Local nVlMinImp	:= GetNewPar("MV_VL10925",5000)
Local nValorRc	:= 0
Local nValTit	:= 0
Local nVlrTit	:= 0
Local lSest		:= SE1->(FieldPos("E1_SEST"))	> 0  //Verifica campo de SEST
Local cModRetPIS:= SA1->A1_ABATIMP   
Local lAltValor	:= STR(nValRec,17,2) != STR(nOldValRec,17,2)       
//Controla o Pis Cofins e Csll na baixa
Local lPccBxCr	:= If (FindFunction("FPccBxCr"),FPccBxCr(),.F.)
Local nValOutImp:= 0
Local cModTot	:= GetNewPar( "MV_MT10925", "1" ) 
Local nProp		:= 1
Local nProp2	:= 1
Local nBaseRet	:= 0  //Base de retencao
Local nSE1Reg	:= SE1->(RECNO())
Local nVlMinIrf	:= 0
Local cChaveTit	:= SE1->(E1_PREFIXO+E1_NUM+E1_PARCELA+E1_TIPO+E1_CLIENTE+E1_LOJA)
Local nX 		:= 0 
Local lTodasFil	:= ExistBlock("MT103FRT")
Local aFil10925	:= {}
Local cFilAtu	:= IIf( lFWCodFil, FWGETCODFILIAL, cFilAnt /*SM0->M0_CODFIL*/ )
Local aArea		:= GetArea()

Local aCli10925	:= {}
Local lVerCliLj	:= ExistBlock("F070LOJA")
Local cQuery	:= "" 	
Local nLoop		:= 0
Local aValorBx	:= {}
Local lAltBxVal	:= .F.

// Considera baixas que geram ou nao movimento bancario.
// 1 = Somente os motivos que geram movimento bancario
// 2 = Considera todos os motivos de baixa.
Local lMotBxMBco := (SuperGetMv("MV_MB10925",.t.,"2") == "1")
Local aBxSemBco := {}
Local lCalcIssBx := !Empty( SE5->( FieldPos( "E5_VRETISS" ) ) ) .and. !Empty( SE1->( FieldPos( "E1_SEQBX"   ) ) ) .and. ;
							!Empty( SE1->( FieldPos( "E1_TRETISS" ) ) ) .and. GetNewPar("MV_MRETISS","1") == "2"  //Retencao do ISS pela emissao (1) ou baixa (2)

Local lAplVlMin	:= .T.
Local aDadosImp	:= Array(5)    
Local aTitBsImp	:= {}
Local lGravou	:= .F.
Local lLojaAtu	:= ( GetNewPar( "MV_LJ10925", "1" ) == "1" )   
Local nTamTit	:= TamSX3("E5_PREFIXO")[1]+TamSX3("E5_NUMERO")[1]+TamSX3("E5_PARCELA")[1]+TamSX3("E5_TIPO")[1]
Local nTamTit2	:= TamSX3("E5_PREFIXO")[1]+TamSX3("E5_NUMERO")[1]+TamSX3("E5_PARCELA")[1]
Local lAchouRA	:= .F.
Local nValProp	:= 0
Local nTotAdto	:= 0		
Local lBaixaAbat := .F.
Local lBxCec	:= .F.
Local lNotBax	:= .F.
Local nTotImpost := 0
Local lAglImp	:= .F.
Local aBaixa	:= {}
Local nY
Local cChaveBx	:= ""
Local lDigitado	:= .F.
Local aTitulos	:= {}
Local lImpInFat	:= .F.	
Local nImpRetEmi := 0
Local lCpoVlMin	:= !Empty( SE1->( FieldPos( "E1_APLVLMN" ) ) )
Local lSE1DtBor	:= !Empty( SE1->( FieldPos( "E1_DTBORDE" ) ) )
Local cTitAtual	:= SE1->(E1_PREFIXO+E1_NUM+E1_PARCELA+E1_TIPO+E1_CLIENTE+E1_LOJA)
Local nValAbat	:= 0
Local nKco			:= 0
Local aRecSE1 := {}
Local nRecSE1 := 0
Local nT := 0
Local lVerifPCC := !Empty( SE1->( FieldPos( "E1_FORNPAI" ) ) ) .and. !Empty( SE1->( FieldPos( "E1_PROCPCC"   ) ) )
Local nTamForn :=  0
Local lGrvIRE1 := .F.
Local nINSSAnts := 0
Local nIRAnts   := 0
Local lTBxOTits := .F.  
Local nTotCsAbt := nTotPisAbt := nTotCofAbt := 0
Local lFina330  := IsInCallStack("FINA330")  
Local lUltBx 	:= .F.
Local lRecIss	:= .F. 
Local lIrPjBxCr		:= If (FindFunction("FIrPjBxCr"),FIrPjBxCr(),.F.)
//639.04 Base Impostos diferenciada
Local lBaseImp	:= If(FindFunction('F040BSIMP'),F040BSIMP(2),.F.)
Local lIrrfBxPj := IIf(FindFunction("FIrPjBxCr"),FIrPjBxCr(),.F.)
Local lJurMulDes:= (SuperGetMv("MV_IMPBAIX",.t.,"2") == "1")

//Chamado SDFPWW
Local cAglutPCC := SuperGetMV("MV_PCCAGCL",,"1")
Local cAglutFil := SuperGetMV("MV_PCCAGFL",,"1")
Local aAreaSM0  := {}
Local aAreaSA1  := {}
Local cCGCSM0   := ""
Local cCGCCli   := ""
Local cCliPessoa := ""
Local cEmpAtu   := ""
Local lAdicPcc	:= .T. //Adicionar ao PCC titulo acumulado quando funcao chamada pelo FINA061.
Local aPccFina061	:=	{} // Impostos jah gerados anteriormente.     
Local nPisFina061	:=  	0
Local nCofFina061	:=	0
Local nCslFina061	:=	0
Local nIrrfFina061:=	0	
Local aAreaSE5		:=	SE5->(GetArea())	
Local aBordAc		:= {} //Bordero acumulado pelo FINA061.
Local aPccAc		:= {} //Valores de PCC retidos de borderos cancelados no FINA061.	
Local	aAreaSfq	:=	SFQ->(GetArea())
Local lCalc061		:= .T. //Se manutenção de bordero (FINA590) criando bordero de recebto de imposto nao preciso executar query´s.     
Local cFilBusca		:= ""

#IFDEF TOP
	Local aStruct   := {} 
	Local aCampos   := {} 
	Local cAliasQry := ""  
	Local cSepNeg   := If("|"$MV_CRNEG,"|",",")
	Local cSepRec   := If("|"$MVRECANT,"|",",")
	Local cSepProv  := If("|"$MVPROVIS,"|",",") 
	Local cSepTxa   := If("|"$MVTXA,"|",",")
	Local cSepTx	 := If("|"$MVTAXA,"|",",")
	Local cAliasSE1 := ""
#ELSE
	Local cIndexSE5
	Local nIndexSe5
	Local nLenNumBor := TamSx3("E1_NUMBOR")[1]
#ENDIF 
               
aFill(aDadosImp,0)

PRIVATE aBaixaSE5 := {}
PRIVATE lRaRtImp  := lFinImp .And.FRaRtImp()     //Define se ha retencao de impostos PCC/IRPJ no R.A 

Default lCalcRet := .F.
Default lCalcRA  := .F.
Default lSE1  := .T.   //Variavel para controle do uso do alias alternativo __SE1 para alguns posicionamentos.
Default lAltData := .F.     
Default nTxMoeda :=0

If Alltrim(FunName())="FINA061" .And. lPccBxCr
	 lAdicPcc			:= !lDadosRet                                
	 aPccFina061	:=	aClone(aPccExc) 
Endif     

If Alltrim(FunName())="FINA590" .And. lPccBxCr
	lCalc061		:= lTotMes //Se manutenção de bordero (FINA590) criando bordero de recebto de imposto nao devo executar query´s.
Endif

nIss     := If(Type("nIss") != "N",0,nIss)
nIrrf    := If(Type("nIrrf") != "N",0,nIrrf)
nValComp := If(Type("nValComp") != "N",0,nValComp)
nValLiq  := If(Type("nValLiq") != "N",0,nValLiq)
dBaixa	:= If(Type("dBaixa") != "D",dDataBase,dBaixa)
nTotAbat := If(Type("nTotAbat") != "N",SumAbatRec( SE1->E1_PREFIXO,SE1->E1_NUM,SE1->E1_PARCELA,SE1->E1_MOEDA, "S"  ,dBaixa,,,@nTotCsAbt,@nTotPisAbt,@nTotCofAbt,,, nTxMoeda),nTotAbat)
nDescont := If(Type("nDescont") != "N",0,nDescont)
nMulta   := If(Type("nMulta") != "N",0,nMulta)
nJuros   := If(Type("nJuros") != "N",0,nJuros)
nAcresc  := If(Type("nAcresc") != "N",0,nAcresc)
nDecresc := If(Type("nDecresc") != "N",0,nDecresc)

nTamForn := If(lVerifPCC,TAMSX3("E1_FORNPAI")[1],0) 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ POR MAIS ESTRANHO QUE PARE€A, ESTA FUNCAO DEVE SER CHAMADA AQUI! ³
//³                                                                  ³
//³ A fun‡„o SomaAbat reabre o SE1 com outro nome pela ChkFile para  ³
//³ efeito de performance. Se o alias auxiliar para a SumAbat() n„o  ³
//³ estiver aberto antes da IndRegua, ocorre Erro de & na ChkFile,   ³
//³ pois o Filtro do SE1 uptrapassa 255 Caracteres.                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
SomaAbat("","","","R")

//Verificar ou nao o limite de 5000 para Pis cofins Csll
// 1 = Verifica o valor minimo de retencao
// 2 = Nao verifica o valor minimo de retencao
If !Empty( SE1->( FieldPos( "E1_APLVLMN" ) ) ) .and. SE1->E1_APLVLMN == "2"
	lAplVlMin := .F.
Endif	  

nDiferImp := 0

//Garanto o tamanho dos arrays de retencao
If Len(aDadosRef) < 7
	aDadosRef := Array(7)
	AFill( aDadosRef, 0 ) 
Endif
If Len(adadosRet) < 7
	aDadosRet := Array(7)
	AFill( aDadosRet, 0 ) 
Endif	        

If Type("aRecnosSE1") == "U"
	aRecnosSE1 := {}
Endif

aValorBx := Array(3)
AFill( aValorBx, 0 ) 

If cAglutPCC == "2" .Or. cAglutPCC == "3"
	If lVerCliLj
		Help( ,, 'HELP',, STR0231 , 1, 0) //'Parâmetro MV_PCCAGCL deve ser "1=Não Aglutina" para utilizar o ponto de entrada "F070LOJA."'
		Return .T.
	EndIf
EndIf
If (cAglutFil == "2" .Or. cAglutFil == "3") .And. lTodasFil
	Help( ,, 'HELP',, STR0232 , 1, 0) //'Parâmetro MV_PCCAGFL deve ser "1=Não Aglutina" para utilizar o ponto de entrada "MT103FRT".'
	Return .T.
EndIf

If !lPccBxCr
	Return .T.
Endif

If (nTotCsAbt + nTotPisAbt + nTotCofAbt) > 0
	Return .T.
Endif

If (lPccBxCr .Or.  lIrrfBxPj )  .And. Alltrim(FunName()) <>"FINA061" 
	aAreaSE5		:=	SE5->(GetArea())
	SE5->(DbSetOrder(7))		
	If SE5->(DbSeek(xFilial("SE5")+SE1->E1_PREFIXO+SE1->E1_NUM+SE1->E1_PARCELA+SE1->E1_TIPO+SE1->E1_CLIENTE+SE1->E1_LOJA)) 
 		While !SE5->(Eof()) .And. SE5->E5_PREFIXO+SE5->E5_NUMERO+SE5->E5_PARCELA+SE5->E5_TIPO+SE5->E5_CLIFOR+SE5->E5_LOJA == ;
							  SE1->E1_PREFIXO+SE1->E1_NUM+SE1->E1_PARCELA+SE1->E1_TIPO+SE1->E1_CLIENTE+SE1->E1_LOJA .And.;
							  xFilial("SE5") == SE5->E5_FILIAL				                                                                                                                  
	     		If	"FINA061" $ Upper(SE5->E5_HISTOR)
						If Alltrim(SE5->E5_MOTBX)	= "IRF"  .And. lIrrfBxPj
							nIrrfFina061		:=	SE5->E5_VALOR			
						ElseIf Alltrim(SE5->E5_MOTBX)	= "PCC" .And. lPccBxCr
							nPisFina061	:=	SE5->E5_VRETPIS
							nCofFina061	:=	SE5->E5_VRETCOF	
							nCslFina061	:=	SE5->E5_VRETCSL
						Endif      						     			     		
	     		Endif
	     		SE5->(Dbskip())   
	    Enddo		
  Endif
	SE5->(RestArea(aAreaSE5))	
Endif
                                                      
If (nIrrfFina061 + nPisFina061 + nCofFina061 + nCslFina061 ) > 0
	If 	lIrrfBxPj .And.  nIrrfFina061 > 0
		nIrrf	:=	0
	Endif
	
	If	lPccBxCr .And. (nPisFina061 + nCofFina061 + nCslFina061) > 0
		nPis		:=	0
		nCofins	:=	0
		nCsll		:=	0			
	Endif	             
	
	Return .T.
Endif
If lFina330
	aAreaSA1	:= SA1->(GetArea())
	If SA1->(Dbseek(xFilial("SA1")+SE1->E1_CLIENTE+SE1->E1_LOJA))		
		cModRetPIS:= SA1->A1_ABATIMP                   
	Endif
	SA1->(RestArea(aAreaSA1))	
Endif
//Natureza nao retem imposto
If cModTot == "2" //Considera apenas titulos com retencao de PCC
	
	If aDadosRef[1] == Nil
		AFill( aDadosRef, 0 ) 
	Endif
	
	If aDadosRet[1] == Nil
		AFill( aDadosRet, 0 )
	Endif 
	
	SED->(dbSetOrder(1))
	If lCalcRA
		cNatur := M->E1_NATUREZ
	Else
		cNatur := SE1->E1_NATUREZ	
	Endif
	
	If lFina330 .AND. (xFilial('SA1')+Alltrim(SA1->A1_COD)+Alltrim(SA1->A1_LOJA)) <> (xFilial('SA1')+Alltrim(SE1->E1_CLIENTE)+Alltrim(SE1->E1_LOJA))
		SA1->(Dbseek(xFilial("SA1")+SE1->E1_CLIENTE+SE1->E1_LOJA))		
	Endif
	SED->(MsSeek(xFilial("SED")+cNatur))
	If !(SED->ED_CALCPIS == "S" .and. SA1->A1_RECPIS  $ "S#P") .and.;
		!(SED->ED_CALCCSL == "S" .and. SA1->A1_RECCSLL $ "S#P") .and.;
		!(SED->ED_CALCCOF == "S" .and. SA1->A1_RECCOFI $ "S#P")
		Return .T.
	Endif
Endif
		
// Verificar se eh PA para não gerar recalculo de impostos.
If !lCalcRA .And. (SE1->E1_TIPO $ MVRECANT) .AND. !lFina330
	Return .T.
Endif
     
// Faz verificacao das baixas do titulo.
// Consiste se o motivo gera ou nao movimento bancario.
If lMotBxMBco .And. !lCalcRA .And. !Fa080MovBc() .and. !lFina330
	nValRec += nPis+nCofins+nCsll+nIrrf+nIss
	nOldValRec := nValRec
	nPis := 0
	nCofins := 0
	nCsll := 0
	nIrrf := 0
	Return .T.	
Endif

//Se o valor liquidado for diferente do valor da baixa total, foi um valor digitado
If nValLiq > 0 .and. (nValLiq+nTotAbat) != SE1->E1_SALDO .and. !lMotBxMbco 
	lDigitado := .T.
	If !lAltValor
		Return .T.	
	Endif
Endif   
//Verificação para diferenciar se for baixa parcial
lAltBxVal := STR(SE1->E1_VALOR,17,2) != STR(SE1->E1_SALDO,17,2)
//Se a validação nao partiu do campo de valor
If !lDigitado .and. !lAltValor .and. ( Type('lF070Auto') =='U' .or. ! lF070Auto)
	nValRec += nPis+nCofins+nCsll+nIss
	If !lAltBxVal
		nValRec += Iif (lIrPjBxCr,0, nIrrf)
	Endif	
Endif

If lCalcRet .and. lAplVlMin

		
		aFil10925 := {}
		aAreaSA1  := SA1->(GetArea())
		SA1->(DbSetOrder(1))
		If !lCalcRA
			SA1->(MsSeek(xFilial("SA1")+SE1->(E1_CLIENTE+E1_LOJA)))
		Else
			SA1->(MsSeek(xFilial("SA1")+M->(E1_CLIENTE+E1_LOJA)))
		EndIf

	aAreaSM0   := SM0->(GetArea())
	If !Empty(cCGCCli := SA1->A1_CGC)
		cCliPessoa := SA1->A1_PESSOA
		SA1->(DbSetOrder(3))
		cEmpAtu    := SM0->M0_CODIGO
		cCGCSM0    := SM0->M0_CGC
		SM0->(DbSetOrder(1))
		SM0->(MsSeek(cEmpAnt))
	Else
		aFil10925 := { cFilant }
	EndIf

	//Se parametro "MV_PCCAGCL" existe com conteudo diferente de 1
	If cAglutFil == "2" .Or. cAglutFil == "3"
		
			Do While !SM0->(Eof()) .And. SM0->M0_CODIGO == cEmpAtu
				//Verifica se a filial tem o mesmo CGC/Raiz de CGC
				If (cAglutFil == "2" .And. cCGCSM0 == SM0->M0_CGC) .Or. (cAglutFil == "3" .And. Left(cCGCSM0,8) == Left(SM0->M0_CGC,8))
					//Verifica se na filial existe fornecedor de mesmo CGC/Raiz de CGC
					cFilBusca := IF(lFwCodFil, FWGETCODFILIAL,SM0->M0_CODFIL)
					If (cAglutPCC != "2" .And. cAglutPCC != "3") .Or. (SA1->(MsSeek(xFilial("SA1",cFilBusca)+Left(cCGCCli,Iif(cAglutPCC == "3" .And. cCliPessoa == "J",8,14)))) .And. cCliPessoa == SA1->A1_PESSOA)
						AAdd(aFil10925,cFilBusca)
					EndIf
				EndIf
				SM0->(DbSkip())
			EndDo
		
	//Verifico todas as filiais apenas quando SA1 compartilhado
	ElseIf lTodasFil
		aFil10925 := ExecBlock( "MT103FRT", .F., .F. ) 
	Else
		aFil10925 := { cFilant }
	Endif
	SM0->(RestArea(aAreaSM0))
	SA1->(RestArea(aAreaSA1))
	If lVerCliLj .And. cAglutPCC != "2" .And. cAglutPCC != "3"
		aCli10925 := ExecBlock("F070LOJA",.F.,.F.)
	Endif
	
	AFill( aDadosRef, 0 ) 
	AFill( aDadosRet, 0 ) 
	nValComp := 0

	For nKco := 1 to Len(aFil10925)
	
		dbSelectArea("SE5")
		cFilAnt := aFil10925[nKco]
		
		If !Empty(cCGCCli) .And. ( cAglutPCC == "2" .Or. cAglutPCC == "3" )
			aAreaSA1 := SA1->(GetArea())
			SA1->(DbSetOrder(3))
			SA1->(MsSeek(xFilial("SA1")+Left(cCGCCli,8)))
			aCli10925 := {}
			Do While !SA1->(Eof()) .And. xFilial("SA1")+Left(cCGCCli,8) == SA1->A1_FILIAL+Left(SA1->A1_CGC,8)
				//Garante o mesmo tipo de cliente e Novo no vetor
				If cCliPessoa == SA1->A1_PESSOA .And. AScan(aCli10925,{|x| x[1]+x[2] == SA1->(A1_COD+A1_LOJA) }) == 0
					//Raiz de CNPJ apenas para pessoa Juridica
					If cCliPessoa == "J" .And. cAglutPCC == "3" .And. Left(SA1->A1_CGC,8) == Left(cCGCCli,8)
						SA1->(AAdd(aCli10925,{A1_COD,A1_LOJA}))
					//Para pessoa fisica ou CPF/CNPJ identico
					ElseIf (cCliPessoa == "F" .Or. cAglutPCC == "2") .And. SA1->A1_CGC == cCGCCli
						SA1->(AAdd(aCli10925,{A1_COD,A1_LOJA}))
					EndIf
				EndIf
				SA1->(DbSkip())
			EndDo
			SA1->(RestArea(aAreaSA1))
		EndIf
		
		#IFDEF TOP 
		        
			aCampos := { "E5_VALOR","E5_VRETPIS","E5_VRETCOF","E5_VRETCSL","E5_VLJUROS","E5_VLMULTA","E5_VLDESCO", "E5_FORNADT", "E5_LOJAADT"}

			If lCalcIssBx
				aadd(aCampos,"E5_VRETISS")
			Endif
			
			If lIrrfBxPj                               
				aadd(aCampos,"E5_VRETIRF")
			Endif

			aStruct := SE5->( dbStruct() ) 	
		
			SE5->( dbCommit() ) 
		   
		  	cAliasQry := GetNextAlias()
		
			cQuery := "SELECT E5_PREFIXO,E5_NUMERO,E5_PARCELA,E5_TIPO,E5_CLIFOR,E5_LOJA, E5_TXMOEDA, "
			cQuery += "E5_SEQ,E5_VALOR,E5_VRETPIS,E5_VRETCOF,E5_VRETCSL,E5_DATA,E5_VLJUROS,"
			cQuery += "E5_VLMULTA,E5_VLDESCO,E5_PRETPIS,E5_PRETCOF,E5_PRETCSL,E5_MOTBX,"
			cQuery += "E5_DOCUMEN,E5_RECPAG,E5_FORNADT,E5_LOJAADT, E5_HISTOR, "
	
			If lCalcIssBx
				cQuery += "E5_VRETISS,"
			Endif

			If lIrrfBxPj                               
				cQuery += "E5_VRETIRF,"
			Endif
			
			IF lRaRtImp
	   			cQuery += "E5_PRINSS, E5_PRISS, "
			Endif

			cQuery += "R_E_C_N_O_ RECNOSE5 FROM "
			cQuery += RetSqlName( "SE5" ) + " SE5 " 
			cQuery += "WHERE " 
			cQuery += "E5_FILIAL='"    + xFilial("SE5")       + "' AND " 		

			If Len(aCli10925) > 0  //Verificar determinados CLIENTES (raiz do CNPJ)
				cQuery += "( " 	
				For nLoop := 1 to Len(aCli10925) 
					cQuery += "(E5_CLIFOR ='"   + aCli10925[nLoop,1]  + "' AND " 	
					cQuery += "E5_LOJA='"       + aCli10925[nLoop,2]  + "') OR "
				Next			
				//Retiro o ultimo OR
				cQuery := Left( cQuery, Len( cQuery ) - 4 ) 
				cQuery += ") AND " 	
			Else  //Apenas o Fornecedor Atual
				If !lCalcRA
					cQuery += "E5_CLIFOR='"		+ SE1->E1_CLIENTE			+ "' AND " 	
					If lLojaAtu  //Considero apenas a loja atual
						cQuery += "E5_LOJA='"		+ SE1->E1_LOJA				+ "' AND "
					EndIf
				Else                                                            
					cQuery += "E5_CLIFOR='"		+ M->E1_CLIENTE			+ "' AND " 	
					If lLojaAtu  //Considero apenas a loja atual					
						cQuery += "E5_LOJA='"		+ M->E1_LOJA				+ "' AND "
					EndIf
				Endif
			Endif
			
			cQuery += "E5_DATA>= '"		+ DToS( dDataIni )      + "' AND "		
			cQuery += "E5_DATA<= '"		+ DToS( dDataFim )      + "' AND " 
			cQuery += "E5_TIPO NOT IN " + FormatIn(MVABATIM,"|") + " AND "
			cQuery += "E5_TIPO NOT IN " + FormatIn(MV_CRNEG,cSepNeg)  + " AND "
			cQuery += "E5_TIPO NOT IN " + FormatIn(MVPROVIS,cSepProv) + " AND "
			cQuery += "E5_RECPAG = 'R' AND "
			cQuery += "E5_MOTBX <> 'FAT' AND "	
			cQuery += "E5_MOTBX <> 'STP' AND "				
			cQuery += "E5_MOTBX <> 'LIQ' AND "			
			cQuery += "E5_SITUACA <> 'C' AND "	
	
			If  Alltrim(FunName()) <>"FINA061" 
				cQuery += "E5_HISTOR NOT LIKE  '%FINA061%' AND "	
   		Endif
   		
			//Apenas titulos que tem retencao de PIS,Cofins e CSLL
			If cModTot == "2"
				cQuery += " ((E5_VRETPIS > 0 OR E5_VRETCOF > 0 OR E5_VRETCSL > 0) OR (E5_MOTBX = 'CMP')) AND "
		   	Endif
			
			cQuery += "D_E_L_E_T_=' '"                                             
			cQuery += "AND NOT EXISTS ( "
			cQuery += "SELECT A.E5_NUMERO "
			cQuery += "FROM "+RetSqlName("SE5")+" A "
			cQuery += "WHERE A.E5_FILIAL='"+xFilial("SE5")+"' AND "
			cQuery +=		"A.E5_PREFIXO=SE5.E5_PREFIXO AND "
			cQuery +=		"A.E5_NUMERO=SE5.E5_NUMERO AND "
			cQuery +=		"A.E5_PARCELA=SE5.E5_PARCELA AND "
			cQuery +=		"A.E5_TIPO=SE5.E5_TIPO AND "
			cQuery +=		"A.E5_CLIFOR=SE5.E5_CLIFOR AND "
			cQuery +=		"A.E5_LOJA=SE5.E5_LOJA AND "
			cQuery +=		"A.E5_SEQ=SE5.E5_SEQ AND "
			cQuery +=		"A.E5_TIPODOC='ES' AND "
			cQuery +=		"A.E5_RECPAG<>'R' AND "
			cQuery +=		"A.D_E_L_E_T_<>'*')"
			
			cQuery := ChangeQuery( cQuery ) 
			
			dbUseArea( .T., "TOPCONN", TcGenQry(,,cQuery), cAliasQry, .F., .T. )
			
			For nLoop := 1 To Len( aStruct ) 
				If !Empty( AScan( aCampos, AllTrim( aStruct[nLoop,1] ) ) ) .and. Alltrim(Upper(aStruct[nLoop,2])) <> "C"
					TcSetField( cAliasQry, aStruct[nLoop,1], aStruct[nLoop,2],aStruct[nLoop,3],aStruct[nLoop,4])
				EndIf 			
			Next
			
			While !( cAliasQRY )->( Eof()) 

				nImpRetEmi := 0
				lUltBx := .F.
				// Consiste se o motivo gera ou nao movimento bancario.
				If lMotBxMBco              
					If !Fa080MovBc((cAliasQRY)->E5_MOTBX)  
						AAdd( aBxSemBco, ( cAliasQRY )->E5_PREFIXO+( cAliasQRY )->E5_NUMERO+( cAliasQRY )->E5_PARCELA+( cAliasQRY )->E5_TIPO+( cAliasQRY )->E5_CLIFOR+( cAliasQRY )->E5_LOJA) 
						(cAliasQRY)->(DbSkip())
						Loop
					Endif
				Endif
				
				//Verificar ou nao o limite de 5000 para Pis cofins Csll
				// 1 = Verifica o valor minimo de retencao
				// 2 = Nao verifica o valor minimo de retencao (estes nao serao considerados na soma dos 5000)
				If !Empty( SE1->( FieldPos( "E1_APLVLMN" ) ) ) .and. SE1->E1_APLVLMN == "2"
					(cAliasQRY)->(DbSkip())
					Loop
				Endif	  

				nProp := 1			
				nProp2 := 1
				nBasePcc := 0
				If ( cAliasQRY )->E5_VRETPIS + ( cAliasQRY )->E5_VRETCOF + ( cAliasQRY )->E5_VRETCSL > 0
					If lSE1
						cAliasSE1 := "SE1"
					Else
						cAliasSE1 := "__SE1"					
					Endif					
					(cAliasSE1)->(dbSetOrder(1))
					IF !((cAliasSE1)->(MsSeek(xFilial("SE1")+(cAliasQRY)->(E5_PREFIXO+E5_NUMERO+E5_PARCELA+E5_TIPO+E5_CLIFOR+E5_LOJA))))
						(cAliasQRY)->(DbSkip())
						Loop
					Endif	 										
               
					//Se for PA, verifica se houve baixa parcial para carregar varivael
					//nProp2 com o percentual proporcional para recalcular os impostos.
					If (cAliasSE1)->E1_TIPO $ MVRECANT
						If (cAliasSE1)->E1_VALOR - (cAliasSE1)->E1_SALDO > 0
							nProp2 := (((cAliasSE1)->E1_VALOR - (cAliasSE1)->E1_SALDO) / (cAliasSE1)->E1_VALOR)
						Endif
					Endif
      			    If (cAliasSE1)->E1_MOEDA > 1
    					nVlrTit := (cAliasSE1)->(E1_VLCRUZ)
      			    Else 
						nVlrTit := (cAliasSE1)->(E1_VALOR)
					EndIf

					If lSest
						nVlrTit += (cAliasSE1)->E1_SEST
					Endif					

					If lCalcIssBx
						nVlrTit -= (cAliasSE1)->E1_ISS
					Endif
					If !lIrrfBxPj
						nImpRetEmi := (cAliasSE1)->(E1_IRRF)
					ElseIf lRaRtImp .and. (cAliasSE1)->E1_TIPO $ MVRECANT
						nVlrTit += (cAliasSE1)->E1_IRRF
		
					EndIf
					nImpRetEmi += (cAliasSE1)->(E1_INSS+E1_ISS)

					If lRaRtImp .and. lPccBxCr .and. (Empty( ( cAliasQRY )->E5_PRETPIS ) .Or. Empty( ( cAliasQRY )->E5_PRETCOF ) .Or. Empty( ( cAliasQRY )->E5_PRETCSL ))
						If (cAliasQry)->E5_TIPO $ MVRECANT
							nVlrTit += (cAliasQRY)->(E5_VRETPIS+E5_VRETCOF+E5_VRETCSL)
						Endif
					EndIf						

					// Posiciona no cliente para verificar modo de retencao de ISS
								// Posiciona no cliente para verificar modo de retencao de ISS
					If !lCalcIssBx
						SA1->( dbSetOrder( 1 ) )
	        			SA1->( MsSeek( xFilial( "SA1" ) + (cAliasQRY)->( E5_CLIFOR + E5_LOJA ) ) )
	        			IF !("FINA040" $ (cAliasSE1)->E1_ORIGEM)
							SC5->(dbSetOrder(1))
							If SC5->(MsSeek(xfilial("SC5")+(cAliasSE1)->E1_PEDIDO))
								lRecIss := (SC5->C5_RECISS == "1" .And. GetNewPar("MV_DESCISS",.F.))
							Else	
								lRecIss := (SA1->A1_RECISS == "1" .And. GetNewPar("MV_DESCISS",.F.))
							Endif
						Else	
							lRecIss := (SA1->A1_RECISS == "1" .And. GetNewPar("MV_DESCISS",.F.))
						Endif
						If !lRecIss	// Cliente nao recolhe
							nImpRetEmi -= (cAliasSE1)->E1_ISS
						EndIf	
					EndIf
					/*If !lCalcIssBx
						SA1->( dbSetOrder( 1 ) )
	        			SA1->( MsSeek( xFilial( "SA1" ) + (cAliasQRY)->( E5_CLIFOR + E5_LOJA ) ) )
						If SA1->A1_RECISS != "1"	// Cliente nao recolhe
							nImpRetEmi -= (cAliasSE1)->E1_ISS
						EndIf	
					EndIf*/

					If lBaseImp
						nBasePCC := (cAliasSE1)->(E1_BASEPIS) 						
					Endif

					//aTitulos
					//[1] Chave do titulo
					//[2]	Valor Baixado bruto
					//[3]	PCC Retido
					//[4]	PCC Retido em baixa intermediaria (sera somado para compor a proporcao na ultima baixa)
					//[5]	IR Retido em baixa intermediaria (sera somado para compor a proporcao na ultima baixa)
															
					//Array para somar os valores de titulo e valores baixados
					If (nX := Ascan(aTitulos,{|x| x[1] == (cAliasQRY)->(E5_PREFIXO+E5_NUMERO+E5_PARCELA+E5_TIPO+E5_CLIFOR+E5_LOJA)})) == 0
						aadd(aTitulos,{(cAliasQRY)->(E5_PREFIXO+E5_NUMERO+E5_PARCELA+E5_TIPO+E5_CLIFOR+E5_LOJA),nVlrTit,0,0,0})
						nX := Len(aTitulos)
						If(cAliasSE1)->(E1_MOEDA)>1
					   		aTitulos[nX,2] := (cAliasSE1)->(E1_VALOR)*(cAliasQRY)-> E5_TXMOEDA
						Else
							aTitulos[nX,2] := nVlrTit	//Somo as bases 
						Endif
					Endif
					If !lJurMulDes .or. lUltBx
						nValProp := (cAliasQRY)->(E5_VALOR+E5_VLDESCO-E5_VLJUROS-E5_VLMULTA)
					Else
						nValProp := (cAliasQRY)->(E5_VALOR)
					Endif
					
					nValAbat := SumAbatRec( (cAliasSE1)->E1_PREFIXO,(cAliasSE1)->E1_NUM,(cAliasSE1)->E1_PARCELA,1, "V"  ,(cAliasQRY)->E5_DATA)
					If !lSE1 .and. (IsInCallStack("FA070LOT") .or. IsInCallStack("FA110AUT"))  //volta a posicionar no registro NF      
 						(cAliasSE1)->(dbSetOrder(1))
 						(cAliasSE1)->(MsSeek(xFilial("SE1")+(cAliasQRY)->(E5_PREFIXO+E5_NUMERO+E5_PARCELA+E5_TIPO+E5_CLIFOR+E5_LOJA)))
					Endif
					If (cAliasSE1)->E1_MOEDA>1
						If nValProp + nValAbat == (cAliasSE1)->E1_VALOR*(cAliasQRY)->E5_TXMOEDA
							nValProp += nValAbat
						Endif
					Else
						If nValProp + nValAbat == (cAliasSE1)->E1_VALOR
							nValProp += nValAbat
						Endif
                    Endif
					If lCalcIssBx .and. lRaRtImp // valor proporcional para saber qual foi a base de calculo do PCC
						nValProp += (cAliasQRY)->(E5_VRETISS)
					Endif
					
					If lIrrfBxPj .and. lRaRtImp // valor proporcional para saber qual foi a base de calculo do PCC
						nValProp += (cAliasQRY)->(E5_VRETIRF)
					EndIf

					aTitulos[nX,3]+= nValProp - IF(lJurMulDes, (cAliasQRY)->(E5_VLJUROS+E5_VLMULTA) , 0)

					//Baixa pelo valor bruto digitado nao devo somar os impostos exceto na baixa final (saldo = 0)
					//(MV_BQ10925 = 1)
					If (Empty( ( cAliasQRY )->E5_PRETPIS ) .Or. Empty( ( cAliasQRY )->E5_PRETCOF ) .Or. Empty( ( cAliasQRY )->E5_PRETCSL ))
						If (aTitulos[nX,2] == nImpRetEmi+( ABS(nValAbat-nImpRetEmi) )+ aTitulos[nX,3]+(cAliasQRY)->(E5_VRETPIS+E5_VRETCOF+E5_VRETCSL)+Iif(lIrrfBxPj,(cAliasQRY)->(E5_VRETIRF),0)) .OR.;
								(cAliasQry)->E5_TIPO $ MVRECANT 
							nValProp += (cAliasQRY)->(E5_VRETPIS+E5_VRETCOF+E5_VRETCSL)+ If (!lRaRtImp, aTitulos[nX,4],0)
							lUltBx := .T.
						ElseIf lMotBxMBco .and. (cAliasSE1)->E1_SALDO == 0
			 				If Ascan(aBxSemBco,(cAliasQRY)->(E5_PREFIXO+E5_NUMERO+E5_PARCELA+E5_TIPO+E5_CLIFOR+E5_LOJA) ) > 0
			 			  		nValProp += (cAliasQRY)->(E5_VRETPIS+E5_VRETCOF+E5_VRETCSL)+ If (!lRaRtImp, aTitulos[nX,4],0)+nValAbat
						  		lUltBx := .T.
						  	Endif
						Else
							aTitulos[nX,4]+= (cAliasQRY)->(E5_VRETPIS+E5_VRETCOF+E5_VRETCSL)
							If lRaRtImp 
								nValProp += (cAliasQRY)->(E5_VRETPIS+E5_VRETCOF+E5_VRETCSL)
							EndIf
							If lIrrfBxPj .and. !lRaRtImp // valor proporcional para saber qual foi a base de calculo do PCC
								aTitulos[nX,4]+=(cAliasQRY)->(E5_VRETIRF)
							Endif
							If (cAliasSE1)->E1_MOEDA>1 .and. nValProp + nValAbat == (cAliasSE1)->E1_VALOR*(cAliasQRY)->E5_TXMOEDA
								nValProp += nValAbat
							Endif
						Endif
						aTitulos[nX,3]+= (cAliasQRY)->(E5_VRETPIS+E5_VRETCOF+E5_VRETCSL) 
					Else
						If lIrrfBxPj .and. !lRaRtImp 
							aTitulos[nX,5]+=(cAliasQRY)->(E5_VRETIRF)//quando não reteve PCC mas reteve IR soma as bases
						Endif
					EndIf						

					//Somo o valor dos impostos retidos na emissao para proporcionalizar corretamente
					If lPccBxCr .And. nValAbat >= 0 
						If (aTitulos[nX,2] == aTitulos[nX,3] + nValAbat)
							nValProp += nValAbat
							If !lUltBx .and. !lRaRtImp  
								nValProp += aTitulos[nX,4]// para nao somar 2x este valor (logo acima já somamos)
							Endif
						Elseif (lIrrfBxPj .and. !lRaRtImp) 
							If aTitulos[nX,2] == ( aTitulos[nX,3] + nValAbat + (cAliasQRY)->(E5_VRETIRF) )
								nValProp += nValAbat	
								If aTitulos[nX,5]> 0
									nValProp += aTitulos[nX,5] + If(lUltBx, (cAliasQRY)->(E5_VRETIRF),0)
								Else
									nValProp += (cAliasQRY)->(E5_VRETIRF)
								Endif   
							Endif
						Endif
             		Else 
	              		If (aTitulos[nX,2] == aTitulos[nX,3] + nImpRetEmi )
							nValProp += nImpRetEmi
						Endif
       			    EndIf               

					//Somo o valor dos impostos retidos no RA para proporcionalizar corretamente
					If lPccBxCr .And. lRaRtImp .And. IsInCallStack("FINA070")
						If (aTitulos[nX,2] == aTitulos[nX,3] + (cAliasQRY)->(E5_VRETPIS+E5_VRETCOF+E5_VRETCSL))
							nValProp += (cAliasQRY)->(E5_VRETPIS+E5_VRETCOF+E5_VRETCSL)
						ElseIf (cAliasQRY)->E5_MOTBX == "CMP" .and. (cAliasQRY)->E5_TIPO <> MVRECANT
							nValProp += (cAliasQRY)->(E5_VRETPIS+E5_VRETCOF+E5_VRETCSL)	
						Endif
						nValProp += (cAliasQRY)->(E5_PRISS+E5_PRINSS)	
       				EndIf               

				 
				    If (cAliasQRY)->(E5_PREFIXO+E5_NUMERO+E5_PARCELA+E5_TIPO+E5_CLIFOR+E5_LOJA) == cChaveTit					
						If SuperGetMv("MV_IMPBAIX",.F.,"2") == "1"
							nProp := nValProp / nVlrTit
//							nProp := (nValProp-(If(lCalcIssBx,(cAliasQRY)->(E5_VRETISS),0))-(If(lIrrfBxPj,(cAliasQRY)->(E5_VRETIRF),0))) /nVlrTit
						Else
							nProp := (nValProp-(cAliasQRY)->E5_VLDESCO) /nVlrTit
//							nProp := ((nValProp-(cAliasQRY)->E5_VLDESCO)-(If(lCalcIssBx,(cAliasQRY)->(E5_VRETISS),0))-(If(lIrrfBxPj,(cAliasQRY)->(E5_VRETIRF),0))) /nVlrTit
						EndIf
					Else
						lTBxOTits := .T.
						nINSSAnts += (cAliasSE1)->(E1_INSS) 
						nIRAnts   += (cAliasSE1)->(E1_IRRF)
						If SuperGetMv("MV_IMPBAIX",.F.,"2") == "1" .or. SE1->E1_SALDO == 0
							nProp := nValProp /nVlrTit							
						Else                                                    
							nProp := (nValProp-(cAliasQRY)->E5_VLDESCO) /nVlrTit													
						EndIf
					EndIf									
					
					IF ( cAliasQRY )->E5_MOTBX =="PCC"
						nProp := 1
					Endif

					If nBasePcc > 0
						nVlrTit := nBasePcc * nProp
					Endif
                    
					If lIrrfBxPj .and. !lRaRtImp // valor proporcional para saber qual foi a base de calculo do PCC
						aTitulos[nX,3] +=(cAliasQRY)->(E5_VRETIRF)
					EndIf

					//Incrementa a base de calculo para gerar os titulos se nao for
					//compensação entre carteiras e baixa de impostos via bordero
					//O registro de compensacao do PA nao deve integrar a base (este eh feito com o registro do
					//titulo principal
					If !(cAliasQRY)->E5_MOTBX $ "PCC#CEC"  .or. ;
						!((cAliasQRY)->E5_MOTBX $ "CMP" .AND. (cAliasQRY)->E5_TIPO $ MVRECANT)
						If nBasePcc == 0
							nVlrTit := nVlrTit * nProp
						Endif
						aDadosRef[1] += nVlrTit * nProp2					
					EndIf
				Else				
					If !(cAliasQRY)->E5_MOTBX $ "PCC#CEC" .and. ;
						!((cAliasQRY)->E5_MOTBX $ "CMP" .and. SUBSTR((cAliasQRY)->E5_DOCUMEN,nTamTit2+1,3) $ MV_CPNEG) .and. ; //Desconsiderar compensacoes com NDF
						!((cAliasQRY)->E5_MOTBX $ "CMP" .AND. (cAliasQRY)->E5_TIPO $ MVRECANT)
						aDadosRef[1] += (cAliasQRY)->(E5_VALOR+E5_VLDESCO-E5_VLJUROS-E5_VLMULTA)					
					EndIf
				Endif

				//Incrementa a base de calculo para gerar os titulos          
				If (cAliasQRY)->E5_MOTBX == "PCC" 
					aDadosRef[1] += nVlrTit - (cAliasQRY)->(E5_VRETPIS+E5_VRETCOF+E5_VRETCSL) 
				EndIf
				
				//Guardo os valores compensados entre carteiras para recompor a base de calculo
				If (cAliasQRY)->E5_MOTBX == "CEC" .And. (cAliasQRY)->E5_RECPAG == "R" .And.;
					(cAliasQRY)->(E5_PREFIXO+E5_NUMERO+E5_PARCELA+E5_TIPO+E5_CLIFOR+E5_LOJA) == cChaveTit
					nValComp += (cAliasQRY)->E5_VALOR
				EndIf
				
				//Recalcula o valor do titulo principal para adicionar no campo
				// com os valores de titulos retidos
				If AliasIndic("SFQ")
					aAreaQry := GetArea()
					SFQ->(dbSetOrder(1))
					cChaveSE5 := (cAliasQRY)->(E5_PREFIXO+E5_NUMERO+E5_PARCELA+E5_TIPO+E5_CLIFOR+E5_LOJA)
					If SFQ->(MsSeek(xFilial("SFQ")+cAliasSE1+cChaveSE5))
						While SFQ->(!Eof()) .and. SFQ->FQ_FILIAL == xFilial("SFQ") .And.;
								SFQ->(FQ_PREFORI+FQ_NUMORI+FQ_PARCORI+FQ_TIPOORI+FQ_CFORI+FQ_LOJAORI) == cChaveSE5
							cChaveSFQ := SFQ->(FQ_PREFDES+FQ_NUMDES+FQ_PARCDES+FQ_TIPODES+FQ_CFDES+FQ_LOJADES)
							(cAliasSE1)->(dbSetOrder(1))
							If (cAliasSE1)->(MsSeek(xFilial(cAliasSE1)+cChaveSFQ))
								aDadosRef[1] += (cAliasSE1)->E1_VALOR 
							EndIf							
							SFQ->(dbSkip())        	
						EndDo
					EndIf							
					RestArea(aAreaQry)
				EndIf
				
				If (cAliasQRY)->E5_MOTBX == "CMP" .OR. ( cAliasQRY )->E5_VRETPIS + ( cAliasQRY )->E5_VRETCOF + ( cAliasQRY )->E5_VRETCSL  > 0

					If lSE1
						cAliasSE1 := "SE1"
					Else
						cAliasSE1 := "__SE1"
					Endif					

					(cAliasSE1)->(dbSetOrder(1))
					IF !((cAliasSE1)->(MsSeek(xFilial("SE1")+(cAliasQRY)->(E5_PREFIXO+E5_NUMERO+E5_PARCELA+E5_TIPO+E5_CLIFOR+E5_LOJA))))
						(cAliasQRY)->(DbSkip())
						Loop
					Endif	 										
				
					//aTitulos
					//[1] Chave do titulo
					//[2]	Valor Baixado bruto
					//[3]	PCC Retido
					//[4]	PCC Retido em baixa intermediaria (sera somado para compor a proporcao na ultima baixa)
															
					If (cAliasQRY)->E5_MOTBX == "CMP" .AND. !(cAliasQRY)->E5_TIPO $ MVRECANT

						nVlrTit := (cAliasSE1)->(E1_VALOR+E1_IRRF+E1_ISS+E1_INSS)
	
						If lSest
							nVlrTit += (cAliasSE1)->E1_SEST
						Endif					
	
						If lCalcIssBx
							nVlrTit -= (cAliasSE1)->E1_ISS
						Endif
	
						If lIrrfBxPj
							nVlrTit -= (cAliasSE1)->(E1_IRRF)
						EndIf

						//Array para somar os valores de titulo e valores baixados
						If (nX := Ascan(aTitulos,{|x| x[1] == (cAliasQRY)->(E5_PREFIXO+E5_NUMERO+E5_PARCELA+E5_TIPO+E5_CLIFOR+E5_LOJA)})) == 0
							aadd(aTitulos,{(cAliasQRY)->(E5_PREFIXO+E5_NUMERO+E5_PARCELA+E5_TIPO+E5_CLIFOR+E5_LOJA),nVlrTit,0,0,0})
							nX := Len(aTitulos)
							aTitulos[nX,2] := nVlrTit		//Somo as bases
						Endif
						aTitulos[nX,3]+= (cAliasQRY)->(E5_VALOR+E5_VLDESCO-E5_VLJUROS-E5_VLMULTA)
					EndIf

					If (Ascan(aTitBsImp,{|x| x == (cAliasQRY)->(E5_PREFIXO+E5_NUMERO+E5_PARCELA+E5_TIPO+E5_CLIFOR+E5_LOJA+E5_SEQ)})) = 0
						
						If !((cAliasQRY)->E5_MOTBX $ "CMP#FAT#STP") 
							//Armazeno os valores calculados por titulo.
							If !(cAliasQRY)->E5_TIPO $ MVRECANT 
								If (cAliasSE1)->E1_PIS > 0
									aDadosImp[1] += (cAliasSE1)->E1_PIS * nProp
									lGravou := .T.
								EndIf	
				
								If (cAliasSE1)->E1_COFINS > 0
									aDadosImp[2] += (cAliasSE1)->E1_COFINS * nProp
									lGravou := .T.							
								EndIf 	
			
								If (cAliasSE1)->E1_CSLL > 0
									aDadosImp[3] += (cAliasSE1)->E1_CSLL * nProp
									lGravou := .T.							
								EndIf	
							Else //Se for PA com imposto pendente
								If (cAliasSE1)->E1_PIS > 0 .and. (cAliasQRY)->E5_PRETPIS == '1'
									aDadosImp[1] += (cAliasSE1)->E1_PIS * nProp
									lGravou := .T.
								EndIf	  
								If (cAliasSE1)->E1_COFINS > 0 .and. (cAliasQRY)->E5_PRETCOF == '1'
									aDadosImp[2] += (cAliasSE1)->E1_COFINS * nProp
									lGravou := .T.
								EndIf	  
								If (cAliasSE1)->E1_CSLL > 0 .and. (cAliasQRY)->E5_PRETCSL == '1'
									aDadosImp[3] += (cAliasSE1)->E1_CSLL * nProp
									lGravou := .T.
								EndIf	  
							EndIf	
						EndIf
						
						//Somo valores dos impostos de IRRF/ISS/INSS dos titulos que foram compensados
						//por PA que não reteve impostos, para recompor a base de calculo
						If (cAliasSE1)->(E1_IRRF+E1_ISS+E1_INSS) > 0 .AND. !(cAliasQRY)->E5_TIPO $ MVRECANT .And.;
							(cAliasQRY)->(E5_PREFIXO+E5_NUMERO+E5_PARCELA+E5_TIPO+E5_CLIFOR+E5_LOJA) != cChaveTit
							aAreaSE5 := (cAliasQRY)->(GetArea())
							dbSelectArea("SE5")
							dbSetOrder(7)
							If MsSeek(xFilial("SE5")+SUBSTR((cAliasQRY)->E5_DOCUMEN,1,nTamTit)+(cAliasQRY)->E5_FORNADT+;
								(cAliasQRY)->E5_LOJAADT)
								If	SE5->E5_PRETPIS == "1" .Or. SE5->E5_PRETCOF == "1" .Or. SE5->E5_PRETCSL == "1" 
									aDadosImp[4] += (cAliasSE1)->(E1_IRRF+E1_ISS+E1_INSS)
									lGravou := .T.							
								EndIf
							EndIf
							RestArea(aAreaSE5)
						EndIf	
						
						//Somo valores dos impostos de IRRF/ISS/INSS dos titulos do tipo PA (Pagto Adiantado) 
						//que foram compensados, para recompor a base de calculo(nBaseImp) do PIS/COF/CSL
						If (cAliasSE1)->(E1_IRRF+E1_ISS+E1_INSS) > 0 .AND. (cAliasQRY)->E5_TIPO $ MVRECANT .And.;
							SUBSTR((cAliasQRY)->E5_DOCUMEN,1,nTamTit)+(cAliasQRY)->(E5_FORNADT+E5_LOJAADT) == cChaveTit
							aAreaSE5 := (cAliasQRY)->(GetArea())
							dbSelectArea("SE5")
							dbSetOrder(7)
							If MsSeek(xFilial("SE5")+(cAliasQRY)->(E5_PREFIXO+E5_NUMERO+E5_PARCELA+E5_TIPO+E5_CLIFOR+E5_LOJA))
								If AliasIndic("SFQ")
									SFQ->(dbSetOrder(2))
									cChaveSE5 := SE5->(E5_PREFIXO+E5_NUMERO+E5_PARCELA+E5_TIPO+E5_CLIFOR+E5_LOJA)
									If SFQ->(MsSeek(xFilial("SFQ")+"SE5"+cChaveSE5))
										lAchouRA := .T.
									EndIf							
								EndIf
								If	(SE5->E5_PRETPIS $ "1# " .Or. SE5->E5_PRETCOF $ "1# " .Or. SE5->E5_PRETCSL $ "1# ") .And.;
									!lAchouRA									
									aDadosImp[5] += (cAliasSE1)->(E1_IRRF+E1_ISS+E1_INSS)
									lGravou := .T.							
									AAdd( aRecnos, ( cAliasQRY )->RECNOSE5 ) 
								EndIf
							EndIf
							RestArea(aAreaSE5)
						EndIf	
						
						If lGravou
							AADD(aTitBsImp,(cAliasQRY)->(E5_PREFIXO+E5_NUMERO+E5_PARCELA+E5_TIPO+E5_CLIFOR+E5_LOJA+E5_SEQ))
							lGravou := .F.
						Endif		  
					Endif
				EndIf				
					
				nImpostos := 0						
	         
				If ( cAliasQRY )->E5_PRETPIS $ " #4"
					aDadosRef[1] += (cAliasQRY)->E5_VRETPIS * nProp2
					nImpostos += (cAliasQRY)->E5_VRETPIS * nProp2
					//Armazeno os valores calculados por titulo, retirando os valores retidos
					If ( cAliasQRY )->E5_VRETPIS + ( cAliasQRY )->E5_VRETCOF + ( cAliasQRY )->E5_VRETCSL > 0 .And.;
						!((cAliasQRY)->E5_MOTBX $ "CMP#FAT#STP") .AND. !(cAliasQRY)->E5_TIPO $ MVRECANT 
						aDadosImp[1] -= (cAliasQRY)->E5_VRETPIS * nProp2
					Endif					
				EndIf 								
		
				If ( cAliasQRY )->E5_PRETCOF $ " #4"
					aDadosRef[1] += ( cAliasQRY )->E5_VRETCOF * nProp2
					nImpostos += (cAliasQRY)->E5_VRETCOF * nProp2
					//Armazeno os valores calculados por titulo, retirando os valores retidos
					If ( cAliasQRY )->E5_VRETPIS + ( cAliasQRY )->E5_VRETCOF + ( cAliasQRY )->E5_VRETCSL  > 0 .And.;
						!((cAliasQRY)->E5_MOTBX $ "CMP#FAT#STP") .AND. !(cAliasQRY)->E5_TIPO $ MVRECANT 
						aDadosImp[2] -= (cAliasQRY)->E5_VRETCOF * nProp2
					Endif					
				EndIf 								
		
				If ( cAliasQRY )->E5_PRETCSL $ " #4"
					aDadosRef[1] += ( cAliasQRY )->E5_VRETCSL * nProp2 
					nImpostos += (cAliasQRY)->E5_VRETCSL * nProp2
					//Armazeno os valores calculados por titulo, retirando os valores retidos
					If ( cAliasQRY )->E5_VRETPIS + ( cAliasQRY )->E5_VRETCOF + ( cAliasQRY )->E5_VRETCSL  > 0 .And.;
						!((cAliasQRY)->E5_MOTBX $ "CMP#FAT#STP") .AND. !(cAliasQRY)->E5_TIPO $ MVRECANT 
						aDadosImp[3] -= (cAliasQRY)->E5_VRETCSL * nProp2
					Endif					
				EndIf 								

				//PRET == "1" -> pendente de retenção	
				If ( cAliasQRY )->E5_PRETPIS == "1" .Or. ( cAliasQry )->E5_PRETCOF == "1" .Or. ( cAliasQry )->E5_PRETCSL == "1" 
					
					If ( cAliasQRY )->E5_PRETPIS == "1"
						aDadosRef[2] += ( cAliasQRY )->E5_VRETPIS * nProp2
					EndIf	
		
					If ( cAliasQRY )->E5_PRETCOF == "1"
						aDadosRef[3] += ( cAliasQRY )->E5_VRETCOF * nProp2 
					EndIf	
							
					If ( cAliasQRY )->E5_PRETCSL == "1"
						aDadosRef[4] += ( cAliasQRY )->E5_VRETCSL * nProp2        
					EndIf 		

					AAdd( aRecnos, ( cAliasQRY )->RECNOSE5 ) 
			
				// Acumula os valores das baixas realizadas parcialmente
				// para ser utilizada na analise de possiveis problemas 
				// de arredondamento que ocorrem na baixa total.
				Else

					If Empty((cAliasQRY)->E5_PRETPIS)
						aValorBx[1] += ( cAliasQRY )->E5_VRETPIS
					EndIf	
		
					If Empty((cAliasQRY)->E5_PRETCOF)
						aValorBx[2] += ( cAliasQRY )->E5_VRETCOF
					EndIf	
							
					If Empty((cAliasQRY)->E5_PRETCSL)
						aValorBx[3] += ( cAliasQRY )->E5_VRETCSL
					EndIf 	
	
				Endif
                 
				( cAliasQRY )->( dbSkip()) 
				
		   EndDo 
		   
			// Fecha a area de trabalho da query 
		   ( cAliasQRY )->( dbCloseArea() ) 
		   dbSelectArea( "SE1" ) 
		
		#ELSE 				//CODEBASE
		
			ChkFile("SE5",.F.,"NEWSE5")
			cIndexSE5 := CriaTrab(,.f.)
				
			cQuery := "E5_FILIAL='"      + xFilial( "SE5" )     + "' .AND. "
			If Len(aCli10925) > 0  //Verificar determinados CLIENTES (raiz do CNPJ)
				cQuery += "( " 	
				For nLoop := 1 to Len(aCli10925) 
					cQuery += "(E5_CLIFOR ='"   + aCli10925[nLoop,1]  + "' .AND. " 	
					cQuery += "E5_LOJA='"       + aCli10925[nLoop,2]  + "') .OR."
				Next			
				cQuery := Left( cQuery, Len( cQuery ) - 5 ) 
				cQuery += ") .AND. " 	
			Else  //Apenas Fornecedor atual
				If !lCalcRA
					cQuery += "E5_CLIFOR='"     + SE1->E1_CLIENTE        + "' .AND. " 	
					If lLojaAtu  //Considero apenas a loja atual
						cQuery += "E5_LOJA='"        + SE1->E1_LOJA           + "' .AND. "
					EndIf
				Else
					cQuery += "E5_CLIFOR='"     + M->E1_CLIENTE        + "' .AND. " 	
					If lLojaAtu  //Considero apenas a loja atual
						cQuery += "E5_LOJA='"        + M->E1_LOJA           + "' .AND. "			
					EndIf
				Endif
			Endif
			cQuery += "DTOS(E5_DATA)>='" + DToS( dDataIni ) + "' .AND. "		
			cQuery += "DTOS(E5_DATA)<='" + DToS( dDataFim ) + "' .AND. "
			cQuery += "E5_RECPAG == 'R' .AND. "
			cQuery += "E5_MOTBX <> 'FAT' .AND. "	
			cQuery += "E5_MOTBX <> 'STP' .AND. "  			
			cQuery += "E5_SITUACA <> 'C' .AND. "	
			//Apenas titulos que tem retencao de PIS,Cofins e CSLL
			If cModTot == "2"
				cQuery += " ((E5_VRETPIS > 0 .OR. E5_VRETCOF > 0 .OR. E5_VRETCSL > 0 ) .OR. (E5_MOTBX = 'CMP')) .AND. "			
		   	Endif
                    
			cQuery += "!(E5_TIPO $ '"+MVABATIM + "/" + MV_CPNEG + "/" + MVPROVIS + "')"
			
			IndRegua("SE5",cIndexSE5,"DTOS(E5_DATA)",, cQuery ,"")
			nIndexSE5 :=RetIndex("SE5")+1
			dbSetIndex(cIndexSE5+OrdBagExt())
			dbSetorder(nIndexSe5)
			SE5->( dbSetOrder( nIndexSE5 ) )
			SE5->( dbSeek( DTOS( dDataIni ), .T. ) ) 
			
			While !SE5->( Eof() ) .And. SE5->E5_DATA >= dDataIni .And. SE5->E5_DATA <= dDataFim 
								
				nImpRetEmi := 0

				//Verifica se tem baixa cancelada
				If TemBxCanc(SE5->(E5_PREFIXO+E5_NUMERO+E5_PARCELA+E5_TIPO+E5_CLIFOR+E5_LOJA+E5_SEQ))
					SE5->( dbskip())
					loop
				EndIf
				
				// Consiste se o motivo gera ou nao movimento bancario.
				If lMotBxMBco              
					If !Fa080MovBc(SE5->E5_MOTBX)
						SE5->(DbSkip())
						Loop
					Endif
				Endif
	
				//Verificar ou nao o limite de 5000 para Pis cofins Csll
				// 1 = Verifica o valor minimo de retencao
				// 2 = Nao verifica o valor minimo de retencao (estes nao serao considerados na soma dos 5000)
				If !Empty( SE1->( FieldPos( "E1_APLVLMN" ) ) ) .and. SE1->E1_APLVLMN == "2"
					SE5->(DbSkip())
					Loop
				Endif	  

				nProp := 1			
				nProp2 := 1			
				If SE5->(E5_VRETPIS + E5_VRETCOF + E5_VRETCSL) > 0
					SE1->(dbSetOrder(1))
					IF !(SE1->(MsSeek(xFilial("SE1")+SE5->(E5_PREFIXO+E5_NUMERO+E5_PARCELA+E5_TIPO+E5_CLIFOR+E5_LOJA))))
						SE5->(DbSkip())
						Loop
					Endif	 										


					//Se for RA, verifica se houve baixa parcial para carregar varivael
					//nProp2 com o percentual proporcional para recalcular os impostos.
					If SE1->E1_TIPO $ MVRECANT
						If SE1->E1_VALOR - SE1->E1_SALDO > 0
							nProp2 := ((SE1->E1_VALOR - SE1->E1_SALDO) / SE1->E1_VALOR)
						Endif
					Endif

					nVlrTit := SE1->(E1_VALOR+E1_IRRF+E1_ISS+E1_INSS)

					If lSest
						nVlrTit += SE1->E1_SEST
					Endif					

					If lCalcIssBx
						nVlrTit -= SE1->E1_ISS
					Endif

					If lIrrfBxPj
						nVlrTit -= SE1->E1_IRRF
					EndIf

					nImpRetEmi := nVlrTit - SE1->E1_VALOR

					//aTitulos
					//[1] Chave do titulo
					//[2]	Valor Baixado bruto
					//[3]	PCC Retido
					//[4]	PCC Retido em baixa intermediaria (sera somado para compor a proporcao na ultima baixa)

					//Array para somar os valores de titulo e valores baixados
					If (nX := Ascan(aTitulos,{|x| x[1] == SE5->(E5_PREFIXO+E5_NUMERO+E5_PARCELA+E5_TIPO+E5_CLIFOR+E5_LOJA)})) == 0
						aadd(aTitulos,{SE5->(E5_PREFIXO+E5_NUMERO+E5_PARCELA+E5_TIPO+E5_CLIFOR+E5_LOJA),nVlrTit,0,0})
						nX := Len(aTitulos)
						aTitulos[nX,2] := nVlrTit		//Somo as bases
					Endif

					nValProp := SE5->(E5_VALOR+E5_VLDESCO-E5_VLJUROS-E5_VLMULTA)
					nValAbat := SumAbatRec(SE1->E1_PREFIXO,SE1->E1_NUM,SE1->E1_PARCELA,SE1->E1_MOEDA, "V"  ,SE5->E5_DATA)
					If nValProp + nValAbat == SE1->E1_VALOR
						nValProp += nValAbat
					Endif

					If lCalcIssBx
						nValProp += SE5->(E5_VRETISS)
					Endif					

					If lIrrfBxPj
						nValProp += SE5->(E5_VRETIRF)
					EndIf

					//Somo o valor dos impostos retidos na emissao para proporcionalizar corretamente
					If (aTitulos[nX,2] == aTitulos[nX,3]+SE5->(E5_VRETPIS+E5_VRETCOF+E5_VRETCSL)+ nImpRetEmi )
						nValProp += nImpRetEmi
					Endif

					aTitulos[nX,3]+= nValProp

					//Baixa pelo valor bruto digitado nao devo somar os impostos exceto na baixa final (saldo = 0)
					//(MV_BQ10925 = 1)
					If (Empty( SE5->E5_PRETPIS ) .Or. Empty( SE5->E5_PRETCOF ) .Or. Empty( SE5->E5_PRETCSL ))
						If aTitulos[nX,2] == nImpRetEmi + aTitulos[nX,3] + SE5->(E5_VRETPIS+E5_VRETCOF+E5_VRETCSL)
							nValProp += aTitulos[nX,4]+SE5->(E5_VRETPIS+E5_VRETCOF+E5_VRETCSL)
						Else
							aTitulos[nX,4]+= SE5->(E5_VRETPIS+E5_VRETCOF+E5_VRETCSL)
						Endif
						aTitulos[nX,3]+= SE5->(E5_VRETPIS+E5_VRETCOF+E5_VRETCSL)
					EndIf						

					nProp := nValProp /nVlrTit

					IF SE5->E5_MOTBX =="PCC"
						nProp := 1
					Endif

					//Incrementa a base de calculo para gerar os titulos se nao for
					//compensação entre carteiras e baixa de impostos via bordero
					If !SE5->E5_MOTBX $ "PCC#CEC"
						nVlrTit := nVlrTit * nProp
						aDadosRef[1] += nVlrTit * nProp2					
					EndIf
				Else				                                                     
					If !SE5->E5_MOTBX $ "PCC#CEC" .and. ;
						!(SE5->E5_MOTBX $ "CMP" .and. SUBSTR(SE5->E5_DOCUMEN,nTamTit2+1,3) $ MV_CPNEG) //Desconsiderar compensacoes com NDF
						aDadosRef[1] += SE5->(E5_VALOR+E5_VLDESCO-E5_VLJUROS-E5_VLMULTA)
					EndIf
				Endif

				//Incrementa a base de calculo para gerar os titulos          
				If SE5->E5_MOTBX == "PCC" 
					aDadosRef[1] += nVlrTit - SE5->(E5_VRETPIS+E5_VRETCOF+E5_VRETCSL) 
				EndIf
				
				//Guardo os valores compensados entre carteiras para recompor a base de calculo
				If SE5->E5_MOTBX == "CEC" .And. SE5->E5_RECPAG == "R" .And.;
					SE5->(E5_PREFIXO+E5_NUMERO+E5_PARCELA+E5_TIPO+E5_CLIFOR+E5_LOJA) == cChaveTit
					nValComp += SE5->E5_VALOR
				EndIf

				//Recalcula o valor do titulo principal para adicionar no campo
				// com os valores de titulos retidos
				If AliasIndic("SFQ")
					aAreaQry := GetArea()
					SFQ->(dbSetOrder(1))
					cChaveSE5 := SE5->(E5_PREFIXO+E5_NUMERO+E5_PARCELA+E5_TIPO+E5_CLIFOR+E5_LOJA)
					If SFQ->(MsSeek(xFilial("SFQ")+"SE1"+cChaveSE5))
						While SFQ->(!Eof()) .and. SFQ->FQ_FILIAL == xFilial("SFQ") .And.;
								SFQ->(FQ_PREFORI+FQ_NUMORI+FQ_PARCORI+FQ_TIPOORI+FQ_CFORI+FQ_LOJAORI) == cChaveSE5
							cChaveSFQ := SFQ->(FQ_PREFDES+FQ_NUMDES+FQ_PARCDES+FQ_TIPODES+FQ_CFDES+FQ_LOJADES)
							SE1->(dbSetOrder(1))
							If SE1->(MsSeek(xFilial("SE1")+cChaveSFQ))
								aDadosRef[1] += SE1->E1_VALOR 
							EndIf							
							SFQ->(dbSkip())        	
						EndDo
					EndIf							
					RestArea(aAreaQry)
				EndIf

				If SE5->E5_MOTBX == "CMP" .OR. SE5->(E5_VRETPIS + E5_VRETCOF + E5_VRETCSL) > 0
					
					SE1->(dbSetOrder(1))
					SE1->(MsSeek(xFilial("SE1")+SE5->(E5_PREFIXO+E5_NUMERO+E5_PARCELA+E5_TIPO+E5_CLIFOR+E5_LOJA)))
               
					//aTitulos
					//[1] Chave do titulo
					//[2]	Valor Baixado bruto
					//[3]	PCC Retido
					//[4]	PCC Retido em baixa intermediaria (sera somado para compor a proporcao na ultima baixa)
															
					If SE5->E5_MOTBX == "CMP" .AND. !SE5->E5_TIPO $ MVRECANT

						nVlrTit := SE1->(E1_VALOR+E1_IRRF+E1_ISS+E1_INSS)
	
						If lSest
							nVlrTit += SE1->E1_SEST
						Endif					
	
						If lCalcIssBx
							nVlrTit -= SE1->E1_ISS
						Endif
	
						If lIrrfBxPj
							nVlrTit -= SE1->E1_IRRF
						Endif

						//Array para somar os valores de titulo e valores baixados
						If (nX := Ascan(aTitulos,{|x| x[1] == SE5->(E5_PREFIXO+E5_NUMERO+E5_PARCELA+E5_TIPO+E5_CLIFOR+E5_LOJA)})) == 0
							aadd(aTitulos,{SE5->(E5_PREFIXO+E5_NUMERO+E5_PARCELA+E5_TIPO+E5_CLIFOR+E5_LOJA),nVlrTit,0,0})
							nX := Len(aTitulos)
							aTitulos[nX,2] := nVlrTit		//Somo as bases
						Endif
						aTitulos[nX,3]+= SE5->(E5_VALOR+E5_VLDESCO-E5_VLJUROS-E5_VLMULTA)
					EndIf

					If (Ascan(aTitBsImp,{|x| x == SE5->(E5_PREFIXO+E5_NUMERO+E5_PARCELA+E5_TIPO+E5_CLIFOR+E5_LOJA+E5_SEQ)})) = 0
						
						If !(SE5->E5_MOTBX $ "CMP#FAT#STP")  

							//Armazeno os valores calculados por titulo.
							If !SE5->E5_TIPO $ MVRECANT 
								If SE1->E1_PIS > 0
									aDadosImp[1] += SE1->E1_PIS * nProp
									lGravou := .T.
								EndIf	
				
								If SE1->E1_COFINS > 0
									aDadosImp[2] += SE1->E1_COFINS * nProp
									lGravou := .T.							
								EndIf	
			
								If SE1->E1_CSLL > 0
									aDadosImp[3] += SE1->E1_CSLL * nProp
									lGravou := .T.							
								EndIf	
							Else //Se for PA com imposto pendente
								If SE1->E1_PIS > 0 .and. SE5->E5_PRETPIS == '1'
									aDadosImp[1] += SE1->E1_PIS * nProp
									lGravou := .T.
								EndIf	  

								If SE1->E1_COFINS > 0 .and. SE5->E5_PRETCOF == '1'
									aDadosImp[2] += SE1->E1_COFINS * nProp
									lGravou := .T.
								EndIf	  

								If SE1->E1_CSLL > 0 .and. SE5->E5_PRETCSL == '1'
									aDadosImp[3] += SE1->E1_CSLL * nProp
									lGravou := .T.
								EndIf	  
							EndIf
						EndIf	       
						

						If lGravou
							AADD(aTitBsImp,SE5->(E5_PREFIXO+E5_NUMERO+E5_PARCELA+E5_TIPO+E5_CLIFOR+E5_LOJA+E5_SEQ))
							lGravou := .F.
						Endif
					Endif				
				EndIf      
				
				nImpostos := 0  

				If SE5->E5_PRETCOF $ " #4"
					aDadosRef[1] += SE5->E5_VRETPIS * nProp2                                
					nImpostos += SE5->E5_VRETPIS * nProp2                                   
					//Armazeno os valores calculados por titulo, retirando os valores retidos
					If SE5->(E5_VRETPIS + E5_VRETCOF + E5_VRETCSL) > 0 .And.;
						!(SE5->E5_MOTBX $ "CMP#FAT#STP") .And. !SE5->E5_TIPO $ MVRECANT  
						aDadosImp[1] -= SE5->E5_VRETPIS * nProp2
					Endif
				EndIf 								

				If SE5->E5_PRETCOF $ " #4"
					aDadosRef[1] += SE5->E5_VRETCOF * nProp2
					nImpostos += SE5->E5_VRETCOF * nProp2
					//Armazeno os valores calculados por titulo, retirando os valores retidos
					If SE5->(E5_VRETPIS + E5_VRETCOF + E5_VRETCSL) > 0 .And.;
						!(SE5->E5_MOTBX $ "CMP#FAT#STP") .And. !SE5->E5_TIPO $ MVRECANT  
						aDadosImp[2] -= SE5->E5_VRETCOF * nProp2
					Endif					
				EndIf 								
	
				If SE5->E5_PRETCSL  $ " #4"
					aDadosRef[1] += SE5->E5_VRETCSL * nProp2
					nImpostos += SE5->E5_VRETCSL * nProp2
					//Armazeno os valores calculados por titulo, retirando os valores retidos
					If SE5->(E5_VRETPIS + E5_VRETCOF + E5_VRETCSL) > 0 .And.;
						!(SE5->E5_MOTBX $ "CMP#FAT#STP") .And. !SE5->E5_TIPO $ MVRECANT 
						aDadosImp[3] -= SE5->E5_VRETCSL * nProp2
					Endif					
				EndIf 								

				If lCalcIssBx
					aDadosRef[1] += SE5->E5_VRETISS
				Endif

				If lIrrfBxPj
					aDadosRef[1] += SE5->E5_VRETIRF
				Endif
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿	
				//³ Adiciona ao array apenas os titulos que calcularam retencao         ³  
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						
				If	SE5->E5_PRETPIS == "1" .Or. SE5->E5_PRETCOF == "1" .Or. SE5->E5_PRETCSL == "1" 
					      
					If SE5->E5_PRETPIS == "1"
						aDadosRef[2] += SE5->E5_VRETPIS * nProp2
					EndIf	
	
					If SE5->E5_PRETCOF == "1"
						aDadosRef[3] += SE5->E5_VRETCOF * nProp2
					EndIf	
						
					If SE5->E5_PRETCSL == "1"
						aDadosRef[4] += SE5->E5_VRETCSL * nProp2       
					EndIf 						
                    
					AAdd( aRecnos, SE5->( RECNO() ) )			

				// Acumula os valores das baixas realizadas parcialmente
				// para ser utilizada na analise de possiveis problemas 
				// de arredondamento que ocorrem na baixa total.
				ElseIf  Empty(SE5->E5_PRETPIS) .Or. Empty(SE5->E5_PRETCOF) .Or. Empty(SE5->E5_PRETCSL)

					If Empty(SE5->E5_PRETPIS)
						aValorBx[1] += SE5->E5_VRETPIS
					EndIf	
		
					If Empty(SE5->E5_PRETCOF)
						aValorBx[2] += SE5->E5_VRETCOF
					EndIf	
							
					If Empty(SE5->E5_PRETCSL)
						aValorBx[3] += SE5->E5_VRETCSL
					EndIf 	

				EndIf	

				SE5->( dbSkip() ) 								
						 
			EndDo          
			NEWSE5->(dbCloseArea())			
			dbSelectArea("SE5")
			dbClearFil()
			RetIndex( "SE5" )
			If !Empty(cIndexSE5)
				FErase (cIndexSE5+OrdBagExt())
			Endif
			dbSetOrder(1)

		#ENDIF    

	Next nKco

	aBordAc	:=	{} 	
	aPccAc	:= {}	
	If lPccBxCr  .And. Alltrim(FunName()) <>"FINA061" .And. lCalc061	
		
		#IFDEF TOP 		        
		   
		  	cAliasQry := GetNextAlias()
			
			cQuery := "SELECT E1_PREFIXO, E1_NUM, E1_PARCELA, E1_TIPO, E1_CLIENTE, E1_LOJA, E1_NUMBOR,  "
			cQuery += "E1_VALOR, E1_SABTPIS, E1_SABTCOF, E1_SABTCSL, E1_SALDO, E1_BAIXA, "		
			cQuery += "R_E_C_N_O_ RECNOSE1 FROM "
			cQuery += RetSqlName( "SE1" ) + " SE1 " 
			cQuery += "WHERE " 
			cQuery += "E1_FILIAL='"    	+ xFilial("SE1")     + "' AND " 			
			cQuery += "E1_CLIENTE='"		+ SE1->E1_CLIENTE		+ "' AND " 	
			cQuery += "E1_LOJA='"			+ SE1->E1_LOJA			+ "' AND "				
			cQuery += " E1_EMISSAO>= '"	+ DToS( dDataIni )   + "' AND "		
			cQuery += " E1_EMISSAO<= '"	+ DToS( dDataFim )   + "' AND " 
			cQuery += " (E1_SABTPIS > 0 OR E1_SABTCOF > 0 OR E1_SABTCSL > 0)  AND "
			cQuery += " E1_NUMBOR <> ' ' AND  "			                     
			cQuery += "D_E_L_E_T_=' '"                                             
								
	 		cQuery 		:= ChangeQuery(cQuery)                 
			dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasQry)

			While !( cAliasQRY )->( Eof())              						
				Aadd(aBordAc,{( cAliasQRY )->E1_SABTPIS,( cAliasQRY )->E1_SABTCOF,( cAliasQRY )->E1_SABTCSL,( cAliasQRY )->E1_VALOR})
				( cAliasQRY )->(Dbskip())       		
			Enddo
		  			
		   ( cAliasQRY )->( dbCloseArea() ) // Fecha a area de trabalho da query   
		   
		  	cAliasQry := GetNextAlias()
			
			cQuery := "SELECT E5_PREFIXO, E5_NUMERO, E5_PARCELA, E5_TIPO, E5_CLIFOR, E5_LOJA, E5_HISTOR, "
			cQuery += "E5_VALOR, E5_PRETPIS, E5_PRETCOF, E5_PRETCSL, E5_VRETPIS, E5_VRETCSL, E5_VRETCOF, "		
			cQuery += "R_E_C_N_O_ RECNOSE5 FROM "
			cQuery += RetSqlName( "SE5" ) + " SE5 " 
			cQuery += "WHERE " 
			cQuery += "E5_FILIAL='"    + 	xFilial("SE5")   	+ "' AND " 			
			cQuery += "E5_CLIFOR='"		+ 	SE1->E1_CLIENTE	+ "' AND " 	
			cQuery += "E5_LOJA='"		+ 	SE1->E1_LOJA		+ "' AND "												
			cQuery += " E5_DATA >= '"	+ 	DToS( dDataIni )  + "' AND "		
			cQuery += " E5_DATA <= '"	+ 	DToS( dDataFim )  + "' AND " 
			cQuery += " ((E5_PRETPIS = '8' AND E5_PRETCOF = '8' AND E5_PRETCSL = '8')  OR  "
			cQuery += " (E5_PRETPIS = '9' AND E5_PRETCOF = '9' AND E5_PRETCSL = '9'))  AND  "

			cQuery += "D_E_L_E_T_=' '"                                             
								
	 		cQuery 		:= ChangeQuery(cQuery)                 
			dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasQry)
     
     		While !( cAliasQRY )->( Eof())              										
				
				Aadd(aPccAc,{(( cAliasQRY )->E5_VRETPIS * (-1)) , ;
									(( cAliasQRY )->E5_VRETCOF * (-1)) ,;
									(( cAliasQRY )->E5_VRETCSL * (-1)) ,( cAliasQRY )->E5_VALOR,""})				
									
				( cAliasQRY )->(Dbskip())       		
			Enddo
	      	      
		   ( cAliasQRY )->( dbCloseArea() ) // Fecha a area de trabalho da query 
	
		#ENDIF    	
      
	Endif			
	aDadosRef[ 6 ] := AClone( aRecnos )            

Endif
cFilAnt := cFilAtu
RestArea(aArea)
If aDadosRef[1] == Nil
	AFill( aDadosRef, 0 ) 
Endif
aDadosRet := aClone(aDadosRef)

//Calculo do Pis, Cofins e Csll
SE1->(dbGoto(nSE1Reg))
SED->(dbSetOrder(1))
SED->(MsSeek(xFilial("SED")+SE1->E1_NATUREZ))
SA1->(dbSetOrder(1))
SA1->(MsSeek(xFilial("SA1")+SE1->E1_CLIENTE+SE1->E1_LOJA))

//Se a validação partiu do campo de valor
If lAltValor 
	nBaseImp := nValRec+nValComp  
	If !lJurMulDes
		nBaseImp := nBaseImp+nDescont-nJuros-nMulta-nAcresc+nDecresc
	Else
		nBaseImp:= nBaseImp-nDescont//+nJuros+nMulta+nAcresc-nDecresc 
	Endif	    
Else	
	If !lJurMulDes
		nBaseImp := nValRec+nDescont+nTotAbat-nJuros-nMulta-nAcresc+nDecresc
	Else
		nBaseImp := nValRec-nDescont+nJuros+nMulta+nAcresc-nDecresc	
	EndIf		
		
Endif
	
//Verificação para diferenciar se for baixa parcial
lAltBxVal := STR(SE1->E1_VALOR,17,2) != STR(SE1->E1_SALDO,17,2)

If !lAltValor                                            
	nBaseImp += nValOutImp + aDadosRet[7] + aDadosImp[4] - aDadosImp[5] + nValComp
Endif

//Caso o titulo possua o valor de base dos impostos preenchidos, considero
//esse valor com base para calculo, desprezando o valor da nota fiscal
nValTit := SE1->E1_VALOR + nValOutImp		//valor bruto do titulo reconstituido (valor + outros impostos)

nProp := 1

If SE1->E1_BASEPIS > 0
	nProp := SE1->E1_BASEPIS/nValtit				//Proporcao entre a base e o valor bruto do titulo
	nValTit := SE1->E1_BASEPIS		
Endif
      
If SE1->E1_MOEDA >1  
	nValTit:= xMoeda(nValTit,SE1->E1_MOEDA,1,dDatabase,3,nTxMoeda)
EndIf

If !lAltValor .and. !lJurMulDes
	nBaseImp := nValTit 				//Base liquida para calculo dos impostos
Else
	nBaseImp	:= nBaseImp * nProp			
Endif	

//Procura pelas baixas deste titulo caso seja baixa parcial e esteja baixando em mes diferente
If lAltBxVal .And. !lAltValor .and. !lJurMulDes
	aBaixa := Sel070Baixa("VL /BA /CP /",SE1->E1_PREFIXO,SE1->E1_NUM,SE1->E1_PARCELA,SE1->E1_TIPO,@nTotAdto,@lBaixaAbat,SE1->E1_CLIENTE,SE1->E1_LOJA,@lBxCec,.F.,@lNotBax,@nTotImpost,@lAglImp,.T.)
	For nY := 1 to Len(aBaixaSE5)
		If !aBaixaSE5[nY][4] $ MVRECANT
			nBaseImp -= aBaixaSE5[nY][8] * nProp 
			If lRaRtImp .And. (aBaixaSE5[nY][21] == " " .or.  (aBaixaSE5[nY][21] == "7" .and. aBaixaSE5[nY][29] $ "CMP")) .And.;
				nBaseImp + aBaixaSE5[nY][18]+aBaixaSE5[nY][19]+aBaixaSE5[nY][20]+aBaixaSE5[nY][30] > SE1->E1_BASEPIS
				nBaseImp -= (aBaixaSE5[nY][18]+aBaixaSE5[nY][19]+aBaixaSE5[nY][20]+aBaixaSE5[nY][30])// subtrair impostos PCC   
			Elseif lRaRtImp .And. lIrrfBxPj 
				nBaseImp -= aBaixaSE5[nY][30] 
			Endif
			If !lJurMulDes
				nBaseImp+= (aBaixaSE5[nY][14]+aBaixaSE5[nY][15])
			Endif        
			If lRaRtImp
		 		nBaseImp -= aBaixaSE5[nY][32]+aBaixaSE5[nY][33]
		 	Endif
						
		EndIf
	Next
EndIf

//Base diferenciada para calculo dos impostos
nBaseRet := nBaseImp += nValComp
If  Len(aPccAc) > 0             
	For nX	:= 1 To Len(aPccAc) 
		aDadosImp[1]	+= aPccAc[nX,1] //Pis
		aDadosImp[2]	+=	aPccAc[nX,2] //Cofins
		aDadosImp[3]	+=	aPccAc[nX,3] //Csll
	Next		                  
Endif                                                    

//PIS
If SE1->E1_PIS > 0                      
	If SE1->E1_MOEDA > 1

		nVlPis:= Round(xMoeda(SE1->E1_PIS,1,SE1->E1_MOEDA,SE1->E1_EMISSAO,5,,If(SE1->E1_TXMOEDA>0,SE1->E1_TXMOEDA,0)),2)
		nVlPis:= Round(xMoeda(nVlPis,SE1->E1_MOEDA,1,dDataBase,5,nTxMoeda),2)
		
	Else 
		nVlPis:= SE1->E1_PIS
	EndIf

	nPis := Round(( nBaseImp * nVlPis ) / nValTit ,2)
	//Verifico possiveis problemas de arredondamento
	If ABS(SE1->E1_PIS - (aDadosRet[2] + aValorBx[1] + nPis)) == 0.01
		nPis += (SE1->E1_PIS - (aDadosRet[2] + aValorBx[1] + nPis)	)
	EndIf		  
	                                                   									
	If ROUND(aDadosImp[1],2) <> ROUND(aDadosRet[2],2) .And. SE1->E1_SALDO > 0 .And. Alltrim(FunName()) <>"FINA061" 
		nPis += aDadosImp[1]
		If nPis < 0
			nPis := 0
		EndIf
	EndIf
	
EndIf

   // COFINS
If SE1->E1_COFINS > 0  
	If SE1->E1_MOEDA > 1

		nVlCofins:= Round(xMoeda(SE1->E1_COFINS,1,SE1->E1_MOEDA,SE1->E1_EMISSAO,5,,If(SE1->E1_TXMOEDA>0,SE1->E1_TXMOEDA,0)),2)
		nVlCofins:= Round(xMoeda(nVlCofins,SE1->E1_MOEDA,1,dDataBase,5,nTxMoeda),2)
		
	Else 
		nVlCofins:= SE1->E1_COFINS
	EndIf

	nCofins := Round(( nBaseImp * nVlCofins ) / nValTit ,2)
	//Verifico possiveis problemas de arredondamento
	If ABS(SE1->E1_COFINS - (aDadosRet[3] + aValorBx[2] + nCofins)) == 0.01
		nCofins += SE1->E1_COFINS - (aDadosRet[3] + aValorBx[2] + nCofins)		
	EndIf		

	If ROUND(aDadosImp[2],2) <> ROUND(aDadosRet[3],2) .And. SE1->E1_SALDO > 0 .And. Alltrim(FunName()) <>"FINA061" 
		nCofins += aDadosImp[2]
		If nCofins < 0
			nCofins := 0
		EndIf
	EndIf
EndIf

// CSLL
If SE1->E1_CSLL > 0  
	If SE1->E1_MOEDA > 1
        nVlCsll:= Round(xMoeda(SE1->E1_CSLL,1,SE1->E1_MOEDA,SE1->E1_EMISSAO,5,,If(SE1->E1_TXMOEDA>0,SE1->E1_TXMOEDA,0)),2)
		nVlCsll:= Round(xMoeda(nVlCsll,SE1->E1_MOEDA,1,dDataBase,5,nTxMoeda),2)
	Else 
		nVlCsll:= SE1->E1_CSLL
	EndIf

	nCsll := Round(( nBaseImp * nVlCsll ) / nValTit ,2)
	//Verifico possiveis problemas de arredondamento 
	If ABS(SE1->E1_CSLL - (aDadosRet[4] + aValorBx[3] + nCsll)) == 0.01
		nCsll += SE1->E1_CSLL - (aDadosRet[4] + aValorBx[3] + nCsll)		
	EndIf
	If ROUND(aDadosImp[3],2) <> ROUND(aDadosRet[4],2) .And. SE1->E1_SALDO > 0 .And. Alltrim(FunName()) <>"FINA061"  
		nCsll += aDadosImp[3]
		If nCsll < 0
			nCsll := 0
		EndIf
	EndIf
EndIf

If  Len(aBordAc) > 0 
	For nX:=	 1 To Len(aBordAc)
		aDadosRet[1]	+= aBordAc[nX,4]	
		aDadosRet[2]	+=	aBordAc[nX,1]
		aDadosRet[3]	+=	aBordAc[nX,2]
		aDadosRet[4] 	+= aBordAc[nX,3]	 	
	Next		
Endif

If GetMV("MV_MRETISS") == "1" //ISS na emissão do título principal
	nIss := SE1->E1_ISS
EndIf
//Verifico o valor a reter
   //Nao retem Pis,Cofins,CSLL
If cModRetPis == "3"  //Nao retem PIS
	nVlRetPis := nPis
	nVlRetCof := nCofins
	nVlRetCsl := nCsll
	nPis := 0
	nCofins := 0
	nCsll := 0
Else
	//Calculo do Sistema
	IF cModRetPis == "1"
		If aDadosRet[1]+ (nBaseRet) <= nVlMinImp .and. lAplVlMin .And. Len(aBordAc) == 0 
			nVlRetPis := nPis
			nVlRetCof := nCofins
			nVlRetCsl := nCsll
			nPis := 0
			nCofins := 0
			nCsll := 0
			aDadosRet[2] := 0 
			aDadosRet[3] := 0 
			aDadosRet[4] := 0
		Endif			
	Endif

	If nPis+nCofins+nCsll+nIrrf+ aDadosRet[2]+ aDadosRet[3]+ aDadosRet[4] > 0
		If nPis+nCofins+nCsll+aDadosRet[2]+ aDadosRet[3]+ aDadosRet[4] > 0
			nVlRetPis := nPis
			nVlRetCof := nCofins
			nVlRetCsl := nCsll
			If Alltrim(FunName())="FINA061" .And. lPccBxCr
				If ValType(aPccFina061) <> "U"				
				 	If Len(aPccFina061) > 0 .And. (aPccFina061[1,1]+aPccFina061[1,2]+aPccFina061[1,3] <> aDadosRet[2]+aDadosRet[3]+aDadosRet[4] )
				 	  lAdicPcc	:=	.F.
				 	Endif
            Endif
				If lAdicPcc   
					nPis    := nVlRetPis + aDadosRet[2]
					nCofins := nVlRetCof + aDadosRet[3]
					nCsll   := nVlRetCsl + aDadosRet[4]				
				Endif								
			Else
				nPis    := nVlRetPis + aDadosRet[2]
				nCofins := nVlRetCof + aDadosRet[3]
				nCsll   := nVlRetCsl + aDadosRet[4]
			Endif		
		Endif
		nValorRc := nValRec - nPis - nCofins - nCsll - nIrrf - nIss

		If nValorRc < 0
			nValorRc += nPis + nCofins + nCsll 
		
			nTotARet := nPis + nCofins + nCsll + nIrrf + nIss
					
			nDiferImp := nValorRc - nTotARet
				
			If nDiferImp < 0                                                           
				nFatorRed := 1 - ( Abs( nDiferImp ) / nTotARet ) 
				nPis  := NoRound( nPis * nFatorRed, 2 ) 
				nCofins := NoRound( nCofins * nFatorRed, 2 )  						
				nIrrf	:= NoRound( nIrrf * nFatorRed, 2 )
				nCsll := nValorRc - ( nPis + nCofins + nIrrf )
			Endif		
		EndIf 
	Else
		//Natureza nao calculou Pis/Cofins/Csll
		aDadosRet[1] := 0
	Endif
Endif

SE1->( RestArea( aAreaSE1 ) ) 

If lAltValor .or. lAltData
	If  Alltrim(FunName()) = "FINA061" 
		nValRec	:=	nOldValRec -	(nPis+nCofins+nCsll+ IIf(lIrrfBxPj,nIrrf,0))
	Else
		nValRec -= nPis+nCofins+nCsll+nIrrf//+nIss
	Endif	
Else
	If  Alltrim(FunName()) = "FINA061"  .And. lIrrfBxPj
		nValRec -= nIrrf
	Endif	
Endif            
nOldValRec := nValRec
nOldPIS		:= nPis
nOldCofins	:= nCofins
nOldCSLL	:= nCsll

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³F070DelTxBx  ³ Autor ³ Adrianne Furtado   ³ Data ³ 20/10/04 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Deleta titulos de imposto Pis Cofins Csll gerados na baixa. ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ FINA070                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function F070DelTxBx(cFil)

Local cChaveImp := ""
Local aAreaDTB := GetArea() 

Default cFil := xFilial("SE1")

If ( cPaisLoc == "BRA" )
	cChaveImp := SE1->(E1_CLIENTE+E1_LOJA+E1_PREFIXO + E1_NUM)
	dbSelectArea("SE1")
	dbSetOrder(2)
	MsSeek(cFil + cChaveImp)

	While !Eof() .And. E1_FILIAL+E1_CLIENTE+E1_LOJA+E1_PREFIXO+E1_NUM == cFil+cChaveImp
		IF E1_TIPO $ "PIS|COF|CSL|IRF" .And.  ;  // STR(SE2->E2_SALDO,17,2) == STR(SE1->E2_VALOR,17,2) .and. ;
			("FINA070" $ SE1->E1_ORIGEM .or. "FINA110" $ SE1->E1_ORIGEM) .and. ;
			SE5->E5_SEQ == SE1->E1_SEQBX
		
			RecLock("SE1",.F.,.T.)
			dbDelete()     

		EndIf
		dbSkip()
	EndDo
Endif

RestArea(aAreaDTB)

Return .T.
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³FA070SITEFºAutor  ³Microsiga           º Data ³  17/06/10   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Funcao para deixar o registro da FIF disponivel            º±±
±±º          ³ para nova conciliacao mediante cancelamento ou estorno da  º±±
±±º          | baixa                                                      º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function FA070SITEF(cPrefixo,cNum,cParc,cTipo)
local lRet := .T.
Local aArea := GetArea()
BEGIN TRANSACTION

If AliasInDic("FIF")
	dbselectarea("FIF")
	dbsetorder(3)
	if dbseek(xFilial("FIF")+cPrefixo+cNum+cParc+cTipo)
		RecLock("FIF",.F.)
		FIF->FIF_STATUS := "1"
		FIF->FIF_PREFIX := space(len(FIF->FIF_PREFIX))
		FIF->FIF_NUM    := space(len(FIF->FIF_NUM))
		FIF->FIF_PARC   := space(len(FIF->FIF_PARC))
		FIF->FIF_TIPO   := space(len(FIF->FIF_TIPO))
		FIF->(msunlock())
	endif
EndIf

END TRANSACTION
RestArea(aArea)
Return lRet


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³F070Help  ºAutor  ³ Pedro Pereira Lima º Data ³  16/08/10   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Inclui help para tratamento da baixa de titulos oriundos   º±±
±±º          ³ do SIGAEEC e SIGAEIC                                       º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
STATIC Function F070Help()
Local aHelpEsp := {}
Local aHelpPor := {}
Local aHelpEng := {}

aHelpEsp	:=	{"Este titulo se genero por modulo SIGAEEC."," No permite mantenimiento."}
aHelpPor	:=	{"Esse título foi gerado pelo SIGAEEC."," Não pode sofrer manutenção."}
aHelpEng	:=	{"This bill was generated by SIGAEEC."," It  cannot be updated."}
PutHelp("PFAORIEEC",aHelpPor,aHelpEng,aHelpEsp,.F.)

aHelpEsp := {"",""}
aHelpPor	:=	{"Verifique se a opção selecionada é ","realmente a que deve ser acionada, ou ","posicione em um outro titulo."}
aHelpEng	:=	{"",""}
PutHelp("SFAORIEEC",aHelpPor,aHelpEng,aHelpEsp,.F.)

aHelpPor	:=	{"Não é possível estornar este movimento"," pois este título possui comissão"," calculada na baixa."}
aHelpEsp	:=	{"No sera posible revertir este movimiento"," pues este titulo posee comision"," calculada en la baja."}
aHelpEng	:=	{"You cannot return this transaction as"," this bills has commission calculated"," in the write-off."}
PutHelp("PFINCOMBX",aHelpPor,aHelpEng,aHelpEsp,.F.)

aHelpPor	:=	{"Por favor, para correto procedimento,"," realize o estorno de todas as baixas ","posteriores a esta e refaça as baixas."}
aHelpEsp 	:= {"Por favor, para el correcto procedimiento,"," realice la reversion de todas las bajas"," posteriores esta y rehaga las bajas."}
aHelpEng	:=	{"For proper procedure, reverse all ","write-offs late than this one", " and write-off again."}
PutHelp("SFINCOMBX",aHelpPor,aHelpEng,aHelpEsp,.F.)

aHelpPor	:=	{"Não é possível estornar este movimento"," pois este título possui impostos"," calculados na baixa."}
aHelpEsp	:=	{"No sera posible revertir este movimiento"," pues este titulo posee impuestos"," calculada en la baja."}
aHelpEng	:=	{"You cannot return this transaction as"," this bills has taxes calculated"," in the write-off."}
PutHelp("PFINIMPBX",aHelpPor,aHelpEng,aHelpEsp,.F.)

aHelpPor	:=	{"Por favor, para correto procedimento,"," realize o estorno de todas as baixas ","posteriores a esta e refaça as baixas."}
aHelpEsp 	:= {"Por favor, para el correcto procedimiento,"," realice la reversion de todas las bajas"," posteriores esta y rehaga las bajas."}
aHelpEng	:=	{"For proper procedure, reverse all ","write-offs late than this one", " and write-off again."}
PutHelp("SFINIMPBX",aHelpPor,aHelpEng,aHelpEsp,.F.)


Return Nil



/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ AjustaHlp º Autor ³ Gustavo Henrique  º Data ³  23/08/11   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Cria mensagens de help ou help de campos necessario        º±±
±±º          ³ para rotina de baixas a receber                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Financeiro - Baixas a Receber                              º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function AjustaHlp()

Local aHelpEsp := {}
Local aHelpPor := {}
Local aHelpEng := {}        
		
aHelpEsp	:=	{ "El motivo de baja informado no " , "permite cheque."   }
aHelpPor	:=	{ "O motivo de baixa informado não ", "permite cheque."   }
aHelpEng	:=	{ "The informed posting reason do " , "not allow checks." }
PutHelp( "PCHQNOPER", aHelpPor, aHelpEng, aHelpEsp, .F. )

aHelpEsp	:=	{ "Verifique el motivo de baja." }
aHelpPor	:=	{ "Verifique o motivo de baixa." }
aHelpEng	:=	{ "Check the informed reason.", "" }
PutHelp( "SCHQNOPER", aHelpPor, aHelpEng, aHelpEsp, .F. )

aHelpPor := aHelpEng := aHelpSpa := {	"A funcionalidade de acerto de  ",;
										"impostos deve ser utilizada em títulos",;
										"cujo cliente e natureza estejam  ",;
										"configurados para o cálculo dos impostos",;
										"PIS, COFINS e CSLL."}

PutHelp("PF070IMPBTN",aHelpPor,aHelpEng,aHelpSpa,.F.)

Return Nil      


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ Fa070VldBco º Autor³ Ramon Teodoro  º Data ³  09/09/11   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Verifica se o cheque já existe e impede a alteração        º±±
±±º          ³ do banco inclusive pela consulta padrão.                   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Financeiro - Baixas a Receber                              º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Fa070VldBco
Local lRet

If FunName() == "FINA070"
	dbSelectArea("SEF")
	DbSetOrder(3)
	MsSeek(xFilial("SEF")+SE1->(E1_PREFIXO+E1_NUM+E1_PARCELA+E1_TIPO))
	If xFilial("SEF")==SEF->EF_FILIAL .And. SEF->EF_CART == "R" .And.;
		SEF->(EF_PREFIXO+EF_TITULO+EF_PARCELA+EF_TIPO) == SE1->(E1_PREFIXO+E1_NUM+E1_PARCELA+E1_TIPO) .And.;
		!aCols[n][Len(aCols[n])] .And. !Empty(aCols[n][4])
		lRet := .f.
	Else
		lRet := .t.
	EndIf
Else 
	lRet := .t.
EndIf

Return lRet
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³FINA070   ºAutor  ³Pâmela Bernardo     º Data ³  01/18/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Função que totaliza os cheques existentes na SEF para      º±±
±±º          ³ Validar se o Valor do cheque é menor que total de todas as º±±
±±º				Baixas efetuadas com esse numero de cheque                º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP  FINA070                                                º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function F070TOTCH(cBanco,cAgencia,cConta,cNum,nValorbx,cCliente)

Local aArea := GetArea()
Local aAreaSef := SEF->(GetArea())
Local nTot := nValorbx

#IFDEF TOP
	Local cQuery
#ENDIF

// Se nao estiver na base ainda, soma o aCols, onde estao os cheques que serao
// cadastrados

	#IFDEF TOP
	   cQuery := "SELECT Sum(EF_VALORBX) Soma FROM "+RetSqlName("SEF")+" WHERE "
	   cQuery += "EF_FILIAL='"+xFilial("SEF")+"' AND "
	   cQuery += "EF_BANCO='"+cBanco+"' AND "
		cQuery += "EF_AGENCIA='"+cAgencia+"' AND "
		cQuery += "EF_CONTA='"+cConta+"' AND "
		cQuery += "EF_NUM='"+cNum+"' AND "
		cQuery += "(EF_FORNECE='"+cCliente+"' OR "
		cQuery += " EF_CLIENTE='"+cCliente+"') AND "
		cQuery += "D_E_L_E_T_<>'*'"
		cQuery := ChangeQuery(cQuery)
		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),"__SOMACHEQ",.T.,.T.)
		nTot += __SOMACHEQ->SOMA
		dbCloseArea()
	#ELSE
		dbSelectArea("SEF")
		SEF->(dbSetOrder(1))      
		SEF->(MsSeek(xFilial("SEF")+cBanco+cAgencia+cConta+cNum))
		While SEF->(!Eof()) .And.;
				SEF->(EF_FILIAL+EF_BANCO+EF_AGENCIA+EF_CONTA+EF_NUM) ==;
				xFilial("SEF")+cBanco+cAgencia+cConta+cNum
			If (SEF->EF_FORNECE == cCliente .Or.;
				SEF->EF_CLIENTE == cCliente) .And. !Empty(SEF->EF_NUM)
			
					nTot += SEF->EF_VALORBX // Proporcionaliza o valor ref. a baixa, devido as baixas parciais
			
			Endif	
			SEF->(dbSkip())
		EndDo
	#ENDIF

RestArea(aAreaSEF)
RestArea(aArea)

Return nTot

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³INTEGDEF  ºAutor  ³Wilson de Godoi      º Data ³ 06/02/2012 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Função para a interação com EAI                             º±±
±±º          ³envio e recebimento                                         º±±
±±º          ³                                                            º±±
±±º          ³                                                            º±±
±±º          ³                                                            º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP  Baixa de Contas a Receber                              º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/ 
Static Function IntegDef( cXml, nType, cTypeMsg)  
		Local aRet := {}  
		aRet:= FINI070( cXml, nType, cTypeMsg )
Return aRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³FA070VlNatºAutor  ³TOTVS               º Data ³  08/08/11   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Valida natureza que realizam movimentos bancarios.          º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function FA070VlNat(cMotBx,lTipo)
Local lRet := .T.

Default lTipo := .T.

If Existblock("F070TPBA")
	lTipo:= ExecBlock("F070TPBA",.f.,.f.)
Endif     

//Verifica se motivo de baixa possui movimentacao financeira              
If MovBcoBx(cMotBx, .T. ) .And. If(lTipo,Posicione("SED",1,xfilial("SED") + SE1->(E1_NATUREZ),"ED_MOVBCO") == "2",.F.)
	Help(" ",1,"MOTIBAIXA", , "",1,0) //"Motivo de baixa inválido para a natureza selecionada. Verifique a opção de movimentação bancária da natureza e não prosseguir."
	lRet:=.F.
Elseif !MovBcoBx(cMotBx, .T. ) .And. If(lTipo,Posicione("SED",1,xfilial("SED") + SE1->(E1_NATUREZ),"ED_MOVBCO") == "1",.F.)
	Help(" ",1,"MOTIBAIXA", , "",1,0) //"Motivo de baixa inválido para a natureza selecionada. Verifique a opção de movimentação bancária da natureza e não prosseguir."
	lRet:=.F.  
EndIf    


Return lRet

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³F070BuscCR ³ Autor ³ Sergio Celestino     ³ Data ³ 02/09/12 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Funcao que Busca para o titulo passado se existem Recebimen-³±±
±±³          ³tos os Creditos        							  		  			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ FINA070 - (Projeto MPE)       					  				  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function F070BuscCR( cAliasB, cCli, cLj )

Local aArBusca	 := GetArea()
Local nRet			:= 0
#IFDEF TOP
	Local cQuery
	Local cTipoTit	:= ""
#ELSE
	Local cIndexC	 	:=""
	Local cCondicao1	:=""	
	Local cKey1   	:=""
	Local nIndexC   	:= 0
	Local cFilter   	:= ""   
#ENDIF

Default cAliasB := ""
Default cCli	 := ""
Default cLj 	 := ""

If Select(cAliasB) <= 0
	SomaAbat("","","","P")
EndIf

If !Empty(cAliasB) .And. !Empty(cCli) .And. !Empty(cLj)
	#IFDEF TOP
		cQuery 	:= "SELECT SE1.E1_TIPO,SUM(E1_SALDO) AS SOMA FROM "+RetSqlName("SE1")+" SE1 "
		cQuery 	+= "WHERE SE1.E1_CLIENTE = '"+cCli+"' "
		cQuery 	+= "AND SE1.E1_LOJA = '"+cLj+"' "
		cTipoTit	:=	""
		cTipoTit	:=	MVRECANT + "|" + MV_CRNEG  
		cQuery 	+= "AND SE1.E1_TIPO IN " + FormatIn(cTipoTit,If("|"$cTipoTit,"|",","))  + " "
		cQuery 	+= "AND SE1.E1_FILIAL = '"+xFilial("SE1")+"' "
		cQuery 	+= "AND SE1.D_E_L_E_T_ = ' ' "
		cQuery 	+= "GROUP BY SE1.E1_TIPO "
		cQuery 	:= ChangeQuery(cQuery)
		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),"__SOMARA",.T.,.T.)
		dbGoTop()
		While !Eof()
			If __SOMARA->E1_TIPO $ MVRECANT .And. __SOMARA->SOMA > 0
				nRet := 1
			Endif
					
			If __SOMARA->E1_TIPO $ MV_CRNEG  .And. __SOMARA->SOMA > 0
				nRet := 2
			Endif
						
			If nRet <> 0
				Exit
			Endif
			If __SOMARA->SOMA == 0 
			   Exit
			Endif   
		EndDo
		
		dbCloseArea()
	#ELSE
	
		cFilter := (cAliasB)->(DbFilter())
	
		dbSelectArea(cAliasB)
		dbSetOrder(2)
		If dbSeek( xFilial("SE1")+cCli+cLj )
			cIndexC := CriaTrab(nil,.f.)
	
			cCondicao1 	:= "(E1_TIPO $ '"+MV_CRNEG + "/" + MVRECANT+ "')"
			cCondicao1 	+= ' .AND. E1_SALDO > 0 '
			cKey1	   	:= "E1_FILIAL+E1_CLIENTE+E1_LOJA"
			
			IndRegua(cAliasB,cIndexC,cKey1,,cCondicao1,OemToAnsi(STR0025)) //"Seleccionando Registros..."
			nIndexC 	:= RetIndex("SE1",cAliasB)
			dbSelectArea(cAliasB)
			#IFNDEF TOP
				dbSetIndex(cIndexC+OrdBagExt())
			#ENDIF
			dbSetOrder(nIndexC + 1 ) 
			
			dbSeek(xFilial("SE1")+cCli+cLj)
			
			While !Eof() .And. xFilial("SE1")+cCli+cLj == (cAliasB)->E1_FILIAL+(cAliasB)->E1_CLIENTE+(cAliasB)->E1_LOJA
				// So Processa titulos 
				// com saldo
				If (cAliasB)->E1_SALDO <= 0
					dbSkip()
					Loop
				Endif
					  
				// Recebimento Antecipado
				If (cAliasB)->E1_TIPO $ MVRECANT
					nRet := 1
				Endif
					
				// NCC
				If (cAliasB)->E1_TIPO $ MV_CRNEG
					nRet := 2
				Endif
						
				If nRet <> 0
					Exit
				Endif
						
				DbSkip()
			EndDo
		EndIf

		FErase (cIndexC+OrdBagExt())
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Recupera a Integridade dos dados                             ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		RetIndex("SE1",cAliasB)
		DbSelectArea(cAliasB)
		Set Filter to &cFilter
		
	#ENDIF
EndIf
        

RestArea( aArBusca )

Return( nRet )
/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³FA070Integ ³ Autor ³Jandir Deodato ³ Data ³ 30.08.12        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Valida a integdef de baixas a receber                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ FINA070                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function FA070Integ(lCancel,lMSG)
Local aAreaXX4:={}
Local aSave:={}
Local lRet:=.T.
Local cExecb
Local lRetExec
Local cMSg
Local aRetMsg:={}
Default lMSG := .T.
aSave:=GetArea()
dbSelectArea('XX4')
aAreaXX4:=XX4->(GetArea())
If !(Alltrim(SE1->E1_TIPO	)=="PR")
	If lCancel
		cMSg:='REVERSALOFACCOUNTRECEIVABLEDOCUMENTDISCHARGE'
	Else
		cMsg:='ACCOUNTRECEIVABLEDOCUMENTDISCHARGE'
	Endif
	If FindFunction("GETROTINTEG") .And. FindFunction("FWHASEAI") .And. FWHasEAI("FINA070",.T.,,.T.) 
	
		XX4->(dbSetOrder(1))
		IF XX4->(DbSeek(Xfilial('XX4')+PADR('FINA070',Len(XX4_ROTINA))+PADR(cMsg,Len(XX4_MODEL))))
			cExecB:= "{ || "+ IIf(Empty(XX4->XX4_EXPFIL),".T.",AllTrim( XX4->XX4_EXPFIL )) +" }"
			cExecB:=&(cExecB)
			lRetExec:=Eval(cExecB)
			If (ValType (lRetExec)=="L" .and. lRetExec .and. !(XX4->XX4_METODO =='1')) 
				If lMSG
					HELP(" ",1,"FA070INTEG",,STR0233,2,0)//O adapter de Baixas a Receber está configurado como ASSÍNCRONO no configurador. Configure-o como SÍNCRONO."
				Endif
				lRet:=.F.
			Endif
		Endif
		If AllTrim(SE1->E1_ORIGEM)=="FINI055" .and. lRet  .and. !lCancel .And. SE1->E1_SITUACA == "0"
			If FWHasEAI("FINI070A",.T.,,.T.) .and. FindFunction('FINI070A')
				SE1->(msUnlock()) 
			
					SetRotInteg('FINI070A')
					MsgRun ( STR0234+" "+rTrim(SE1->E1_NUM)+ " " +STR0235,STR0236,{||aRetMsg:=FinI070A()} )//"Atualizando título" "a valor presente..." Valor Presente
					RecLock("SE1",.F.)
					If ValType(aRetMSg[1]) <> "U" .and. !aRetMsg[1]
						If ValType (aRetMsg[2]) <> "U" .and. aRetMsg[2] <> Nil .and. !Empty (aRetMsg[2])
							MsgAlert(STR0237+ CRLF+aRetMsg[2])//"Foi realizada uma tentativa de atualização do título, e foi retornada a seguinte mensagem:"
						Else
							MsgAlert(STR0238+" " + Rtrim(SE1->E1_NUM)+". "+STR0239)//"Ocorreu um erro inesperado na tentativa de atualização do título " "Verifique as configurações da integração  e tente novamente."
						Endif
						lRet :=.F.
					ElseIF Valtype (aRetMSg[1]) =="U"
						MsgAlert(STR0238+" " + Rtrim(SE1->E1_NUM)+". "+STR0239)//"Ocorreu um erro inesperado na tentativa de atualização do título " "Verifique as configurações da integração  e tente novamente."
						lRet:=.F.
					Endif		
			
			Else
				lRet:=.F.
				MsgAlert(STR0240)//"Para realizar as baixas de integrações como TIN, é necessário cadastrar o adapter da rotina FINI070A - UPDATECONTRACTPARCEL."
			Endif
		Endif
	Endif
Endif
RestArea(aAreaXX4)
RestArea(aSAve)
	
Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³F070VenBx ºAutor  ³Clovis Magenta      º Data ³  24/01/13   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Função que valida se nas baixas parciais a serem canceladas º±±
±±º          ³existiu calculo de comissão na baixa. Impedirá cancelamento.º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function F070VenBx(aBaixa,nOpBaixa)
Local lProssegue 	:= .T.
Local nTamaBaixa	:= Len(aBaixa)
Local aArea			:= GetArea()
Local nX				:= 0
Local nVendedores := fa440CntVen()

F070Help()

If (nTamaBaixa > 1) .and. (nOpBaixa < nTamaBaixa)                      
	Dbselectarea('SA3')
	DbSetOrder(1)
	For nX:=1 to nVendedores

		cVendedor := SE1->( FieldGet( FieldPos( "E1_VEND"+Alltrim(Str(nX)) ) ) )
		If dbSeek(xFilial('SA3')+cVendedor)
			If SA3->A3_ALBAIXA > 0
				lProssegue := .F.
				HELP(" ",1, "FINCOMBX" )
				Exit
			Endif
		Endif

	Next nX
Endif

RestArea(aArea)

Return lProssegue

    
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³FINA070   ºAutor  ³TOTVS           º Data ³  03/18/13   		º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Conversão do nPis, nCofins, nCSLL para a nova taxa digitadaº±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ FINA070                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function F070CnvPcc(nTxMoeda, nMoeda)    

Local lPccBxCr  := If (FindFunction("FPccBxCr"),FPccBxCr(),.F.)
Local lIrPjBxCr	:= If (FindFunction("FIrPjBxCr"),FIrPjBxCr(),.F.) //Controla IRPJ na baixa
Local nOutImp   := 0

DEFAULT nTxMoeda	:= 0
DEFAULT nMoeda 		:= 1

If lPccBxCr
	f070TotMes(dBaixa,.T.,,,,nTxMoeda)     
Endif     

If lIrPjBxCr
   	nIrrf:=FCaIrBxCR(nValRec,,,nTxMoeda)
EndIf     	

nTotAbat  := SumAbatRec(SE1->E1_PREFIXO,SE1->E1_NUM,SE1->E1_PARCELA,SE1->E1_MOEDA,"S",dBaixa,@nOutImp,,,,,,, nTxMoeda) 
nTotAbImp:=nOutImp
Fa070Val(0,nTxMoeda)
fa070ValVR(nTxMoeda)

return
	
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³ Fa070Desc   ³ Autor ³ Andrea V. Santiago    ³ Data ³ 10/10/13 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Abre tela para a configuracao do tipo do desconto a ser       ³±±
±±³          ³ enviado no bloco F100 do SPED PIS/COFINS.                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ Fa070Desc()                 									  	 	 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Gen‚rico 			              										 ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Fa070DesCI(lTpDesc,lNatApura)           
Local oDlgDesc, oVlDesc

If cPaisLoc == "BRA" .And. lTpDesc .And. lNatApura	 .And. nDescont > 0	         		                      	
	                       		
	DEFINE MSDIALOG oDlgDesc FROM 10,05 TO 14,45 TITLE "Tipo de Desconto " // "Tipo de Desconto"
                 
   @	6,2 Say "Desconto: " SIZE 31,07 OF oDlgDesc PIXEL		               
	@  5,35  MSCOMBOBOX oVlDesc VAR cTpDesc ITEMS {"C=Condicional","I=Incondicional"} SIZE 70, 47 OF oDlgDesc PIXEL  
		
	DEFINE SBUTTON FROM 5,120 TYPE 1 ACTION oDlgDesc:End() ENABLE OF oDlgDesc
	ACTIVATE MSDIALOG oDlgDesc CENTERED                   	
	   
Endif

Return .T.
	     
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³F070ImpBx ºAutor  ³TOTVS               º Data ³  26/06/13   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Função que valida se nas baixas parciais a serem canceladas º±±
±±º          ³Se o PCC ou IR está na baixa                                º±±                                                                                   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function F070ImpBx(aBaixa,nOpBaixa)
Local lProssegue      := .T.
Local nTamaBaixa      := Len(aBaixa)
Local aAreaSE1        := SE1->(GetArea())
Local lPccBxCr        := If (FindFunction("FPccBxCr"),FPccBxCr(),.F.)
Local lIrPjBxCr       := If (FindFunction("FIrPjBxCr"),FIrPjBxCr(),.F.) //Controla IRPJ na baixa

If (nTamaBaixa > 1) .and. (nOpBaixa < nTamaBaixa)
	If lPccBxCr .or. lIrPjBxCr
		lProssegue := .F.
		HELP(" ",1, "FINIMPBX" )
	Endif
Endif

RestArea(aAreaSE1)
Return lProssegue
