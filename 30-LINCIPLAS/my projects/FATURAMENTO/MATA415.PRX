#INCLUDE "PROTHEUS.CH" 
#INCLUDE "MATA415.CH"
#INCLUDE "TBICONN.CH"

Static aStackTMP

/*/
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбддддддддддд©╠╠
╠╠ЁFun┤└o    ЁMATA415   Ё Autor Ё Eduardo Riera         Ё Data Ё 08.12.97  Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддаддддддддддд╢╠╠
╠╠ЁSintaxe   ЁMata415()                                                    Ё╠╠
╠╠Ё          ЁRotina de atualizacao dos Orcamentos de venda                Ё╠╠   
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁNenhum                                                       Ё╠╠
╠╠Ё          Ё                                                             Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё                                                             Ё╠╠
╠╠Ё          Ё                                                             Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁDescri┤└o ЁEsta rotina permite as operacoes de pesquisa, visualizacao,  Ё╠╠
╠╠Ё          Ёinclusao, alteracao, exclusao, copia, cancelamento e efetiva-Ё╠╠
╠╠Ё          Ёcao de um orcamento de venda.                                Ё╠╠
╠╠Ё          ЁO objetivo principal desta rotina eh de servir como ferramen-Ё╠╠
╠╠Ё          Ёta em uma negociacao de venda e/ou numa negociacao B2B.      Ё╠╠
╠╠Ё          ЁPara tanto, a ferramenta dispoe de algumas caracteristicas   Ё╠╠
╠╠Ё          Ёque julga-se essencial para o comprimento de seu principal   Ё╠╠
╠╠Ё          Ёobjetivo:                                                    Ё╠╠
╠╠Ё          Ёa) Formulacao do preco de venda: Esta caracteristica benefi- Ё╠╠
╠╠Ё          Ёcia tanto ao cliente, quando ao representante comercial que  Ё╠╠
╠╠Ё          Ёpode dizer exatamente ao seu cliente o valor do desembolso   Ё╠╠
╠╠Ё          Ёfinanceiro                                                   Ё╠╠
╠╠Ё          Ёb) Desconto em cascata(d1+d2+d3+d4): Os percentuais de desconЁ╠╠
╠╠Ё          Ёto sao efetuados sobre o preco de lista.                     Ё╠╠
╠╠Ё          Ёc) Desconto de Indenizacao: O valor do desconto eh dado sobreЁ╠╠
╠╠Ё          Ёa soma total do valor da venda.                              Ё╠╠
╠╠Ё          Ёd) Desconto por item: O valor do desconto eh dado sobre o va-Ё╠╠
╠╠Ё          Ёlor total do item.                                           Ё╠╠
╠╠Ё          Ё                                                             Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё Generico                                                    Ё╠╠
╠╠юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/

Function MATA415(xAutoCab,xAutoItens,nOpcAuto,lSimulacao)

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁDeclaracao das variaveis                                      Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
Local aCores := {	{ 'SCJ->CJ_STATUS=="A"' , 'ENABLE' },; 	//Orcamento em Aberto
	{ 'SCJ->CJ_STATUS=="B"' , 'DISABLE'},;					//Orcamento Baixado
	{ 'SCJ->CJ_STATUS=="C"' , 'BR_PRETO'},;					//Orcamento Cancelado
	{ 'SCJ->CJ_STATUS=="D"' , 'BR_AMARELO'},;				//Orcamento nao Orcado
	{ 'SCJ->CJ_STATUS=="E"' , 'BR_AZUL' },;	    			//Orcamento aprovado
	{ 'SCJ->CJ_STATUS=="F"' , 'BR_MARROM' }}				//Orcamento bloqueado

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Tratamento de Filtro para o browse                           Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
Local aIndex		:= {}
Local bFiltraBrw	:= {|| Nil}	
Local cFiltro		:= ''
Local lMT415Brw		:= .F.

DEFAULT lSimulacao  := .F.

Private lOnUpdate   := .T.
Private cCadastro	:= OemtoAnsi(STR0001) 					//"Or┤amentos de Venda"
Private l415Auto 	:= ( Valtype(xAutoCab) == "A"  .and. ValType(xAutoItens) == "A" )
Private aAutoCab	:= {}
Private aAutoItens	:= {}
Private aRotina		:= MenuDef()
Private lShowOpc	:= .T.

AjustaSIX()//Ajusta arquivo de Indices
AjustaSX3()//Ajusta dicionario
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁInicializacao da Mbrowse ou rotina automatica                 Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
dbSelectArea("SCJ")
dbSetOrder(1)
MsSeek(xFilial())
If ( AMIIn(5) )
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁVerifica se o tamanho do campo CJ_TABELA                      Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If Len(SCJ->CJ_TABELA)<>3
		Aviso("SX3",STR0049,{"OK"}) //"O estrutura do campo CJ_TABELA esta diferente do padrao, solicitamos que seja alterado para Caracter de 3, antes de utilizar a rotina"
	Else
		If  ( Type("l415Auto") <> "U" .And. l415Auto )
			DEFAULT nOpcAuto := 3
			lOnUpdate  := !lSimulacao
			aAutoCab   := xAutoCab
			aAutoItens := xAutoItens
			MBrowseAuto(nOpcAuto,Aclone(aAutoCab),"SCJ")
			xAutoCab   := aAutoCab
			xAutoItens := aAutoItens			
		Else
			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Ponto de Entrada para alterar cores do Browse do Cadastro    Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			If ExistBlock("MA415COR")
				aCores := ExecBlock("MA415COR",.F.,.F.,aCores)
			EndIf
			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Ponto de Entrada para filtro antes da apresentacao do Browse Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			If ExistBlock("MT415BRW")
				cFiltro := ExecBlock("MT415BRW",.F.,.F.)
				If ValType(cFiltro) == "C" .And. !Empty(cFiltro)
					CursorWait()
					bFiltraBrw := {|| FilBrowse("SCJ",@aIndex,@cFiltro) }
					Eval(bFiltraBrw)
					CursorArrow()
					lMT415Brw := .T.					
				EndIf
			EndIf
			mBrowse( 6, 1,22,75,"SCJ",,,,,,aCores)
			If lMT415Brw
				EndFilBrw("SCJ",aIndex)
			Endif	
		EndIf
	EndIf
EndIf
Return(.T.)
/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁA415IncluiЁ Autor Ё Eduardo Riera         Ё Data Ё08.12.97  Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Programa de Inclusao de Orcamentos de Venda                Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe e Ё Void A415Inclui(ExpC1,ExpN1,ExpN2,ExpX1,[ExpC2])           Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpC1 = Alias do arquivo                                   Ё╠╠
╠╠Ё          Ё ExpN2 = Numero do registro                                 Ё╠╠
╠╠Ё          Ё ExpN3 = Opcao selecionada                                  Ё╠╠
╠╠Ё          Ё ExpX4 = Sem funcao                                         Ё╠╠
╠╠Ё          Ё ExpC5 = Numero do orcamento incluido ( passado por refe-   Ё╠╠
╠╠Ё          Ё rencia                                                     Ё╠╠
╠╠Ё          Ё ExpC6 = Codigo do cliente                                  Ё╠╠
╠╠Ё          Ё ExpC7 = Loja do Cliente                                    Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё MATA415                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function A415Inclui(cAlias,nReg,nOpcx,xPar,cNumOrc,cCodCli,cLoja)

Local aArea		:= GetArea("SCJ")
Local nStack    := GetSX8Len() 
Local nX 		:= 0
Local nOpcA		:= 0
Local cArqSCL	:= ""
Local cArqSCK	:= ""
Local cNumAnt   := "" 
Local oDlg
Local oSay
Local oGetDB
Local aPosObj   := {}
Local aObjects  := {}
Local aSize     := {}
Local aPosGet   := {}
Local aInfo     := {}
Local nGetLin   := 0
Local lBloqueia	:= .F.

Private aTela[0][0]
Private aGets[0]
Private bCampo		:= { |nField| Field(nField) }
Private aHeader		:= {}
Private aHeaderSCL	:= {}
Private aHeaderSCK	:= {}
PRIVATE aGEMCVnd    := {"",{},{}} //Template GEM - Condicao de Venda

l415Auto := If(Type("l415Auto")<>"U",l415Auto,.F.)
l416Auto := If(Type("l416Auto")<>"U",l416Auto,.F.)

DEFAULT cNumOrc := ""
DEFAULT cCodCli := ""
DEFAULT cLoja   := ""
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Inicializa as Variaveis da Enchoice                          Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
dbSelectArea("SCJ")
For nX:= 1 To FCount()
	M->&(EVAL(bCampo,nX)) := CriaVar(FieldName(nX),.T.)
Next nX
If !Empty(cCodCli)
	M->CJ_CLIENTE := cCodCli
	M->CJ_CLIENT  := cCodCli	
	M->CJ_LOJA    := cLoja
EndIf
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Monta aHeader                                                Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
A415Monta(@cArqSCK,@cArqSCL,.T.)
aHeader := aHeaderSCK
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Atualiza o aHeader para a Rotina Automatica                  Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
dbSelectArea("TMP1")
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Montagem da Tela                                             Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If ( !l415Auto )
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Habilita a Tecla F4                                          Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Faz o calculo automatico de dimensoes de objetos     Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
	aSize := MsAdvSize()

	aObjects := {}
	AAdd( aObjects, { 100, 100, .t., .t. } )
	AAdd( aObjects, { 100, 100, .t., .t. } )
	AAdd( aObjects, { 100, 015, .t., .f. } )

	aInfo := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 3, 3 }
	aPosObj := MsObjSize( aInfo, aObjects )

	aPosGet := MsObjGetPos(aSize[3]-aSize[1],315,;
		{{005,070,110,175,215,280}} )
	nGetLin := aPosObj[3,1]

	SetKey(VK_F4,{||A415F4()})
	DEFINE MSDIALOG oDlg TITLE OemtoAnsi(STR0001) From aSize[7],0 to aSize[6],aSize[5] of oMainWnd PIXEL//"Or┤amentos de Venda"
	EnChoice( "SCJ" ,nReg,nOpcx,,,,,aPosObj[1],,3,,,"A415VldTOk",,,.F.)
	oGetDb := MsGetDB():New(aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],nOpcx,"A415LinOk","A415TudOk","+CK_ITEM",.T., , ,.T.,,"TMP1","A415FldOk")
	Private oGetDad := oGetDb
	@ nGetLin,aPosGet[1,1] SAY oSay PROMPT OemToAnsi(STR0034) 	SIZE 060,009 PIXEL OF oDlg //"Total do Or┤amento "
	@ nGetLin,aPosGet[1,3] SAY oSay PROMPT OemToAnsi(STR0035) 	SIZE 060,009 PIXEL OF oDlg //"Descontos "
	@ nGetLin,aPosGet[1,5] SAY oSay PROMPT OemToAnsi("=")		SIZE 060,009 PIXEL OF oDlg //"="
	@ nGetLin,aPosGet[1,2] SAY oSay PROMPT 0    					SIZE 040,009 PICTURE PesqPict("SCK","CK_VALOR",16) PIXEL OF oDlg
	oSay:Cargo := "Valor"
	@ nGetLin,aPosGet[1,4] SAY oSay PROMPT 0    						SIZE 040,009 PICTURE PesqPict("SCK","CK_VALOR",14) PIXEL OF oDlg
	oSay:Cargo := "Desconto"
	@ nGetLin,aPosGet[1,6] SAY oSay PROMPT 0                     SIZE 040,009 PICTURE PesqPict("SCK","CK_VALOR",16) PIXEL OF oDlg
	oSay:Cargo := "Total"
	ACTIVATE MSDIALOG oDlg ON INIT Ma415Bar(oDlg,{||nOpcA:=1,IIf(oGetDb:TudoOk().And.Obrigatorio(aGets,aTela).And.A415VldTOk(@lBloqueia),oDlg:End(),nOpcA:=0)},{||oDlg:End()}, nOpcx)
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Desabilita a Tecla F4                                        Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	SetKey(VK_F4,Nil)
Else
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁTratamento da Rotina automatica                               Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	nOpcA := 0
	If EnchAuto(cAlias,aAutoCab,{|| Obrigatorio(aGets,aTela)}) .And. MsGetDBAuto("TMP1",aAutoItens,"A415LinOk",{|| A415TudOk() .And. A415VldTOk(@lBloqueia)},aAutoCab,aRotina[nOpcx][4])
		nOpcA := 1
	EndIf	
EndIf

If ( nOpcA == 1 )
	Begin Transaction
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Efetua tratamento do Bonus                                   Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		
		cNumAnt := M->CJ_NUM
		A415Bonus(1)
		If Type("lOnUpDate") == "U" .Or. lOnUpdate
			If ( A415Grava(1,lBloqueia) )
				//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Seta a varivel de numero ( chamada do FATA300 )              Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
				cNumOrc := M->CJ_NUM
				While GetSX8Len() > nStack
					ConfirmSX8()
				EndDo
				EvalTrigger()       
				//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Exibe mensagem caso tenha alterado o numero do orcamento     Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
				If ( cNumAnt <> M->CJ_NUM )			
					If !( Type("l415Auto") <> "U" .And. l415Auto )
						Help(" ",1,"NUMSEQ",,M->CJ_NUM,4,15)
					EndIf     
				EndIf 								
				MEnviaMail("026",{SCJ->CJ_NUM,SCJ->CJ_CLIENTE,SCJ->CJ_LOJA,SA1->A1_NOME})									
			EndIf
		Else
			aAutoCab := MsAuto2Ench("SCJ")
			aAutoItens := MsAuto2GDb(aHeader,"TMP1")	
		EndIf
	End Transaction
Else
	While GetSX8Len() > nStack
		RollBackSX8()
	EndDo
EndIf               

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Executa ponto de entrada na saida                            Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If ExistBlock( "MA415END" ) 
	ExecBlock( "MA415END", .F., .F., { nOpcA, 1 } ) 
EndIf 	

A415DesMonta(@cArqSCK,@cArqSCL)
MsUnLockAll()
RestArea(aArea)
Return(.T.)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁA415VisualЁ Autor Ё Eduardo Riera         Ё Data Ё12.12.97  Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Programa de Visualizacao de Orcamentos de Venda            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe e Ё Void A415Visual(ExpC1,ExpN1,ExpN2)                         Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpC1 = Alias do arquivo                                   Ё╠╠
╠╠Ё          Ё ExpN1 = Numero do registro                                 Ё╠╠
╠╠Ё          Ё ExpN2 = Opcao selecionada                                  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё MATA415                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function A415Visual(cAlias,nReg,nOpcx)

Local aArea		:= GetArea()
Local nX		:= 0
Local cArqSCL	:= ""
Local cArqSCK	:= ""
Local oSay
Local oDlg
Local oGetDb
Local aPosObj   := {}
Local aObjects  := {}
Local aSize     := {}
Local aPosGet   := {}
Local aInfo     := {}
Local nGetLin   := 0
Private aTela[0][0]
Private aGets[0]
Private bCampo		:= { |nField| Field(nField) }
Private aHeader		:= {}
Private aHeaderSCL	:= {}
Private aHeaderSCK	:= {}
PRIVATE aGEMCVnd    := {"",{},{}} //Template GEM - Condicao de Venda
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Inicializa as Variaveis da Enchoice                          Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
dbSelectArea("SCJ")
For nX:= 1 To FCount()
	M->&(EVAL(bCampo,nX)) := FieldGet(nX)
Next nX
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Monta aHeader                                                Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
A415Monta(@cArqSCK,@cArqSCL,.F.)
aHeader := aHeaderSCK
//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Faz o calculo automatico de dimensoes de objetos     Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
aSize := MsAdvSize()

aObjects := {}
AAdd( aObjects, { 100, 100, .t., .t. } )
AAdd( aObjects, { 100, 100, .t., .t. } )
AAdd( aObjects, { 100, 015, .t., .f. } )

aInfo := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 3, 3 }
aPosObj := MsObjSize( aInfo, aObjects )

aPosGet := MsObjGetPos(aSize[3]-aSize[1],315,;
	{{005,070,110,175,215,280}} )
nGetLin := aPosObj[3,1]

DEFINE MSDIALOG oDlg TITLE OemtoAnsi(STR0001) From aSize[7],0 to aSize[6],aSize[5] of oMainWnd PIXEL
EnChoice( "SCJ", nReg,nOpcx,,,,,aPosObj[1],,3,,,,,,.F.)
oGetDb := MsGetDB():New(aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],nOpcx,"A415LinOk","A415TudOk","+CK_ITEM",.T., , ,.T., ,"TMP1")
Private oGetDad := oGetDb
@ nGetLin,aPosGet[1,1] SAY oSay PROMPT OemToAnsi(STR0034) 	SIZE 060,009 PIXEL OF oDlg //"Total do Or┤amento "
@ nGetLin,aPosGet[1,3] SAY oSay PROMPT OemToAnsi(STR0035)	SIZE 060,009 PIXEL OF oDlg //"Descontos "
@ nGetLin,aPosGet[1,5] SAY oSay PROMPT OemToAnsi("=")		SIZE 060,009 PIXEL OF oDlg //"="
@ nGetLin,aPosGet[1,2] SAY oSay PROMPT 0    					SIZE 040,009 PICTURE PesqPict("SCK","CK_VALOR",16) PIXEL OF oDlg
oSay:Cargo := "Valor"
@ nGetLin,aPosGet[1,4] SAY oSay PROMPT 0    					SIZE 040,009 PICTURE PesqPict("SCK","CK_VALOR",14) PIXEL OF oDlg
oSay:Cargo := "Desconto"
@ nGetLin,aPosGet[1,6] SAY oSay PROMPT 0                     SIZE 040,009 PICTURE PesqPict("SCK","CK_VALOR",16) PIXEL OF oDlg
oSay:Cargo := "Total"
A415Total(oDlg)
ACTIVATE MSDIALOG oDlg ON INIT Ma415Bar(oDlg,{||nOpcA:=1,IIf(oGetDb:TudoOk(),oDlg:End(),nOpcA:=0)},{||oDlg:End()}, nOpcx)
A415DesMonta(@cArqSCK,@cArqSCL)
MsUnLockAll()
RestArea(aArea)
Return(.T.)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁA415AlteraЁ Autor Ё Eduardo Riera         Ё Data Ё11.12.97  Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Programa de Alteracao de Orcamentos de Venda               Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё Void A415Altera(ExpC1,ExpN1,ExpN2)                         Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpC1 = Alias do arquivo                                   Ё╠╠
╠╠Ё          Ё ExpN1 = Numero do registro                                 Ё╠╠
╠╠Ё          Ё ExpN2 = Opcao selecionada                                  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё MATA415                                                    Ё╠╠
╠╠цддддддддддедддддддддддддддбдддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё   DATA   Ё Programador   ЁManutencao efetuada                         Ё╠╠
╠╠цддддддддддедддддддддддддддедддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё03/08/2007ЁNorbert Waage  ЁNa simulacao da alteracao via ExecAuto, nao Ё╠╠
╠╠Ё          Ё               Ёconsidera os itens deletados.               Ё╠╠
╠╠юддддддддддадддддддддддддддадддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function A415Altera(cAlias,nReg,nOpcx)

Local aArea		:= GetArea()
Local nX		:= 0
Local nOpcA		:= 0
Local nStack    := GetSX8Len() 
Local lContinua	:= .T.
Local cArqSCL	:= ""
Local cArqSCK	:= ""
Local oDlg
Local oSay
Local oGetDb
Local aPosObj   := {}
Local aObjects  := {}
Local aSize     := {}
Local aPosGet   := {}
Local aInfo     := {}
Local nGetLin   := 0
Local aTmp		:= {} 
Local lBloqueia	:= .F.

Private aTela[0][0]
Private aGets[0]
Private bCampo := { |nField| Field(nField) }
Private aHeader 	:= {}
Private aHeaderSCL:= {}
Private aHeaderSCK:= {}
PRIVATE aGEMCVnd    := {"",{},{}} //Template GEM - Condicao de Venda

l415Auto := If(Type("l415Auto")<>"U",l415Auto,.F.)

//зддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁSe for uma simulacao, ignora os itens deletados    Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддды
If l415Auto .AND. !(Type("lOnUpDate") == "U" .Or. lOnUpdate)
	For nX := 1 To Len(aAutoItens)
		If aScan(aAutoItens[nX],{|x| AllTrim(Upper(x[1])) == "AUTDELETA"}) == 0
			AAdd(aTmp,aClone(aAutoItens[nX]))
		EndIf
	Next nX
	aAutoItens	:= aClone(aTmp)
	aTmp		:= {}
EndIf

//-- Como e alteracao so ira mostrar opcionais
//-- caso alterere algo na linha (controle na A415FldOk)
lShowOpc := .F.

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё VerIfica se o Orcamento pode ser Alterado                    Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If ( MaCanAltOrc() )
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Inicializa as Variaveis da Enchoice                          Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	dbSelectArea("SCJ")
	lContinua := SoftLock("SCJ")
	For nX:= 1 To FCount()
		M->&(EVAL(bCampo,nX)) := FieldGet(nX)
	Next nX
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Monta aHeader                                                Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If A415Monta(@cArqSCK,@cArqSCL,.F.,{|| MaCanAltOrc() })
		aHeader := aHeaderSCK	
		If Type("l415Auto") != "L" .or. !l415Auto
			//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Faz o calculo automatico de dimensoes de objetos     Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
			aSize := MsAdvSize()

			aObjects := {}
			AAdd( aObjects, { 100, 100, .t., .t. } )
			AAdd( aObjects, { 100, 100, .t., .t. } )
			AAdd( aObjects, { 100, 015, .t., .f. } )

			aInfo := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 3, 3 }
			aPosObj := MsObjSize( aInfo, aObjects )

			aPosGet := MsObjGetPos(aSize[3]-aSize[1],315,;
				{{005,070,110,175,215,280}} )
			nGetLin := aPosObj[3,1]
			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Habilita a Tecla F4                                          Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			SetKey(VK_F4,{||A415F4()})
			DEFINE MSDIALOG oDlg TITLE OemtoAnsi(STR0001) From aSize[7],0 to aSize[6],aSize[5] of oMainWnd PIXEL//"Or┤amentos de Venda"
			EnChoice( "SCJ" ,nReg,nOpcx,,,,,aPosObj[1],,3,,,"A415VldTOk",,,.F.)
			oGetDb := MsGetDB():New(aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],nOpcx,"A415LinOk","A415TudOk","+CK_ITEM",.T., , ,.T.,,"TMP1","A415FldOk")
			Private oGetDad := oGetDb
			@ nGetLin,aPosGet[1,1] SAY oSay PROMPT OemToAnsi(STR0034) 	SIZE 060,009 PIXEL OF oDlg //"Total do Or┤amento "
			@ nGetLin,aPosGet[1,3] SAY oSay PROMPT OemToAnsi(STR0035) 	SIZE 060,009 PIXEL OF oDlg //"Descontos "
			@ nGetLin,aPosGet[1,5] SAY oSay PROMPT OemToAnsi("=")        SIZE 060,009 PIXEL OF oDlg //"="
			@ nGetLin,aPosGet[1,2] SAY oSay PROMPT 0    						SIZE 040,009 PICTURE PesqPict("SCK","CK_VALOR",16) PIXEL OF oDlg
			oSay:Cargo := "Valor"
			@ nGetLin,aPosGet[1,4] SAY oSay PROMPT 0    						SIZE 040,009 PICTURE PesqPict("SCK","CK_VALOR",14) PIXEL OF oDlg
			oSay:Cargo := "Desconto"
			@ nGetLin,aPosGet[1,6] SAY oSay PROMPT 0                     SIZE 040,009 PICTURE PesqPict("SCK","CK_VALOR",16) PIXEL OF oDlg
			oSay:Cargo := "Total"
			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Deleta os bonus para reavaliacao                             Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			A415Bonus(2)
			A415Total(oDlg)
			ACTIVATE MSDIALOG oDlg ON INIT Ma415Bar(oDlg,{||nOpcA:=1,IIf(A415VldTOk(@lBloqueia).And.oGetDb:TudoOk().And.Obrigatorio(aGets,aTela),oDlg:End(),nOpcA:=0)},{||oDlg:End()},nOpcx)
			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Desabilita a Tecla F4                                        Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			SetKey(VK_F4,Nil)
		Else
			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//ЁTratamento da Rotina automatica                               Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			nOpcA := 0
			If EnchAuto(cAlias,aAutoCab,{|| Obrigatorio(aGets,aTela)}) .And. MsGetDBAuto("TMP1",aAutoItens,"A415LinOk",{|| A415TudOk() .And. A415VldTOk(@lBloqueia)},aAutoCab,aRotina[nOpcx][4])
				nOpcA := 1
			EndIf
		EndIf
	EndIf
	If ( nOpcA == 1 )
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Efetua tratamento do Bonus                                   Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		A415Bonus(1)
		If Type("lOnUpDate") == "U" .Or. lOnUpdate
			Begin Transaction
				dbSelectArea(cAlias)
				MsGoto(nReg)
				If ( A415Grava(2,lBloqueia) )
					EvalTrigger()
					While GetSX8Len() > nStack 
						ConfirmSX8()
					EndDo
				EndIf
			End Transaction
		Else
			aAutoCab := MsAuto2Ench("SCJ")
			aAutoItens := MsAuto2GDb(aHeader,"TMP1")	
		EndIf
	Else
		While GetSX8Len() > nStack 
			RollBackSX8()
		EndDo
	EndIf
	
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Executa ponto de entrada na saida                            Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If ExistBlock( "MA415END" ) 
		ExecBlock( "MA415END", .F., .F., { nOpcA, 2 } ) 
	EndIf 	
	
	A415DesMonta(@cArqSCK,@cArqSCL)
Else
	Help(" ",1,"A415ALTERA")
EndIf
MsUnLockAll()
RestArea(aArea)
Return(.T.)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁA415ExcluiЁ Autor Ё Eduardo Riera         Ё Data Ё11.12.97  Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Programa de Alteracao de Orcamentos de Venda               Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё Void A415Exclui(ExpC1,ExpN1,ExpN2)                         Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpC1 = Alias do arquivo                                   Ё╠╠
╠╠Ё          Ё ExpN1 = Numero do registro                                 Ё╠╠
╠╠Ё          Ё ExpN2 = Opcao selecionada                                  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё MATA415                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
function A415Exclui(cAlias,nReg,nOpcx)

Local aArea		:= GetArea()
Local nX		:= 0  
Local nStack    := GetSX8Len() 
Local nOpcA		:= 0
Local cArqSCL	:= ""
Local cArqSCK	:= ""
Local lContinua:= .T.
Local oDlg
Local oSay
Local oGetDb
Local aPosObj   := {}
Local aObjects  := {}
Local aSize     := {}
Local aPosGet   := {}
Local aInfo     := {}
Local nGetLin   := 0

Private aTela[0][0]
Private aGets[0]
Private bCampo := { |nField| Field(nField) }
Private aHeader		:= {}
Private aHeaderSCL	:= {}
Private aHeaderSCK	:= {}
PRIVATE aGEMCVnd    := {"",{},{}} //Template GEM - Condicao de Venda
l415Auto := If(Type("l415Auto")<>"U",l415Auto,.F.)
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё VerIfica se o Orcamento pode ser Excluido                    Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If ( MaNoDelOrc("SCJ") )
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Inicializa as Variaveis da Enchoice                          Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	dbSelectArea("SCJ")
	lContinua := Softlock("SCJ")
	For nX:= 1 To FCount()
		M->&(EVAL(bCampo,nX)) := FieldGet(nX)
	Next nX
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Monta aHeader                                                Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If A415Monta(@cArqSCK,@cArqSCL,.F.,{|| MaNoDelOrc() })
		aHeader := aHeaderSCK
		If Type("l415Auto") != "L" .or. !l415Auto
			//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Faz o calculo automatico de dimensoes de objetos     Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
			aSize := MsAdvSize()

			aObjects := {}
			AAdd( aObjects, { 100, 100, .t., .t. } )
			AAdd( aObjects, { 100, 100, .t., .t. } )
			AAdd( aObjects, { 100, 015, .t., .f. } )

			aInfo := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 3, 3 }
			aPosObj := MsObjSize( aInfo, aObjects )

			aPosGet := MsObjGetPos(aSize[3]-aSize[1],315,;
				{{005,070,110,175,215,280}} )
			nGetLin := aPosObj[3,1]

			DEFINE MSDIALOG oDlg TITLE OemtoAnsi(STR0001) From aSize[7],0 to aSize[6],aSize[5] of oMainWnd PIXEL//"Or┤amentos de Venda"
			EnChoice("SCJ",nReg,nOpcx,,,,,APosObj[1],,3,,,,,,.F.)
			oGetDb := MsGetDB():New(aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],nOpcx,"A415LinOk","A415TudOk","+CK_ITEM",.T., , ,.T., ,"TMP1")
			Private oGetDad := oGetDb			
			@ nGetLin,aPosGet[1,1] SAY oSay PROMPT OemToAnsi(STR0034) 	SIZE 060,009 PIXEL OF oDlg //"Total do Or┤amento "
			@ nGetLin,aPosGet[1,3] SAY oSay PROMPT OemToAnsi(STR0035) 	SIZE 060,009 PIXEL OF oDlg //"Descontos "
			@ nGetLin,aPosGet[1,5] SAY oSay PROMPT OemToAnsi("=")        SIZE 060,009 PIXEL OF oDlg //"="
			@ nGetLin,aPosGet[1,2] SAY oSay PROMPT 0    						SIZE 040,009 PICTURE PesqPict("SCK","CK_VALOR",16) PIXEL OF oDlg
			oSay:Cargo := "Valor"
			@ nGetLin,aPosGet[1,4] SAY oSay PROMPT 0    						SIZE 040,009 PICTURE PesqPict("SCK","CK_VALOR",14) PIXEL OF oDlg
			oSay:Cargo := "Desconto"
			@ nGetLin,aPosGet[1,6] SAY oSay PROMPT 0                     SIZE 040,009 PICTURE PesqPict("SCK","CK_VALOR",16) PIXEL OF oDlg
			oSay:Cargo := "Total"
			A415Total(oDlg)
			ACTIVATE MSDIALOG oDlg ON INIT Ma415Bar(oDlg,{||nOpcA:=1,IIf(oGetDb:TudoOk(),oDlg:End(),nOpcA:=0)},{||oDlg:End()},nOpcx)
		Else
			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//ЁTratamento da Rotina automatica                               Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			nOpcA := 0
			If EnchAuto(cAlias,aAutoCab,{|| Obrigatorio(aGets,aTela)}) .And. MsGetDBAuto("TMP1",aAutoItens,"AllwaysTrue",{|| .T. },aAutoCab,aRotina[nOpcx][4])
				nOpcA := 1
			EndIf
		EndIf
		If ( ExistBlock("MT415EXC") )
			If (!ExecBlock("MT415EXC",.F.,.F.))
				nOpcA := 0
			EndIf
		EndIf
	Else
		Help(" ",1,"A415EXCLUI")
	EndIf
	If ( nOpcA == 1 )
		If Type("lOnUpDate") == "U" .Or. lOnUpdate
			Begin Transaction
				If ( A415Grava(3) )
					EvalTrigger()
					While GetSX8Len() > nStack 
						ConfirmSX8()
					EndDo
				EndIf
			End Transaction
		Else
			aAutoCab := MsAuto2Ench("SCJ")
			aAutoItens := MsAuto2GDb(aHeader,"TMP1")			
		EndIf
	Else
		While GetSX8Len() > nStack 
			RollBackSX8()
		EndDo 
	EndIf               

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Exclui a amarracao com os conhecimentos                      Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	MsDocument( "SCJ", SCJ->( RecNo() ), 2, , 3 ) 
	
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Executa ponto de entrada na saida                            Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If ExistBlock( "MA415END" ) 
		ExecBlock( "MA415END", .F., .F., { nOpcA, 3 } ) 
	EndIf 	
	
	A415DesMonta(@cArqSCK,@cArqSCL)
Else
	Help(" ",1,"A415EXCLUI")
EndIf
MsUnLockAll()
RestArea(aArea)
Return(.T.)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁA415Copia Ё Autor Ё Eduardo Riera         Ё Data Ё05.01.98  Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Programa de Copia dos Orcamentos de Venda                  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё Void A415Copia (ExpC1,ExpN1,ExpN2)                         Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpC1 = Alias do arquivo                                   Ё╠╠
╠╠Ё          Ё ExpN1 = Numero do registro                                 Ё╠╠
╠╠Ё          Ё ExpN2 = Opcao selecionada                                  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё MATA415                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function A415Copia(cAlias,nReg,nOpcx)

Local aArea		:= GetArea()
Local nX		:= 0
Local nOpcA		:= 0
Local nStack    := GetSX8Len() 
Local cArqSCL	:= ""
Local cArqSCK	:= ""
Local lMt415Cpy := ExistBlock("MT415CPY")
Local oDlg
Local oSay
Local oGetDB
Local aPosObj   := {}
Local aObjects  := {}
Local aSize     := {}
Local aPosGet   := {}
Local aInfo     := {}
Local aCposCopy := {}
Local nGetLin   := 0
Local lContinua := .T.

Private aTela[0][0]
Private aGets[0]
Private bCampo := { |nField| Field(nField) }
Private aHeader 	:= {}
Private aHeaderSCL	:= {}
Private aHeaderSCK	:= {}
PRIVATE aGEMCVnd    := {"",{},{}} //Template GEM - Condicao de Venda

IF ( (ExistBlock("M415COPIA")) )
	If (! ExecBlock("M415COPIA",.F.,.F.) )
		lContinua := .F.
	EndIf
EndIf

If ( xFilial("SCJ") == SCJ->CJ_FILIAL ) .And. lContinua
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Inicializa as Variaveis da Enchoice                          Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	
	If lMt415Cpy
		aCposCopy := ExecBlock("MT415CPY",.F.,.F.)
	Endif	
	
	dbSelectArea("SCJ")
	For nX:= 1 To FCount()
		If Ascan(aCposCopy, EVAL(bCampo,nX) ) == 0
			M->&(EVAL(bCampo,nX)) := FieldGet(nX)
		Else	                                  
			M->&(EVAL(bCampo,nX)) := CriaVar(EVAL(bCampo,nX),.T.)
		Endif	
	Next nX
	M->CJ_NUM     := CriaVar("CJ_NUM",.T.)
	M->CJ_EMISSAO := dDataBase
	M->CJ_VALIDA  := CriaVar("CJ_VALIDA",.T.)
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Monta aHeader                                                Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	A415Monta(@cArqSCK,@cArqSCL,.F., , ,.T.)	
	aHeader := aHeaderSCK

	//
	// Template GEM - Gestao de Empreendimentos Imobiliarios
	//
	// faz a copia da condicao de venda se a mesma tiver 
	// uma vinculacao com a condicao de pagamento
	//
	If ExistTemplate("GEMCPYORC") 
		// copia a condicao de venda
		aGEMCVnd := ExecTemplate("GEMCPYORC",.F.,.F.,{ aGEMCVnd ,SCJ->CJ_NUM ,SCJ->CJ_CONDPAG })
	EndIf

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Habilita a Tecla F4                                          Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	SetKey(VK_F4,{||A415F4()})
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Faz o calculo automatico de dimensoes de objetos     Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
	aSize := MsAdvSize()

	aObjects := {}
	AAdd( aObjects, { 100, 100, .t., .t. } )
	AAdd( aObjects, { 100, 100, .t., .t. } )
	AAdd( aObjects, { 100, 015, .t., .f. } )

	aInfo := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 3, 3 }
	aPosObj := MsObjSize( aInfo, aObjects )

	aPosGet := MsObjGetPos(aSize[3]-aSize[1],315,;
		{{005,070,110,175,215,280}} )
	nGetLin := aPosObj[3,1]

	DEFINE MSDIALOG oDlg TITLE OemtoAnsi(STR0001) From aSize[7],0 to aSize[6],aSize[5] of oMainWnd PIXEL//"Or┤amentos de Venda"
	EnChoice( "SCJ" ,nReg,nOpcx,,,,,aPosObj[1],,3,,,"A415VldTOk",,,.F.)
	oGetDb := MsGetDB():New(aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],nOpcx,"A415LinOk","A415TudOk","+CK_ITEM",.T., , ,.T., ,"TMP1", , ,.F.)
	Private oGetDad := oGetDb	
	@ nGetLin,aPosGet[1,1] SAY oSay PROMPT OemToAnsi(STR0034) 	SIZE 060,009 PIXEL OF oDlg //"Total do Or┤amento "
	@ nGetLin,aPosGet[1,3] SAY oSay PROMPT OemToAnsi(STR0035) 	SIZE 060,009 PIXEL OF oDlg //"Descontos "
	@ nGetLin,aPosGet[1,5] SAY oSay PROMPT OemToAnsi("=")        SIZE 060,009 PIXEL OF oDlg //"="
	@ nGetLin,aPosGet[1,2] SAY oSay PROMPT 0    						SIZE 040,009 PICTURE PesqPict("SCK","CK_VALOR",16) PIXEL OF oDlg
	oSay:Cargo := "Valor"
	@ nGetLin,aPosGet[1,4] SAY oSay PROMPT 0    						SIZE 040,009 PICTURE PesqPict("SCK","CK_VALOR",14) PIXEL OF oDlg
	oSay:Cargo := "Desconto"
	@ nGetLin,aPosGet[1,6] SAY oSay PROMPT 0                     SIZE 040,009 PICTURE PesqPict("SCK","CK_VALOR",16) PIXEL OF oDlg
	oSay:Cargo := "Total"
	A415Total(oDlg)
	ACTIVATE MSDIALOG oDlg ON INIT Ma415Bar(oDlg,{||nOpcA:=1,IIf(oGetDb:TudoOk(),oDlg:End(),nOpcA:=0)},{||oDlg:End()},nOpcx)
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Desabilita a Tecla F4                                        Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	SetKey(VK_F4,Nil)
	If ( nOpcA == 1 )
		If Type("lOnUpDate") == "U" .Or. lOnUpdate
			Begin Transaction
				If ( A415Grava(1) )
					EvalTrigger()
					While GetSX8Len() > nStack 
						ConfirmSX8()
					EndDo
					If (ExistBlock("MT415G1"))
						ExecBlock("MT415G1",.F.,.f.)
					EndIf
				EndIf
			End Transaction
		Else
			aAutoCab := MsAuto2Ench("SCJ")
			aAutoItens := MsAuto2GDb(aHeader,"TMP1")			
		EndIf
	Else
		While GetSX8Len() > nStack 
			RollBackSX8()
		EndDo 
	EndIf
	
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Executa ponto de entrada na saida                            Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If ExistBlock( "MA415END" ) 
		ExecBlock( "MA415END", .F., .F., { nOpcA, 1 } ) 
	EndIf 	
	
	A415DesMonta(@cArqSCK,@cArqSCL)
	MsUnLockAll()
EndIf
RestArea(aArea)
Return(.T.)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁA415CancelЁ Autor Ё Eduardo Riera         Ё Data Ё07.01.98  Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Programa de Cancelamento dos Orcamentos de Venda           Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe e Ё Void A415Cancel(ExpC1,ExpN1,ExpN2)                         Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpC1 = Alias do arquivo                                   Ё╠╠
╠╠Ё          Ё ExpN1 = Numero do registro                                 Ё╠╠
╠╠Ё          Ё ExpN2 = Opcao selecionada                                  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё MATA416                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function A415Cancel(cAlias,nReg,nOpcx)

Local aArea		:= GetArea()
Local nX		:= 0
Local cArqSCL	:= ""
Local cArqSCK	:= ""
Local nOpcA		:= 0
Local lContinua := .T.
Local oDlg
Local oSay
Local oGetDb

Private aTela[0][0]
Private aGets[0]
Private bCampo := { |nField| Field(nField) }
Private aHeader		:= {}
Private aHeaderSCL	:= {}
Private aHeaderSCK	:= {}
PRIVATE aGEMCVnd    := {"",{},{}} //Template GEM - Condicao de Venda

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Somente pode ser cancelados orcamentos em aberto             Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If ( MaCanAltOrc() )
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Inicializa as Variaveis da Enchoice                          Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	dbSelectArea("SCJ")
	lContinua := Softlock("SCJ")
	For nX:= 1 To FCount()
		M->&(EVAL(bCampo,nX)) := FieldGet(nX)
	Next nX
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Monta aHeader                                                Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	lContinua := A415Monta(@cArqSCK,@cArqSCL,.F.,{ || MaCanAltOrc() })
	aHeader := aHeaderSCK
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Habilita a Tecla F4                                          Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	SetKey(VK_F4,{||A415F4()})
	If  ( lContinua )
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Faz o calculo automatico de dimensoes de objetos     Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
		aSize := MsAdvSize()

		aObjects := {}
		AAdd( aObjects, { 100, 100, .t., .t. } )
		AAdd( aObjects, { 100, 100, .t., .t. } )
		AAdd( aObjects, { 100, 015, .t., .f. } )

		aInfo := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 3, 3 }
		aPosObj := MsObjSize( aInfo, aObjects )

		aPosGet := MsObjGetPos(aSize[3]-aSize[1],315,;
			{{005,070,110,175,215,280}} )
		nGetLin := aPosObj[3,1]

		DEFINE MSDIALOG oDlg TITLE OemtoAnsi(STR0001) From aSize[7],0 to aSize[6],aSize[5] of oMainWnd PIXEL//"Or┤amentos de Venda"
		EnChoice( "SCJ", nReg,nOpcx,,,,,aPosObj[1],,3,,,,,,.F.)
		oGetDb := MsGetDB():New(aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],nOpcx,"A415LinOk","A415TudOk","+CK_ITEM",.T., , ,.T., ,"TMP1")
		Private oGetDad := oGetDb		
		@ nGetLin,aPosGet[1,1] SAY oSay PROMPT OemToAnsi(STR0034) 	SIZE 060,009 PIXEL OF oDlg //"Total do Or┤amento "
		@ nGetLin,aPosGet[1,3] SAY oSay PROMPT OemToAnsi(STR0035) 	SIZE 060,009 PIXEL OF oDlg //"Descontos "
		@ nGetLin,aPosGet[1,5] SAY oSay PROMPT OemToAnsi("=")        SIZE 060,009 PIXEL OF oDlg //"="
		@ nGetLin,aPosGet[1,2] SAY oSay PROMPT 0    						SIZE 040,009 PICTURE PesqPict("SCK","CK_VALOR",16) PIXEL OF oDlg
		oSay:Cargo := "Valor"
		@ nGetLin,aPosGet[1,4] SAY oSay PROMPT 0    						SIZE 040,009 PICTURE PesqPict("SCK","CK_VALOR",14) PIXEL OF oDlg
		oSay:Cargo := "Desconto"
		@ nGetLin,aPosGet[1,6] SAY oSay PROMPT 0                     SIZE 040,009 PICTURE PesqPict("SCK","CK_VALOR",16) PIXEL OF oDlg
		oSay:Cargo := "Total"
		A415Total(oDlg)
		ACTIVATE MSDIALOG oDlg ON INIT Ma415Bar(oDlg,{||nOpcA:=1,IIf(oGetDb:TudoOk(),oDlg:End(),nOpcA:=0)},{||oDlg:End()}, nOpcx)
	Else
		Help(" ",1,"A415CANCEL")
	EndIf
	
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁPonto de entrada para a validacao do cancelamento             Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	
	If ExistBlock("M415CANC")
		nOpca := ExecBlock("M415CANC",.F.,.F.,nOpca)
	Endif
	
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁDesabilita a Tecla F4                                         Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	SetKey(VK_F4,Nil)
	If ( nOpcA == 1 )
		Begin Transaction
			dbSelectArea("SCJ")
			RecLock("SCJ",.F.)
			dbSelectArea("SCK")
			dbSetOrder(1)
			MsSeek(xFilial("SCK")+SCJ->CJ_NUM)
			While ( !Eof() .And. SCK->CK_FILIAL==xFilial("SCK") .And.;
					SCK->CK_NUM == SCJ->CJ_NUM )
				MaAvalOrc("SCK",4)
				dbSelectArea("SCK")
				dbSkip()
			EndDo
			MaAvalOrc("SCJ",14)
			MsUnlock()
		End Transaction
	EndIf
	A415DesMonta(@cArqSCK,@cArqSCL)
Else
	Help(" ",1,"A415CANCEL")
EndIf
MsUnLockAll()
RestArea(aArea)
Return(.T.)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁA415Baixa Ё Autor Ё Eduardo Riera         Ё Data Ё07.01.98  Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Programa de Baixa dos Orcamentos de Venda                  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe e Ё Void A415Baixa (ExpC1,ExpN1,ExpN2)                         Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpC1 = Alias do arquivo                                   Ё╠╠
╠╠Ё          Ё ExpN1 = Numero do registro                                 Ё╠╠
╠╠Ё          Ё ExpN2 = Opcao selecionada                                  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё MATA416                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function A415Baixa(cAlias,nReg,nOpcx)

Local aArea		:= GetArea()
Local nX		:= 0
Local cArqSCL	:= ""
Local cArqSCK	:= ""
Local lContinua	:= .T.
Local nOpcA    	:= 0
Local oDlg
Local oSay
Local oGetDb

Local aSize       := {}
Local aPosObj     := {}
Local aPosGet     := {}
Local aInfo       := {}
Local aObjects    := {}
Local nLinGet     := 0
Local lMt415Aut   := Existblock("MT415AUT")

Private aTela[0][0]
Private aGets[0]
Private bCampo 		:= { |nField| Field(nField) }
Private aHeader		:= {}
Private aHeaderSCL	:= {}
Private aHeaderSCK	:= {}
PRIVATE aGEMCVnd    := {"",{},{}} //Template GEM - Condicao de Venda

l415Auto := If(Type("l415Auto")<>"U",l415Auto,.F.)
l416Auto := If(Type("l416Auto")<>"U",l416Auto,.F.)

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Somente pode ser Baixados orcamentos em aberto               Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If ( SCJ->CJ_STATUS == "A" )

	If lMt415Aut
		If !Execblock("MT415AUT",.F.,.F.)
			Return
		Endif
	Endif

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Inicializa as Variaveis da Enchoice                          Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	dbSelectArea("SCJ")
	lContinua := Softlock("SCJ")
	For nX:= 1 To FCount()
		M->&(EVAL(bCampo,nX)) := FieldGet(nX)
	Next nX
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Monta aHeader                                                Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	A415Monta(@cArqSCK,@cArqSCL,.F.)
	aHeader := aHeaderSCK
	If !(l416Auto)
		If  ( lContinua )
			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Habilita a Tecla F4                                          Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			SetKey(VK_F4,{||A415F4()})

			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Definicao automatica - Tamanho de objetos                    Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			aSize := MsAdvSize( , .F. )

			aObjects := {}

			AAdd( aObjects, { 100, 100, .T., .T. } )
			AAdd( aObjects, { 100, 100, .T., .T. } )
			AAdd( aObjects, { 100,  15, .T., .F. } ) 						

			aInfo := { aSize[1], aSize[2], aSize[3], aSize[4], 3, 3 }

			aPosObj := MsObjSize( aInfo, aObjects )

			DEFINE MSDIALOG oDlg TITLE OemtoAnsi(STR0001) From aSize[7],0 to aSize[6],aSize[5] PIXEL //"Or┤amentos de Venda"

			EnChoice( "SCJ", nReg,2,,,,,aPosObj[1],,3,,,,,,.F.)
			oGetDb := MsGetDB():New(aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],2,"A415LinOk","A415TudOk","+CK_ITEM",.F., , ,.T., ,"TMP1")
			Private oGetDad := oGetDb			

			aPosGet := MsObjGetPos(aPosObj[3,4]-aPosObj[3,2],315,;
				{{005,110,215,070,175,280}} )

			nLinGet := aPosObj[3,1]							

			@ nLinGet,aPosGet[1,1] SAY oSay PROMPT OemToAnsi(STR0034) 	SIZE 060,009 PIXEL OF oDlg //"Total do Or┤amento "
			@ nLinGet,aPosGet[1,2] SAY oSay PROMPT OemToAnsi(STR0035) 	SIZE 060,009 PIXEL OF oDlg //"Descontos "
			@ nLinGet,aPosGet[1,3] SAY oSay PROMPT OemToAnsi("=")        SIZE 060,009 PIXEL OF oDlg //"="
			@ nLinGet,aPosGet[1,4] SAY oSay PROMPT 0    						SIZE 040,009 PICTURE PesqPict("SCK","CK_VALOR",16) PIXEL OF oDlg
			oSay:Cargo := "Valor"
			@ nLinGet,aPosGet[1,5] SAY oSay PROMPT 0    						SIZE 040,009 PICTURE PesqPict("SCK","CK_VALOR",14) PIXEL OF oDlg
			oSay:Cargo := "Desconto"
			@ nLinGet,aPosGet[1,6] SAY oSay PROMPT 0                     SIZE 040,009 PICTURE PesqPict("SCK","CK_VALOR",16) PIXEL OF oDlg
			oSay:Cargo := "Total"
			A415Total(oDlg)
			ACTIVATE MSDIALOG oDlg ON INIT Ma415Bar(oDlg,{||nOpcA:=1,oDlg:End()},{||oDlg:End()},nOpcx)
			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Desabilita a Tecla F4                                        Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			SetKey(VK_F4,Nil)
		EndIf
	Else
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁTratamento da Rotina automatica                               Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		nOpcA := 0
		If EnchAuto(cAlias,aAutoCab,{|| Obrigatorio(aGets,aTela)}) .And. MsGetDBAuto("TMP1",aAutoItens,"A415LinOk",{|| .T.},aAutoCab,aRotina[nOpcx][4])
			nOpcA := 1
		EndIf
	EndIf
	If ( ExistBlock("MT415EFT") )
		If (!ExecBlock("MT415EFT",.F.,.F.,{nOpcA}))
			nOpcA := 0
		Endif	
	EndIf	
	If ( nOpcA == 1 )
		Begin Transaction
			a416GrvBx()
		End Transaction
	EndIf
	A415DesMonta(@cArqSCK,@cArqSCL)
Else
	Help(" ",1,"A415BAIXA")
EndIf
MsUnLockAll()
RestArea(aArea)
Return(.F.)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁA415Impri Ё Autor Ё Eduardo Riera         Ё Data Ё22.12.97  Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Programa de Impressao                                      Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe e Ё Void A415Impri()                                           Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё MATA415                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function A415Impri()

Local cPrinter := GetMV("MV_ORCIMPR")
Local lPrinter := ExistBlock(cPrinter,,.T.)

If (lPrinter)
	ExecBlock(cPrinter,.F.,.F.)
EndIf
dbSelectArea("SCJ")
Return(.T.)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁA415Monta   Ё Autor ЁEduardo Riera        Ё Data Ё 08.12.97 Ё╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Monta aHeader e aCols dos arquivos Temporarios             Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё Void A415Monta(Void)                                       Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁExpC1: Arquivo temporario SCK                               Ё╠╠
╠╠Ё          ЁExpC2: Arquivo temporario SCL                               Ё╠╠
╠╠Ё          ЁExpL3: Indica se esta no modulo de inclusao                 Ё╠╠
╠╠Ё          ЁExpB4: CodeBlock a ser avaliado para o retorno da funcao    Ё╠╠
╠╠Ё          ЁExpL5: Indica se os campos de Cotacao devem ser habilitados Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё ExpL1: Conforme indicado pelo CodeBlock em ExpB4, quando o Ё╠╠
╠╠Ё          Ё este indicar .F., a execucao da funcao sera interrompida.  Ё╠╠
╠╠Ё          Ё Para o codeblock sera informado o alias do SCJ e do SCK.   Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё MATA415                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function A415Monta(cArqSCK,cArqSCL,lInclui,bBlock,lCotacao, lCopia)

Local aArea		:= GetArea()
Local aAreaSCK	:= SCK->(GetArea())
Local aAreaSCL	:= SCL->(GetARea())
Local aNoFields	:= {}
Local aCpoNCopia:= {}
Local lRetorno	:= .T.

Local bCond   				// Se bCond .T. executa bAction1, senao executa bAction2
Local bAction1 				// Retornar .T. para considerar o registro e .F. para desconsiderar
Local bAction2 				// Retornar .T. para considerar o registro e .F. para desconsiderar

Local aAreaSX3		:= SX3->(GetArea()) 
Local aCpoVirtual 	:= {}

#IFDEF TOP
	Local cQuery   := ""
#ENDIF

DEFAULT lCotacao := !Empty(lCotacao)
DEFAULT bBlock   := {|| .T.}
DEFAULT aStackTMP:= {}
DEFAULT lCopia   := .F.

Private lRetAux := .T.

bCond    := {|| .T.}
bAction1 := bBlock
bAction2 := {|| .F. }

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Monta aHeader do SCK                                         Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
aHeader := {}

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Verifica se o TMP1 esta aberto                               Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If Select("TMP1")<>0
	dbSelectArea("TMP1")
	dbCloseArea()
EndIf

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Monta arquivo de Trabalho TMP1                               Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
dbSelectArea("SCK")
dbSetOrder(1)

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Guarda o TMP1 na Pilha - Utilizado no Sales Tracker          Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
aadd(aStackTMP ,{cArqSCK,"",""})

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Preenchimento do arquivo temporario                          Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁFillGetDb( nOpcx, cAlias, cTrb, cKeyInd, nOrder, cSeekKey, bSeekWhile, uSeekFor, aNoFields, aYesFields,		Ё
//Ё			  lOnlyYes, cQuery, bMountFile )																							Ё
//ЁnOpcx			- Opcao (inclusao, exclusao, etc.)																					Ё
//ЁcAlias		- Alias da tabela referente aos itens																				Ё
//ЁcTrb			- Alias da tabela temporaria																							Ё
//ЁcKeyInd		- Chave com indice a ser criado caso nao exista no SINDEX													Ё
//ЁnOrder		- Ordem do SINDEX																											Ё
//ЁcSeekKey		- Chave de pesquisa																										Ё
//ЁbSeekWhile	- Loop na tabela cALias																									Ё
//ЁuSeekFor		- Valida cada registro da tabela cALias (retornar .T. para considerar e .F. para desconsiderar)	Ё
//ЁaNoFields	- Array com nome dos campos que serao excluidos na montagem do aHeader									Ё
//ЁaYesFields	- Array com nome dos campos que serao incluidos na montagem do aHeader									Ё
//ЁlOnlyYes		- Flag indicando se considera somente os campos declarados no aYesFields + campos do usuario		Ё
//ЁcQuery		- Query para filtro da tabela cAlias (se for TOP e cQuery estiver preenchido, desconsidera 		Ё
//Ё				  parametros cSeekKey e bSeekWhiele) 																				Ё
//ЁbMountFile	- Preenchimento do aCols pelo usuario (aHeader e aCols ja estarao criados)								Ё
//ЁaNoHeader	- Array com campos que serao utilizados porem nЦo podem se apresentados no aHeader. Ex. campos	Ё
//Ё              utilizados no cKeyInd                                                                         Ё
//ЁlInclui		- Se inclusao passar .T. para qua aCols seja incializada com 1 linha em branco                  Ё
//ЁaCpoEmpty	- Array com campos que serao apresentados vazios (utilizado na opcao Copia)                     Ё
//ЁaCpoVirtual	- Array com campos virtuais que devem ser apresentados                                          Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
cQuery := ""
cSeek := ""
cWhile := ""
If ( !lInclui )  
	#IFDEF TOP
		cQuery := "SELECT SCK.*,SCK.R_E_C_N_O_ SCKRECNO "
		cQuery += "FROM "+RetSqlName("SCK")+" SCK "
		cQuery += "WHERE "
		cQuery += "SCK.CK_FILIAL='"+xFilial("SCK")+"' AND "
		cQuery += "SCK.CK_NUM='"+SCJ->CJ_NUM+"' AND "
		cQuery += "SCK.D_E_L_E_T_<>'*'"
		cQuery += "ORDER BY "+SqlOrder(SCK->(IndexKey()))
	#ENDIF		
	cSeek := xFilial("SCK")+SCJ->CJ_NUM
	cWhile := "SCK->CK_FILIAL + SCK->CK_NUM"
EndIf

If lCopia
	aCpoNCopia := { "CK_NUMPV","CK_NUMOP","CK_CONTRAT","CK_ITEMCON"}	
EndIf


//здддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁInclusao de campos virtuais em uso na tabela SCKЁ
//юдддддддддддддддддддддддддддддддддддддддддддддддды
aCpoVirtual := {}
DbSelectArea("SX3")
DbSetOrder(1)
DbSeek("SCK")
While !EOF() .AND. SX3->X3_ARQUIVO == "SCK"
	If X3Uso(SX3->X3_USADO) .AND. cNivel >= SX3->X3_NIVEL .AND. SX3->X3_CONTEXT == "V"
		aAdd(aCpoVirtual, AllTrim(SX3->X3_CAMPO)) 					
	EndIf
	DbSkip()
End                                    
RestArea(aAreaSX3)
If aScan(aCpoVirtual, {|x|x=="CK_OPER"}) <= 0
	aAdd(aCpoVirtual, "CK_OPER")
EndIf	

FillGetDb(2, "SCK", "TMP1",,1,cSeek, { || &cWhile },{{bCond,bAction1,bAction2}},aNoFields,,,cQuery,,,lInclui,aCpoNCopia,aCpoVirtual)
aHeaderSCK := aClone(aHeader)
	
lRetorno := lRetAux
If ( lRetorno )

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Verifica se o TMP2 esta aberto                               Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If Select("TMP2")<>0
		dbSelectArea("TMP2")
		dbCloseArea()
	EndIf

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Preenchimento do arquivo temporario                          Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды	
	cQuery := ""
	cSeek := ""
	cWhile := ""

	If ( !lInclui )   
		#IFDEF TOP		
			cQuery := "SELECT SCL.*,SCL.R_E_C_N_O_ SCLRECNO "
			cQuery += "FROM "+RetSqlName("SCL")+" SCL "
			cQuery += "WHERE "
			cQuery += "SCL.CL_FILIAL='"+xFilial("SCL")+"' AND "
			cQuery += "SCL.CL_NUM='"+SCJ->CJ_NUM+"' AND "
			cQuery += "SCL.D_E_L_E_T_<>'*'"
			cQuery := ChangeQuery(cQuery)		
		#ELSE
			cSeek := xFilial("SCL")+SCJ->CJ_NUM
			cWhile := "SCL->CL_FILIAL + SCL->CL_NUM"
		#ENDIF
	EndIf			
	
	aHeader := {}
	aNoHeader := {}
	AADD(aNoHeader,"CL_NUM")
	AADD(aNoHeader,"CL_ITEMORC")
	FillGetDb(2, "SCL", "TMP2","CL_NUM+CL_ITEMORC+CL_ITEM+CL_PRODUTO",1,cSeek, { || &cWhile }, , , , ,cQuery , ,aNoHeader)
	aHeaderSCL := aClone(aHeader)
		
EndIf
RestArea(aAreaSCK)
RestArea(aAreaSCL)
RestArea(aArea)
Return(lRetorno)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁA415DesMontaЁ Autor ЁEduardo Riera        Ё Data Ё 08.12.97 Ё╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Desmonta os arquivos temporarios.                          Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё Void A415DesMonta( )                                       Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁExpC1: Arquivo temporario SCK                               Ё╠╠
╠╠Ё          ЁExpC2: Arquivo temporario SCL                               Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё MATA415                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function A415DesMonta(cArqSCK,cArqSCL)


If Select("TMP1")<>0
	dbSelectArea("TMP1")
	dbCloseArea()
	FErase(cArqSCK+GetDBExtension())
	FErase(cArqSCK+OrdBagExt())
EndIf
If Select("TMP2")<>0
	dbSelectArea("TMP2")
	dbCloseArea()
	FErase(cArqSCL+GetDBExtension())
	FErase(cArqSCL+OrdBagExt())
EndIf
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Controle de Pilha do Sales Tracker - Quando o orcamento e chamada varias vezes Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
aStackTMP  := aDel(aStackTMP ,Len(aStackTMP ))
aStackTMP  := aSize(aStackTMP ,Len(aStackTMP )-1)
If !Empty(aStackTMP)
	If !Empty(aStackTMP[Len(aStackTMP ),1])
		dbUseArea(.T.,,aStackTMP [Len(aStackTMP ),1],"TMP1",.F.,.F.)
	EndIf	
	If !Empty(aStackTMP[Len(aStackTMP ),2])	
		dbUseArea(.T.,,aStackTMP [Len(aStackTMP ),2],"TMP2",.F.,.F.)
	EndIf
	If !Empty(aStackTMP[Len(aStackTMP ),3])	
		dbSetIndex( aStackTMP [Len(aStackTMP),3] + OrdBagExt() )
	EndIf
EndIf

Return(.T.)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁA415Grava   Ё Autor ЁEduardo Riera        Ё Data Ё 08.12.97 Ё╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Grava os Orcamentos de Venda.                              Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё Void A415Grava(nExpN1)                                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ nExpN1 : 1 - Inclusao / 2 - Alteracao / 3 - Excluir        Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё MATA415                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function A415Grava(nOpcx,lBloqOrc)

Local aRecTMP1  := {}

Local nX  := 0
Local nPosField:= 0
Local cItem    := "00"
Local cItemAnt := ""
Local cNumOrc  := ""
Local cMay     := "" 
Local lGravou  := .F.
Local l415Grava:= ExistBlock("M415GRV")
Local nCnt     := 0

Default lBloqOrc	:= .F.

//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё nOpcx: 1 - Inclusao de Registros                     Ё
//Ё nOpcx: 2 - Alteracao de Registros                    Ё
//Ё nOpcx: 3 - Exclusao de Registros                     Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁPosiciona Cliente                                             Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
dbSelectArea("SA1")
dbSetorder(1)
MsSeek(xFilial("SA1")+M->CJ_CLIENTE+M->CJ_LOJA)
If ( nOpcx == 1 ) // Inclusao     

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Verifica se existe algum item a ser gravado                  Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	
	dbSelectArea("TMP1")
	dbGotop()
	While ( !Eof() )
		If ( !TMP1->CK_FLAG .And. !Empty(TMP1->CK_PRODUTO)  )
			lGravou := .T.
			Aadd(aRecTMP1,TMP1->(Recno()))			
		Endif		
		dbSelectArea("TMP1")
		dbSkip()
	EndDo

	dbSelectArea("TMP2")
	dbGotop()       
	While ( !Eof() )	
		If ( !TMP2->CL_FLAG )
			lGravou := .T.
			Exit
		Endif		
		dbSelectArea("TMP2")
		dbSkip()
	EndDo

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Verifica se o numero ja foi gravado                          Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	cNumOrc := M->CJ_NUM 
	cMay := "SCJ"+ Alltrim(xFilial("SCJ"))
	SCJ->(dbSetOrder(1))
	While ( SCJ->( DbSeek(xFilial("SCJ")+cNumOrc) ) .or. !MayIUseCode(cMay+cNumOrc) )
		cNumOrc := Soma1(cNumOrc,Len(M->CJ_NUM))
	EndDo
                     
	M->CJ_NUM := cNumOrc 

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Efetua a Gravacao do Cabecario                               Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	
	If ( lGravou )
		dbSelectArea("SCJ")
		RecLock("SCJ",.T.)
		For nX := 1 To FCount()
			If ("FILIAL" $ FieldName(nX) )
				FieldPut(nX,xFilial())
			ElseIf (("TABELA" $ FieldName(nX)) .And. (M->&(FieldName(nX)) == PadR("1",Len(DA0->DA0_CODTAB))))
				FieldPut(nX,"")
			Else
				FieldPut(nX,M->&(FieldName(nX)))
			EndIf
		Next nX
		
		If lBloqOrc
			MaAvalOrc("SCJ",11,"F")
		Else
			MaAvalOrc("SCJ",11)
		EndIf

	EndIf

    SCJ->(FkCommit())

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Efetua a Gravacao do Itens                                   Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	cItem := "00"
	
	For nCnt := 1 to Len(aRecTMP1)

		TMP1->(MsGoto(aRecTMP1[nCnt]))
	
		dbSelectArea("SCK")
		RecLock("SCK",.T.)
		For nX := 1 To FCount()
			nPosField := TMP1->(FieldPos(SCK->(FieldName(nX))))
			If ( nPosField <> 0 )
				FieldPut(nX,TMP1->(FieldGet(nPosField)))
			EndIf
		Next nX
		SCK->CK_FILIAL := xFilial("SCK")
		SCK->CK_NUM    := M->CJ_NUM
		SCK->CK_CLIENTE:= M->CJ_CLIENTE
		SCK->CK_LOJA   := M->CJ_LOJA
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Aqui ┌ feita a regrava┤фo dos itens do SCK E SCL             Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		dbSelectArea("TMP2")
		dbSetOrder(1)
		MsSeek(M->CJ_NUM+TMP1->CK_ITEM,.F.)
		cItem := Soma1(cItem)
		If ( Found() )
			TMP2->CL_ITEMORC := cItem
		EndIf
		SCK->CK_ITEM   := cItem
		lGravou        := .T.
		MaAvalOrc("SCK",1)
		
	Next
	
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Efetua a Gravacao dos Sub-itens TMP2                         Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	cItem := "00"
	dbSelectArea("TMP2")
	dbGotop()
	While ( !Eof() )
		If ( !TMP2->CL_FLAG .And. lGravou )
			dbSelectArea("SCK")
			dbSetOrder(1)
			MsSeek(xFilial()+TMP2->CL_NUM+TMP2->CL_ITEMORC,.F.)
			If ( Found() )
				dbSelectArea("SCL")
				RecLock("SCL",.T.)
				For nX := 1 To FCount()
					nPosField := TMP2->(FieldPos(SCL->(FieldName(nX))))
					If ( nPosField <> 0 )
						FieldPut(nX,TMP2->(FieldGet(nPosField)))
					EndIf
				Next nX
				cItem := Soma1(cItem)
				SCL->CL_FILIAL := xFilial("SCL")
				SCL->CL_NUM    := M->CJ_NUM
				SCL->CL_ITEM   := cItem
				SCL->CL_NUMOP  := ""
				lGravou := .T.
			EndIf
		EndIf
		cItemAnt := TMP2->CL_ITEMORC
		dbSelectArea("TMP2")
		dbSkip()
		If ( cItemAnt <> TMP2->CL_ITEMORC )
			cItem := "00"
		EndIf
	EndDo    
	
	//
	// Template GEM - Gestao de Empreendimentos Imobiliarios
	//
	// Grava a condicao de venda customizada.
	If ExistTemplate("GEMXGRA415",,.T.)
		// atualiza o status do empreendimento
		ExecTemplate("GEMXGRA415",.F.,.F.,{ nOpcx ,SCJ->CJ_NUM ,SCJ->CJ_CONDPAG })
	EndIf
	
EndIf

If ( nOpcx == 2 ) // Alteracao
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Efetua a Gravacao do Itens                                   Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	dbSelectArea("TMP1")
	dbGotop()
	While ( !Eof() )
		dbSelectArea("SCK")
		dbSetOrder(1)
		MsSeek(xFilial("SCK")+M->CJ_NUM+TMP1->CK_ITEM,.F.)
		If (  Found() )
			RecLock("SCK",.F.)
			If ( TMP1->CK_FLAG )
				MaAvalOrc("SCK",2)
				dbDelete()
			EndIf
		Else
			If ( !TMP1->CK_FLAG .And. !Empty(TMP1->CK_PRODUTO) )//.Or.!Empty(TMP1->CK_CLIPROD)) )
				RecLock("SCK",.T.)
			EndIf
		EndIf
		If ( !TMP1->CK_FLAG .And. !Empty(TMP1->CK_PRODUTO) )//.Or.!Empty(TMP1->CK_CLIPROD)) )
			For nX := 1 To FCount()
				nPosField := TMP1->(FieldPos(SCK->(FieldName(nX))))
				If ( nPosField <> 0 )
					FieldPut(nX,TMP1->(FieldGet(nPosField)))
				EndIf
			Next nX
			SCK->CK_FILIAL := xFilial("SCK")
			SCK->CK_NUM    := M->CJ_NUM
			SCK->CK_CLIENTE:= M->CJ_CLIENTE
			SCK->CK_LOJA   := M->CJ_LOJA
			lGravou := .T.
			MaAvalOrc("SCK",1)
		EndIf
		dbSelectArea("TMP1")
		dbSkip()
	EndDo
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Efetua a Gravacao dos Sub-itens TMP2                         Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	cItem := "00"
	dbSelectArea("TMP2")
	dbGotop()
	While ( !Eof() )
		dbSelectArea("SCK")
		dbSetOrder(1)
		MsSeek(xFilial()+TMP2->CL_NUM+TMP2->CL_ITEMORC,.F.)
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё VerIfico se o SCK nao foi deletado                           Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If ( Found() )
			dbSelectArea("SCL")
			dbSetOrder(1)
			MsSeek(xFilial("SCL")+TMP2->CL_NUM+TMP2->CL_ITEMORC+TMP2->CL_ITEM,.F.)
			If ( Found() )
				RecLock("SCL",.F.)
				If ( TMP2->CL_FLAG )
					dbDelete()
				EndIf
			Else
				If ( !TMP2->CL_FLAG )
					RecLock("SCL",.T.)
				EndIf
			EndIf
			If ( !TMP2->CL_FLAG )
				For nX := 1 To FCount()
					nPosField := TMP2->(FieldPos(SCL->(FieldName(nX))))
					If ( nPosField <> 0 )
						FieldPut(nX,TMP2->(FieldGet(nPosField)))
					EndIf
				Next nX
				cItem := Soma1(cItem)
				SCL->CL_FILIAL := xFilial("SCL")
				SCL->CL_NUM    := M->CJ_NUM
				SCL->CL_ITEM   := cItem  // Refaz itens do SCL
				lGravou := .T.
			EndIf
		Else
			dbSelectArea("SCL")
			dbSetOrder(1)
			MsSeek(xFilial("SCL")+TMP2->CL_NUM+TMP2->CL_ITEMORC,.F.)

			While ( !Eof() .And. SCL->CL_FILIAL  == xFilial("SCL") .And.;
					SCL->CL_NUM     == TMP2->CL_NUM   .And.;
					SCL->CL_ITEMORC == TMP2->CL_ITEMORC )
				RecLock("SCL",.F.)
				dbDelete()
				dbSelectArea("SCL")
				dbSkip()
			EndDo
		EndIf
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Aqui e' controlada a quebra de itens pelo SCK                Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		cItemAnt := TMP2->CL_ITEMORC
		dbSelectArea("TMP2")
		dbSkip()
		If ( cItemAnt <> TMP2->CL_ITEMORC )
			cItem := "00"
		EndIf
	EndDo
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Efetua a Gravacao do Cabecalho                               Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If ( lGravou )
		dbSelectArea("SCJ")
		RecLock("SCJ",.F.)
		For nX := 1 To FCount()
			If ("FILIAL" $ FieldName(nX) )
				FieldPut(nX,xFilial())
			ElseIf (("TABELA" $ FieldName(nX)) .And. (M->&(FieldName(nX)) == PadR("1",Len(DA0->DA0_CODTAB))))
				FieldPut(nX,"")
			Else
				FieldPut(nX,M->&(FieldName(nX)))
			EndIf
		Next nX

		If lBloqOrc 
			MaAvalOrc("SCJ",11,"F")
		Else                      
			MaAvalOrc("SCJ",11)
		EndIf

		//
		// Template GEM - Gestao de Empreendimentos Imobiliarios
		//
		// Grava a condicao de venda customizada.
		If ExistTemplate("GEMXGRA415",,.T.)
			// atualiza o status do empreendimento
			ExecTemplate("GEMXGRA415",.F.,.F.,{ nOpcx ,SCJ->CJ_NUM ,SCJ->CJ_CONDPAG })
		EndIf
	Else
		dbSelectArea("SCJ")
		RecLock("SCJ",.F.)
		MaAvalOrc("SCJ",12)
		dbDelete()
	EndIf
EndIf
If ( nOpcx == 3 ) // Exclui
	//
	// Template GEM - Gestao de Empreendimentos Imobiliarios
	//
	// Grava a condicao de venda customizada.
	If ExistTemplate("GEMXGRA415",,.T.)
		// atualiza o status do empreendimento
		ExecTemplate("GEMXGRA415",.F.,.F.,{ nOpcx ,SCJ->CJ_NUM ,SCJ->CJ_CONDPAG })
	EndIf
	
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Deleta os Sub-Itens existentes.                              Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	dbSelectArea("SCL")
	dbSetOrder(1)
	MsSeek(xFilial("SCL")+M->CJ_NUM,.T.)
	While ( !Eof() .And. xFilial("SCL") == SCL->CL_FILIAL .And.;
			SCL->CL_NUM == M->CJ_NUM )
		RecLock("SCL")
		dbDelete()
		dbSelectArea("SCL")
		dbSkip()
	EndDo
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Deleta os Itens existentes.                                  Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	dbSelectArea("SCK")
	dbSetOrder(1)
	MsSeek(xFilial("SCK")+M->CJ_NUM,.T.)
	While ( !Eof() .And. xFilial("SCK") == SCK->CK_FILIAL .And.;
			SCK->CK_NUM == M->CJ_NUM )
		RecLock("SCK")
		MaAvalOrc("SCK",2)
		dbDelete()
		dbSelectArea("SCK")
		dbSkip()
	EndDo
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Deleta o Cabecario                                           Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	dbSelectArea("SCJ")
	RecLock("SCJ")
	dbDelete()
	MaAvalOrc("SCJ",12)
EndIf

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Ponto de Entrada Generico (1-Inc, 2-Alt, 3-Deleta            Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If l415Grava
	Execblock("M415GRV",.F.,.F.,{ nOpcx } )
Endif
Return(lGravou)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁA415LinOk   Ё Autor ЁEduardo Riera        Ё Data Ё 08.12.97 Ё╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Validacao da Linha na GetDadosDB                           Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё Logical A415LinOk(Void)                                    Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё MATA415                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function A415LinOk(oGetDb)

Local aArea        := GetArea()
Local aAreaTMP1    := {}
Local aContrato    := {}
Local lRetorno     := .T.
Local nValor       := 0
Local nX           := 0
Local nPOpcional   := aScan(aHeader,{|x| AllTrim(x[2]) == "CK_OPC"})

l415Auto := If(Type("l415Auto")<>"U",l415Auto,.F.)
l416Auto := If(Type("l416Auto")<>"U",l416Auto,.F.)
//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё VerIfica se a linha foi deletada                     Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
If  !TMP1->CK_FLAG
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё VerIfica se os campos obrigatorios foram preenchidos Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If ( lRetorno .And.  Empty(TMP1->CK_PRODUTO)  .Or.;
			Empty(TMP1->CK_TES   )  .Or.;
			Empty(TMP1->CK_LOCAL) )

		HELP(" ",1,"OBRIGAT")
		lRetorno := .F.
	EndIf
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё VerIfica se o Valor Total da Linha ┌ valido                  Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If ( lRetorno )
		nValor := (TMP1->CK_QTDVEN * TMP1->CK_PRCVEN)
		nValor := A410Arred(nValor,"CK_VALOR")
		If ( nValor != TMP1->CK_VALOR )
			Help(" ",1,"A415TOTAL")
			lRetorno := .F.
		EndIf
	EndIf
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Verifica se o Valor do desconto ┌ Valido.                    Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If (TMP1->(FieldPos("CK_VALDESC")) > 0 )
		If ( lRetorno .And. IIf(TMP1->CK_VALOR>0,TMP1->CK_VALOR <= 0,.F.) )
			Help(" ",1,"A415DESCON")
			lRetorno := .F.
		EndIf
	EndIf
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Verifica a validade do contrato de parceria                  Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If TMP1->(FieldPos("CK_CONTRAT"))<>0 .And. TMP1->(FieldPos("CK_ITEMCON"))<>0
		dbSelectArea("TMP1")
		aAreaTMP1 := GetArea()
		dbGotop()
		While ( !Eof() )
			If !TMP1->CK_FLAG .And. TMP1->(RecNo()) <> aAreaTMP1[3] .And. !Empty(TMP1->CK_CONTRAT)
				nX := aScan(aContrato,{|x| x[1] == TMP1->CK_CONTRAT .And. x[2] == TMP1->CK_ITEMCON})
				If nX == 0
					aadd(aContrato,{TMP1->CK_CONTRAT,TMP1->CK_ITEMCON,TMP1->CK_QTDVEN})
					nX := Len(aContrato)
				Else
					aContrato[nX][3] += TMP1->CK_QTDVEN
				EndIf
			EndIf
			dbSelectArea("SCK")
			dbSetOrder(1)
			If MsSeek(xFilial("SCK")+M->CJ_NUM+TMP1->CK_ITEM) .And. !Empty(SCK->CK_CONTRAT)
				nX := aScan(aContrato,{|x| x[1] == SCK->CK_CONTRAT .And. x[2] == SCK->CK_ITEMCON})
				If nX == 0
					aadd(aContrato,{SCK->CK_CONTRAT,SCK->CK_ITEMCON,0})
					nX := Len(aContrato)
				EndIf
				aContrato[nX][3] -= SCK->CK_QTDVEN
			EndIf
			dbSelectArea("TMP1")
			dbSkip()
		EndDo
		RestArea(aAreaTMP1)
		dbSelectArea("ADB")
		dbSetOrder(1)
		If !Empty(TMP1->CK_CONTRAT) .And. MsSeek(xFilial("ADB")+TMP1->CK_CONTRAT+TMP1->CK_ITEMCON)
			nX := aScan(aContrato,{|x| x[1] == ADB->ADB_NUMCTR .And. x[2] == ADB->ADB_ITEM})
			If TMP1->CK_QTDVEN > ADB->ADB_QUANT-ADB->ADB_QTDEMP-If(nX>0,aContrato[nX][3],0)
				Help(" ",1,"A415QTDCTR")
				lRetorno := .F.
			EndIf
		EndIf
	EndIf
	
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Valida as colunas do browse referente ao SIGAPMS                       Ё
	//Ё Colunas: C6_PROJPMS, C6_EDTPMS, C6_TASKPMS                             Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If lRetorno .AND. ( TMP1->(FieldPos("CK_PROJPMS"))  > 0 .AND. TMP1->(FieldPos("CK_EDTPMS")) > 0 .AND. TMP1->(FieldPos("CK_TASKPMS")) > 0 )
		lRetorno := a415VldPMS(TMP1->CK_PROJPMS,TMP1->CK_EDTPMS,TMP1->CK_TASKPMS )
	EndIf
	
EndIf

If l415Auto .or. l416Auto
	A415Total()
Else
	A415Total(oGetDb:oWnd)
EndIf

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁAtualiza os Opcionais                                                   Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If lRetorno .And. lShowOpc .And. !(l415Auto .Or. l416Auto) .And. nPOpcional > 0
	lRetorno := SeleOpc(3,"MATA415",TMP1->CK_PRODUTO,,,TMP1->CK_OPC,"M->CK_PRODUTO",,TMP1->CK_QTDVEN,TMP1->CK_ENTREG)
	If lRetorno
		lShowOpc := .F.
	EndIf
EndIf

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Validacao do ponto de entrada                                Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If lRetorno .And. ExistBlock("A415LIOK")
	lRetorno := ExecBlock("A415LIOK",.F.,.F.)
EndIf

RestArea(aArea)
Return(lRetorno)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁA415TudOk   Ё Autor ЁEduardo Riera        Ё Data Ё 08.12.97 Ё╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Validacao da GetDadosDB                                    Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё Logical A415TudOk(Void)                                    Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё MATA415                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function A415TudOk(oGetd)

Local aArea    := GetArea()
Local aContrato:= {}
Local lRetorno := .T.
Local nValor   := 0
Local nTotOrc  := 0
Local nX       := 0
Local oDlg

l415Auto := If(Type("l415Auto")<>"U",l415Auto,.F.)
l416Auto := If(Type("l416Auto")<>"U",l416Auto,.F.)

If !(l415Auto .or. l416Auto)
	oDlg := oGetd:oWnd
EndIf
dbSelectArea("TMP1")
dbGotop()
While ( !Eof() .And. lRetorno )
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Verifica se a linha foi deletada                     Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If ( !TMP1->CK_FLAG .And. !Empty(TMP1->CK_PRODUTO) )
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Verifica se os camos obrigatorios foram preenchidos  Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If ( lRetorno .And. Empty(TMP1->CK_TES   ) .Or.;
				Empty(TMP1->CK_LOCAL ) )
			HELP(" ",1,"OBRIGAT")
			lRetorno := .F.
		EndIf
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Verifica se o Valor Total da Linha ┌ valido                  Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If ( lRetorno )
			nValor := A410Arred((TMP1->CK_QTDVEN * TMP1->CK_PRCVEN),"CK_VALOR")
			If ( nValor <> TMP1->CK_VALOR )
				Help(" ",1,"A415TOTAL")
				lRetorno := .F.
			EndIf
		EndIf
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё VerIfica se o Valor do desconto ┌ Valido.                    Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If (TMP1->(FieldPos("CK_VALDESC")) > 0 )
			If ( lRetorno .And. IIf(TMP1->CK_VALOR>0,TMP1->CK_VALOR <= 0,.F.) )
				Help(" ",1,"A415DESCON")
				lRetorno := .F.
			EndIf
		EndIf
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Verifica o contrato de parceria                              Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If TMP1->(FieldPos("CK_CONTRAT"))<>0 .And. TMP1->(FieldPos("CK_ITEMCON"))<>0 .And. !Empty(TMP1->CK_CONTRAT)
			nX := aScan(aContrato,{|x| x[1] == TMP1->CK_CONTRAT .And. x[2] == TMP1->CK_ITEMCON})
			If nX == 0
				aadd(aContrato,{TMP1->CK_CONTRAT,TMP1->CK_ITEMCON,TMP1->CK_QTDVEN})
				nX := Len(aContrato)
			Else
				aContrato[nX][3] += TMP1->CK_QTDVEN
			EndIf
		EndIf
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Verifica o contrato de parceria                              Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If TMP1->(FieldPos("CK_CONTRAT"))<>0 .And. TMP1->(FieldPos("CK_ITEMCON"))<>0 .And. !Empty(TMP1->CK_PRODUTO)
			dbSelectArea("SCK")
			dbSetOrder(1)
			If MsSeek(xFilial("SCK")+M->CJ_NUM+TMP1->CK_ITEM) .And. !Empty(SCK->CK_CONTRAT)
				nX := aScan(aContrato,{|x| x[1] == SCK->CK_CONTRAT .And. x[2] == SCK->CK_ITEMCON})
				If nX == 0
					aadd(aContrato,{SCK->CK_CONTRAT,SCK->CK_ITEMCON,0})
					nX := Len(aContrato)
				EndIf
				aContrato[nX][3] -= SCK->CK_QTDVEN
			EndIf
		EndIf
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Verifica as faixas da condicao de pagamento                  Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		nTotOrc += TMP1->CK_VALOR
		
	EndIf
	dbSelectArea("TMP1")
	dbSkip()
EndDo
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Verifica o contrato de parceria                              Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If lRetorno
	For nX := 1 To Len(aContrato)
		dbSelectArea("ADB")
		dbSetOrder(1)
		MsSeek(xFilial("ADB")+aContrato[nX][1]+aContrato[nX][2])
		If aContrato[nX][3] > ADB->ADB_QUANT-ADB->ADB_QTDEMP
			Help(" ",1,"A410CTRQT2")
			lRetorno := .F.
		EndIf
	Next nX
EndIf

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁVerifica a Condicao de Pagamento Tipo 9                                 Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If !A415Tipo9()
	lRetorno  := .F.
EndIf

//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁVerifica se a tabela de precos eh valida                 Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If lRetorno
	lRetorno := MaVldTabPrc(M->CJ_TABELA,M->CJ_CONDPAG,Nil,M->CJ_EMISSAO)
EndIf
If lRetorno
	dbSelectArea("SA1")
	dbSetOrder(1)
	If MsSeek(xFilial("SA1")+M->CJ_CLIENTE+M->CJ_LOJA)
		If !RegistroOk("SA1")
			lRetorno	 := .F.
		Endif	
	Endif
Endif	
If lRetorno
	//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁCalcula apenas se houver um valor de desconto preenchido Ё
	//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If !( Empty(M->CJ_DESC1) .And. Empty(M->CJ_DESC2) .And. ;
			Empty(M->CJ_DESC3) .And. Empty(M->CJ_DESC4) )
		a415DesCab()
	EndIf
	a415Total(oDlg)
EndIf
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Verifica as faixas da condicao de pagamento                  Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If lRetorno
	dbSelectArea("SE4")
	dbSetOrder(1)
	MsSeek(xFilial("SE4")+M->CJ_CONDPAG)
	If nTotOrc > SE4->E4_SUPER .AND. SE4->E4_SUPER <> 0
		Help(" ","1","LJLIMSUPER")
		lRetorno := .F.
	ElseIf nTotOrc < SE4->E4_INFER .AND. SE4->E4_INFER <> 0
		Help(" ","1","LJLIMINFER")
		lRetorno := .F.
	Endif
EndIf


If lRetorno .And. ExistBlock("A415TDOK")
	lRetorno := ExecBlock("A415TDOK",.F.,.F.)
EndIf
RestArea(aArea)
Return(lRetorno)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁA415F4      Ё Autor ЁEduardo Riera        Ё Data Ё 08.12.97 Ё╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Tratamento da tecla F4.                                    Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё Void A415F4(Void)                                          Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ a,b,c                                                      Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё MATA415                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function A415F4(a,b,c)

Local lRetorno	:= .T.
Local oDlg
Local nOpcA		:= 0
Local nOpcx		:= 0
Local aArea		:= GetArea()
Local cArqMem  := CriaTrab(,.F.)
Local bSetKey	:= SetKey(VK_F4,Nil)
Local cCadastro:= OemToAnsi(STR0020) //"Sugest└o de Or┤amento"
__MSave( cArqMem, "*", .T. )

If ! Empty(TMP1->CK_PRODUTO) 
	If !Empty(TMP1->CK_QTDVEN) .And. "CK_QTDVEN" $ Trim(ReadVar()) 
		Private oGet
		Private aCols  := {}
		Private aHeader := {}	
		SetKey(VK_F4,{|a,b,c| a415F4Stoq(a,b,c)})	
		If ( INCLUI .Or. ALTERA )
			// array aRotina redimensionado para 3 posicoes na funcao a415PCpy. 
			// Por este motivo nao podemos atribuir 4 para nOpcx e sim 3. Se atribuirmos 4 ocorre erro na MsGetDados.
			If len(aRotina) > 3
				nOpcx := 4
			Else 				
				nOpcx := 3
			EndIf	
		Else
			nOpcx := 2
		EndIf
		aHeader := aHeaderSCL
		aCols   := a415GeraCL()
		DEFINE MSDIALOG oDlg TITLE cCadastro From 15,0 to 28,80
		@ 01,01 SAY STR0021+TMP1->CK_PRODUTO+" "+SubStr(TMP1->CK_DESCRI,1,30)   //"Produto : "
		@ 1.5,01 SAY STR0022+TransForm(TMP1->CK_QTDVEN,PesqPict("SCK","CK_QTDVEN")) //"Qtd.Base: "
		oGet := MsGetDados():New(32,1,094,316,nOpcx,"AllwaysTrue","AllwaysTrue","+CL_ITEM",.T.)
		ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{||nOpcA:=1,IIf(oGet:TudoOk(),oDlg:End(),nOpcA:=0)},{||oDlg:End()})
		If ( nOpcA == 1  .And. (INCLUI .Or. ALTERA) .And.;
		!Empty(aCols) )
			a415GrvSCL(aHeader,aCols)
		EndIf
	Else
		a415F4Stoq(a,b,c)
	EndIf
EndIf
__MRestore( cArqMem, .T. )
Ferase(Trim(cArqMem)+".mem")
aHeader := aHeaderSCK
SetKey(VK_F4,bSetKey)
RestArea(aArea)
Return(lRetorno)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁA415F4STOQ  Ё Autor ЁEduardo Riera        Ё Data Ё 13.01.98 Ё╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Tratamento da tecla F4.                                    Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё Void A415F4STOQ(Void)                                      Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ a,b,c                                                      Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё MATA415                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function A415F4Stoq(a,b,c)

Local aArea	   := GetArea()
Local cProduto := ""
Local cFilEnt  := Nil
Local cCampo   := AllTrim(StrTran(ReadVar(),"M->",""))
Local nPosPrd  := 0

If ( SubStr(cCampo,1,2) $ "CL" .And. cCampo <> "CL_PRODUTO" )
	nPosPrd  := aScan(aHeader,{|x| AllTrim(x[2]) == "CL_PRODUTO" })
	If ( nPosPrd > 0 )
		cProduto := aCols[n,nPosPrd]
	EndIf
ElseIf ( SubStr(cCampo,1,2) $ "CK" .And. cCampo <> "CK_PRODUTO")
	nPosPrd  := aScan(aHeader,{|x| AllTrim(x[2]) == "CK_PRODUTO" })
	If ( nPosPrd > 0 )
		cProduto := TMP1->CK_PRODUTO
	EndIf
EndIf
If SCK->(FieldPos("CK_FILENT"))>0
	cFilEnt := TMP1->CK_FILENT
EndIf
If !Empty(cProduto)
	MaViewSB2(cProduto,cFilEnt)
EndIf

RestArea(aArea)
Return(Nil)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁA415Prod    Ё Autor ЁEduardo Riera        Ё Data Ё 08.12.97 Ё╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Validacao da Digitacao do Produto                          Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё Logical A415Prod(Void)                                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё MATA415                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function A415Prod(cProduto)

Local lRetorno := .T.
Local aArea    := GetArea()
Local cDescri  := ""
Local cAnterior:= ""

//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Funcao utilizada para verificar a ultima versao dos fontes      Ё
//Ё SIGACUS.PRW, SIGACUSA.PRX e SIGACUSB.PRX, aplicados no rpo do   |
//| cliente, assim verificando a necessidade de uma atualizacao     |
//| nestes fontes. NAO REMOVER !!!							        Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
IF !(FindFunction("SIGACUS_V") .and. SIGACUS_V() >= 20050512)
    Final("Atualizar SIGACUS.PRW !!!")
Endif
IF !(FindFunction("SIGACUSA_V") .and. SIGACUSA_V() >= 20050512)
    Final("Atualizar SIGACUSA.PRX !!!")
Endif
IF !(FindFunction("SIGACUSB_V") .and. SIGACUSB_V() >= 20090204)
    Final(STR0079)
Endif

cProduto := IIf(cProduto==Nil,M->CK_PRODUTO,cProduto)

//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Posiciona Registros                                  Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
dbSelectArea("SA7")
dbSetOrder(1)
If ( MsSeek(xFilial()+M->CJ_CLIENTE+M->CJ_LOJA+cProduto,.T.) )
	cDescri := SA7->A7_DESCCLI
EndIf

dbSelectArea("SB1")
dbSetOrder(1)
MsSeek(xFilial()+cProduto,.T.)

If ( Found() )
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Preenche os dados vidos do Produto                   Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
	dbSelectArea("TMP1")
	If (FieldPos("CK_UM") > 0 )
		TMP1->CK_UM       := SB1->B1_UM
	EndIf
	If (FieldPos("CK_TES") > 0 )
		If (!Empty(RetFldProd(SB1->B1_COD,"B1_TS")))
			TMP1->CK_TES      := RetFldProd(SB1->B1_COD,"B1_TS")
		Else
			TMP1->CK_TES      := A415TesPad()
		EndIf
	EndIf
	If (FieldPos("CK_LOCAL") > 0 )
		TMP1->CK_LOCAL    := RetFldProd(SB1->B1_COD,"B1_LOCPAD")
	EndIf
	If (FieldPos("CK_COMIS1") > 0 )
		TMP1->CK_COMIS1   := SB1->B1_COMIS
	EndIf
	If (FieldPos("CK_DESCRI") > 0 )
		TMP1->CK_DESCRI   := PadR(IIf(Empty(cDescri),SB1->B1_DESC,cDescri),Len(TMP1->CK_DESCRI))
	EndIf
	If (FieldPos("CK_CONTRAT") > 0 )
		TMP1->CK_CONTRAT := ""
	EndIf
	If (FieldPos("CK_ITEMCON") > 0 )
		TMP1->CK_ITEMCON := ""
	EndIf
Else
	lRetorno := .F.
EndIf

//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Qdo e'dIferente apaga os sub-itens.                  Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
If ( lRetorno .And. TMP1->CK_PRODUTO <> cProduto )
	cAnterior := TMP1->CK_PRODUTO
	TMP1->CK_PRODUTO := cProduto
	a415GrvSCL(aHeaderSCL,{})
	TMP1->CK_PRODUTO := cAnterior
EndIf

RestArea(aArea)
Return(lRetorno)


/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁA415SCLPrd  Ё Autor ЁEduardo Riera        Ё Data Ё 08.12.97 Ё╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Validacao da Digitacao do Produto                          Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё Void A415SCLPrd(Void)                                      Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё MATA415                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function A415SCLPrd()

Local lRetorno := .T.
Local nPosDesc := 0
Local aArea    := GetArea()

dbSelectArea("SBH")
dbSetOrder(1)
MsSeek(xFilial()+M->CL_PRODUTO,.T.)

If ( !Eof() .And. xFilial()==SBH->BH_FILIAL .And.;
		M->CL_PRODUTO == SBH->BH_PRODUTO )
	Help(" ",1,"A415SCLPRD")
	lRetorno := .f.
EndIf

If ( M->CL_PRODUTO == TMP1->CK_PRODUTO )
	lRetorno := .F.
	Help(" ",1,"A415SCLPRD")
Else
	nPosDesc := aScan(aHeader,{|x| Trim(x[2])=="CL_DESCRI" })
	If ( nPosDesc > 0 )
		dbSelectArea("SB1")
		dbSetOrder(1)
		MsSeek(xFilial()+M->CL_PRODUTO,.T.)
		aCols[n,nPosDesc] := SB1->B1_DESC
	EndIf
EndIf
RestArea(aArea)
Return(lRetorno)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁA415SCLQtd  Ё Autor ЁEduardo Riera        Ё Data Ё 08.12.97 Ё╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Validacao da Digitacao da Quantidade                       Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё Void A415SCLQtd(Void)                                      Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё MATA415                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function A415SCLQtd()

Local nPos := 0
nPos := aScan(aHeader,{|x| Trim(x[2]) == "CL_QTDVEN" })
If ( nPos > 0 )
	aCols[n,nPos]  := M->CL_QUANT * TMP1->CK_QTDVEN
EndIf
Return(.T.)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁA415Descon  Ё Autor ЁEduardo Riera        Ё Data Ё 09.12.97 Ё╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Validacao da Digitacao do Percentual de Desconto           Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё ExpL1 := A415Descon( [ ExpL2 ] )                           Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё MATA415                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function A415Descon()

Local aArea		:= GetArea()
Local lRetorno	:= .T.
Local nValor      := TMP1->CK_VALOR
Local nPDesc      := TMP1->CK_DESCONT
Local nVlDesc     := TMP1->CK_VALDESC
Local nPrcVen	:= 0

If "CK_DESCONT"$ReadVar()
	nPDesc := M->CK_DESCONT
EndIf

If ( TMP1->(FieldPos("CK_VALDESC")) > 0 .And.;
		TMP1->(FieldPos("CK_DESCONT"))>0  .And.;
		TMP1->CK_QTDVEN>0 .And.;
		TMP1->CK_PRCVEN>0 )
		
	nPrcVen  := FtDescItem(FtDescCab(TMP1->CK_PRUNIT,{M->CJ_DESC1,M->CJ_DESC2,M->CJ_DESC3,M->CJ_DESC4}),TMP1->CK_PRCVEN,TMP1->CK_QTDVEN,@nValor,@M->CK_DESCONT,@nVlDesc,nVlDesc,1,,If(cPaisLoc=="CHI",M->CJ_MOEDA,NIL))
	
	If ( nPrcVen <= 0 )
		TMP1->CK_DESCONT  := 0
		TMP1->CK_VALDESC  := 0
		Help(" ",1,"A415DESCON")
		lRetorno := .F.
	Else
		TMP1->CK_PRCVEN  := nPrcVen
		TMP1->CK_VALOR   := nValor
		TMP1->CK_DESCONT := nPDesc
		TMP1->CK_VALDESC := nVlDesc	
	EndIf
EndIf
RestArea( aArea )
Return(lRetorno)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁA415VlDesc  Ё Autor ЁEduardo Riera        Ё Data Ё 09.12.97 Ё╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Validacao da Digitacao do Valor de Desconto                Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё Logical A415VlDesc(Void)                                   Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё MATA415                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function A415VlDesc()

Local aArea       := GetArea()
Local lRetorno    := .T.
Local nValor      := TMP1->CK_VALOR
Local nPDesc      := TMP1->CK_DESCONT
Local nVlDesc     := TMP1->CK_VALDESC

If ( TMP1->(FieldPos("CK_VALDESC")) > 0 .And. TMP1->(FieldPos("CK_DESCONT")) > 0 )	
	If ((nVlDesc == 0) .AND. ( M->CK_VALDESC >= nValor )) .OR.;
		(nVlDesc > 0) .AND. ( M->CK_VALDESC >= (nValor + nVlDesc))
		Help(" ",1,"A415DESCON")
		lRetorno := .F.
	Else	
		TMP1->CK_PRCVEN  := FtDescItem(FtDescCab(TMP1->CK_PRUNIT,{M->CJ_DESC1,M->CJ_DESC2,M->CJ_DESC3,M->CJ_DESC4}),TMP1->CK_PRCVEN,TMP1->CK_QTDVEN,@nValor,@nPDesc,@M->CK_VALDESC,@nVlDesc,2,,If(cPaisLoc=="CHI",M->CJ_MOEDA,NIL))
		TMP1->CK_VALOR   := nValor
		TMP1->CK_DESCONT := nPDesc
		TMP1->CK_VALDESC := nVlDesc
	EndIf	
EndIf
RestArea( aArea )
Return(lRetorno)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁA415QtdVen  Ё Autor ЁEduardo Riera        Ё Data Ё 09.12.97 Ё╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Validacao da Digitacao da Quantidade.                      Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё Logical A415QtdVen(Void)                                   Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё MATA415                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function A415QtdVen()

Local aArea    := GetArea()
Local aAreaTMP1:= {}
Local aContrato:= {}
Local lRetorno := .T.
Local nQtdVen  := M->CK_QTDVEN
Local nValor   := TMP1->CK_VALOR
Local nX       := 0
Private CK_DESCONT := 0
l415Auto := If(Type("l415Auto")<>"U",l415Auto,.F.)
l416Auto := If(Type("l416Auto")<>"U",l416Auto,.F.)
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Verifica a validade do contrato de parceria                  Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If TMP1->(FieldPos("CK_CONTRAT"))<>0 .And. TMP1->(FieldPos("CK_ITEMCON"))<>0
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Verifica a validade dos contratos de parceria        Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
	dbSelectArea("TMP1")
	aAreaTMP1 := GetArea()
	dbGotop()
	While ( !Eof() )
		If !TMP1->CK_FLAG .And. TMP1->(RecNo()) <> aAreaTMP1[3] .And. !Empty(TMP1->CK_CONTRAT)
			nX := aScan(aContrato,{|x| x[1] == TMP1->CK_CONTRAT .And. x[2] == TMP1->CK_ITEMCON})
			If nX == 0
				aadd(aContrato,{TMP1->CK_CONTRAT,TMP1->CK_ITEMCON,TMP1->CK_QTDVEN})
				nX := Len(aContrato)
			Else
				aContrato[nX][3] += TMP1->CK_QTDVEN
			EndIf			
		EndIf
		dbSelectArea("SCK")
		dbSetOrder(1)
		If MsSeek(xFilial("SCK")+M->CJ_NUM+TMP1->CK_ITEM) .And. !Empty(SCK->CK_CONTRAT)
			nX := aScan(aContrato,{|x| x[1] == SCK->CK_CONTRAT .And. x[2] == SCK->CK_ITEMCON})
			If nX == 0
				aadd(aContrato,{SCK->CK_CONTRAT,SCK->CK_ITEMCON,0})
				nX := Len(aContrato)
			EndIf
			aContrato[nX][3] -= SCK->CK_QTDVEN
		EndIf		
		dbSelectArea("TMP1")
		dbSkip()
	EndDo
	RestArea(aAreaTMP1)
	dbSelectArea("ADB")
	dbSetOrder(1)
	If !Empty(TMP1->CK_CONTRAT) .And. MsSeek(xFilial("ADB")+TMP1->CK_CONTRAT+TMP1->CK_ITEMCON)
		nX := aScan(aContrato,{|x| x[1] == ADB->ADB_NUMCTR .And. x[2] == ADB->ADB_ITEM})
		If nQtdVen > ADB->ADB_QUANT-ADB->ADB_QTDEMP-If(nX>0,aContrato[nX][3],0)
			Help(" ",1,"A415QTDCTR")
			lRetorno := .F.
		EndIf
	EndIf
EndIf
If lRetorno
	//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Qdo for rotina automatica (sigaauto), fara as operacoes Ё
	//Ё necessarias e retornara com o acols de origem           Ё
	//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	PRIVATE aCols
	aCols := a415GeraCL()
	a415GrvSCL(aHeaderSCL,aCols)
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Atualiza Valor Total                                 Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
	TMP1->CK_QTDVEN   := M->CK_QTDVEN // Forca a gravacao para usar as funcoes internas
	TMP1->CK_VALOR    := A410Arred(TMP1->CK_QTDVEN * TMP1->CK_PRCVEN,"CK_VALOR")
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Atualiza Desconto pelo percentual                    Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If (  TMP1->(FieldPos("CK_PRUNIT")) > 0 .And.;
			TMP1->(FieldPos("CK_DESCONT")) > 0 .And.;
			TMP1->(FieldPos("CK_VALDESC")) > 0)
		M->CK_DESCONT := 0 // Para compatibilizar com A415Descon
		lRetorno := a415Descon()
	EndIf
	If ( !lRetorno )
		TMP1->CK_VALOR    := nValor
		TMP1->CK_QTDVEN   := nQtdVen
	EndIf
	If ( lRetorno )
		TMP1->CK_QTDVEN := M->CK_QTDVEN
	EndIf
EndIf
RestArea(aArea)
Return(lRetorno)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁA415PrcVen  Ё Autor ЁEduardo Riera        Ё Data Ё 09.12.97 Ё╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Validacao da Digitacao do Preco de Venda.                  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё Void A415PrcVen(Void)                                      Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё MATA415                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function A415PrcVen()

Local lRetorno  := .T.
Local nAnterior:= 0
Private CK_DESCONT := 0
//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Atualiza Valor Total                                 Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
TMP1->CK_VALOR := a410Arred(TMP1->CK_QTDVEN * M->CK_PRCVEN,"CK_VALOR")
//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Atualiza Desconto pelo percentual                    Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
If (  TMP1->(FieldPos("CK_PRUNIT")) > 0 .And.;
		TMP1->(FieldPos("CK_DESCONT")) > 0 .And.;
		TMP1->(FieldPos("CK_VALDESC")) > 0)
	nAnterior       := M->CK_PRCVEN // Para compatibilizar com A415Descon
	TMP1->CK_PRCVEN := M->CK_PRCVEN // Para compatibilizar com A415Descon
	If TMP1->CK_DESCONT > 0
		TMP1->CK_DESCONT := 0  // Para compatibilizar com A415Descon
		TMP1->CK_VALDESC := 0
	EndIf
EndIf
Return(lRetorno)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤фo    ЁA415CliPad  Ё Autor ЁEduardo Riera        Ё Data Ё 09.12.97 Ё╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Inicializacao do Cliente Padrao                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё Void A415CliPad(Void)                                      Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё MATA415                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function A415CliPad()

Local aArea        := GetArea()
Local cCliente     := GetMv("MV_ORCLIPD")
Local aAreaSX3     := {}   // Armazena a Area do SX3
Local cA1Tipo      := "R"  // Valor Padrao do Campo A1_TIPO
Local cCodCli      := ""
Local cLojCli      := ""

cCodCli := Left( cCliente, Len( SA1->A1_COD ) ) 
cLojCli := Right( cCliente, Len( SA1->A1_LOJA ) )      

If !Empty( cCodCli ) .And. !Empty( cLojCli )

	dbSelectArea("SA1")
	dbSetOrder(1)
	MsSeek(xFilial()+cCliente)
	
	If ( !Found() )
	
		If cPaisLoc <> "BRA"	
			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Seleciona a Primeira opcao do ComboBox ref. ao Campo A1_TIPO Ё
			//Ё 25/10/2001 - Sergio Camurca                                  Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	
			dbSelectArea("SX3")
			aAreaSX3 :=GetArea()
			dbSetOrder(2)
			If ( MsSeek("A1_TIPO") )
				cA1Tipo := Substr(X3CBOX(),1,1)
			Endif
			RestArea(aAreaSX3)
			dbSelectArea("SA1")
	
		Endif
	
		Reclock("SA1",.T.)
		SA1->A1_FILIAL    := xFilial("SA1")
		SA1->A1_COD       := Substr(cCliente,1,Len(SA1->A1_COD))
		SA1->A1_TIPO      := cA1Tipo
		SA1->A1_LOJA      := Substr(cCliente,1+Len(SA1->A1_COD),Len(SA1->A1_LOJA))
		SA1->A1_NOME      := STR0032 //"CLIENTE PADRAO P/ ORCAMENTO"
		SA1->A1_NREDUZ    := STR0033 //"ORCAMENTO"
		SA1->A1_MUN       := "."
		SA1->A1_EST       := GetMv("MV_ESTADO")
		SA1->A1_BAIRRO    := "."
		SA1->A1_END       := "."
	
	EndIf
	RestArea(aArea)
EndIf	
	
Return(cCliente)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤фo    ЁA415CondPad Ё Autor ЁEduardo Riera        Ё Data Ё 09.12.97 Ё╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Inicializacao do Cliente Padrao                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё Void A415CondPad(Void)                                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё MATA415                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function A415CondPad()

Local aArea    := GetArea()
Local cCondPag := GetMv("MV_ORCPGPD")

dbSelectArea("SE4")
dbSetOrder(1)
MsSeek(xFilial()+cCondPag)
If ( !Found() .And. !Empty(cCondPag))
	Reclock("SE4",.T.)
	SE4->E4_FILIAL   := xFilial()
	SE4->E4_CODIGO   := cCondPag
	SE4->E4_TIPO     := "1"
	SE4->E4_COND     := "0"
	SE4->E4_DESCRI   := STR0033 //"ORCAMENTO"
	MsUnlock()
EndIf
RestArea(aArea)
Return(cCondPag)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤фo    ЁA415TesPad  Ё Autor ЁEduardo Riera        Ё Data Ё 09.12.97 Ё╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Inicializacao do Tes Padrao                                Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё Void A415TesPad(Void)                                      Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё MATA415                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function A415TesPad()

Local aArea    := GetArea()
Local cTes     := GetMv("MV_TESSAI")

dbSelectArea("SF4")
dbSetOrder(1)
MsSeek(xFilial()+cTes)
If ( !Found() )
	cTes := Space(3)
EndIf
RestArea(aArea)
Return(cTes)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбдддддддбдддддддддддддддддддддддбддддддбддддддддд©╠╠
╠╠ЁFuncao    ЁA415Tabela Ё Autor ЁEduardo Riera          Ё Data Ё09.12.97 Ё╠╠
╠╠цддддддддддедддддддддддадддддддадддддддддддддддддддддддаддддддаддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁCalculo da Tabela de Preco                                  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁExpC1: Codigo do Produto                                    Ё╠╠
╠╠Ё          ЁExpC2: Codigo da Tabela                                     Ё╠╠
╠╠Ё          ЁExpN3: Quantidade                                           Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁExpN1: Preco do produto                                     Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁDescri┤└o ЁEsta rotina tem como objetivo retornar a preco de venda     Ё╠╠
╠╠Ё          Ёdo produto                                                  Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁObservacaoЁ  Usado nos campos C6_PRODUTO e C6_QTDVEN                   Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё Materiais                                                  Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function A415Tabela(cProduto,cTabprec,nQtde)

Local aArea     := GetArea()
Local aAreaSB1  := SB1->(GetArea())
Local aAreaADA  := ADA->(GetArea())
Local aAreaADB  := ADB->(GetArea())
Local aAreaTMP1 := {}
Local aContrato := {}
Local nX        := 0
Local nPrcVen   := 0
Local cOpcional := IIf(SCK->(FIELDPOS("CK_OPC"))<>0,TMP1->CK_OPC,"")
Local cOpc      := ""
Local cCliente  := M->CJ_CLIENTE
Local cLoja     := M->CJ_LOJA
Local cAliasADA := "ADA"
Local cAliasADB := "ADB"
Local lQuery    := .F.
Local lValido   := .T.
Local lContrat  := SuperGetMV("MV_PRCCTR")

#IFDEF TOP
	Local aStruADB  := {}
	Local cQuery    := ""
#ENDIF

DEFAULT nQtde   := 0

//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Verifica se ha contrato de parceria                  Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
If lContrat .And. TMP1->(FieldPos("CK_CONTRAT"))<>0 .And. TMP1->(FieldPos("CK_ITEMCON"))<>0
	dbSelectArea("ADB")
	dbSetOrder(2)
	#IFDEF TOP
		aStruADB := ADB->(dbStruct())	
		lQuery := .T.
		cAliasADA := "A415TABELA"
		cAliasADB := "A415TABELA"

		cQuery := "SELECT ADB.*,ADA.ADA_MOEDA "
		cQuery += "FROM "+RetSqlName("ADB")+" ADB, "
		cQuery += RetSqlName("ADA")+" ADA "
		cQuery += "WHERE ADB.ADB_FILIAL='"+xFilial("ADB")+"' AND "
		cQuery += "ADB.ADB_CODCLI='"+cCliente+"' AND "
		cQuery += "ADB.ADB_LOJCLI='"+cLoja+"' AND "
		cQuery += "ADB.ADB_CODPRO='"+cProduto+"' AND "
		cQuery += "ADB.D_E_L_E_T_=' ' AND "
		cQuery += "ADA.ADA_FILIAL='"+xFilial("ADA")+"' AND "
		cQuery += "ADA.ADA_NUMCTR=ADB.ADB_NUMCTR AND "
		cQuery += "ADA.ADA_STATUS IN ('B','C') AND "
		cQuery += "ADA.D_E_L_E_T_=' '  "		

		cQuery := ChangeQuery(cQuery)

		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasADB)

		For nX := 1 To Len(aStruADB)
			If aStruADB[nX][2] <> "C"
				TcSetField(cAliasADB,aStruADB[nX][1],aStruADB[nX][2],aStruADB[nX][3],aStruADB[nX][4])
			EndIf
		Next nX
	#ELSE
		MsSeek(xFilial("ADB")+cCliente+cLoja+cProduto)
	#ENDIF
	While ( !Eof() .And. xFilial("ADB") == (cAliasADB)->ADB_FILIAL .And.;
			(cAliasADB)->ADB_CODCLI == cCliente .And.;
			(cAliasADB)->ADB_LOJCLI == cLoja .And.;
			(cAliasADB)->ADB_CODPRO == cProduto )		
		If !lQuery
			dbSelectArea("ADA")
			dbSetOrder(1)
			MsSeek(xFilial("ADA")+(cAliasADB)->ADB_NUMCTR)
			lValido := ADA->ADA_STATUS<>"A" .And. ADA->ADA_STATUS<>"D"
		Else
			lValido := .T.
		EndIf
		If lValido
			//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Verifica o saldo de contratos deste orcamento        Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды		
			If Empty(aContrato)
				dbSelectArea("TMP1")
				aAreaTMP1 := GetArea()
				dbGotop()
				While ( !Eof() )
					If !TMP1->CK_FLAG .And. TMP1->(RecNo()) <> aAreaTMP1[3] .And. !Empty(TMP1->CK_CONTRAT)
						nX := aScan(aContrato,{|x| x[1] == TMP1->CK_CONTRAT .And. x[2] == TMP1->CK_ITEMCON})
						If nX == 0
							aadd(aContrato,{TMP1->CK_CONTRAT,TMP1->CK_ITEMCON,TMP1->CK_QTDVEN})
							nX := Len(aContrato)
						Else
							aContrato[nX][3] += TMP1->CK_QTDVEN
						EndIf
					EndIf
					dbSelectArea("SCK")
					dbSetOrder(1)
					If MsSeek(xFilial("SCK")+M->CJ_NUM+TMP1->CK_ITEM) .And. !Empty(SCK->CK_CONTRAT)
						nX := aScan(aContrato,{|x| x[1] == SCK->CK_CONTRAT .And. x[2] == SCK->CK_ITEMCON})
						If nX == 0
							aadd(aContrato,{SCK->CK_CONTRAT,SCK->CK_ITEMCON,0})
							nX := Len(aContrato)
						EndIf
						aContrato[nX][3] -= SCK->CK_QTDVEN
					EndIf					
					dbSelectArea("TMP1")
					dbSkip()
				EndDo
				RestArea(aAreaTMP1)
			EndIf
			nX := aScan(aContrato,{|x| x[1] == (cAliasADB)->ADB_NUMCTR .And. x[2] == (cAliasADB)->ADB_ITEM})
			If (cAliasADB)->ADB_QUANT > (cAliasADB)->ADB_QTDEMP+If(nX>0,aContrato[nX][3],0)
				nPrcVen := xMoeda((cAliasADB)->ADB_PRCVEN,(cAliasADA)->ADA_MOEDA,M->CJ_MOEDA,dDataBase,TamSx3("CK_PRCVEN")[2])
				If TMP1->CK_QTDVEN > (cAliasADB)->ADB_QUANT-(cAliasADB)->ADB_QTDEMP-If(nX>0,aContrato[nX][3],0)
					TMP1->CK_QTDVEN := (cAliasADB)->ADB_QUANT-(cAliasADB)->ADB_QTDEMP-If(nX>0,aContrato[nX][3],0)
				EndIf
				TMP1->CK_CONTRAT := (cAliasADB)->ADB_NUMCTR
				TMP1->CK_ITEMCON := (cAliasADB)->ADB_ITEM
				Exit
			EndIf
		EndIf
		dbSelectArea(cAliasADB)
		dbSkip()
	EndDo
	If lQuery
		dbSelectArea(cAliasADB)
		dbCloseArea()
		dbSelectArea("ADB")
	EndIf
EndIf
If nPrcVen == 0
	dbSelectArea("SB1")
	dbSetOrder(1)
	If ( !Empty(cProduto) .And. dbSeek(xFilial("SB1")+cProduto,.F.) )
		nPrcVen := MaTabPrVen(cTabPrec,cProduto,nQtde,cCliente,cLoja,M->CJ_MOEDA,M->CJ_EMISSAO)
	EndIf
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Aqui ┌ efetuado o tratamento dIferencial de Precos para os   Ё
	//Ё Opcionais do Produto.                                        Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	While !Empty(cOpcional)
		cOpc      := SubStr(cOpcional,1,At("/",cOpcional)-1)
		cOpcional := SubStr(cOpcional,At("/",cOpcional)+1)
		dbSelectArea("SGA")
		dbSetOrder(1)
		If ( MsSeek(xFilial("SGA")+cOpc) )
			nPrcVen += SGA->GA_PRCVEN
		EndIf
	EndDo
EndIf
RestArea(aAreaADA)
RestArea(aAreaADB)
RestArea(aAreaSB1)
RestArea(aArea)
Return (nPrcVen)

/*/
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤└o    ЁA415GeraCLЁ Autor Ё Eduardo Riera         Ё Data Ё 05.12.97 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤└o Ё Programa de Preenchimento da aCols                         Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe e Ё aCols A415GeraCL(Void)                                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё Generico                                                   Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Static Function A415GeraCL()

Local aArea		:= GetArea()
Local aHeader  := {}
Local aCols    := {}
Local nUsado	:= 0
Local nX	:= 0
Local nAcols	:= 0
Local nPos		:= 0
Local nPos2		:= 0
Local nPosItem  := 0
Local nItem		:= 0

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Monta aCols                                                  Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
aHeader := aHeaderSCL
nUsado  := Len(aHeader)
nPosItem:= aScan(aHeader,{|x| Trim(x[2]) == "CL_ITEM" })
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё VerIfico se existe no temporario registros validos           Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
dbSelectArea("TMP2") // SCL
dbSetOrder(1)
MsSeek(M->CJ_NUM+TMP1->CK_ITEM,.T.)
While  ( !Eof() .And. M->CJ_NUM+TMP1->CK_ITEM==TMP2->CL_NUM+TMP2->CL_ITEMORC )
	aadd( aCols , Array(nUsado+1) )
	nACols := Len(aCols)
	For nX := 1 To Len(aHeader)
		If ( aHeader[nX][10] <> "V")
			aCols[nAcols][nX] := FieldGet(FieldPos(aHeader[nX][2]))
		Else
			aCols[nAcols][nX] := CriaVar(aHeader[nX][2],.T.)
		EndIf
	Next nX
	aCols[nAcols,nUsado+1] := TMP2->CL_FLAG
	dbSelectArea("TMP2")
	dbSkip()
EndDo
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Verifico se o Produto e'uma sugestao de venda e se o acols   Ё
//Ё esta vazia                                                   Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If ( Empty(aCols) )
	nItem := 0
	dbSelectArea("SBH")
	dbSetOrder(1)
	MsSeek(xFilial()+TMP1->CK_PRODUTO,.T.)
	While  ( !Eof() .And. xFilial()==SBH->BH_FILIAL .And. ;
			SBH->BH_PRODUTO == TMP1->CK_PRODUTO )
		aadd( aCols , Array(nUsado+1) )
		nACols := Len(aCols)
		nPos   := 0
		For nX := 1 To Len(aHeader)
			Do Case
			Case Trim(aHeader[nX,2])=="CL_PRODUTO"
				aCols[nACols,nX] := SBH->BH_CODCOMP
				If ( nPos > 0 )
					dbSelectArea("SB1")
					dbSetOrder(1)
					MsSeek(xFilial()+aCols[nAcols,nX])
					aCols[nAcols,nPos] := PadL(SB1->B1_DESC,TamSX3("CL_DESCRI")[1])
				EndIf
				nPos := nX
			Case Trim(aHeader[nX,2])=="CL_QUANT"
				aCols[nACols,nX] := SBH->BH_QUANT
			Case Trim(aHeader[nX,2])=="CL_DESCRI"
				If ( nPos > 0 )
					dbSelectArea("SB1")
					dbSetOrder(1)
					MsSeek(xFilial()+aCols[nAcols,nPos])
					aCols[nAcols,nX] := PadL(SB1->B1_DESC,TamSX3("CL_DESCRI")[1])
				EndIf
				nPos := nX
			OtherWise     
				If !( AllTrim(aHeader[nX,2]) $ "CL_ALI_WT/CL_REC_WT")	
					aCols[nAcols][nX] := CriaVar(aHeader[nX][2],.T.)
				EndIf
			EndCase
		Next nX
		aCols[nAcols,nPosItem] := StrZero(++nItem,2)
		aCols[nAcols,nUsado+1] := .F.
		dbSelectArea("SBH")
		dbSkip()
	EndDo
EndIf
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Se nфo existir registros validos e'criada uma aCols vazia    Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If ( Empty(aCols) )
	aadd( aCols , Array(nUsado+1) )
	nACols := Len(aCols)
	For nX := 1 To nUsado
		If !( AllTrim(aHeader[nX,2]) $ "CL_ALI_WT/CL_REC_WT")	
			aCols[nAcols][nX] := CriaVar(aHeader[nX][2],.T.)
		EndIf	
	Next nX
	aCols[nAcols,nUsado+1] := .F.
	aCols[nAcols,nPosItem] := "01"
EndIf
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Inicializa o a Qtd Necessaria para o orcamento               Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
nPos := aScan(aHeader,{|x| Trim(x[2]) == "CL_QTDVEN" })
nPos2:= aScan(aHeader,{|x| Trim(x[2]) == "CL_QUANT" })
nACols := Len(aCols)
If ( nPos > 0 .And. nPos2 > 0 )
	For nX := 1 To nACols
		aCols[nX,nPos]  := aCols[nX,nPos2] * TMP1->CK_QTDVEN
	Next
EndIf
RestArea(aArea)
Return(aCols)

/*/
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤└o    ЁA415GrvSCLЁ Autor Ё Eduardo Riera         Ё Data Ё 05.12.97 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤└o Ё Programa de Atualizacao do Arquivo Temporario referente ao Ё╠╠
╠╠Ё          Ё SCL                                                        Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe e Ё Void A415GrvSCL(aHeader,aCols)                             Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ aHeader e Acols                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё Generico                                                   Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Static Function A415GrvSCL(aHeader,aCols)

Local nX  := 0
Local nX2 := 0
Local nUsado  := nAcols   := 0
Local nProduto:= aScan(aHeader,{|x| Trim(x[2])=="CL_PRODUTO" })
Local aArea   := GetArea()

dbSelectArea("TMP2")
dbSetOrder(1)
MsSeek(M->CJ_NUM+TMP1->CK_ITEM,.T.)
While  ( !Eof() .And. M->CJ_NUM+TMP1->CK_ITEM==TMP2->CL_NUM+TMP2->CL_ITEMORC )
	RecLock("TMP2")
	dbDelete()
	MsUnlock()
	dbSelectArea("TMP2")
	dbSkip()
EndDo
nAcols := Len(aCols)
For nX := 1 To nAcols
	If ( nProduto > 0 )
		If ( !Empty(aCols[nX,nProduto]) )
			nUsado := Len(aHeader)
			RecLock("TMP2",.T.)
			For nX2 := 1 To nUsado
				If !( AllTrim(aHeader[nX2,2]) $ "CL_ALI_WT/CL_REC_WT")				
					nPosField := FieldPos(Trim(aHeader[nX2,2]))
					If ( nPosField > 0 )
						FieldPut(nPosField,aCols[nX,nX2])
					EndIf
				EndIf
			Next nX2
			TMP2->CL_NUM      := M->CJ_NUM
			TMP2->CL_ITEMORC  := TMP1->CK_ITEM
			TMP2->CL_FLAG     := aCols[nX,nUsado+1]
			MsUnlock()
		EndIf
	EndIf
Next nX
RestArea(aArea)
Return(.T.)

/*/
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤└o    ЁA415PrcLisЁ Autor Ё Eduardo Riera         Ё Data Ё 06.01.98 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤└o Ё Programa de Calculo do Preco de Lista                      Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe e Ё Void A415PrcLis(Void)                                      Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё MATA415                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function A415PrcLista()

Local aArea		:= GetArea()
Local nPrcLista := 0

dbSelectArea("TMP2")
dbSetOrder(1)
MsSeek(M->CJ_NUM+TMP1->CK_ITEM,.T.)

While ( !Eof() .And. M->CJ_NUM == TMP2->CL_NUM .And. ;
		TMP2->CL_ITEMORC == TMP1->CK_ITEM )

	dbSelectArea("SB1")
	dbSetOrder(1)
	MsSeek(xFilial()+TMP2->CL_PRODUTO)

	If ( !TMP2->CL_FLAG )

		nPrcLista += (TMP2->CL_QUANT*a415Tabela(TMP2->CL_PRODUTO,M->CJ_TABELA,TMP1->CK_QTDVEN*TMP2->CL_QUANT))

	EndIf

	dbSelectArea("TMP2")
	dbSkip()
EndDo

RestArea(aArea)
Return(nPrcLista)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁA415Total   Ё Autor ЁViviani Barcellos    Ё Data Ё 15.01.99 Ё╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Totalizacao do orcamento                                   Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   ЁVoid A415Total(Void)                                        Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ oDlg: Objeto Windows                                       Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё MATA415                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Static Function A415Total(oDlg)

Local aArea   	:= GetArea()
Local aAreaTmp1	:= TMP1->(GetArea())
Local nTotVal 	:= 0
Local nTotDesc	:= 0
Local nPerDesc  := M->CJ_DESC4
Local nX  := 0
Local aControl

l415Auto := If(Type("l415Auto")<>"U",l415Auto,.F.)
l416Auto := If(Type("l416Auto")<>"U",l416Auto,.F.)

If !(l415Auto) .and. !(l416Auto)
	aControl := oDlg:aControls
EndIf
//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁSoma o os valores e os descontos, mostrando-os na telaЁ
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
dbSelectArea("TMP1")
dbGotop()
While ( !Eof() )
	If ( !TMP1->CK_FLAG )
		nTotVal  += TMP1->CK_VALOR
		If (TMP1->CK_PRUNIT > TMP1->CK_PRCVEN)
			nTotDesc += A410Arred((TMP1->CK_PRUNIT * TMP1->CK_QTDVEN),"CK_VALOR") - A410Arred((TMP1->CK_PRCVEN * TMP1->CK_QTDVEN),"CK_VALOR")
		Else
			nTotDesc += TMP1->CK_VALDESC
		EndIf
	EndIf
	dbSelectArea("TMP1")
	dbSkip()
EndDo
nTotDesc += M->CJ_DESCONT
nTotVal  -= M->CJ_DESCONT
nTotDesc += A410Arred(nTotVal*M->CJ_PDESCAB/100,"C6_VALOR")
nTotVal  -= A410Arred(nTotVal*M->CJ_PDESCAB/100,"C6_VALOR")
//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁCalcula o Desconto por Total                          Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
If nTotVal > 0 .And. FtRegraDesc(4,nTotVal+nTotDesc,@M->CJ_DESC4) <> nPerDesc
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁSoma o os valores e os descontos, mostrando-os na telaЁ
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
	nTotVal := 0
	nTotDesc:= 0
	dbSelectArea("TMP1")
	dbGotop()
	While ( !Eof() )
		If ( !TMP1->CK_FLAG )
			nTotVal  += TMP1->CK_VALOR
			If (TMP1->CK_PRUNIT > TMP1->CK_PRCVEN)
				nTotDesc += A410Arred((TMP1->CK_PRUNIT * TMP1->CK_QTDVEN),"CK_VALOR") - A410Arred((TMP1->CK_PRCVEN * TMP1->CK_QTDVEN),"CK_VALOR")
			Else
				nTotDesc += TMP1->CK_VALDESC
			EndIf
		EndIf
		dbSelectArea("TMP1")
		dbSkip()
	EndDo
EndIf

//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁSoma as variaveis da Enchoice                                  Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
nTotVal += M->CJ_FRETE
nTotVal += M->CJ_SEGURO
nTotVal += M->CJ_DESPESA
nTotVal += M->CJ_FRETAUT

If !( l415Auto .or. l416Auto)
	For nX := 1 To Len(aControl)
		If ( ValType(aControl[nX]:Cargo)=="C")
			Do Case
			Case ( aControl[nX]:Cargo $ "Total" )
				aControl[nX]:SetText(nTotVal)
			Case ( aControl[nX]:Cargo $ "Desconto" )
				aControl[nX]:SetText(nTotDesc)
			Case ( aControl[nX]:Cargo $ "Valor" )
				aControl[nX]:SetText(nTotVal+nTotDesc)
			EndCase
		EndIf
	Next nX
EndIf
RestArea(aAreaTmp1)
RestArea(aArea)
Return(Nil)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁA415DesCabЁ Autor Ё Eduardo Riera         Ё Data Ё18.02.99  Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁRecalculo automatico do desconto de cabecalho sobre o preco Ё╠╠
╠╠Ё          Ёde lista.                                                   Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁSempre .T.                                                  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁExpL1 : Default (.T.)                                       Ё╠╠
╠╠Ё          ЁExpL2 : Recalcular ( .T. )                                  Ё╠╠
╠╠цддддддддддедддддддддддддддбдддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё   DATA   Ё Programador   ЁManutencao Efetuada                         Ё╠╠
╠╠цддддддддддедддддддддддддддедддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё          Ё               Ё                                            Ё╠╠
╠╠юддддддддддадддддддддддддддадддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/

Function a415DesCab(lTodos,lRecal,lRecItens)

Local aArea		:= GetArea()
Local aAreaTmp1:= TMP1->(GetArea())
Local nPrcVen   := 0
Local nX        := 0
Local oDlg

DEFAULT lTodos	:= .T.
DEFAULT lRecal  := .T.
DEFAULT lRecItens  := .F.

dbSelectArea("TMP1")
If ( lTodos )
	dbGotop()
EndIf
While ( !Eof() )
	nPrcVen := TMP1->CK_PRUNIT
	If ( nPrcVen > 0.00 )

		//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁDeve re-calcular o desconto dos items para pegar o desconto escalavel, Ё
		//Ёque eh por item, mais calculado no total.                              Ё
		//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If lRecItens	
			TMP1->CK_DESCONT	:=	FtRegraDesc(2)
		Endif	
		nPrcVen := a415PrcDes(nPrcVen)
		nPrcVen := (nPrcVen*(1-TMP1->CK_DESCONT/100))
		nPrcVen := a410Arred(nPrcVen,"CK_PRCVEN")
		TMP1->CK_PRCVEN := nPrcVen
		TMP1->CK_VALOR  := A410Arred(TMP1->CK_QTDVEN * TMP1->CK_PRCVEN,"CK_VALOR")
		TMP1->CK_VALDESC:= (a415PrcDes(TMP1->CK_PRUNIT)-nPrcVen)*TMP1->CK_QTDVEN
		TMP1->CK_DESCONT:= NoRound((1-(nPrcVen/a415PrcDes(TMP1->CK_PRUNIT)))*100,TamSx3("CK_DESCONT")[2])
	EndIf
	If ( lTodos )
		dbSelectArea("TMP1")
		dbSkip()
	Else
		Exit
	EndIf
EndDo
RestArea(aAreaTmp1)
If lRecal
	If (Type("l415Auto") == "L" .and. l415Auto) .or. (Type("l416Auto") == "L" .and. l416Auto)
		A415Total()
	Else
		oDlg := GetWndDefault()
		A415Total(oDlg)
		For nX := 1 To Len(oDlg:aControls)
			oDlg:aControls[nX]:ReFresh()
		Next nX
	EndIf
EndIf
RestArea(aAreaTmp1)
RestArea( aArea )
Return(.T.)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁA415PrcDesЁ Autor Ё Eduardo Riera         Ё Data Ё18.02.99  Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁCalculo do Preco de Venda com desconto de cabecalho sobre o Ё╠╠
╠╠Ё          Ёpreco de Lista.                                             Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁExpN1: Valor com desconto de cabecalho                      Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁExpN1: Preco de Lista                                       Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддбдддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё   DATA   Ё Programador   ЁManutencao Efetuada                         Ё╠╠
╠╠цддддддддддедддддддддддддддедддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё          Ё               Ё                                            Ё╠╠
╠╠юддддддддддадддддддддддддддадддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Static Function A415PrcDes(nPrcLista)

nPrcLista := FtDescCab(nPrcLista,{M->CJ_DESC1,M->CJ_DESC2,M->CJ_DESC3,M->CJ_DESC4})

Return(nPrcLista)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁMa415Bar  Ё Autor Ё Eduardo Riera         Ё Data Ё24.03.2000Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё EnchoiceBar especIfica do Mata415                          Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё Nenhum                                                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ Nenhum                                                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё ExpC1 -> Tabela alterada - p/ gatilho                      Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpO1: Objeto Dialog                                       Ё╠╠
╠╠Ё          Ё ExpB2: Code Block para o Evento Ok                         Ё╠╠
╠╠Ё          Ё ExpB3: Code Block para o Evento Cancel                     Ё╠╠
╠╠Ё          Ё ExpN4: nOpc transmitido pela mbrowse                       Ё╠╠
╠╠цддддддддддедддддддддддддддбдддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё   DATA   Ё Programador   ЁManutencao Efetuada                         Ё╠╠
╠╠цддддддддддедддддддддддддддедддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё          Ё               Ё                                            Ё╠╠
╠╠юддддддддддадддддддддддддддадддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Static Function Ma415Bar(oDlg,bOk,bCancel,nOpc)

Local aUsButtons:= {}
Local aButtons 	:= {}
Local lOpcPadrao:= GetNewPar("MV_REPGOPC","N") == "N"
Local nPOpcional:= If(lOpcPadrao,aScan(aHeader,{|x| AllTrim(x[2])=="CK_OPC"}),aScan(aHeader,{|x| AllTrim(x[2])=="CK_MOPC"}))

aadd(aButtons,{"POSCLI",{|| If(!Empty(M->CJ_CLIENTE),a450F4Con(),.F.)},OemToAnsi(STR0041), OemToAnsi(STR0058) })	//"Posi┤└o de Cliente"
aadd(aButtons,{"RELATORIO",{||Ma415Impos(aRotina[ nOpc, 4 ])},OemToAnsi(STR0042),OemToAnsi(STR0059) })	//"Planilha Financeira"
If ( RpcCheckTbi() )
	aadd(aButtons,{"VENDEDOR",{|| Ma415TbiCl()},OemToAnsi(STR0036),OemToAnsi(STR0060)}) //"Inclusao de cliente TBI"
EndIf

If aRotina[ nOpc, 4 ] == 2
	AAdd(aButtons,{ "BMPVISUAL", {|| A415Track() }, OemToAnsi(STR0050),OemToAnsi(STR0061) } )  // "System Tracker"
EndIf 	
If ( nOpc == 1 .Or. nOpc == 2 .Or. nOpc == 5 ) .And. nPOpcional > 0
	Aadd(aButtons,{"SDUCOUNT", {|| SeleOpc(2,"MATA415",TMP1->CK_PRODUTO,,,Ma415Opc(lOpcPadrao),"M->CK_PRODUTO",.T.) } ,STR0072,STR0073}) //"Opcionais Selecionados"###"Opcionais"
EndIf

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Adiciona botoes do usuario na EnchoiceBar                              Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If ExistBlock("MA415BUT")
	If ValType( aUsButtons := ExecBlock( "MA415BUT", .F., .F. ) ) == "A"
		AEval( aUsButtons, { |x| AAdd( aButtons, x ) } )
	EndIf
EndIf
Return (EnchoiceBar(oDlg,bOK,bcancel,,aButtons))

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁMa415TbiClЁ Autor Ё Eduardo Riera         Ё Data Ё24.03.2000Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁRotina de atualizacao do Orcamento para clientes nao cadas- Ё╠╠
╠╠Ё          Ёtrados quando vindos do TBI.                                Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё Nenhum                                                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ Nenhum                                                     Ё╠╠
╠╠цддддддддддедддддддддддддддбдддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё   DATA   Ё Programador   ЁManutencao Efetuada                         Ё╠╠
╠╠цддддддддддедддддддддддддддедддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё          Ё               Ё                                            Ё╠╠
╠╠юддддддддддадддддддддддддддадддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Static Function Ma415TbiCl()

Local aArea				:= GetArea()
Local lIncSA1			:= .F.

If ( Empty(M->CJ_CLIENTE) .And. !Empty(M->CJ_CNPJCLI) )
	dbSelectArea("SA1")
	dbSetOrder(3)
	If ( !MsSeek(xFilial("SA1")+M->CJ_CNPJCLI) )
		cCadastro := OemToAnsi(STR0036) //"Inclusao de cliente TBI"
		Processa({|| lIncSA1 := TbSocioCli(M->CJ_CNPJCLI)})
		If ( lIncSA1 )
			dbSelectArea("SA1")
			dbSetOrder(3)
			MsSeek(xFilial("SA1")+M->CJ_CNPJCLI)
			M->CJ_CLIENTE := SA1->A1_COD
			M->CJ_LOJA := SA1->A1_LOJA
			M->CJ_NOMCLI	:= SA1->A1_NOME
		Else
			Help(" ",1,"TBIfAULT")
		EndIf
	Else
		M->CJ_CLIENTE	:= SA1->A1_COD
		M->CJ_LOJA		:= SA1->A1_LOJA
		M->CJ_NOMCLI	:= SA1->A1_NOME
	EndIf
EndIf
lRefresh := .T.
RestArea(aArea)
Return(.T.)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁA415B2BC_RЁ Autor Ё Eduardo Riera         Ё Data Ё04.04.2000Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁRotina de Recepcao das cotacoes via Portal                  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe e Ё Void A415B2BC(Void)                                        Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpA1 = [1] IdentIficacao do Campo                         Ё╠╠
╠╠Ё          Ё         [2] Conteudo do Campo                              Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё Portal TBI                                                 Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function A415B2BC_R()

Processa({|| TbiCot2Orc(,,.T.)}) //"Recebendo dados do Portal TBI"
Processa({|| TbiPC2Orc(,,.T.)}) //"Recebendo dados do Portal TBI"
Return(.T.)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁA415Envia Ё Autor Ё Eduardo Riera         Ё Data Ё04.04.2000Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁRotina de Envio dos dados via TBI                           Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe e Ё Void A415B2BC(Void)                                        Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpA1 = [1] IdentIficacao do Campo                         Ё╠╠
╠╠Ё          Ё         [2] Conteudo do Campo                              Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё Portal TBI                                                 Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function A415Envia()

Processa({|| TbiSendOrc(,,,,.T.)})
CloseBrowse()
Return(.T.)

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁA415TabAlt  Ё Autor ЁSergio Silveira      Ё Data Ё22/05/2000Ё╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Funcao de Alteracao da tabela de precos.                   Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё ExpC1 := A415TabAlt()                                      Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ Nenhum                                                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё ExpC1 -> Tabela alterada - p/ gatilho                      Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё MATA415                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Function A415TabAlt()

Local aArea      := GetArea()
Local aAreaSB1   := SB1->(GetArea())

LOCAL nRegAtu    := 0
LOCAL nPrcVen    := 0
LOCAL nVlrTabela := 0

LOCAL lPrcOrc    := GetMv("MV_PRCORC")

nRegAtu := TMP1->( RecNo() )

//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Atualiza os valores dos itens com base na tabela     Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
TMP1->( dbGoTop() )
While TMP1->( !Eof() )

	If lPrcOrc

		dbSelectArea("TMP2")
		dbSetOrder(1)
		If MsSeek(M->CJ_NUM+TMP1->CK_ITEM,.T.)

			nPrcVen := 0

			While ( !Eof() .And. M->CJ_NUM == TMP2->CL_NUM .And. ;
					TMP2->CL_ITEMORC == TMP1->CK_ITEM )

				dbSelectArea("SB1")
				dbSetOrder(1)
				MsSeek(xFilial()+TMP2->CL_PRODUTO)

				If ( !TMP2->CL_FLAG )
					nPrcVen += (TMP2->CL_QUANT*A415Tabela(TMP2->CL_PRODUTO,M->CJ_TABELA,TMP2->CL_QUANT*TMP1->CK_QTDVEN))
				EndIf

				dbSelectArea("TMP2")
				dbSkip()
			EndDo
		Else
			nPrcVen := A415Tabela(TMP1->CK_PRODUTO,M->CJ_TABELA,TMP1->CK_QTDVEN)
		Endif	

	Else
		nPrcVen := A415Tabela(TMP1->CK_PRODUTO,M->CJ_TABELA,TMP1->CK_QTDVEN)
	Endif	    	

	nVlrTabela := nPrcVen

	RecLock("TMP1",.F.)	
	TMP1->CK_PRUNIT  := nVlrTabela
	TMP1->CK_PRCVEN  := nPrcVen
	TMP1->CK_VALOR   := A410Arred(TMP1->CK_QTDVEN*TMP1->CK_PRCVEN,"CK_VALOR")
	MsUnlock()

	TMP1->(dbSkip())

EndDo

//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Aplica os descontos de cabecalho e de item           Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
A415DesCab( .T. )

//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Posiciona na linha original                          Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
TMP1->( MsGoto( nRegAtu ) )

If Type("oGetDad") <> "U"
	oGetDad:oBrowse:Refresh()
Endif	

RestArea(aAreaSB1)
RestArea(aArea)

Return( M->CJ_TABELA )

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁA415TabCli  Ё Autor ЁSergio Silveira      Ё Data Ё22/05/2000Ё╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Alteracao da tabela de precos com base na tabela do clienteЁ╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё ExpC1 := A415TabCli()                                      Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ Nenhum                                                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё ExpC1 -> Tabela do cliente                                 Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё MATA415                                                    Ё╠╠
╠╠цддддддддддедддддддддддддддбдддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё   DATA   Ё Programador   ЁManutencao efetuada                         Ё╠╠
╠╠цддддддддддедддддддддддддддедддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё13/08/2007ЁNorbert Waage  ЁBops 130060: Atualiza a tabela de precos no Ё╠╠
╠╠Ё          Ё               Ёcabecalho da ExecAuto, quando houver.       Ё╠╠
╠╠юддддддддддадддддддддддддддадддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Function A415TabCli()

LOCAL cTabCli		// Codigo da tabela do cliente
Local nPos			// Posicao do campo CJ_TABELA no array aAutoCab

cTabCli      := SA1->A1_TABELA
M->CJ_TABELA := cTabCli     

If !Empty(SA1->A1_DESC)
	M->CJ_DESC1 := SA1->A1_DESC
EndIf

//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁSe for uma rotina automatica, atualiza a tabela no aAutoCabЁ
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If l415Auto .AND. ((nPos := aScan(aAutoCab,{|x|x[1] == "CJ_TABELA"})) > 0) .AND. !Empty(cTabCli)
	aAutoCab[nPos][2] := M->CJ_TABELA
EndIf

//зддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Atualiza os valores com base na nova tabela Ё
//юддддддддддддддддддддддддддддддддддддддддддддды
If !GetNewPar("MV_ORRECAL",.F.) .And. Select("TMP1") > 0 //Desabilita o recalculo do OrГamento de venda.
	A415TabAlt()
EndIf

Return( cTabCli )

/*/
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбддддддддддд©╠╠
╠╠ЁFun┤└o    ЁMA415ImposЁ Autor Ё Eduardo Riera         Ё Data Ё24.07.2000 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддаддддддддддд╢╠╠
╠╠Ё          ЁMa415Impos(nOpc)                                             Ё╠╠
╠╠Ё          ЁFuncao de calculo dos impostos contidos no orcamento de vendaЁ╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ nOpc                                                        Ё╠╠
╠╠Ё          Ё                                                             Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁNenhum                                                       Ё╠╠
╠╠Ё          Ё                                                             Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁDescri┤└o ЁEsta funcao efetua os calculos de impostos (ICMS,IPI,ISS,etc)Ё╠╠
╠╠Ё          Ёcom base nas funcoes fiscais, a fim de possibilitar ao usua- Ё╠╠
╠╠Ё          Ёrio o valor de desembolso financeiro.                        Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё Generico                                                    Ё╠╠
╠╠юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Static Function Ma415Impos( nOpc )

Local aArea		:= GetArea()
Local aAreaSA1	:= SA1->(GetArea())
Local aAreaTMP1 := TMP1->(GetArea())
Local aTitles   := {STR0043,STR0044,STR0062} //"Nota Fiscal"###"Duplicatas###"Rentabilidade"
Local aDupl     := {}
Local aFlHead   := { STR0045,STR0046 } //"Vencimento"###"Valor"
Local aVencto   := {}
Local aRFHead   := { RetTitle("C6_PRODUTO"),RetTitle("C6_VALOR"),STR0063,STR0064,STR0065,STR0066}//"C.M.V"###"Vlr.Presente"###"Lucro Bruto"###"Margem de ContribuiГЦo(%)"
Local aRentab   := {}
Local nX        := 0
Local nAcerto   := 0
Local nPrcLista := 0
Local nValMerc  := 0
Local nDesconto := 0
Local nAcresFin := 0
Local nQtdPeso  := 0
Local nItem     := 0
Local cParc     := ""
Local dDataCnd  := M->CJ_EMISSAO
Local lCondVenda := .F.  // Template GEM - Se existe condicao de venda
Local oDlg
Local oDupl
Local oFolder
Local oRentab
//зддддддддддддддддддддддддддддддддддддддддддддд©
//ЁPosiciona Registros                          Ё
//юддддддддддддддддддддддддддддддддддддддддддддды
dbSelectArea("SA1")
dbSetOrder(1)
MsSeek(xFilial("SA1")+If(!Empty(M->CJ_CLIENT),M->CJ_CLIENT,M->CJ_CLIENTE)+M->CJ_LOJAENT)

dbSelectArea("SE4")
dbSetOrder(1)
MsSeek(xFilial("SE4")+M->CJ_CONDPAG)

//зддддддддддддддддддддддддддддддддддддддддддддд©
//ЁInicializa a funcao fiscal                   Ё
//юддддддддддддддддддддддддддддддддддддддддддддды
MaFisSave()
MaFisEnd()
MaFisIni(Iif(Empty(M->CJ_CLIENT),M->CJ_CLIENTE,M->CJ_CLIENT),;// 1-Codigo Cliente/Fornecedor
	M->CJ_LOJAENT,;		// 2-Loja do Cliente/Fornecedor
	"C",;				// 3-C:Cliente , F:Fornecedor
	"N",;				// 4-Tipo da NF
	SA1->A1_TIPO,;		// 5-Tipo do Cliente/Fornecedor
	Nil,;
	Nil,;
	Nil,;
	Nil,;
	"MATA461")

//Na argentina o calculo de impostos depende da serie.
If cPaisLoc == 'ARG'
	MaFisAlt('NF_SERIENF',LocXTipSer('SA1',MVNOTAFIS))
Endif

//зддддддддддддддддддддддддддддддддддддддддддддд©
//ЁAgrega os itens para a funcao fiscal         Ё
//юддддддддддддддддддддддддддддддддддддддддддддды
dbSelectArea("TMP1")
dbGotop()
While ( !Eof() )
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Verifica se a linha foi deletada                     Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If ( !TMP1->CK_FLAG .And. !Empty(TMP1->CK_PRODUTO) )
		//зддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁPosiciona Registros                          Ё
		//юддддддддддддддддддддддддддддддддддддддддддддды			
		SB1->(dbSetOrder(1))
		If SB1->(MsSeek(xFilial("SB1")+TMP1->CK_PRODUTO))
			nQtdPeso := TMP1->CK_QTDVEN*SB1->B1_PESO
		EndIf
	    SB2->(dbSetOrder(1))
	    SB2->(MsSeek(xFilial("SB2")+TMP1->CK_PRODUTO+TMP1->CK_LOCAL))
	    SF4->(dbSetOrder(1))
	    SF4->(MsSeek(xFilial("SF4")+TMP1->CK_TES))
		//зддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁCalcula o preco de lista                     Ё
		//юддддддддддддддддддддддддддддддддддддддддддддды
		nValMerc  := TMP1->CK_VALOR
		nPrcLista := TMP1->CK_PRUNIT
		nQtdPeso  := 0
		nItem++
		If ( nPrcLista == 0 )
			nPrcLista := A410Arred(nValMerc/TMP1->CK_QTDVEN,"CK_PRCVEN")
		EndIf
		nAcresFin := A410Arred(TMP1->CK_PRCVEN*SE4->E4_ACRSFIN/100,"D2_PRCVEN")
		nValMerc  += A410Arred(nAcresFin*TMP1->CK_QTDVEN,"D2_TOTAL")
		nDesconto := A410Arred(nPrcLista*TMP1->CK_QTDVEN,"D2_DESCON")-nValMerc
		nDesconto := IIf(nDesconto==0,TMP1->CK_VALDESC,nDesconto)
		nDesconto := Max(0,nDesconto)
		nPrcLista += nAcresFin
		
		//Para os outros paises, este tratamento e feito no programas que calculam os impostos.
		If cPaisLoc=="BRA"
  			nValMerc  += nDesconto
		Endif
		
		//зддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁVerifica a data de entrega para as duplicatasЁ
		//юддддддддддддддддддддддддддддддддддддддддддддды
		If ( TMP1->(FieldPos("CK_ENTREG"))>0 )
			If ( dDataCnd > TMP1->CK_ENTREG .And. !Empty(TMP1->CK_ENTREG) )
				dDataCnd := TMP1->CK_ENTREG
			EndIf
		Else
			dDataCnd  := M->CJ_EMISSAO
		EndIf
		//зддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁAgrega os itens para a funcao fiscal         Ё
		//юддддддддддддддддддддддддддддддддддддддддддддды
		MaFisAdd(TMP1->CK_PRODUTO,;   	// 1-Codigo do Produto ( Obrigatorio )
			TMP1->CK_TES,;	   	// 2-Codigo do TES ( Opcional )
			TMP1->CK_QTDVEN,;  	// 3-Quantidade ( Obrigatorio )
			nPrcLista,;		  	// 4-Preco Unitario ( Obrigatorio )
			nDesconto,; 	// 5-Valor do Desconto ( Opcional )
			"",;	   			// 6-Numero da NF Original ( Devolucao/Benef )
			"",;				// 7-Serie da NF Original ( Devolucao/Benef )
			0,;					// 8-RecNo da NF Original no arq SD1/SD2
			0,;					// 9-Valor do Frete do Item ( Opcional )
			0,;					// 10-Valor da Despesa do item ( Opcional )
			0,;					// 11-Valor do Seguro do item ( Opcional )
			0,;					// 12-Valor do Frete Autonomo ( Opcional )
			nValMerc,;			// 13-Valor da Mercadoria ( Obrigatorio )
			0)					// 14-Valor da Embalagem ( Opiconal )
	
		SB1->(dbSetOrder(1))
		If SB1->(MsSeek(xFilial("SB1")+TMP1->CK_PRODUTO))
			nQtdPeso := TMP1->CK_QTDVEN*SB1->B1_PESO
		Endif	
		//зддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁAltera peso para calcular frete              Ё
		//юддддддддддддддддддддддддддддддддддддддддддддды
		MaFisAlt("IT_PESO",nQtdPeso,nItem)
		MaFisAlt("IT_PRCUNI",nPrcLista,nItem)
		MaFisAlt("IT_VALMERC",nValMerc,nItem)
	
		//зддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁAnalise da Rentabilidade                     Ё
		//юддддддддддддддддддддддддддддддддддддддддддддды
		If SF4->F4_DUPLIC=="S"
			nY := aScan(aRentab,{|x| x[1] == TMP1->CK_PRODUTO})
			If nY == 0
				aadd(aRenTab,{TMP1->CK_PRODUTO,0,0,0,0,0})
				nY := Len(aRenTab)
			EndIf
			
			If cPaisLoc=="BRA"
				aRentab[nY][2] += (nValMerc - nDesconto)
			Else
				aRentab[nY][2] += nValMerc
			Endif			
			aRentab[nY][3] += TMP1->CK_QTDVEN*SB2->B2_CM1	
		EndIf
	EndIf	
	dbSelectArea("TMP1")
	dbSkip()
EndDo
//зддддддддддддддддддддддддддддддддддддддддддддд©
//ЁIndica os valores do cabecalho               Ё
//юддддддддддддддддддддддддддддддддддддддддддддды
MaFisAlt("NF_FRETE",M->CJ_FRETE)
MaFisAlt("NF_SEGURO",M->CJ_SEGURO)
MaFisAlt("NF_AUTONOMO",M->CJ_FRETAUT)
MaFisAlt("NF_DESPESA",M->CJ_DESPESA)
MaFisAlt("NF_DESCONTO",MaFisRet(,"NF_DESCONTO")+MaFisRet(,"NF_VALMERC")*M->CJ_PDESCAB/100)
MaFisAlt("NF_DESCONTO",MaFisRet(,"NF_DESCONTO")+SCJ->CJ_DESCONT)
MaFisWrite(1)
//
// Template GEM - Gestao de Empreendimentos Imobiliarios
//
// Verifica se a condicao de pagamento tem vinculacao com uma condicao de venda
//
If ExistTemplate("GMCondPagto")
	lCondVenda := ExecTemplate("GMCondPagto",.F.,.F.,{M->CJ_CONDPAG,} )
	If ValType("lCondVenda") # "L"
		lCondVenda := .f.
	EndIf
EndIf
//зддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁCalcula os venctos conforme a condicao de pagto  Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддды
dbSelectarea("SE4")
dbSetOrder(1)
MsSeek(xFilial("SE4")+M->CJ_CONDPAG)
If ((SE4->E4_TIPO=="9".AND.!(INCLUI.OR.ALTERA)).OR.SE4->E4_TIPO<>"9")
	If SE4->E4_TIPO=="9"
		cParc := "1"
		For nX := 1 To SuperGetMv("MV_NUMPARC")
			If SCJ->(FieldPos("CJ_DATA"+cParc))<> 0 .And. SCJ->(FieldPos("CJ_PARC"+cParc))<> 0 .And. !Empty(SCJ->(FieldGet(FieldPos("CJ_PARC"+cParc))))
				aadd(aDupl,{SCJ->(FieldGet(FieldPos("CJ_DATA"+cParc))),SCJ->(FieldGet(FieldPos("CJ_PARC"+cParc)))})
			EndIf
			cParc := Soma1(cParc)
		Next nX
	Else
		aDupl := Condicao(MaFisRet(,"NF_BASEDUP"),M->CJ_CONDPAG,MaFisRet(,"NF_VALIPI"),dDataCnd,MaFisRet(,"NF_VALSOL"),,,nAcresFin)
	EndIf
	If Empty(aDupl)
		aDupl := {{Ctod(""),0}}
	EndIf
	If ! lCondVenda
		For nX := 1 To Len(aDupl)
			nAcerto += aDupl[nX][2]   
			If AllTrim(SE4->E4_COND) == "%"
				aDupl[nX][2] := aDupl[nX][2]*MaFisRet(,"NF_BASEDUP")/100
			EndIf  
		Next nX
		
		If AllTrim(SE4->E4_COND) <> "%"
			aDupl[Len(aDupl)][2] += MaFisRet(,"NF_BASEDUP") - nAcerto
		EndIf  
	EndIf	
	aVencto := aClone(aDupl)
	For nX := 1 To Len(aDupl)
		aDupl[nX][2] := TransForm(aDupl[nX][2],PesqPict("SE1","E1_VALOR"))
	Next nX
Else
	aDupl   := {{Ctod(""),TransForm(MaFisRet(,"NF_BASEDUP"),PesqPict("SE1","E1_VALOR"))}}
	aVencto := {{dDataBase,MaFisRet(,"NF_BASEDUP")}}
EndIf

//
// Template GEM - Gestao de empreendimentos Imobiliarios
// Gera os vencimentos e valores das parcelas conforme a condicao de venda
//
If lCondVenda .AND. ExistTemplate("GMMA415Dupl")
	aVencto := ExecTemplate("GMMA415Dupl",.F.,.F.,{M->CJ_NUM ,M->CJ_CONDPAG,dDataCnd,,MaFisRet(,"NF_BASEDUP") ,aVencto}) 
	aDupl := {}
	aEval(aVencto ,{|aTitulo| aAdd( aDupl ,{transform(aTitulo[1],x3Picture("E1_VENCTO")) ,transform(aTitulo[2],x3Picture("E1_VALOR"))}) })
	If Len(aDupl) == 0
		aDupl := {{Ctod(""),TransForm(MaFisRet(,"NF_BASEDUP"),PesqPict("SE1","E1_VALOR"))}}
		aVencto := {{dDataBase,MaFisRet(,"NF_BASEDUP")}}
	EndIf
EndIf

//зддддддддддддддддддддддддддддддддддддддддддддд©
//ЁAnalise da Rentabilidade - Valor Presente    Ё
//юддддддддддддддддддддддддддддддддддддддддддддды
a415RentVP( @aRenTab ,@aVencto )

//зддддддддддддддддддддддддддддддддддддддддддддд©
//ЁMonta a tela de exibicao dos valores fiscais Ё
//юддддддддддддддддддддддддддддддддддддддддддддды
DEFINE MSDIALOG oDlg TITLE OemToAnsi(STR0042) FROM 09,00 TO 28,80 //"Planilha Financeira"
oFolder := TFolder():New(001,001,aTitles,{"HEADER"},oDlg,,,, .T., .F.,315,140)
//зддддддддддддддддддддддддддддддддддддддддддддд©
//ЁFolder 1                                     Ё
//юддддддддддддддддддддддддддддддддддддддддддддды
MaFisRodape(1,oFolder:aDialogs[1],,{005,001,310,60},Nil,.T.)
@ 070,005 SAY RetTitle("F2_FRETE")		SIZE 40,10 PIXEL OF oFolder:aDialogs[1]
@ 070,105 SAY RetTitle("F2_SEGURO")		SIZE 40,10 PIXEL OF oFolder:aDialogs[1]
@ 070,205 SAY RetTitle("F2_DESCONT")	SIZE 40,10 PIXEL OF oFolder:aDialogs[1]
@ 085,005 SAY RetTitle("F2_FRETAUT")	SIZE 40,10 PIXEL OF oFolder:aDialogs[1]
@ 085,105 SAY RetTitle("F2_DESPESA")	SIZE 40,10 PIXEL OF oFolder:aDialogs[1]
@ 085,205 SAY RetTitle("F2_VALFAT")		SIZE 40,10 PIXEL OF oFolder:aDialogs[1]
@ 070,050 MSGET MaFisRet(,"NF_FRETE")		PICTURE PesqPict("SF2","F2_FRETE",16,2)		SIZE 50,07 PIXEL WHEN .F. OF oFolder:aDialogs[1]
@ 070,150 MSGET MaFisRet(,"NF_SEGURO")  	PICTURE PesqPict("SF2","F2_SEGURO",16,2)	SIZE 50,07 PIXEL WHEN .F. OF oFolder:aDialogs[1]
@ 070,250 MSGET MaFisRet(,"NF_DESCONTO")	PICTURE PesqPict("SF2","F2_DESCONTO",16,2)	SIZE 50,07 PIXEL WHEN .F. OF oFolder:aDialogs[1]
@ 085,050 MSGET MaFisRet(,"NF_AUTONOMO")	PICTURE PesqPict("SF2","F2_FRETAUT",16,2)	SIZE 50,07 PIXEL WHEN .F. OF oFolder:aDialogs[1]
@ 085,150 MSGET MaFisRet(,"NF_DESPESA")		PICTURE PesqPict("SF2","F2_DESPESA",16,2)	SIZE 50,07 PIXEL WHEN .F. OF oFolder:aDialogs[1]
@ 085,250 MSGET MaFisRet(,"NF_BASEDUP")		PICTURE PesqPict("SF2","F2_VALFAT",16,2)	SIZE 50,07 PIXEL WHEN .F. OF oFolder:aDialogs[1]
@ 105,005 TO 106,310 PIXEL OF oFolder:aDialogs[1]
@ 110,005 SAY OemToAnsi(STR0047)   SIZE 40,10 PIXEL OF oFolder:aDialogs[1] //"Total da Nota"
@ 110,050 MSGET MaFisRet(,"NF_TOTAL")      PICTURE PesqPict("SF2","F2_VALBRUT",16,2) 	SIZE 50,07 PIXEL WHEN .F. OF oFolder:aDialogs[1]
@ 110,270 BUTTON OemToAnsi(STR0048)			SIZE 040,11 FONT oFolder:aDialogs[1]:oFont ACTION oDlg:End() OF oFolder:aDialogs[1] PIXEL		//"Sair"
//зддддддддддддддддддддддддддддддддддддддддддддд©
//ЁFolder 2                                     Ё
//юддддддддддддддддддддддддддддддддддддддддддддды
@ 005,001 LISTBOX oDupl FIELDS TITLE aFlHead[1],aFlHead[2] SIZE 310,095 	OF oFolder:aDialogs[2] PIXEL
oDupl:SetArray(aDupl)
oDupl:bLine := {|| aDupl[oDupl:nAt] }
@ 105,005 TO 106,310 PIXEL OF oFolder:aDialogs[2]
If cPaisLoc == "BRA"
	@ 110,005 SAY RetTitle("F2_VALFAT")		SIZE 40,10 PIXEL OF oFolder:aDialogs[2]
Else
	@ 110,005 SAY OemToAnsi(STR0051)    		SIZE 40,10 PIXEL OF oFolder:aDialogs[2]
Endif	
@ 110,050 MSGET MaFisRet(,"NF_BASEDUP")		PICTURE PesqPict("SF2","F2_VALFAT",16,2)	SIZE 50,07 PIXEL WHEN .F. OF oFolder:aDialogs[2]
@ 110,270 BUTTON OemToAnsi(STR0048)			SIZE 040,11 FONT oFolder:aDialogs[1]:oFont ACTION oDlg:End() OF oFolder:aDialogs[2] PIXEL	//"Sair"
//зддддддддддддддддддддддддддддддддддддддддддддд©
//ЁFolder 3                                     Ё
//юддддддддддддддддддддддддддддддддддддддддддддды
@ 005,001 LISTBOX oRentab FIELDS TITLE aRFHead[1],aRFHead[2],aRFHead[3],aRFHead[4],aRFHead[5],aRFHead[6] SIZE 310,095 	OF oFolder:aDialogs[3] PIXEL
@ 110,270 BUTTON OemToAnsi(STR0048)			SIZE 040,11 FONT oFolder:aDialogs[3]:oFont ACTION oDlg:End() OF oFolder:aDialogs[3] PIXEL		//"Sair"
If Empty(aRentab)
	aRentab   := {{"",0,0,0,0,0}}
EndIf
oRentab:SetArray(aRentab)
oRentab:bLine := {|| aRentab[oRentab:nAt] }

//
// Template GEM - Gestao de empreendimentos imobiliarios
// Manutencao dos itens da condicao de venda 
//
If ExistTemplate("GMMA415CVND",,.T.) .AND. HasTemplate("LOT")
	If ExistTemplate("GMMA415Dupl")
		@ 110,170 BUTTON OemToAnsi("Cond. de Venda") SIZE 050,11 FONT oFolder:aDialogs[1]:oFont ;
		          ACTION ( ExecTemplate("GMMA415CVND",.F.,.F.,{nOpc ,M->CJ_NUM ,M->CJ_CONDPAG ,dDataCnd ,MaFisRet(,"NF_BASEDUP")}) ;
		                  ,aVencto := ExecTemplate("GMMA415Dupl",.F.,.F.,{M->CJ_NUM ,M->CJ_CONDPAG,dDataCnd,,MaFisRet(,"NF_BASEDUP"),aVencto}) ;
		                  ,( aDupl := {} ,aEval(aVencto ,{|aTitulo| aAdd( aDupl ,{transform(aTitulo[1],x3Picture("E1_VENCTO")) ,transform(aTitulo[2],x3Picture("E1_VALOR"))})}) ;
		                     ,a415RentVP( @aRenTab ,@aVencto ) );
		                  ,(oDupl:SetArray(aDupl),	oDupl:bLine := {|| aDupl[oDupl:nAt] }) ;
		                  ,(oRentab:SetArray(aRenTab) ,oRentab:bLine := {|| aRenTab[oRenTab:nAt] }) ) ;
		          OF oFolder:aDialogs[2] PIXEL
	EndIf
EndIf
@ 110,270 BUTTON OemToAnsi(STR0048)	SIZE 040,11 FONT oFolder:aDialogs[1]:oFont ACTION oDlg:End() OF oFolder:aDialogs[2] PIXEL	//"Sair"

ACTIVATE MSDIALOG oDlg CENTERED
MaFisEnd()
MaFisRestore()

RestArea(aAreaTMP1)
RestArea(aAreaSA1)
RestArea(aArea)
Return

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁMaCanAltOrc Ё Autor ЁEduardo Riera        Ё Data Ё22/07/2000Ё╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁVerifica se Orcamento de Venda pode ser alterado.           Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё ExpC1 := MaCanAltOrc()                                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpC1: Alias do SCJ                                        Ё╠╠
╠╠Ё          Ё ExpC2: Alias do SCK                                        Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё ExpL1 -> Indica se o item de Orcamento pode ser alterado   Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁObservacaoЁ O SCJ deve estar posicionado e a funcao deve ser processadaЁ╠╠
╠╠Ё          Ё item a item do SCK.                                        Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё MATA415                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function MaCanAltOrc()

Local lRetorno := .T.

If ( !SCJ->CJ_STATUS $ "A#D#E#F" )
	lRetorno := .F.
Else
	If Select("TMP1")<>0
		lRetorno := Empty(SCK->CK_NUMOP)
	EndIf
EndIf

If !lRetorno 
	lRetAux := lRetorno
EndIf

Return(lRetorno)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁMaNoDelOrc  Ё Autor ЁMarco Bianchi        Ё Data Ё03/01/2007Ё╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁVerifica se Orcamento de Venda pode ser Deletado.           Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё ExpC1 := MaCanDelOrc()                                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpC1: Alias do SCJ                                        Ё╠╠
╠╠Ё          Ё ExpC2: Alias do SCK                                        Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё ExpL1 -> Indica se o item de Orcamento pode ser deletado   Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁObservacaoЁ O SCJ deve estar posicionado e a funcao deve ser processadaЁ╠╠
╠╠Ё          Ё item a item do SCK.                                        Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё MATA415                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Static Function MaNoDelOrc()

Local lRetorno := .T.
Local cPedido := ""

If Select("TMP1")<>0
	lRetorno := Empty(CK_NUMOP)
	If ( SCJ->CJ_STATUS == "B" .And. lRetorno)
		If !Empty(CK_NUMPV)
			cPedido := CK_NUMPV
			dbSelectArea("SC5")
			dbSetOrder(1)
			If MsSeek(xFilial("SC5")+cPedido)
				lRetorno := .F.	
			EndIf
		EndIf
	EndIf
EndIf

If !lRetorno 
	lRetAux := lRetorno
EndIf

Return(lRetorno)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁMaCanDelOrc Ё Autor ЁEduardo Riera        Ё Data Ё22/07/2000Ё╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁVerifica se Orcamento de Venda pode ser Deletado.           Ё╠╠
╠╠Ё          ЁEsta funcao e uilizada pelo fonte FATA300.                  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё ExpC1 := MaCanDelOrc()                                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpC1: Alias do SCJ                                        Ё╠╠
╠╠Ё          Ё ExpC2: Alias do SCK                                        Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё ExpL1 -> Indica se o item de Orcamento pode ser deletado   Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁObservacaoЁ O SCJ deve estar posicionado e a funcao deve ser processadaЁ╠╠
╠╠Ё          Ё item a item do SCK.                                        Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё MATA415                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/

Function MaCanDelOrc(cAliasSCJ,cAliasSCK)
Local lRetorno := .T.
If ( cAliasSCK <> Nil )
	lRetorno := Empty((cAliasSCK)->CK_NUMOP)
	If ( (cAliasSCJ)->CJ_STATUS == "B" .And. lRetorno)
		If !Empty((cAliasSCK)->CK_NUMPV)
			dbSelectArea("SC5")
			dbSetOrder(1)
			If MsSeek(xFilial("SC5")+(cAliasSCK)->CK_NUMPV)
				lRetorno := .F.	
			EndIf
		EndIf
	EndIf
EndIf

Return(lRetorno)
/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁMaAvalOrc   Ё Autor ЁEduardo Riera        Ё Data Ё04/10/2000Ё╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁRotina de avaliacao dos eventos de um orcamento             Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё ExpC1 := MaCanDelOrc()                                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpC1: Alias do SCJ ou SCK                                 Ё╠╠
╠╠Ё          Ё ExpN2: Evento que deve ser avaliado                        Ё╠╠
╠╠Ё          Ё        [01] Implantacao do item do Orcamento de Venda (SCK)Ё╠╠
╠╠Ё          Ё        [02] Estorno do item do Orcamento de Venda (SCK)    Ё╠╠
╠╠Ё          Ё        [03] Baixa do item do Orcamento de Venda (SCK)      Ё╠╠
╠╠Ё          Ё        [04] Cancelamento do Orcamento de venda ( SCK )     Ё╠╠
╠╠Ё          Ё        [11] Implantacao de um orcamento de venda ( SCJ )   Ё╠╠
╠╠Ё          Ё        [12] Estorno de um orcamento de venda ( SCJ )       Ё╠╠
╠╠Ё          Ё        [13] Baixa do Orcamento de venda                    Ё╠╠
╠╠Ё          Ё        [14] Cancelamento do Orcamento de venda             Ё╠╠
╠╠Ё          Ё ExpC3: Status padrao para o orcamento(Default ABERTO)      Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё ExpL1 -> Indica se o item de Orcamento pode ser deletado   Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁObservacaoЁ O SCJ deve estar posicionado, pois sobre atualizacao, o SCKЁ╠╠
╠╠Ё          Ёnao sofre atualizacao mas deve estar posicionado tb.        Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё MATA415                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function MaAvalOrc(cAlias,nEvento,cStatus)

Local aArea := GetArea()

DEFAULT cStatus := "A"

Do Case
Case nEvento == 01
	//зддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁAtualiza os dados Complementares             Ё
	//юддддддддддддддддддддддддддддддддддддддддддддды
	cStatus := IIf((cAlias)->CK_ITEM=="01","A",cStatus)
	If ( (cAlias)->CK_VALOR == 0 )
		cStatus := "D" //Nao Orcado
	EndIf
	
	dbSelectArea("SCJ")
	RecLock("SCJ",.F.)
	SCJ->CJ_STATUS := cStatus
	MsUnlock()	
	
	//зддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁAtualiza os dados do contrato de parceria    Ё
	//юддддддддддддддддддддддддддддддддддддддддддддды	
	If !Empty(SCK->CK_CONTRAT) .And. !Empty(SCK->CK_ITEMCON)
		dbSelectArea("ADB")
		dbSetOrder(1)
		If MsSeek(xFilial("ADB")+SCK->CK_CONTRAT+SCK->CK_ITEMCON)
			RecLock("ADB")
			ADB->ADB_QTDEMP += SCK->CK_QTDVEN
			If ADB->ADB_QTDEMP > ADB->ADB_QUANT
				ADB->ADB_QTDEMP -= SCK->CK_QTDVEN
				RecLock("SCK")
				SCK->CK_CONTRAT := ""
				SCK->CK_ITEMCON := ""
				MsUnLock()
				dbSelectArea("ADB")
			EndIf
			MsUnLock()
		EndIf
	EndIf
Case nEvento == 02
	//зддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁAtualiza os dados Complementares             Ё
	//юддддддддддддддддддддддддддддддддддддддддддддды
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁNeogrid - Verifica a existencia da Administracao colaborativa de PedidosЁ
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If ( !Empty((cAlias)->CK_COTCLI) .And. NeoEnable("001"))
		NeoEnvOrc((cAlias)->CK_NUM)
	EndIf
	//зддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁAtualiza os dados do contrato de parceria    Ё
	//юддддддддддддддддддддддддддддддддддддддддддддды	
	If !Empty(SCK->CK_CONTRAT) .And. !Empty(SCK->CK_ITEMCON)
		dbSelectArea("ADB")
		dbSetOrder(1)
		If MsSeek(xFilial("ADB")+SCK->CK_CONTRAT+SCK->CK_ITEMCON)
			RecLock("ADB")
			ADB->ADB_QTDEMP -= SCK->CK_QTDVEN
			MsUnLock()
		EndIf
	EndIf
Case nEvento == 03
	//зддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁAtualiza os dados Complementares             Ё
	//юддддддддддддддддддддддддддддддддддддддддддддды
	cStatus := "B" //Baixado
	//зддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁAtualiza os dados do contrato de parceria    Ё
	//юддддддддддддддддддддддддддддддддддддддддддддды	
	If !Empty(SCK->CK_CONTRAT) .And. !Empty(SCK->CK_ITEMCON)
		dbSelectArea("ADB")
		dbSetOrder(1)
		If MsSeek(xFilial("ADB")+SCK->CK_CONTRAT+SCK->CK_ITEMCON)
			RecLock("ADB")
			ADB->ADB_QTDEMP -= SCK->CK_QTDVEN
			MsUnLock()
		EndIf
	EndIf
Case nEvento == 04
	//зддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁAtualiza os dados Complementares             Ё
	//юддддддддддддддддддддддддддддддддддддддддддддды
	//зддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁAtualiza os dados do contrato de parceria    Ё
	//юддддддддддддддддддддддддддддддддддддддддддддды	
	If !Empty(SCK->CK_CONTRAT) .And. !Empty(SCK->CK_ITEMCON)
		dbSelectArea("ADB")
		dbSetOrder(1)
		If MsSeek(xFilial("ADB")+SCK->CK_CONTRAT+SCK->CK_ITEMCON)
			RecLock("ADB")
			ADB->ADB_QTDEMP -= SCK->CK_QTDVEN
			MsUnLock()
		EndIf
	EndIf	
Case nEvento == 11
	//зддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁAtualiza os dados Complementares             Ё
	//юддддддддддддддддддддддддддддддддддддддддддддды
	cStatus := IIf(Empty((cAlias)->CJ_CLIENTE) .Or. Empty((cAlias)->CJ_CONDPAG),"D",cStatus)
	(cAlias)->CJ_STATUS := cStatus
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁNeogrid - Verifica a existencia da Administracao colaborativa de PedidosЁ
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If ( cStatus == "A" )
		If ( !Empty((cAlias)->CJ_COTCLI) .And. NeoEnable("001"))
			NeoEnvOrc((cAlias)->CJ_NUM)
		EndIf
	EndIf
Case nEvento == 12
	//зддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁAtualiza os dados Complementares             Ё
	//юддддддддддддддддддддддддддддддддддддддддддддды
Case nEvento == 13
	//зддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁAtualiza os dados Complementares             Ё
	//юддддддддддддддддддддддддддддддддддддддддддддды
	(cAlias)->CJ_STATUS := cStatus
Case nEvento == 14
	//зддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁAtualiza os dados Complementares             Ё
	//юддддддддддддддддддддддддддддддддддддддддддддды
	cStatus := "C"
	(cAlias)->CJ_STATUS := cStatus
	If ( ExistBlock("A415CANC") )
		ExecBlock("A415CANC",.F.,.F.,{cAlias})
	EndIf
EndCase
RestArea(aArea)
Return(.T.)
/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁMa415Auto   Ё Autor ЁEduardo Riera        Ё Data Ё19/10/2000Ё╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁRotina de avaliacao do cancelamento dos Orcamentos por      Ё╠╠
╠╠Ё          Ёvencimento de validade.                                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё ExpC1 := Ma415Auto()                                       Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ Nenhum                                                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё Nenhum                                                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁObservacaoЁEsta rotina efetua o cancelamento automatico dos orcamento  Ё╠╠
╠╠Ё          Ёapos o vencimento da validade do mesmo.                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё MATA415                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function Ma415Auto()

Local aArea      	:= GetArea()
Local lEnd       	:= .F.
Local lContinua 	:= GetMv("MV_ORCVLD") .And. __cInterNet <> "AUTOMATICO"
If ( lContinua )
	If ( GetMv("MV_ORCUVLD") >= dDataBase )
		lContinua := .F.
	EndIf
	GetMv("MV_ORCVLD")
	dbSelectArea("SX6")
	lContinua := MsRLock()
	If ( lContinua )
		Processa({|lEnd| Ma415PAuto(@lEnd)},STR0036,OemToAnsi(STR0037),.F.)		//"Cancelamento de Orcamentos vencidos"###"Cancelando orcamentos..."
		dbSelectArea("SX6")
		GetMv("MV_ORCVLD")
		MsRUnLock()
	EndIf
EndIf
RestArea(aArea)
Return(.T.)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁMa415PAuto  Ё Autor ЁEduardo Riera        Ё Data Ё19/10/2000Ё╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁRotina de avaliacao do cancelamento dos Orcamentos por      Ё╠╠
╠╠Ё          Ёvencimento de validade (Processamento)                      Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё ExpC1 := Ma415Auto()                                       Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ Nenhum                                                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё Nenhum                                                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁObservacaoЁEsta rotina efetua o cancelamento automatico dos orcamento  Ё╠╠
╠╠Ё          Ёapos o vencimento da validade do mesmo.                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё MATA415                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Static Function Ma415PAuto(lEnd)

Local aArea     := GetArea()
Local aAreaSCJ  := SCJ->(GetArea())
Local aAreaSCK  := SCK->(GetArea())
Local aStruSCK
Local cAlias    := "SCJ"
Local cAliasSCK := ""
Local cQuery    := ""
Local lQuery    := .T.
Local nX        := 0
ProcRegua(LastRec())
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁAtualiza a data de ultima checagem                      Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддды
GetMv("MV_ORCUVLD")
dbSelectArea("SX6")
If ( Found() )
	PutMv("MV_ORCUVLD",Dtos(dDataBase))
EndIf
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁPesquisa os Orcamentos em aberto                        Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддды
#IFDEF TOP
	If ( TcSrvType()<>"AS/400" )
		lQuery := .T.
		cQuery := "MA415PAUTO"
		cQuery := "SELECT CJ_FILIAL,CJ_NUM,R_E_C_N_O_ SCJRECNO "
		cQuery += "FROM "+RetSqlName("SCJ")+" SCJ "
		cQuery += "SCJ.CJ_FILIAL='"+xFilial("SCJ")+"' AND "
		cQuery += "SCJ.CJ_VALIDA<'"+Dtos(dDataBase)+"' AND "
		cQuery += "SCJ.CJ_STATUS NOT IN('B','C') AND "
		cQuery += "SCJ.D_E_L_E_T_=' '"
		cQuery := ChangeQuery(cQuery)
		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAlias,.T.,.T.)
	Else
#ENDIF	
	dbSelectArea("SCJ")
	dbSetOrder(1)
	dbSeek(xFilial("SCJ"))
	#IFDEF TOP
	EndIf
	#ENDIF	
While ( !Eof() .And. xFilial("SCJ") == (cAlias)->CJ_FILIAL )
	If ( dDataBase > (cAlias)->CJ_VALIDA .And. (cAlias)->CJ_STATUS<>"B#C" )
		If ( lQuery )
			SCJ->(MsGoto((cAlias)->SCJRECNO))
		EndIf
		Begin Transaction
			dbSelectArea("SCK")
			dbSetOrder(1)
			If ( lQuery )
				DEFAULT aStruSCK  := SCK->(dbStruct())
				cAliasSCK := "MA415PAUTO2"
				cQuery := "SELECT * "
				cQuery += "FROM "+RetSqlName("SCK")+" SCK "
				cQuery += "SCK.CK_FILIAL='"+xFilial("SCK")+" AND "
				cQuery += "SCK.CK_NUM='"+SCJ->CJ_NUM+"' AND "
				cQuery += "SCK.D_E_L_E_T=' ' "
				cQuery += "ORDER BY "+SqlOrder(SCJ->(IndexKey()))
				cQuery := ChangeQuery(cQuery)
				dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSCK,.T.,.T.)
				For nX := 1 To Len(aStruSCK)
					If ( aStruSCK[nX][2] <> "C" )
						TcSetField(cAlias,aStruSCK[nX][1],aStruSCK[nX][2],aStruSCK[nX][3],aStruSCK[nX][4])
					EndIf
				Next nX
			Else
				MsSeek(xFilial("SCK")+SCJ->CJ_NUM)
			EndIf
			While ( !Eof() .And. (cAliasSCK)->CK_FILIAL==xFilial("SCK") .And. ;
					(cAliasSCK)==SCJ->CJ_NUM )
				MaAvalOrc(cAliasSCK,4)
				dbSelectArea(cAliasSCK)
				dbSkip()
			EndDo
			If ( lQuery )
				dbSelectArea(cAliasSCK)
				dbCloseArea()
				dbSelectArea("SCK")
			EndIf
			MaAvalOrc("SCJ",14)
		End Transaction
	EndIf
	dbSelectArea(cAlias)
	dbSkip()
	If lEnd
		Exit
	EndIf
EndDo
If ( lQuery )
	dbSelectArea(cAlias)
	dbCloseArea()
	dbSelectArea("SCJ")
EndIf
RestArea(aAreaSCJ)
RestArea(aAreaSCK)
RestArea(aArea)
Return(.T.)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁA415VldTOk  Ё Autor ЁEduardo Riera        Ё Data Ё01/06/2001Ё╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁRotina de validacao da tudoOk da Enchoice                   Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ Nenhum                                                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё ExpL1: Dados validos?                                      Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁObservacaoЁEsta rotina efetua a validacao da TudoOk da Enchoice        Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё MATA415                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function A415VldTOk(lBloqueia)

Local lRet      := .T.
Local aProdDesc := {}
Local aAreaTMP  := TMP1->( GetArea() ) 
Local lGrvBloq	:= SuperGetMV("MV_FATBLOR",,.T.)		// "Permite gravacao mesmo com bloqueio de regra"

Default lBloqueia	:= .F.

TMP1->( dbGoTop() )
While TMP1->( !Eof() )

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Inclui os produtos para validacao                      Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддды

	nDescon  := 0
	
	cProduto := TMP1->CK_PRODUTO
	cItem    := TMP1->CK_ITEM
	nDescon  := (100 - (TMP1->CK_PRCVEN / TMP1->CK_PRUNIT) * 100 )

	//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Inclui apenas se nao existir                           Ё
	//Ё       Estrutura do array aProdDesc                                          Ё			
	//Ё       [1] - Codigo do Produto                                               Ё
	//Ё       [2] - Item do Pedido de Venda                                         Ё
	//Ё       [3] - Preco de Venda                                                  Ё
	//Ё       [4] - Preco de Lista                                                  Ё						
	//Ё       [5] - % do Desconto Concedido no item do pedido                       Ё
	//Ё       [6] - % do Desconto Permitido pela regra (FtRegraNeg)                 Ё
	//Ё       [7] - Indica se sera necessario verificar o saldo de verba            Ё
	//Ё                             01 - Bloqueio de regra de negocio               Ё
	//Ё                             02 - Bloqueio para verificacao de verba         Ё
	//Ё       [8] - Valor a ser abatido da verba caso seja aprovada (FtVerbaVen)    Ё			
	//Ё       [9] - Flag que indica se o item sera analisado nas regras             Ё							
	//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	AAdd( aProdDesc, {cProduto,cItem,TMP1->CK_PRCVEN,TMP1->CK_PRUNIT,nDescon,0,"",0,.T.} )
	
	TMP1->(dbSkip())
	
Enddo
                                        
RestArea( aAreaTMP )

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Verifica Regra e nao analisa verba (ultimo parametro .F.)    Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
lRet := FtRegraNeg(M->CJ_CLIENTE,M->CJ_LOJA,M->CJ_TABELA,M->CJ_CONDPAG,,@aProdDesc, !lGrvBloq, .F. )

lBloqueia := !lRet

If !lRet .AND. lGrvBloq
	lRet := .T.
EndIf

Return( lRet )                


/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁA415Bonus Ё Autor ЁEduardo Riera          Ё Data Ё15.05.2001Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁRotina de tratamento da regra de bonificacao para interface Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁExpN1: Tipo de operacao                                     Ё╠╠
╠╠Ё          Ё       [1] Inclusao do bonus                                Ё╠╠
╠╠Ё          Ё       [2] Exclusao do bonus                                Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁNenhum                                                      Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁDescri┤└o ЁEsta rotina tem como objetivo avaliar a regra de bonificacaoЁ╠╠
╠╠Ё          Ёe adicionar na respectiva interface                         Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁObservacaoЁ                                                            Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё Materiais/Distribuicao/Logistica                           Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Static Function A415Bonus(nTipo)

Local aArea     := GetArea()
Local aBonus    := {}
Local nX        := 0
Local nY        := 0
Local nZ        := 0
Local nW		:= 0
Local cItem     := ""
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁVerifica os bonus                                       Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If nTipo == 1
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁVerifica os bonus por item de venda                     Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	aBonus   := FtRgrBonus("TMP1",{"CK_PRODUTO","CK_QTDVEN","CK_TES"},M->CJ_CLIENTE,M->CJ_LOJA,M->CJ_TABELA,M->CJ_CONDPAG)
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁRecupera os bonus ja existentes                         Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддды	
	aBonus   := FtRecBonus("TMP1",{"CK_PRODUTO","CK_QTDVEN","CK_TES","CK_FLAG"},aBonus)
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁGrava os novos bonus                                    Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддды		
	nY := Len(aBonus)
	If nY > 0
		nZ := TMP1->(RecNo())
		TMP1->(dbGoBottom())
		cItem := TMP1->CK_ITEM
		TMP1->(dbGoto(nZ))
		For nX := 1 To nY
			cItem := Soma1(cItem)
			RecLock("TMP1",.T.)
			For nW := 1 To Len(aHeader)
				FieldPut(nW,CriaVar(aHeader[nW,2],.T.))
			Next nW
			TMP1->CK_ITEM    := cItem
			TMP1->CK_PRODUTO := aBonus[nX][1]
			A415Prod(TMP1->CK_PRODUTO)
			TMP1->CK_QTDVEN  := aBonus[nX][2]
			TMP1->CK_TES     := aBonus[nX][3]
			If ( TMP1->CK_PRCVEN == 0 )
				TMP1->CK_PRCVEN := 1
				TMP1->CK_VALOR  := TMP1->CK_QTDVEN
			Else
				TMP1->CK_VALOR := A410Arred(TMP1->CK_QTDVEN*TMP1->CK_PRCVEN,"CK_VALOR")
			EndIf
			MsUnLock()
		Next nX
	EndIf
Else
	FtDelBonus("TMP1",{"CK_PRODUTO","CK_QTDVEN","CK_TES","CK_FLAG"})	
EndIf
RestArea(aArea)
Return(.T.)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁMA415VldFl  Ё Autor ЁEduardo Riera        Ё Data Ё01/06/2001Ё╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁRotina de validacao das filiais de entrega/Venda            Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ Nenhum                                                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё ExpL1: Dados validos?                                      Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁObservacaoЁEsta rotina efetua a validacao das filiais de entrega/Venda Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё MATA415                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function Ma415VldFl()

Local lRetorno := .F.

lRetorno := Empty(xFilial("SCJ")+xFilial("SF4")+xFilial("SB1")+xFilial("SE4"))
lRetorno := lRetorno .And. Empty(xFilial("DA0")+xFilial("SBM"))

Return(lRetorno)


/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁMa415TrackЁ Autor Ё Sergio Silveira       Ё Data Ё18/12/2001Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Faz o tratamento da chamada do System Tracker              Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё .T.                                                        Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ Nenhum                                                     Ё╠╠
╠╠цддддддддддедддддддддддддддбдддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё   DATA   Ё Programador   ЁManutencao Efetuada                         Ё╠╠
╠╠цддддддддддедддддддддддддддедддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё          Ё               Ё                                            Ё╠╠
╠╠юддддддддддадддддддддддддддадддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Static Function A415Track()

Local aEnt     := {}
Local cOrc     := M->CJ_NUM
Local nRecTMP1 := TMP1->( Recno() )

//зддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Percorre os itens do orcamento              Ё
//юддддддддддддддддддддддддддддддддддддддддддддды

TMP1->( dbGotop() )

While !TMP1->( Eof() )
	AAdd( aEnt, { "SCK", cOrc + TMP1->CK_ITEM } )
	TMP1->( dbSkip() )
EndDo 	

MaTrkShow( aEnt )

TMP1->( dbGoto( nRecTMP1 ) )

Return( .T. )

/*
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбддддддддддд©╠╠
╠╠ЁFun┤└o    ЁA415LegendЁAutor  Ё Sergio Silveira       Ё Data Ё26/06/2003 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддаддддддддддд╢╠╠
╠╠Ё          ЁDemonstra a legenda das cores da mbrowse                     Ё╠╠
╠╠Ё          Ё                                                             Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁNenhum                                                       Ё╠╠
╠╠Ё          Ё                                                             Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁNenhum                                                       Ё╠╠
╠╠Ё          Ё                                                             Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁDescri┤└o ЁEsta rotina monta uma dialog com a descricao das cores da    Ё╠╠
╠╠Ё          ЁMbrowse.                                                     Ё╠╠
╠╠Ё          Ё                                                             Ё╠╠
╠╠Ё          Ё                                                             Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё Materiais                                                   Ё╠╠
╠╠юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Function A415Legend()

Local aCores := {	{ 'ENABLE'    , STR0052 },; //'Orcamento em Aberto'
					{ 'DISABLE'   , STR0053 },; //'Orcamento Baixado'
					{ 'BR_PRETO'  , STR0054 },; //'Orcamento Cancelado' 	
					{ 'BR_AMARELO', STR0055 },; //'Orcamento nao Orcado'
					{ 'BR_AZUL'   , STR0056 },; //'Orcamento aprovado'
					{ 'BR_MARROM' , STR0078 }}  //'Orcamento bloqueado'
					
//зддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Ponto de Entrada para alterar cores da legenda    Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддды
If ExistBlock("MA415LEG")
	aCores := ExecBlock("MA415LEG",.F.,.F.,aCores)
Endif
					
BrwLegenda(cCadastro,STR0057,aCores )   //Pedido Liberado
	
Return(.T.)

/*/
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбддддддддддд©╠╠
╠╠ЁFun┤└o    ЁMyMata415 Ё Autor Ё Eduardo Riera         Ё Data Ё10.10.2003 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддаддддддддддд╢╠╠
╠╠Ё          ЁRotina de teste da rotina automatica do programa MATA415     Ё╠╠
╠╠Ё          Ё                                                             Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁNenhum                                                       Ё╠╠
╠╠Ё          Ё                                                             Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁNenhum                                                       Ё╠╠
╠╠Ё          Ё                                                             Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁDescri┤└o ЁEsta rotina tem como objetivo efetuar testes na rotina de    Ё╠╠
╠╠Ё          Ёorcamento de venda                                           Ё╠╠
╠╠Ё          Ё                                                             Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё Materiais                                                   Ё╠╠
╠╠юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function MyMata415()

Local aCabec := {}
Local aItens := {}
Local aLinha := {}
Local nX     := 0
Local nY     := 0
Local cDoc   := ""
Local lOk    := .T.
PRIVATE lMsErroAuto := .F.
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//| Abertura do ambiente                                         |
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
ConOut(Repl("-",80))
ConOut(PadC("Teste de Inclusao de 10 orcamentos de venda  com 30 itens cada",80))
PREPARE ENVIRONMENT EMPRESA "99" FILIAL "01" MODULO "FAT" TABLES "SC5","SC6","SA1","SA2","SB1","SB2","SF4","SCJ","SCK","SCL"
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//| Verificacao do ambiente para teste                           |
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
dbSelectArea("SB1")
dbSetOrder(1)
If !SB1->(MsSeek(xFilial("SB1")+"PA001"))
	lOk := .F.
	ConOut("Cadastrar produto: PA001")
EndIf
dbSelectArea("SF4")
dbSetOrder(1)
If !SF4->(MsSeek(xFilial("SF4")+"501"))
	lOk := .F.
	ConOut("Cadastrar TES: 501")
EndIf
dbSelectArea("SE4")
dbSetOrder(1)
If !SE4->(MsSeek(xFilial("SE4")+"001"))
	lOk := .F.
	ConOut("Cadastrar condicao de pagamento: 001")
EndIf
If !SB1->(MsSeek(xFilial("SB1")+"PA002"))
	lOk := .F.
	ConOut("Cadastrar produto: PA002")
EndIf
dbSelectArea("SA1")
dbSetOrder(1)
If !SA1->(MsSeek(xFilial("SA1")+"CL000101"))
	lOk := .F.
	ConOut("Cadastrar cliente: CL000101")
EndIf
If lOk
	ConOut("Inicio: "+Time())
	For nY := 1 To 1
		cDoc := GetSxeNum("SCJ","CJ_NUM")
		RollBAckSx8()
		aCabec := {}
		aItens := {}
		aadd(aCabec,{"CJ_NUM"   ,cDoc,Nil})
		aadd(aCabec,{"CJ_CLIENTE",SA1->A1_COD,Nil})
		aadd(aCabec,{"CJ_LOJACLI",SA1->A1_LOJA,Nil})
		aadd(aCabec,{"CJ_LOJAENT",SA1->A1_LOJA,Nil})
		aadd(aCabec,{"CJ_CONDPAG",SE4->E4_CODIGO,Nil})
		For nX := 1 To 1
			aLinha := {}
			aadd(aLinha,{"CK_ITEM",StrZero(nX,2),Nil})
			aadd(aLinha,{"CK_PRODUTO",SB1->B1_COD,Nil})
			aadd(aLinha,{"CK_QTDVEN",1,Nil})
			aadd(aLinha,{"CK_PRCVEN",100,Nil})
			aadd(aLinha,{"CK_PRUNIT",100,Nil})			
			aadd(aLinha,{"CK_VALOR",100,Nil})
			aadd(aLinha,{"CK_TES","501",Nil})
			aadd(aItens,aLinha)
		Next nX
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//| Teste de Inclusao                                            |
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		MATA415(aCabec,aItens,3)
		If !lMsErroAuto
			ConOut("Incluido com sucesso! "+cDoc)	
		Else
			ConOut("Erro na inclusao!")
		EndIf
	Next nY
	ConOut("Fim  : "+Time())
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//| Teste de alteracao                                           |
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	aCabec := {}
	aItens := {}
	aadd(aCabec,{"CJ_NUM",cDoc,Nil})

	aLinha := {}
	aadd(aLinha,{"LINPOS","CK_ITEM","01"})
	aadd(aLinha,{"AUTDELETA","S",Nil})
	aadd(aItens,aLinha)

	For nX := 2 To 2
		aLinha := {}
		aadd(aLinha,{"CK_ITEM",StrZero(nX,2),Nil})
		aadd(aLinha,{"CK_PRODUTO",SB1->B1_COD,Nil})
		aadd(aLinha,{"CK_QTDVEN",2,Nil})
		aadd(aLinha,{"CK_PRCVEN",100,Nil})
		aadd(aLinha,{"CK_PRUNIT",100,Nil})			
		aadd(aLinha,{"CK_VALOR",200,Nil})
		aadd(aLinha,{"CK_TES","501",Nil})
		aadd(aItens,aLinha)
	Next nX	
	ConOut(PadC("Teste de alteracao",80))
	ConOut("Inicio: "+Time())
	MATA415(aCabec,aItens,4)
	ConOut("Fim  : "+Time())
	ConOut(Repl("-",80))
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//| Teste de Exclusao                                            |
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	ConOut(PadC("Teste de exclusao",80))
	ConOut("Inicio: "+Time())
	MATA415(aCabec,aItens,5)
	If !lMsErroAuto
		ConOut("Exclusao com sucesso! "+cDoc)	
	Else
		ConOut("Erro na exclusao!")
	EndIf
	ConOut("Fim  : "+Time())
	ConOut(Repl("-",80))
EndIf
RESET ENVIRONMENT
Return(.T.)


//зддддддддддддддддддддддддддддддддддддддддддддд©
//ЁAnalise da Rentabilidade - Valor Presente    Ё
//юддддддддддддддддддддддддддддддддддддддддддддды
Static Function a415RentVP( aRenTab ,aVencto )
Local nItem := 0
Local nY    := 0
Local nX    := 0

If len(aRenTab) > 0 .AND. (aRentab[Len(aRentab)][1] == "")
	aSize( aRentab ,Len(aRentab)-1)
	For nX := 1 To Len(aRentab)
		aRentab[nX][2] := val(StrTran(StrTran(aRentab[nX][2],".",""),",","."))
		aRentab[nX][3] := val(StrTran(StrTran(aRentab[nX][3],".",""),",","."))
		aRentab[nX][4] := 0
		aRentab[nX][5] := 0
		aRentab[nX][6] := 0
	Next nX
EndIf

dbSelectArea("TMP1")
dbGotop()
While ( !Eof() )
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Verifica se a linha foi deletada                     Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If ( !TMP1->CK_FLAG .And. !Empty(TMP1->CK_PRODUTO) )
		nItem++
		nY := aScan(aRentab,{|x| x[1] == TMP1->CK_PRODUTO})
		If nY <> 0
			aRentab[nY][4] += Max(Ma410Custo(nItem,aVencto,TMP1->CK_TES,TMP1->CK_PRODUTO,TMP1->CK_LOCAL,TMP1->CK_QTDVEN),0)
			aRentab[nY][5] := Max(aRentab[nY][4]-aRentab[nY][3],0)
			aRentab[nY][6] := aRentab[nY][5]/aRentab[nY][4]*100
		EndIf
	EndIf
	dbSelectArea("TMP1")
	dbSkip()	
EndDo
aadd(aRentab,{"",0,0,0,0,0})
For nX := 1 To Len(aRentab)
	If nX <> Len(aRentab)
		aRentab[Len(aRentab)][2] += aRentab[nX][2]
		aRentab[Len(aRentab)][3] += aRentab[nX][3]
		aRentab[Len(aRentab)][4] += aRentab[nX][4]
		aRentab[Len(aRentab)][5] += aRentab[nX][5]
		aRentab[Len(aRentab)][6] := aRentab[Len(aRentab)][5]/aRentab[Len(aRentab)][4]*100
	EndIf	
	aRentab[nX][2] := TransForm(aRentab[nX][2],"@e 999,999,999.999999")
	aRentab[nX][3] := TransForm(aRentab[nX][3],"@e 999,999,999.999999")
	aRentab[nX][4] := TransForm(aRentab[nX][4],"@e 999,999,999.999999")
	aRentab[nX][5] := TransForm(aRentab[nX][5],"@e 999,999,999.999999")
	aRentab[nX][6] := TransForm(aRentab[nX][6],"@e 999,999,999.999999")
Next nX
If Existblock("MA415RVP")
	aRentab := ExecBlock("MA415RVP",.F.,.F.,aRentab)
EndIf
Return( .T. )


/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    Ёa415PCpy  Ё Autor Ё Eduardo Riera         Ё Data Ё09/12/2005Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Prepara a funcao de copia para evitar que seja chamada a   Ё╠╠
╠╠Ё          Ё janela de filiais                                          Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁExpC1: Alias do cabecalho do orcamento de venda             Ё╠╠
╠╠Ё          ЁExpN2: Recno do cabecalho do orcamento de venda             Ё╠╠
╠╠Ё          ЁExpN3: Opcao do arotina                                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё Nenhum                                                     Ё╠╠
╠╠цддддддддддедддддддддддддддбдддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё   DATA   Ё Programador   ЁManutencao Efetuada                         Ё╠╠
╠╠цддддддддддедддддддддддддддедддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё          Ё               Ё                                            Ё╠╠
╠╠юддддддддддадддддддддддддддадддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Function a415PCpy(cAlias,nReg,nOpc)

Local aRotBkp := aClone(aRotina)                  


Private aRotina		:= { {STR0002,"AxPesqui"  , 0 , 1 },; 	//"Pesquisar"
	{STR0003,"A415Visual", 0 , 2 },; 						//"Visualizar"
	{STR0004,"A415Copia", 0 , 3  }}							//"Incluir"
	

a415Copia(calias,nReg,3)

aRotina := aClone(aRotBkp)

Return(.T.)

/*
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤└o    ЁA415VldPrjЁ Autor Ё Reynaldo Miyashita    Ё Data Ё 03.02.2006╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤└o ЁValidacao do codigo da tarefa digitada.                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      ЁMATA415                                                     Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Function A415VldPrj()
Local lRet	:= .F.
Local aArea		:= GetArea()
Local aAreaAF8	:= AF8->(GetArea())
Local aAreaAFC	:= AFC->(GetArea())
Local aAreaAF9	:= AF9->(GetArea())
Local cContVar	:=	&(ReadVar())

AF8->(dbSetOrder(1))
If TMP1->(FieldPos("CK_PROJPMS"))>0 .AND. AF8->(dbSeek(xFilial()+TMP1->CK_PROJPMS))
	If AllTrim(ReadVar())=="M->CK_TASKPMS"
		AF9->(dbSetOrder(1))
		If AF9->(dbSeek(xFilial()+AF8->AF8_PROJET+AF8->AF8_REVISA+cContVar))
			// tarefa pode ser faturada
			If AF9->(AF9_FATURA) =="1" 
				lRet := .T.
				If TMP1->(FieldPos("CK_EDTPMS" )) > 0
					TMP1->CK_EDTPMS := SPACE(LEN(AFC->AFC_EDT))
				EndIf
			Else                          				
				HELP("   ",1,"VLDTSKRFAT")
			EndIf
		Else
			HELP("   ",1,"REGNOIS")
		EndIf             
	Else
		AFC->(dbSetOrder(1))
		If AFC->(dbSeek(xFilial()+AF8->AF8_PROJET+AF8->AF8_REVISA+cContVar))
			// EDT pode ser faturada
			If AFC->(AFC_FATURA) =="1" 
				lRet := .T.
				If TMP1->(FieldPos("CK_TASKPMS")) > 0 
					TMP1->CK_TASKPMS := SPACE(LEN(AF9->AF9_TAREFA))
				EndIf
			Else
				HELP("   ",1,"VLDEDTFAT")
			EndIf
		Else
			HELP("   ",1,"REGNOIS")
		EndIf
	EndIf
Else
	HELP("   ",1,"REGNOIS")
EndIf

RestArea(aAreaAF9)
RestArea(aAreaAFC)
RestArea(aAreaAF8)
RestArea(aArea)
Return lRet

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    Ёa415VldPMSЁ Autor Ё Reynaldo Miyashita    Ё Data Ё03.06.2005Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁRotina para validar as colunas do browse de itens no pedido Ё╠╠
╠╠Ё          Ё de orcamebto. Colunas de Projeto, EDT e Tarefa referentes  Ё╠╠
╠╠Ё          Ё ao sigapms                                                 Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё lOk - Se as colunas do browse estao certas                 Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁNenhum                                                      Ё╠╠
╠╠юддддддддддадддддддддддддддадддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function a415VldPMS( cCK_PROJPMS ,cCK_EDTPMS ,cCK_TASKPMS )
Local lOk := .F.
Local aArea := GetArea()

// se foi informado o codigo do projeto
If ! Empty(cCK_PROJPMS)
    // verifica se projeto existe
    AF8->(dbSetOrder(1))
	If AF8->(dbSeek(xFilial("AF8")+cCK_PROJPMS ))
		// se foi informado EDT e Tarefa
		If ( !Empty(cCK_EDTPMS) .AND. !Empty(cCK_TASKPMS) )
			HELP("   ",1,"VLDPMSFAT",,STR0067 + CRLF + STR0068 ,1)
		Else
			// EDT preenchida
			If ! Empty(cCK_EDTPMS)
			    // verifica se a EDT existe 
		        AFC->(dbSetOrder(1))
				If AFC->(dbSeek(xFilial("AFC")+AF8->AF8_PROJET+AF8->AF8_REVISA+cCK_EDTPMS ))
					// a EDT И faturavel
					If AFC->AFC_FATURA == "1"
						lOk := .T.	
					Else
						HELP("   ",1,"VLDEDTFAT")
					EndIf
				Else
					HELP("   ",1,"VLDEDTEXIST",,STR0069 ,1)
				EndIf
			Else
				// Tarefa preenchida
				If ! Empty(cCK_TASKPMS)
				    // verifica se a TAREFA existe 
			        AF9->(dbSetOrder(1))
					If AF9->(dbSeek(xFilial("AF9")+AF8->AF8_PROJET+AF8->AF8_REVISA+cCK_TASKPMS ))
						// a TAREFA И faturavel
						If AF9->AF9_FATURA == "1"
							lOk := .T.	
						Else
							HELP("   ",1,"VLDTSKRFAT")
						EndIf
					Else
						HELP("   ",1,"VLDTSKEXIST",,STR0070 ,1)
					EndIf
					
				// EDT e Tarefa naum foram preenchidas
				Else
					lOk := .T.
				EndIf
			EndIf
		EndIf
	Else 
		HELP("   ",1,"VLDPRJEXIST",,STR0071 ,1)
	EndIf	
Else
	lOk := .T.
EndIf

RestArea( aArea )

Return( lOk )

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁPrograma  ЁMenuDef   Ё Autor Ё Marco Bianchi         Ё Data Ё01/09/2006Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Utilizacao de menu Funcional                               Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁArray com opcoes da rotina.                                 Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁParametros do array a Rotina:                               Ё╠╠
╠╠Ё          Ё1. Nome a aparecer no cabecalho                             Ё╠╠
╠╠Ё          Ё2. Nome da Rotina associada                                 Ё╠╠
╠╠Ё          Ё3. Reservado                                                Ё╠╠
╠╠Ё          Ё4. Tipo de Transa┤└o a ser efetuada:                        Ё╠╠
╠╠Ё          Ё	  1 - Pesquisa e Posiciona em um Banco de Dados           Ё╠╠
╠╠Ё          Ё    2 - Simplesmente Mostra os Campos                       Ё╠╠
╠╠Ё          Ё    3 - Inclui registros no Bancos de Dados                 Ё╠╠
╠╠Ё          Ё    4 - Altera o registro corrente                          Ё╠╠
╠╠Ё          Ё    5 - Remove o registro corrente do Banco de Dados        Ё╠╠
╠╠Ё          Ё5. Nivel de acesso                                          Ё╠╠
╠╠Ё          Ё6. Habilita Menu Funcional                                  Ё╠╠
╠╠цддддддддддедддддддддддддддбдддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё   DATA   Ё Programador   ЁManutencao efetuada                         Ё╠╠
╠╠цддддддддддедддддддддддддддедддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё          Ё               Ё                                            Ё╠╠
╠╠юддддддддддадддддддддддддддадддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/

Static Function MenuDef()
     
Private aRotina := {	{STR0002,"AxPesqui"  	, 0 , 1, 0,.F.},; 	//"Pesquisar"
							{STR0003,"A415Visual"	, 0 , 2, 0,NIL},; 	//"Visualizar"
							{STR0004,"A415Inclui"	, 0 , 3, 0,NIL},; 	//"Incluir"
							{STR0005,"A415Altera"	, 0 , 4, 0,NIL},; 	//"Alterar"
							{STR0038,"A415Exclui"	, 0 , 5, 0,NIL},;		//"Exclui"
							{STR0039,"A415Cancel" 	, 0 , 4, 0,NIL},;		//"Cancela"
							{STR0006,"A415Impri" 	, 0 , 2, 0,NIL},; 	//"impRimir"
							{STR0007,"A415PCpy"  	, 0 , 4, 0,NIL},;		//"Copiar"
							{STR0057,"A415Legend"	, 0 , 2, 0,.F.},;		//"Legenda"
							{STR0076,"MsDocument"	, 0 , 4, 0 ,NIL} }		//"Conhecimento"


If ExistBlock("MA415MNU")
	ExecBlock("MA415MNU",.F.,.F.)
EndIf

Return(aRotina)

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁMa415Opc  Ё Autor Ё Kleber Dias Gomes     Ё Data Ё09/01/2007Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Funcao de retorno do codigo do opcional.                   Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpL1 = Repete o mesmo grupo de opcional                   Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё Nenhum                                                     Ё╠╠
╠╠цддддддддддедддддддддддддддбдддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё   DATA   Ё Programador   ЁManutencao Efetuada                         Ё╠╠
╠╠цддддддддддедддддддддддддддедддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё          Ё               Ё                                            Ё╠╠
╠╠юддддддддддадддддддддддддддадддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Function Ma415Opc(lOpcPadrao)

Local cOpcional   := ""
Local aROpc       := {}
Local nI          := 0

Default lOpcPadrao:= GetNewPar("MV_REPGOPC","N") == "N"

If lOpcPadrao
	cOpcional := TMP1->CK_OPC
Else
	aROpc:=STR2Array(TMP1->CK_MOPC,.F.)
	If !Empty(aRopc)
		For nI := 1 To Len(aROpc)
			cOpcional += aROpc[nI,2]
		Next
	Endif
Endif

Return cOpcional   

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    Ёa415Tipo9 Ё Autor Ё Marcelo Alexandre     Ё Data Ё 23.04.07 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁValidacao da Condicao de Pagamento Tipo 9                   Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁExpL1: Logico                                               Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ                                                            Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддбдддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё   DATA   Ё Programador   ЁManutencao Efetuada                         Ё╠╠
╠╠цддддддддддедддддддддддддддедддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё          Ё               Ё                                            Ё╠╠
╠╠юддддддддддадддддддддддддддадддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function A415TIPO9()

Local aArea     := GetArea()
Local aAreaSE4  := SE4->(GetArea())

Local cParcela  := "123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ0"
Local cMv1Dup   := GetMV( "MV_1DUP" )
Local dParc     := Ctod("")
Local nParc     := 0
Local nAux      := 0
Local nTotal    := 0
Local nValor    := 0
Local nOrder    := IndexOrd() 
Local nY        := 0 
Local nX        := 0     
Local nMaxTipo9 := 26     
Local nParcelas := SuperGetMv("MV_NUMPARC")     
Local nRecBkp	:= 0

Local lRet      := .T.
Local lIPI      := (GetMV("MV_IPITP9") == "S")
Local lMt415Parc:= Existblock("MT415PC") 
Local lParc     := .T.             
                   
aPHelpPor	:=	{"Esse orГamento possui condiГЦo de paga-",;
				 "mento do Tipo 9, mas a soma das parce- ",;
				 "las nЦo batem com o total do orГamento."}
				 
aPHelpEng := aPHelpEsp := aPHelpPor	                 
PutHelp("PA415TIPO9",aPHelpPor,aPHelpEng,aPHelpEsp,.T.)
// Solucao
aPHelpPor	:= {"Verifique os campos de valor e data das ",;
              "parcelas do cabeГalho do orГamento. "}
aPHelpEng	:= aPHelpPor
aPHelpSpa	:= aPHelpPor	
PutHelp("SA415TIPO9",aPHelpPor,aPHelpEng,aPHelpEsp,.T.)

aPHelpPor	:=	{"Esse orГamento possui condiГЦo de paga-",;
				 "mento do Tipo 9 a soma das parcelas nЦo",;
				 "totalizam 100%."}
				 
aPHelpEng := aPHelpEsp := aPHelpPor	                 
PutHelp("PA415TIPO9P",aPHelpPor,aPHelpEng,aPHelpEsp,.T.)
// Solucao
aPHelpPor	:= {"Verifique os percentuais digitados nas ",;
                 "parcelas do cabeГalho do orГamento. "} 
aPHelpEng	:= aPHelpPor
aPHelpSpa	:= aPHelpPor	
PutHelp("SA415TIPO9P",aPHelpPor,aPHelpEng,aPHelpEsp,.T.)

If ( ExistBlock("M415TIP9") )
	lRet := ExecBlock("M415TIP9",.F.,.F.)
Else
	
	nRecBkp	:= TMP1->(RECNO())
	dbSelectArea("TMP1")
	dbGotop()
	While !Eof()

		//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Verifica se a linha foi deletada                     Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If ( !TMP1->CK_FLAG .And. !Empty(TMP1->CK_PRODUTO) )
			nValor := TMP1->CK_VALOR
		EndIf
		nTotal += nValor
		nValor := 0
		dbSkip()
	End

	nTotal := nTotal + M->CJ_FRETE + M->CJ_DESPESA + M->CJ_SEGURO + M->CJ_FRETAUT
	
	TMP1->(DBGOTO(nRecBkp))
	
	// permite que o numero de parcela possa se manipulado por customizaГЦo, independente do parametro
	If lMt415Parc
		nParcelas := Execblock("MT415PC",.F.,.F.)
	Endif
	

	For nX:=1 to nParcelas
		nParc := &("M->CJ_PARC"+Substr(cParcela,nx,1))
		dParc := &("M->CJ_DATA"+Substr(cParcela,nx,1))
		If nParc > 0 .And. Empty(dParc)
			lParc := .F.
		EndIf
		nAux  += nParc
	Next nX
	
	If !lParc
		Help(" ",1,"A415TIPO9")		
		lRet := .F.		
	Else	
		dbSelectArea("SE4")
		dbSetOrder(1)
		If MsSeek(xFilial()+M->CJ_CONDPAG)
			If SE4->E4_TIPO =="9"
				If AllTrim(SE4->E4_COND) = "0"
				
					If	( lIpi .And. NoRound(nTotal,2) > NoRound(nAux,2) ) .Or. ;
						( !lIpi .And. NoRound(nTotal,2) <> NoRound(nAux,2) )
						
						Help(" ",1,"A415TIPO9")
					
						If lRet
							If ( Type("l415Auto") == "U" .or. ! l415Auto )
								OpcQW:=MsgYesNo(OemToAnsi(STR0074),OemToAnsi(STR0075))  //"Confirma a Inclusao do Orcamento ?"###"Aten┤└o"
								If !OpcQW 				// Abandona
									lRet := .F.
								EndIf
							EndIf
						EndIf						
					EndIf
				ElseIf AllTrim(SE4->E4_COND) = "%" .And. nAux # 100
					Help(" ",1,"A415TIPO9P")
				
					If lRet
						If ( Type("l415Auto") == "U" .or. ! l415Auto )
							OpcQW:=MsgYesNo(OemToAnsi(STR0074),OemToAnsi(STR0075))  //"Confirma a Inclusao do Orcamento ?"###"Aten┤└o"
							If !OpcQW 				// Abandona
								lRet := .F.
							EndIf
						EndIf
					EndIf					
					
				EndIf
			EndIf
		EndIf
	EndIf	
Endif

RestArea(aAreaSE4)
RestArea(aArea)

Return lRet
/*/
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбддддддддддд©╠╠
╠╠ЁFun┤└o    ЁAjustaSIX Ё Autor Ё Cleverson             Ё Data Ё25/04/2008 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддаддддддддддд╢╠╠
╠╠Ё          ЁRotina para atualizar o arquivo de indice.                   Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁNenhum                                                       Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁNenhum                                                       Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁDescri┤└o ЁEsta rotina tem como objetivo efetuar as atualizacoes no     Ё╠╠
╠╠Ё          Ёarquivo de indice.                                           Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё Materiais                                                   Ё╠╠
╠╠юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/

Static Function AjustaSIX()

dbSelectArea('SIX')
dbSetOrder(1)
If dbSeek("SCJ3") .And. SIX->SHOWPESQ == "S" .And. Empty(SIX->F3)
	RecLock("SIX",.F.)
	SIX->F3 := "SA1"
	dbCommit()
	MsUnLock()
EndIf            

Return

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠иммммммммммяммммммммммкмммммммяммммммммммммммммммммкммммммяммммммммммммм╩╠╠
╠╠╨Programa  ЁMT415Pros ╨Autor  ЁVendas CRM          ╨ Data Ё  07/02/08   ╨╠╠
╠╠лммммммммммьммммммммммймммммммоммммммммммммммммммммйммммммоммммммммммммм╧╠╠
╠╠╨Desc.     Ё O prospect somente pode estar preenchido se o cliente for  ╨╠╠
╠╠╨          Ё padrao                                                     ╨╠╠
╠╠лммммммммммьмммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Uso       Ё AP                                                         ╨╠╠
╠╠хммммммммммомммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╪╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Function MT415Pros() 

Local aArea		:= GetArea()
Local aAreaSUS	:= SUS->(GetArea())
Local lRet		:= .T.
Local cCliente	:= SuperGetMv("MV_ORCLIPD")				//Se o cliente for padrao
Local cCodPro	:= ""									//Codigo do prospect
Local cLjPro	:= ""									//Loja do prospect
Local cCodCli	:= TamSx3("CJ_CLIENTE")[1]				//Codigo do cliente
Local cLojCli	:= TamSx3("CJ_LOJA")[1]					//Loja do cliente

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁCertifica que o indice atual eh US_FILIAL+US_COD+US_LOJAЁ
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддды
SUS->(DbSetOrder(1))

If SCJ->(FieldPos("CJ_PROSPE")) > 0
	cCodPro	:= Space(TamSx3("CJ_PROSPE")[1])		//Codigo do prospect
	cLjPro	:= Space(TamSx3("CJ_LOJPRO")[1])		//Loja do prospect
	
	If "CJ_PROSPE" $ ReadVar()
		lRet := ExistCpo( "SUS" )
	ElseIf "CJ_LOJPRO" $ ReadVar()
		lRet := ExistCpo( "SUS", M->CJ_PROSPE + M->CJ_LOJPRO )
	ElseIf "CJ_CLIENTE" $ ReadVar() .AND. !Empty( &(ReadVar()) ) .AND. M->CJ_CLIENTE+M->CJ_LOJA <> cCliente
		M->CJ_PROSPE	:= cCodPro
		M->CJ_LOJPRO	:= cLjPro
	EndIf
	
	If lRet .AND. "CJ_PROSPE" $ ReadVar() .AND. !Empty( &(ReadVar()) ) .AND.;
		M->CJ_CLIENTE+M->CJ_LOJA <> cCliente
	
		M->CJ_PROSPE	:= cCodPro
		M->CJ_LOJPRO	:= cLjPro
		lRet := .F.
	
		If !l415Auto
			Alert(	STR0077 + SubStr(cCliente,1,cCodCli) +;
				" - " + SubStr(cCliente,cCodCli + 1,cLojCli) ) //"Para utilizaГЦo do prospect, favor alterar o cСdigo do cliente para o padrЦo de orГamento: "
		Endif
	Endif
Endif	

RestArea(aAreaSUS)
RestArea(aArea)

Return(lRet)

/*
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤└o    ЁA415FldOk Ё  AutorЁ Andre Anjos			Ё Data Ё 28/01/09 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤└o Ё Valida campos na alteracao e inclusao					  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё Mata415                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Function A415FldOk()
Local lRet := .T.

lShowOpc := .T.

Return(lRet)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    Ё AjustaSX3Ё Autor Ё Andre Anjos			Ё Data Ё01/09/2005Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Ajuste do dicionario de dados		                      Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё MATA650			                        				  |╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Static Function AjustaSX3()
Local cValid := ""

dbSelectArea("SX3")
dbSetOrder(2)
dbSeek("CK_PRODUTO")
If ("SeleOpc" $ X3_VALID)
	cValid := StrTran(AllTrim(SX3->X3_VALID),".AND. SeleOpc(3,,,,,TMP1->CK_OPC)","")
	RecLock("SX3",.F.)
	Replace X3_VALID With cValid
	MsUnLock()
EndIf
Return