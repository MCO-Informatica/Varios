#Include "FiveWin.ch"
#INCLUDE "GPER140.CH"

/*/

ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ GPER140  ³ Autor ³ Mauro                   ³ Data ³ 30/05/95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Impressao de Rescisao                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ GPER140()                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Esecifico                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data     ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Natie       ³ 04/10/01 ³------³ Alteracao Pergunte "Simples"             ³±±
±±³Natie       | 19/10/01 |------|  Subst.Impres.Word pela Impres.Grafica   ³±±   
±±³Natie       | 23/11/01 |------|  Transf.TMSPrinter  para IMPGRFC         ³±±   
±±³Natie       | 30/11/01 |011609|  Codigo de mov.Av.Previo -IMPGRFC        ³±±   
±±³Natie       | 06/12/01 |------|  Pergunte -Dt publicacao dissidio/acordo ³±±
±±³Natie       | 06/02/02 |------|  TMSPrinter - Carr.obj p/impr.grafica e  ³±±
±±³            |          |      |  Traducao Valid Perg.                    ³±±
±±³Emerson     ³ 21/03/02 ³------³ Ajustar teste do RA_DEMISSA por conta do ³±±
±±³            |          |      | calculo de rescisao para o mes seguinte. ³±±
±±³Natie       | 16/05/02 |------| aDtResc - armazena dt de geracao de resc.³±±
±±³Emerson     ³ 02/09/02 ³------³ Exclusao da funcao ValidPerg().          ³±±
±±³Andreia     ³ 01/10/02 ³------³ Impressao do percentual de pensao alimen-³±±
±±³            ³          ³------³ a partir do cadastro de beneficiarios.   ³±±
±±³Mauro       | 06/11/02 |060647| Tratar Cod.Afas.Qdo.Resc.Meses Seguintes ³±±
±±³            |          |      | Criada Var.cCodR p/usar Impgrfc.     	³±±
±±³Priscila    | 18/11/02 |------| Inclusao de opcao para Novo Lay out Termo³±±
±±³            |          |------| Rescisao (Zebrado e Grafico), retirado a ³±±
±±³            |          |------| opcao de Pre-Impresso.                   ³±±
±±³Mauro       | 02/01/03 |Q.3569| Cod.Afast. "U" LISTAR COMO "U1"		    ³±±
±±³Marinaldo   | 15/04/03 |063300| Nao imprimir Valores Zerados  "	        ³±±
±±³Marinaldo   ³ 09/05/03 ³Melhor³Se existir o campo RQ_IMPCTRE verificar se³±±
±±³            ³          ³      ³deve imprimir o % da Pensao Alimenticia no³±±
±±³            ³          ³      ³Termo de Rescisao de Trabalho             ³±±
±±³Natie       | 27/10/03 |Melhor|Novos Codigos Identifidadores p/mv_par16, ³±±
±±³            ³          ³      ³p/Impressao Ferias/13o. em Avos ou Horas  ³±±
±±³Natie       | 10/11/03 |Melhor|Passa a Considerar filtros do Usuario na  ³±±
±±³            |          |      |impressao Grafica ( SETDEFAULT)           ³±±
±±³Natie       | 19/11/03 |067738|Imprime ref 13o salario em avos/dias sepa-|±±
±±³            |          |      |radamente das ref. de Ferias-Nova Pergunta³±±  
±±³Mauro       | 16/12/03 |067867|Nao passava o roteiro na Buaca % pensao   |±±
±±³Pedro Eloy  | 12/01/04 |067191|Foi usada a funcao fConvHoras() para    a |±±
±±³            | 		  |      |conversao da quantidade em horas.         |±±
±±³Pedro Eloy  | 09/02/04 |Melhor|Ajuste no relatorio pois esta saltando Pag|±±
±±³Pedro Eloy  | 25/03/04 |      |Aplicado o SetPgEject(.T.) para ejectar   |±±
±±³            |          |      |apos impressao de uma rescisao individual |±±
±±³Ricardo D.  | 04/11/04 |075083|Ajuste na impressao de rescisoes complemen|±±
±±³            |          |      |tares quando tiver mais de uma complemen- |±±
±±³            |          |      |tar em meses posteriores.                 |±±
±±³Ricardo D.  | 07/01/05 |077585|Ajustadas as opcoes para o simples e recri|±±
±±³            |          |      |ada a pergunta mv_par21.                  |±±
±±³Tania       ³29/07/2005³084485³Inclusao das perguntas 25 e 26 no Sx1, pa-³±±
±±³            ³          ³      ³ra impressao do Orgao Homologador no cam- ³±±
±±³            ³          ³      ³po 63 do Termo de Rescisao.               ³±±
±±³Natie/      |17/10/2005|087243|Ajuste no retorno do relatorio p/o browse ³±±
±±³Ricardo     |          |      |mantendo o filtro de browse selecionado   ³±±
±±³            |          |      |anteriormente no calculo da rescisao.     ³±±
±±³Natie       |16/12/2005|088503|Retirada do SetPgEject- configurar param. ³±±
±±³            |          |      |MV_CANSALT e MV_SALTPAG                   ³±±
±±³Andreia     |11/07/2006|101891|Correcao na filtragem das datas de demissao³±±
±±³            |          |      |Homologacao e geracao, na impressao do    ³±±
±±³            |          |      |termo de rescisao.                        ³±±
±±³Natie       |25/10/2006|108848|Ajuste Perguntas em Espanhol              ³±±
±±³Luiz Gustavo|24/01/2007³      ³Retiradas funcoes de ajuste de dicionario ³±±
±±³Andreia     |02/08/2007|127150|Se a data de dissidio estiver preenchida  ³±±
±±³            |          |      |atualiza a variavel lDissídio para ser    ³±±
±±³            |          |      |usada na impressao da GRFC do dissidio.   ³±±
±±³Renata Elena³03/08/07  ³129577³Ajuste na emissao grafica para SPOOL      ³±±
±±³Pedro Eloy  | 27/09/07 |124125|Criado 4 perguntas para exibir na impressao³±±
±±³            |          |      |RG e Nome do emprededor e tipo de salario.³±±
±±³            |          |      |e se deseja imprimir as inf. extra ex.func.³±±
±±³Pedro Eloy  |13/12/2007|137085|Ajustado o filtro do aReturn no relatorio.³±±
±±³Pedro Eloy  |12/02/2008|138409|Criado o parametro para geracao das resci-³±±
±±³            |          |      |soes todas dentro do mesmo mes.           ³±±
±±³Pedro Eloy  |14/02/2008|140199|Ajustado no relatorio o tratamento condicao±±
±±³            |          |      |para geracao quando folmes < data rescisao.±±
±±³Erika       |26/03/08  |141416|Ajuste das traducoes dos perguntes inclui- ±±
±±³            |          |      |dos por meio da funcao AjustaGP140Sx1.     ±±
±±³Marcelo     ³ 09/06/08 ³146082³Criacao da variavel lImpInfExt p/ imprimir³±±
±±³            ³          ³      ³as informacoes extra de ex-funcionarios.  ³±±
±±³Renata      ³10/03/09  ³4930/ ³Declaracao variavel lComplem, para indicar³±±
±±³            ³          ³2009  ³se e rescisao complementar.			    ³±±    
±±³Alex        ³31/07/09  ³18844/³Ajuste para tratamento do Grupo de Campos ³±±
±±³            ³          ³2009  ³Filial.			                        ³±±    
±±³Marcelo     ³20/08/2010³018371³Ajuste em AjustaGP140Sx1 para nao recriar ³±±
±±³            ³          ³      ³as perguntas em cada acesso a rotina.     ³±±    
±±|Luis Ricardo|20/10/2010|22535/|Alteracao da funcao TMSPrinter() para		³±±
±±|Cinalli     |          |2010  |TNewMSPrinter() para evitar problema de	³±± 
±±|            |          |      |impressao de fontes trocadas ao imprimir	³±± 
±±|            |          |      |relatorios graficos e nao graficos.		³±±   
±±|Marcia Moura|05/01/2011|000030|Correçao do campo Pensao Allimenticia 	³±± 
±±³Mauricio MR ³11/01/2011³000081³Ajuste para impressao do periodo aquisiti-³±±
±±|            |          |2011  |vo.                                    	³±±   
±±³Mauricio MR ³13/01/2011³000608³Ajuste para carregar verbas com incidencia³±±
±±|            |          |2011  |de DSR.                                 	³±±   
±±³Kelly       ³18/02/2011³000954³Inclusao de campos Sequencia e Num.ID no  ³±±
±±|            |          |2011  |array de verbas p/ Homolognet. Ajuste nas ³±±   
±±|            |          |      |funcoes GPER140Sum e fPerAquisito.    	³±±   
±±³Renata      ³11/05/2011³010639³Ajuste na funcao fPerAquisito no calc_fer ³±±
±±|            |          |2011  |para passar a data de demissao, somando os³±±   
±±|            |          |      |dias de aviso caso seja indenizado		³±±  
±±³Mauricio MR ³22/06/2011³013409³Ajuste para descontar as verbas de descon-³±±
±±³            ³          ³2011  ³tos no saldo de salario qdo solicitado.	³±± 
±±³Allyson M.  ³03/01/2012³033277³Ajuste p/ nao realizar a chamada da funcao³±±
±±³            ³          ³2011  ³SetDefault() p/ relatorio TRCT pois depen-³±± 
±±|            |          |      |dendo da configuracao da impressao, causa	³±±  
±±|            |          |      |o travamento do SmartClient, que segundo a³±±  
±±|            |          |      |equipe de Frame e Tecnologia, ocorre devi-³±±  
±±|            |          |      |do a utilizacao de funcoes do relatorio R3³±±  
±±|            |          |      |em conjunto com a classe TMSPrinter().	³±±
±±³Gustavo M.  ³09/03/2012³005141³Ajuste para valores referentes ao Total   ³±±
±±³            ³          ³2012  ³Rescisorio Bruto e ao Total Deducoes quan-³±±  
±±³            ³          ³		 ³do selecionado mais que uma via pra imp.  ³±±  
±±³Allyson M.  ³26/03/2012³006928³Ajuste para impressao correta de ferias   ³±±
±±³            ³          ³2012  ³indenizadas com data de demissao anterior ³±±  
±±³            ³          ³TEQVQH³ao per. aquisitivo, que seja uma sexta e  ³±±  
±±³            ³          ³		 ³que seja efetuado pagamento do sab/dom.   ³±±  
±±³Mauricio MR ³29/03/2012³005224³Ajuste para recuperar o salario anterior  ³±±
±±|            |          |2012  |ao afastamento (campo 23 do TRCT).		³±±   
±±|            |          |TELW64|                                        	³±±
±±³Leandro Dr. ³22/05/2012³      ³Ajuste para no codigo de afastamento consi³±±
±±³            ³          ³TFARJL³derar o cadastro do parametro 32.       	³±±
±±³Gustavo M.  ³28/06/2012³      ³Ajuste para carregar a descricao do campo ³±±
±±³            ³          ³TFEZML³22 da tabela S019.				    	³±±
±±³Luis Artuso ³09/08/2012³018646³Ajuste para efetuar calculo de ferias pro-³±±
±±|            |          |2012  |porcionais apenas em dias.         		³±± 
±±³            ³          ³TFKVQQ³                      			    	³±±
±±³Luis Ricardo³26/09/2012³024444³Ajuste na Pesquisa dos Beneficiarios (SRQ)³±±
±±³Cinalli	   ³		  ³  2012³conforme a Filial e Matricula do funciona-³±±
±±³			   ³		  ³TFTBD4³rio posicionado no momento da impressao.	³±±
±±³Renata Elena³19/10/2012³TFWZI3³Ajuste na inclusao da verba insuf. saldo  ³±±
±±|            |          |      |id 045)no array aHomV, p/na impressao do  ³±±
±±|            |          |      |TRCT nao considera-lo no campo 95(outros) ³±±
±±|            |          |      |Ajuste na funcao GPER140Sum para gravar o ³±±
±±|            |          |      |% do Adic. Insalubr e Periculosidade	    ³±±
±±³Renata Elena³23/10/2012³TFWZI3³Ajuste na funcao GPER140Sum, p/retornar o ³±±
±±|            |          |      |valor apenas qdo o valor > 0 e variavel   ³±±
±±|            |          |      |lPagVal = .T. 							³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Static __SRVKey  //-- Chave para carga das verbas com incidencia de DSR
Function GPER140
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis Locais (Basicas)                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local cString  :="SRA"        			   			// aLias do arquivo principal (Base)
Local aOrd     := {STR0001,STR0002} 	//" Matricula "###" Centro de Custo "
Local nTotregs,nMult,nPosAnt,nPosAtu,nPosCnt,cSav20,cSav7 //Regua
Local cDesc1   := STR0003				//"Termo de Recis„o do Contrato "
Local cDesc2   := STR0004				//"Ser  impresso de acordo com os parametros solicitados pelo"
Local cDesc3   := STR0005				//"usu rio."
Local cSavAlias,nSavRec,nSavOrdem          
Local aVPensao := {}
Local cPensao :=""
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis Private(Basicas)                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private aReturn := {STR0006, 1,STR0007, 1, 2, 1, "",1 }	//"Zebrado"###"Administra‡„o"
Private nomeprog:="GPER140"
Private anLinha := { },nLastKey := 0
Private cPerg   :="GPR140"
Private aAreaSRA:= SRA->(getarea())
   
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis UtinLizadas na funcao IMPR                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private Titulo  := STR0008		//"EMISSO TERMO RESCISO DO CONTRATO"
Private AT_PRG  := "GPER140"
Private wCabec0 := 3
Private wCabec1 := ""
Private wCabec2 := ""
Private wCabec3 := ""
Private Contfl  := 1
Private nLi     := 0
Private nTamanho:="M"   

//Objetos p/ Impresssao Grafica 
Private oFont07,oFont08, oFont10, oFont15, oFont10n, oFont21, oFont12,oFont16
Private oPrint  

AjustaSx1()

oFont07	:= TFont():New("Courier New",07,07,,.F.,,,,.T.,.F.)
oFont08	:= TFont():New("Courier New",08,08,,.T.,,,,.T.,.F.)		//negrito 
oFont10	:= TFont():New("Courier New",10,10,,.F.,,,,.T.,.F.)
oFont10n:= TFont():New("Courier New",10,10,,.T.,,,,.T.,.F.)
oFont12	:= TFont():New("Courier New",12,12,,.F.,,,,.T.,.F.)		//Normal s/negrito
oFont15	:= TFont():New("Courier New",15,15,,.T.,,,,.T.,.F.)
oFont21 := TFont():New("Courier New",21,21,,.T.,,,,.T.,.T.)
oFont16	:= TFont():New("Arial",16,16,,.T.,,,,.T.,.F.)

cSavAlias := Alias()
nSavRec   := RecNo()
nSavOrdem := IndexOrd()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ajusta pergunta no SX1                                       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
AjustaGP140Sx1() 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica as perguntas selecionadas                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
pergunte("GPR140",.F.)
  
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utinLizadas para parametros                        ³
//³ Variavel  Ordem                                              ³
//³ mv_par01  01    //  Zebrado / Grafico / Pre Impresso / Homolog Grafica / grfc Grafica ³
//³ mv_par02  02    //  FiLial De                                ³
//³ mv_par03  03    //  FiLial Ate                               ³
//³ mv_par04  04    //  Matricula De                             ³
//³ mv_par05  05    //  Matricula Ate                            ³
//³ mv_par06  06    //  Centro De Custo De                       ³
//³ mv_par07  07    //  Centro De Custo Ate                      ³
//³ mv_par08  08    //  Normal Ou Complementar                   ³
//³ mv_par09  09    //  Imprime CGC  Sim ou Nao                  ³
//³ mv_par10  10    //  Data Demissao De                         ³
//³ mv_par11  11    //  Data Demissao Ate                        ³
//³ mv_par12  12    //  Data Geracao De                          ³
//³ mv_par13  13    //  Data Geracao Ate                         ³
//³ mv_par14  14    //  Imprime Data de Homologacao              ³
//³ mv_par15  15    //  N§ de Vias                               ³
//³ mv_par16  16    //  Imp. Ferias    		 Avos ou Dias        ³
//³ mv_par17  17    //  Data de Homologa‡Æo De                   ³
//³ mv_par18  18    //  Data de Homologa‡Æo At‚                  ³
//³ mv_par19  19    //  Nome para Contato                        ³
//³ mv_par20  20    //  DDDD/Telefone                            ³
//³ mv_par21  21    //  Optante do Simples                       ³
//³ mv_par22  22    //  Dt. Entrega GRFC                         ³
//³ mv_par23  23    //  Recol.Dissidio (dt homologacao/acordo Col³
//³ mv_par24  24    //  Imp. 13o Salario  	 Avos ou Dias        ³
//³ mv_par25  25    //  Tipo do Orgao Homologador                ³
//³ mv_par26  26    //  Cod.Sindicato usado para o Ministerio.   ³
//³ mv_par27  27    //  Rg                  					 ³
//³ mv_par28  28    //  Nome do Empregador                       ³
//³ mv_par29  29    //  1- Salario Termo Rescisao / 2- Sal.Base  ³
//³ mv_par30  30    //  Impressao informacoes extra do ex.func.	 ³ 
//³ mv_par33  33	//	Grupo de Verbas que compoem a remuneracao³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
   
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Envia controle para a funcao SETPRINT                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
wnrel:="GPER140"            //Nome Default do relatorio em Disco
wnrel:=SetPrint(cString,wnrel,cPerg,@Titulo,cDesc1,cDesc2,cDesc3,.F.,aOrd,,nTamanho)
If nLastKey = 27
	Return
Endif

If ( MV_PAR01 == 4 ) .AND.  ( MV_PAR16 == 2 ) // O calculo de ferias deve ser impresso em 'avos'.
	MV_PAR16	:= 1
EndIf

If !(Mv_par01 = 2 .or. Mv_par01 = 4 .or. Mv_par01 = 5)
	SetDefault(aReturn,cString)
Endif

If nLastKey = 27
	Return
Endif  

If Mv_par01 = 2 .or. Mv_par01 = 4 .or. Mv_par01 = 5

	//-- Objeto para impressao grafica

	// Alteracao da funcao TMSPrinter() para TNewMSPrinter() para evitar problema de
	// impressao de fontes trocadas ao imprimir relatorios graficos e nao graficos.
	
	oPrint	:= TNewMSPrinter():New( If(Mv_par01 = 2 .or. Mv_par01 == 4 , ;
								     "Termo de Rescisao de Contrato de Trabalho ",;
 	                                 "GRFC - Guia de Recolhimento Rescisorio do FGTS e da Contribuicao Social") ;
 	                            )

	oPrint:SetPortrait()               
Endif
   
RptStatus({|lEnd| GP140Imp(@lEnd,wnRel,cString)},Titulo)

If Mv_par01==5 .or. mv_par01==2 .or. mv_par01==4
	oPrint:Preview()  							// Visualiza impressao grafica antes de imprimir
Endif	

dbselectarea(cSavAlias)
dbsetorder(nSavOrdem)
dbgoto(nSavrec)   

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ GP140imp ³ Autor ³ R.H. - Mauro          ³ Data ³ 26.04.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Recibo de Quitacao                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ GPER140(void)                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function GP140IMP(lEnd,WnRel,cString)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis Locais (Programa)                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local cVerCodFol
Local cFilMat
Local nI
Local nImprVias
Local nx           
Local nAuxP:= 0
Local nAuxD:= 0  

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Variaveis de Acesso do Usuario                               ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Local cAcessaSRA	:= &( " { || " + ChkRH( "GPER140" , "SRA" , "2" ) + " } " )
Local cAcessaSRG	:= &( " { || " + ChkRH( "GPER140" , "SRG" , "2" ) + " } " )
Local cAcessaSRR	:= &( " { || " + ChkRH( "GPER140" , "SRR" , "2" ) + " } " )
Local aCodBenef := {}
Local nHoras	:= ""
Local nS		:= 1
Local cIndCond  := ""
Local cFiltraRh := ""
Local nCont		:= 0
Local nContIni	:= 0            
Local aAreaSRQ    
Local aTS019 	:= {}    
Local nPos	 	:= 0
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Busca de remuneracao anterior								 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local cGrpVerbas		:= ""  
Local cFilProc      	:= "" 
Local lModoSRV			:= FWModeAccess("SRV",3) <>'E'  
	

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis Privadas(Programa)                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private nProv   	:= nDesc := 0.00
Private cCausa  	:= Space(30)
Private cCodSaq 	:= "  "
Private cImpSac 	:= " "
Private cAvPrevio	:= " "
Private cDtDissid 	:= " " 
Private aPdv    	:= {}
Private aHomV	 	:= {}
Private aHomD		:=	{}
Private aCodFol 	:= {}         // Matriz com Codigo da folha
Private nLin    	:= 0
Private aInfo   	:= {}         // Matriz com dados da empresa                       
Private cArqWord 	:= MV_PAR23
Private aDtResc 	:={}		  // Matriz com  data Geracao da Rescisao/complemento
Private nPerPensa 	:= 0
Private nPerFGTS  	:= 0
Private cCodR		:= " "
Private aTitulo   	:= {} 
Private lDissidio	:= !empty( MV_PAR23 )
Private cFilter		:= aReturn[7]
Private lComplem	:= If(mv_par08==2,.T.,.F.)//	Tipo de Rescisao(Normal/Complementar)
Private dDataDem	:= Ctod("")  //Data Demissao
Private nDiasAv		:= 0 		 // Dias de Aviso
Private cTafaFer    := GetMv("MV_TAFAFER" )      	// Trata Perda de Periodo Para Afastados  

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Busca de remuneracao anterior								 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

Private cVerbas_Aux		:= ""


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Ordem Do Relatorio                                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nOrdem  := aReturn[8]
cSQL	:= ""

FilialDe    :=  mv_par02                       //  FiLial De                   	³
FilialAte   :=  mv_par03                       //  FiLial Ate                  	³
cMatDe      :=  mv_par04                       //  Matricula De                	³
cMatAte     :=  mv_par05                       //  Matricula Ate               	³
cCCDe       :=  mv_par06                       //  Centro De Custo De          	³
cCCAte      :=  mv_par07                       //  Centro De Custo Ate         	³
dDataDemDe  :=  mv_par10                       //  Data Demissao De            	³
dDataDemAte	:=  mv_par11                       //  Data Demissao Ate           	³
lImpInfExt  :=  IIf(mv_par30 == 1,.T.,.F.)    //  Imp. inform. extra ex.func 	³
cGrpVerbas	:= AllTrim( mv_par33 ) 				//  Grupo de Verbas				³

#IFDEF TOP 

	cSQL := "SELECT * "	
	cSQL += " FROM " +	RetSqlName("SRA")
	cSQL += " WHERE "  
	cSQL += " RA_FILIAL  between  '" + FilialDe +"' AND '" +FilialAte  +"' AND "
	cSQL += " RA_MAT     between  '" + cMatDe    +"' AND '" +cMatAte     +"' AND "
	cSQL += " RA_CC      between  '" + cCCDe     +"' AND '" +cCCAte      +"' AND "
	If TcSrvType() != "AS/400"
		cSQL += "  D_E_L_E_T_ <> '*' "
	Else
		cSQL += "  @DELETED@  <> '*' "	
	Endif 

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Se houver filtro executa parse para converter expressoes adv para SQL    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ! Empty(cFilter)
		cSQL += " and " + GPEParSQL(cFilter)
	Endif   

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Define order by de acordo com a ordem...                                 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	If nOrdem == 1
		cSQL += " ORDER BY RA_FILIAL, RA_MAT "
	ElseIf nOrdem == 2
		cSQL += " ORDER BY RA_FILIAL, RA_CC,RA_MAT "
	Endif

	cSQL     	:= ChangeQuery(cSQL)
	aStruSRA 	:= SRA->(dbStruct())
	SRA->( dbCloseArea() ) //Fecha o SRA para uso da Query
	dbUseArea(.T.,"TOPCONN",TcGenQry(,,cSQL),"SRA",.F.,.T.)
	For nS := 1 To Len(aStruSRA)
		If ( aStruSRA[nS][2] <> "C" )
			TcSetField("SRA",aStruSRA[nS][1],aStruSRA[nS][2],aStruSRA[nS][3],aStruSRA[nS][4])
		EndIf
	Next nS
#Else	
	dbSelectArea("SRA")
	If nOrdem == 1
		cIndCond := "SRA->RA_FILIAL+SRA->RA_MAT"
	ElseIf nOrdem == 2
		cIndCond := "SRA->RA_FILIAL+SRA->RA_CC+SRA->RA_MAT"
	Endif

	cFiltraRh   :=  " SRA->RA_FILIAL >= '"+FilialDe+"' .And. SRA->RA_FILIAL <= '"+FilialAte+"' .And. " 
	cFiltraRh   +=  " SRA->RA_MAT >= '"+cMatDe+"' .And. SRA->RA_MAT <= '"+cMatAte+"' .And. SRA->RA_CC >= '"+cCCDe+"' .And. "
	cFiltraRh   +=  " SRA->RA_CC <= '"+cCCAte+"'"

	cArqNtx  := CriaTrab(NIL,.F.)
	IndRegua("SRA",cArqNtx,cIndCond,,cFiltraRh,"Selecionando Registros...")          //"Selecionando Registros..."
	DbGoTop()

#ENDIF

Private cFiliAnt := "__cFiliAnt__"
   
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Carrega Regua de Processamento                               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
SRA->( SetRegua(RecCount()) )
SetPrc(0,0)

If cPaisLoc == "BRA"
	If !Empty( cGrpVerbas )          
		cFilProc := If( lModoSRV, Space( FWGETTAMFILIAL ), SRA->RA_FILIAL )
		gp140GrpVerb(cFilProc,@cVerbas_Aux,cGrpVerbas) 
	EndIf	
Endif	

While SRA->( !Eof() )
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Movimenta Regua de Processamento                             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	IncRegua()
      
	aPdv := {}
	aHomV:= {}
	aHomD:= {}
	aPdd := {} 
	aDtResc	:= {}
	cFilMat := SRA->( RA_FILIAL + RA_MAT )

	If lEnd
		@Prow()+1,0 PSAY cCancel
		Exit
    Endif

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Consiste Parametrizacao do Intervalo de Impressao            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	#IFNDEF TOP
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Consiste Filtro da setprint						             ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !Empty(cFilter) .And. !&(cFilter)
			dbSelectArea("SRA")
			dbSkip()
			Loop
		EndIf
	#ENDIF

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³Consiste Filiais e Acessos                                             ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	IF SRA->( !( RA_FILIAL $ fValidFil() ) .or. !Eval( cAcessaSRA ) )
		dbSelectArea("SRA")
      	SRA->( dbSkip() )
       	Loop
	EndIF

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Carrega Variaveis Codigos Da Folha                           ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	If ( cFiliAnt # SRA->RA_FILIAL )
		cFiliAnt := SRA->RA_FILIAL
		If !fP_CodFol(@aCodFol,cFiliAnt)
			Return
		Endif 
		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Carrega Codigos de verba com incidencia de DSR               ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		LoadCodDSR(.F., cFiliAnt)   
		
		If cPaisLoc == "BRA"
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Atualiza o grupo de verbas conforme a Filial que esta sendo processada	³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If !lModoSRV .And. !Empty( cGrpVerbas ) .And. cFilProc # cFilAnt
				cVerbas_Aux	:= ""
		   		gp140GrpVerb(cFilAnt,@cVerbas_Aux,cGrpVerbas) 
		   		cFilProc	:= cFilAnt
		 	EndIf	                       
        Endif
        
	Endif

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Define Codigos de Identificadores para o pergunte mv_par16, ³
	//³ para calcular Ferias em Avos ou em Horas.                   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cVerCodFol:=aCodFol[086,1]+"*"+ ;	//  Ferias Indenizadas
	            aCodFol[087,1]+"*"+ ;	//  Ferias Proporcionais
	            aCodFol[230,1]+"*"+ ;	//	Ferias s/ Av.Previo Indenizado
	            aCodFol[248,1]+"*"+ ;	//	Media Ferias Vencidas Rescisao
	            aCodFol[249,1]+"*"+ ;	//	Media Ferias Proporcionais Rescisao
	            aCodFol[252,1]			//	Media Ferias Sobre Aviso Indenizado Rescisao
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Define Codigos de Identificadores para o pergunte mv_par24, ³
	//³ para calcular 13 Sal.em Avos ou em Horas.                   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cVerCod13 :=aCodFol[114,1]+"*"+ ;	//  13o. na Indenizacao
	            aCodFol[115,1]+"*"+ ;  	//  13o. Sobre Aviso Previo
	            aCodFol[251,1]+"*"+ ; 	//	Media 13o. Salario Rescisao
	            aCodFol[253,1]        	//	Media 13o. Salario Sobre Aviso Indenizado Rescisao
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Procura no Arquivo de Cabecalho da Rescisao "SRG"            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("SRG")
	If SRG->( dbSeek( cFilMat ) .and. ( RG_FILIAL $ fValidFil() .and. Eval( cAcessaSRG ) ) ) //Consiste Filial e Acessos
		dDataDem := SRG->RG_DATADEM
		nDiasAv	 := SRG->RG_DAVISO

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Quando Complementar Ver Se Existe + de Um Registro no "SRG"  ³
		//³ e Poscionar no Ultimo Registro do Funcionario.               ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		nContSrg := 0
		If Mv_Par08 = 2
			While SRG->( !Eof() .And. ( cFilMat ) == ( RG_FILIAL + RG_MAT ) )
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Adiciona somente os cabecalhos das rescisoes complementares. ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				Aadd(aDtResc,{SRG->RG_FILIAL+SRG->RG_MAT,SRG->RG_DTGERAR,SRG->RG_DATADEM,SRG->RG_DATAHOM}) //-- Data da ultima Geracao da Rescisao/complemento
				nContSrg++
				SRG->( dbSkip() )
			Enddo
			SRG->( dbSkip(-1) )
			If nContSrg <= 1
				dbSelectArea("SRA")
				SRA->( dbSkip() )
				Loop
			Endif
			else
			nContSrg++
		Endif

		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³Consiste Periodos do SRG                                      ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		If  SRG->(;
					(RG_DATADEM >= dDataDemDe .And. RG_DATADEM <= mv_par11) .And.;
					(RG_DTGERAR >= mv_par12 .And. RG_DTGERAR <= mv_par13) .And.;
					(RG_DATAHOM >= mv_par17 .And. RG_DATAHOM <= mv_par18)	   ;	
				 )	
			// controle de impressao rescisao completar
            If MV_PAR31 == 1 .And. Mv_Par08 = 2  // Nao para todas complementares // complementares
            	nContIni:= nContSrg
            ElseIf MV_PAR31 == 2 .And. Mv_Par08 = 2		// Sim para todas as complementares  // Complementares
            	nContIni:= If(nContSrg > 1,2,nContSrg)
            Else				// Normal
				nContIni:= 1            
            Endif
			For nCont:= nContIni to nContSrg
				aPdv 	:= {}
				aHomV	:=	{}
				aHomD	:= {}
				aPdd 	:= {} 
	            If Mv_Par08 = 2  // complementar
					If MV_PAR31 = 1    // imprimir somente a ultima rescisao.
						For nx := 2 to Len(aDtResc)
							If	(aDtResc[nx,3] >= dDataDemDe .And. aDtResc[nx,3] <= mv_par11) .And.;
								(aDtResc[nx,2] >= mv_par12 .And. aDtResc[nx,2] <= mv_par13) .And.;
								(aDtResc[nx,4] >= mv_par17 .And. aDtResc[nx,4] <= mv_par18)
								SRG->( dbseek( aDtResc[nx,1] + dtos( aDtResc[nx,2] ) ) )
							endif
						next nx
					Else	
						If	(aDtResc[nCont,3] >= dDataDemDe .And. aDtResc[nCont,3] <= mv_par11) .And.;
							(aDtResc[nCont,2] >= mv_par12 .And. aDtResc[nCont,2] <= mv_par13) .And.;
							(aDtResc[nCont,4] >= mv_par17 .And. aDtResc[nCont,4] <= mv_par18)
							SRG->( dbseek( aDtResc[nCont,1] + dtos( aDtResc[nCont,2] ) ) )
						Endif
			        Endif
	            EndIf
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Procura Arquivo de Verbas da Rescisao                        ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			dbSelectArea("SRR")
			nProv := nDesc := 0.00
			If SRR->( dbSeek( cFilMat + "R" ) )
				While SRR->( !Eof() .And. ( cFilMat + "R" ) == ( RR_FILIAL + RR_MAT + RR_TIPO3 ) )
					
					/*
					ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					³Consiste Filiais e Acessos                                             ³
					ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
					IF SRR->( !( RR_FILIAL $ fValidFil() ) .or. !Eval( cAcessaSRR ) )
						dbSelectArea("SRR")
				      	SRR->( dbSkip() )
				       	Loop
					EndIF
					
					/*
					ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					³Consiste data de geracao                                               ³
					ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
					If ( SRR->RR_DATA == SRG->RG_DTGERAR )
					
						/*
						ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						³Nao imprime valores Zerados                                            ³
						ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
						IF ( SRR->RR_VALOR == 0.00 )
							SRR->( dbSkip() )
							Loop
						EndIF
					
						/*
						ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						³Verifica o Tipo da Verba e Carrega no aPd? correspondente              ³
						ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
						nHoras	:= IF (SRR->RR_TIPO1=="H",fConvHoras(SRR->RR_HORAS,"1"),SRR->RR_HORAS)

						If PosSrv(SRR->RR_PD,SRA->RA_FILIAL,"RV_TIPOCOD") == "1" //Provento
						/*	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
							³ Ferias                                                                ³
							ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
                           If mv_par16==2 .and. SRR->RR_PD$cVerCodFol .And. (SRR->RR_HORAS - Int(SRR->RR_HORAS)) = 0.12 
	                           	Aadd(aPdv  , DescPd(SRR->RR_PD,SRA->RA_FILIAL)+' '+Str(Int(nHoras)*2.5,6,2)+' '+Transform(SRR->RR_VALOR,"@E 99,999,999.99") )
	                           	Aadd(aHomV ,{DescPd(SRR->RR_PD,SRA->RA_FILIAL),    (Int(nHoras)*2.5) ,    SRR->RR_VALOR,SRR->RR_PD,GPER140IV(SRR->RR_PD,1),GPER140IV(SRR->RR_PD,2),0,SRR->RR_SEQ,SRR->RR_NUMID })
                           ElseIf mv_par16==1 .and. SRR->RR_PD$cVerCodFol .And. (SRR->RR_HORAS - Int(SRR->RR_HORAS)) # 0.12
	                           	Aadd(aPdv	, DescPd(SRR->RR_PD,SRA->RA_FILIAL)+' '+Str((SRR->RR_HORAS / 2.5) + 0.12 ,6,2)+' '+Transform(SRR->RR_VALOR,"@E 99,999,999.99") )
	                           	Aadd(aHomV	,{DescPd(SRR->RR_PD,SRA->RA_FILIAL),    ((SRR->RR_HORAS / 2.5) + 0.12),    SRR->RR_VALOR,SRR->RR_PD,GPER140IV(SRR->RR_PD,1),GPER140IV(SRR->RR_PD,2),0,SRR->RR_SEQ,SRR->RR_NUMID })
                           Else
							/*	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
								³ 13o Salario                                                           ³
								ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	                           If mv_par24==2 .and. SRR->RR_PD$cVerCod13 .And. (SRR->RR_HORAS - Int(SRR->RR_HORAS)) = 0.12 
			                        	Aadd(aPdv,DescPd(SRR->RR_PD,SRA->RA_FILIAL)+' '+Str(Int(SRR->RR_HORAS)*2.5,6,2)+' '+ Transform(SRR->RR_VALOR,"@E 99,999,999.99") )
			                        	Aadd(aHomV,{DescPd(SRR->RR_PD,SRA->RA_FILIAL),  (Int(SRR->RR_HORAS)*2.5)     ,SRR->RR_VALOR,SRR->RR_PD,GPER140IV(SRR->RR_PD,1),GPER140IV(SRR->RR_PD,2),0,SRR->RR_SEQ,SRR->RR_NUMID })
        	                   	ElseIf mv_par24==1 .and. SRR->RR_PD$cVerCod13 .And. (SRR->RR_HORAS - Int(SRR->RR_HORAS)) # 0.12
	        	                   	Aadd(aPdv,DescPd(SRR->RR_PD,SRA->RA_FILIAL)+' '+Str((nHoras / 2.5) + 0.12 ,6,2)+' '+Transform(SRR->RR_VALOR,"@E 99,999,999.99") )
	        	                   	Aadd(aHomV,{DescPd(SRR->RR_PD,SRA->RA_FILIAL),  ((nHoras / 2.5) + 0.12),    SRR->RR_VALOR,SRR->RR_PD,GPER140IV(SRR->RR_PD,1),GPER140IV(SRR->RR_PD,2),0,SRR->RR_SEQ,SRR->RR_NUMID })
								Else					
                          			Aadd(aPdv,DescPd(SRR->RR_PD,SRA->RA_FILIAL)+' '+Str(nHoras,6,2)+' '+Transform(SRR->RR_VALOR,"@E 99,999,999.99") )
                          			Aadd(aHomV,{DescPd(SRR->RR_PD,SRA->RA_FILIAL),  nHoras,    SRR->RR_VALOR,SRR->RR_PD,GPER140IV(SRR->RR_PD,1),GPER140IV(SRR->RR_PD,2),If(SRR->RR_PD # aCodfol[045,1],0,1),SRR->RR_SEQ,SRR->RR_NUMID })
                          		Endif	
                           Endif
                           nProv += SRR->RR_VALOR
						Elseif PosSrv(SRR->RR_PD,SRA->RA_FILIAL,"RV_TIPOCOD") == "2" .And. SRR->RR_PD # aCodfol[126,1]  // Desconto e Despreza Liq. da Rescisao
							Aadd(aPdd,DescPd(SRR->RR_PD,SRA->RA_FILIAL)+' '+Str(nHoras,6,2)+" "+Transform(SRR->RR_VALOR,"@E 99,999,999.99") )
							Aadd(aHomD,{DescPd(SRR->RR_PD,SRA->RA_FILIAL),nHoras,SRR->RR_VALOR,SRR->RR_PD,GPER140IV(SRR->RR_PD,1),GPER140IV(SRR->RR_PD,2),0,SRR->RR_SEQ,SRR->RR_NUMID })
							nDesc += SRR->RR_VALOR
						Endif
					Endif

					SRR->( dbSkip() )

				Enddo

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Carrega Matriz Com Dados da Empresa                          ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				fInfo(@aInfo,SRA->RA_FILIAL)
				cCausa  	:= Space(30)
				cCodSaq 	:= "  "
				cImpSac 	:= " "    
				cCodR		:= " "
				fPHist82(SRA->RA_FILIAL ,"32",SRG->RG_TIPORES+"  ",57,3,@cCausa)
				fCarrTab( @aTS019,"S019")
				IF  (nPos := aScan(aTS019,{|x| x[5] == cCausa })) > 0    
					cCausa = aTS019 [nPos][6]
				Endif	
				fPHist82(SRA->RA_FILIAL ,"32",SRG->RG_TIPORES+"  ",49,2,@cCodSaq)
				fPHist82(SRA->RA_FILIAL ,"32",SRG->RG_TIPORES+"  ",56,1,@cImpSac)
				fPHist82(SRA->RA_FILIAL ,"32",SRG->RG_TIPORES+"  ",32,1,@cAvPrevio)
				fPHist82(SRA->RA_FILIAL ,"32",SRG->RG_TIPORES+"  ",48,1,@cCodR)				
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Codigo de movimentacao do FGTS                                ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				//-- Se funcionario estiver demitido no cadastro considera afastamento do cadastro 
				//-- caso contrario utiliza o afastamento do tipo da rescisao parametro 32.
				cCodR 	:= If (SRA->RA_SITFOLH = "D" , SRA->RA_AFASFGT , cCodR)
				If cCodR == "I"
					cCodR := "I1"
				Elseif cCodR == "U"
					cCodR := "U1"
				Elseif cCodR == "1"
					cCodR := "U3"
				Elseif cCodR == "2"
					cCodR := "I2"
				Elseif cCodR == "3"
					cCodR := "I3"
				Elseif cCodR == "4"
					cCodR := "I4"
				Elseif cCodR =="N"
			   		cCodR := "N2"
				Elseif cCodR =="5"
	   				cCodR := "N1"
				Elseif cCodR =="V"
	   				cCodR := "U2"
				Elseif cCodR =="9"
	   				cCodR := "S3"
				Elseif cCodR =="S"
	   				cCodR := "S2"
			    EndIf		
                
				//--Codigo da Empresa no FGTS
				cCodFgts := Space(13)
				If fPHist82(SRA->RA_FILIAL,"14",SRA->RA_FILIAL+SRA->RA_TPCONTR+"3") .Or. fPHist82(SRA->RA_FILIAL,"14",Space(FWGETTAMFILIAL)+SRA->RA_TPCONTR+"3")
					cCodFgts := Left(SRX->RX_TXT,13)
				Endif

				//-- Busca percentual de pensao alimenticia no cadastro de Beneficiarios
				aCodBenef := {}
				nPerPensa := 0
				nPerFGTS  := 0               
				aVPensao := {}
				cPensao := ""
				aAreaSRQ:=GetArea()

				//-- Busca Beneficiarios de acordo com a Filial e Matricula Atual
				If SRQ->( DbSeek( cFilMat ) )
					While SRQ->(RQ_FILIAL+RQ_MAT) == cFilMat
						nPerFGTS+=SRQ->RQ_PERFGTS   
						if !empty(SRQ->RQ_VERBFOL)
							aAdd(avPensao,SRQ->RQ_VERBFOL)
						endif
						if !empty(SRQ->RQ_VERBFER)
							aAdd(avPensao,SRQ->RQ_VERBFER)
						endif
						if !empty(SRQ->RQ_VERB131)
							aAdd(avPensao,SRQ->RQ_VERB131)
						endif
						if !empty(SRQ->RQ_VERB132)
							aAdd(avPensao,SRQ->RQ_VERB132)
						endif
						if !empty(SRQ->RQ_VERBPLR)
							aAdd(avPensao,SRQ->RQ_VERBPLR)
						endif
							
						SRQ->(DBSKIP())
					Enddo				
				Endif
				RestArea(aAreaSRQ)
				if len(avPensao)<>0
					for nI := 1 to len(aVpensao)
						cPensao := cPensao + avPensao[nI]
						if nI < len(avPensao)
							cPensao := cPensao +"/"
						endif
					next nI
				endif

				fBusCadBenef(@aCodBenef,"FOL")				
				For nI := 1 to len(aCodBenef)
					IF ( aCodBenef[nI,15] == "S" ) //Apenas se Imprime % no Termo de Rescisao.
						nPerPensa += aCodBenef[nI,2]
					EndIF	
				Next nI   

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Imprime o Numero de Vias Especificado                        ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				SET CENTURY OFF
				nAuxP:= nProv
				nAuxD:= nDesc
				For nImprVias := 1 to mv_Par15
					If Mv_par01 == 1
	    				If cPaisLoc == "BRA"
							If ExistBlock("IMPRESZ1")
								ExecBlock('IMPRESZ1',.F.,.F.)						
							Else	
								ExecBlock('IMPRESZ',.F.,.F.)						
							Endif	
						Else
							GeraTitulo() // Carrega o nome dos titulos dos campos para impressao.
							ExecBlock('IMPLIFIN',.F.,.F.)
						EndIf
					ElseIf Mv_par01 == 2
						If ExistBlock("IMPRESG1")
							ExecBlock('IMPRESG1',.F.,.F.)						
						Else	
							ExecBlock('IMPRESG',.F.,.F.)						
						Endif	
					ElseIF Mv_par01 == 3
						If ExistBlock("IMPRESC1")
							ExecBlock('IMPRESC1',.F.,.F.)						
						Else	
							ExecBlock('IMPRESC',.F.,.F.)						
						Endif	
					ElseIF Mv_par01 == 4
						
						If ExistBlock("IMPRESH1")
							ExecBlock('IMPRESH1',.F.,.F.)						
						Else	              
							ExecBlock('IMPRESH',.F.,.F.)						
						Endif	
					ElseIF Mv_par01 == 5
						If ExistBlock("IMPGRFC1")
							ExecBlock('IMPGRFC1',.F.,.F.)						
						Else	              
							ExecBlock('IMPGRFC',.F.,.F.)						
						Endif	
					Endif
					nProv:= nAuxP
					nDesc:= nAuxD					
				Next nImprVias
				If nTData == 10
					SET CENTURY ON
				Else
					SET CENTURY OFF
				Endif
			Endif
	    	Next  // impressao da rescisao complementar.
		Endif
	Endif
	dbSelectArea("SRA")
	SRA->( dbSkip() )
Enddo
   
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Termino do relatorio                                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SRA")
SRA->( dbCloseArea() )

#IFnDEF TOP 
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Eliminando o arquivo temporario.    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	fErase( cArqNtx + OrdBagExt() )
#Endif

dbSelectArea("SRA")
RetIndex("SRA")	

RestArea(aAreaSRA)

nlin:= 0	
Set Device To Screen
If (aReturn[5] = 1 .and. mv_par01 # 5 .And. mv_par01 # 2 .And. mv_par01 # 4)
	Set Printer To
	Commit
	ourspool(wnrel)
Endif
   
MS_FLUSH()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Elimina variaveis statics		    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
LoadCodDSR(.T.) 
__SRVKey:= Nil

Return



/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³GPER140   ºAutor  ³Microsiga           º Data ³  09/27/01   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³                                                            º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP5                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function fSelGrfc()
&(ReadVar()) := cGetFile("GRFC.DOT | GRFC.DOT ", "Selecione arquivo ")
Return(.T.)

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºProgram   ³GeraTituloºAuthor ³Alexandre Silva     º Date ³  13-03-03   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Alimenta a matriz a  aTitulo que sera usada no relatorio    º±±
±±º          ³IMPLIFIN, com os titulos dos campos constantes no SX3.      º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUse       ³ SigaGPE - Localizacoes                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/

Static Function GeraTitulo()

If Len(aTitulo) = 0
	aAdd(aTitulo,Rtrim(RetTitle("A2_CGC"))			+" ")
	aAdd(aTitulo,Rtrim(RetTitle("A2_FILIAL"))		+" ")
	aAdd(aTitulo,Rtrim(RetTitle("RA_CC"))			+" ")
	aAdd(aTitulo,Rtrim(RetTitle("RA_NOME"))			+" ")
	aAdd(aTitulo,Rtrim(RetTitle("RA_MAT"))			+" ")
	aAdd(aTitulo,Rtrim(RetTitle("RA_ENDEREC"))		+" ")
	aAdd(aTitulo,Rtrim(RetTitle("RA_BAIRRO"))		+" ")
	aAdd(aTitulo,Rtrim(RetTitle("RA_MUNICIP"))		+" ")
	aAdd(aTitulo,Rtrim(RetTitle("RA_ESTADO"))		+" ")
	aAdd(aTitulo,Rtrim(RetTitle("RA_CEP"))			+" ")
	aAdd(aTitulo,Rtrim(RetTitle("RA_CIC"))			+" ")
	aAdd(aTitulo,Rtrim(RetTitle("RA_NASC"))			+" ")
	aAdd(aTitulo,Rtrim(RetTitle("RA_MAE"))			+" ")
	aAdd(aTitulo,Rtrim(RetTitle("RG_SALMES"))		+" ")
	aAdd(aTitulo,Rtrim(RetTitle("RA_ADMISSA"))		+" ")
	aAdd(aTitulo,Rtrim(RetTitle("RG_DTAVISO"))		+" ")
	aAdd(aTitulo,Rtrim(RetTitle("RG_DATADEM"))		+" ")
EndIf

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³fOpSimples³ Autor ³ Ricardo Duarte Costa  ³ Data ³ 07/01/05 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Selecionar o tipo de Opcao pelo Simples.                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ fSituacao() 												  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Generico 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Function fOpSimples()

Local cTitulo:=""
Local lRet := .F.
Local MvPar
Local MvParDef:=""

Private aSimples :={}
l1Elem := .T.

cAlias := Alias() 					 // Salva Alias Anterior

MvPar:=&(Alltrim(ReadVar()))		 // Carrega Nome da Variavel do Get em Questao
mvRet:=Alltrim(ReadVar())			 // Iguala Nome da Variavel ao Nome variavel de Retorno

aSimples := {;
			STR0010,; 	//	"1 - nao optante"
			STR0011,;	//  "2 - optante - faturamento anual ate R$ 1.200.000,00"
			STR0012,;	//  "3 - optante - faturamento anual superior a R$ 1.200.000,00"
			STR0013,;	//  "4 - nao optante - produtor rural pessoa fisica (CEI e FPAS 604)"
			STR0014,;	//  "5 - nao optante - Liminar para nao recolhimento da C.Social"
			STR0015;	//  "6 - optante - fat.anual > R$1.200.000,00 - Liminar p/nao recolh.C.Social"
		}  
MvParDef:="123456"
cTitulo :=STR0016  //	"Opcao Simples"

While !lRet
	IF f_Opcoes(@MvPar,cTitulo,aSimples,MvParDef,,,l1Elem)  // Chama funcao f_Opcoes
		&MvRet := mvpar                                                                          // Devolve Resultado
	EndIF
	If !empty(mvpar)
		lRet := .T.
	Endif
Enddo

dbSelectArea(cAlias) 								 // Retorna Alias

Return( MvParDef )


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³fGetOrgao	 ³ Autor ³ Tania Bronzeri       ³ Data ³27/07/2005³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Retorna Dados do Sindicato, baseado na SRA 			  	  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Function fGetOrgao(cSindic,cFilSindic)

Local aSaveSRA	:= SRA->( GetArea() )
Local cOrgao	:=	space(155)

dbSelectArea("RCE")
If RCE->(IndexOrd()) != 0
	dbSetOrder(1)
	dbSeek(cFilSindic+cSindic)	
EndIf
If !Eof()
	cOrgao	:=	AllTrim(cOrgao)+      RCE->RCE_ENTSIN
	cOrgao	:=	AllTrim(cOrgao)
	If !Empty(RCE->RCE_ENTSIN)
		cOrgao	:=	cOrgao+" - "
	EndIf
	cOrgao	:=	cOrgao+RCE->RCE_DESCRI
	cOrgao	:=	AllTrim(cOrgao)
	If !Empty(RCE->RCE_DESCRI)
		cOrgao	:=	cOrgao+" - "
	EndIf
	cOrgao	:=	cOrgao+RCE->RCE_ENDER
	cOrgao	:=	AllTrim(cOrgao)
	If !Empty(RCE->RCE_ENDER)
		cOrgao	:=	cOrgao+", " +RCE->RCE_NUMER
	EndIf
	cOrgao	:=	AllTrim(cOrgao)
	If !Empty(RCE->RCE_ENDER) .And. !Empty(RCE->RCE_NUMER)
		cOrgao	:=	cOrgao+" - "
	EndIf                       
	cOrgao	:=	cOrgao+RCE->RCE_MUNIC
	cOrgao	:=	AllTrim(cOrgao)
	If !Empty(RCE->RCE_MUNIC)
		cOrgao	:=	cOrgao+" - "
	EndIf                    
	cOrgao	:=	cOrgao+RCE->RCE_UF
	cOrgao	:=	AllTrim(cOrgao)   
	If !Empty(RCE->RCE_UF)
		cOrgao	:=	cOrgao+" - "
	EndIf
	If !Empty(FieldGet(FieldPos("RCE_DDD"))) .And. !Empty(FieldGet(FieldPos("RCE_Fone")))
		cOrgao	:=	cOrgao+"Fone: "
	EndIf
	If !Empty(FieldGet(FieldPos("RCE_DDD")))
		cOrgao	:=	cOrgao+"("+FieldGet(FieldPos("RCE_DDD"))
		cOrgao	:=	AllTrim(cOrgao)+")"
	EndIF
	If !Empty(FieldGet(FieldPos("RCE_FONE")))
		cOrgao	:=	cOrgao+FieldGet(FieldPos("RCE_FONE"))
	EndIf
	cOrgao	:=	AllTrim(cOrgao)                  
EndIf

RestArea(aSaveSRA)

Return( cOrgao )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³GPER140   ºAutor  ³Microsiga           º Data ³  10/25/06   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³                                                            º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function AjustaGP140Sx1() 
Local aRegs	    := {}
Local aHelp		:= {}
Local aHelpE	:= {}
Local aHelpI	:= {}
Local cHelp		:= ""

aHelp := {	"Informe o numero do R.G. do  ",;
            "empregador."}
aHelpE:= { "Informe el numero de C.I. del",;
           " empleador."}
aHelpI:= { "Inform the employer´s ID number."}

cHelp := ".GPR14027."
PutSX1Help("P"+cHelp,aHelp,aHelpI,aHelpE)
Aadd(aRegs,{"GPR140","27","R.G. do Empregador?"      ,"¿C.I. del Empleador?"      ,"Employer´s ID?"      ,"MV_CHR","C",15,0,0,"G",,"MV_PAR27",""         ,""         ,""         ,"" ,"",""         ,""          ,""          ,"","",""             ,""             ,""             ,"","","","","","","","","","","","","","S",aHelp,aHelpI,aHelpE,cHelp})


aHelp := { "Informe o nome do empregador." }
aHelpE:= { "Informe el nombre del empleador." }
aHelpI:= { "Inform the employer´s name." }
cHelp := ".GPR14028."
PutSX1Help("P"+cHelp,aHelp,aHelpI,aHelpE)
Aadd(aRegs,{"GPR140","28","Nome do Empregador?"      ,"¿Nombre del empregador?"      ,"Employer´s name?"      ,"MV_CHS","C",30,0,0,"G",,"MV_PAR28",""         ,""         ,""         ,"" ,"",""         ,""          ,""          ,"","",""             ,""             ,""             ,"","","","","","","","","","","","","","S",aHelp,aHelpI,aHelpE,cHelp})


aHelp := {	"Informe o tipo do Salario   ",;
            "que deseja exibir, Salario  ",;
            "Incorporado ou Salario Base."}

aHelpE:= {	"Informe el tipo de Salario  ",;
            "que desea exibir, Salario   ",;
            "Incorporado o Salario Base."}
            
aHelpI:= {	"Inform the Salary type",;
            "you wish to print, Incorporated Salary ",;
            "or Base Salary."}
cHelp := ".GPR14029."
PutSX1Help("P"+cHelp,aHelp,aHelpI,aHelpE)
Aadd(aRegs,{cPerg ,"29","Tipo do Salario  ?"     		,"¿Tipo del Salario ?"   ,"Salary Type  ?"    		 ,"MV_CHT","C" ,01 ,0  ,1  ,"C"  ,""   ,"MV_PAR29","Termo Rescisão"    ,"Termo Rescisión"    ,"Rescission Term" ,""             ,""   ,"Sal.do Cadastro"          ,"Sal.del Cadastro"        ,"Sal. from Register"     ,""  ,"","" ,"" ,"" ,"","","" ,"","","","","","","" ,"","","","S",aHelp,aHelpI,aHelpE,cHelp})


aHelp := {	"Informe se deseja imprimir na linha  do  ",;
            "rodape a informação: Função   ",;
            "Centro de custo + banco/agencia." }

aHelpE:= {	"Informe se desea imprimir en la linha del  ",;
            "rodape la información: Función   ",;
            "Centro del costo + banco/agencia." }
            
aHelpI:= {	"Inform if you want to print on footer´s ",;
            "line the information: Function ",;
            "Cost Center + Bank/Agency." }
cHelp := ".GPR14030."
PutSX1Help("P"+cHelp,aHelp,aHelpI,aHelpE)
Aadd(aRegs,{cPerg ,"30","Imprimir inf.extra ?"    ,"¿Imprimir inf.extra ?"   ,"Print extra inf.?"    		 ,"MV_CHU","C" ,01 ,0  ,1  ,"C"  ,""   ,"MV_PAR30","Sim"    ,"Si"    ,"Yes" ,""             ,""   ,"Não"          ,"No"        ,"No"     ,""  ,"","" ,"" ,"" ,"","","" ,"","","","","","","" ,"","","","S",aHelp,aHelpI,aHelpE,cHelp})

aHelp := {	"Se desejar imprimir todas ",;
            " rescisoes complementares ",;
            "de acordo com parametros. " }

aHelpE:= {	"Se desear imprimir todas ",;
            " las rescisiones complementares ",;
            "de acuerdo con los parametros. " }
            
aHelpI:= {	"If you wish to print all ",;
            "complementary recissions ",;
            "according to parameters. " }

cHelp := ".GPR14031."
PutSX1Help("P"+cHelp,aHelp,aHelpI,aHelpE)
Aadd(aRegs,{cPerg ,"31","Todas rescisoes complem.?"    ,"¿Todas las Resc. Complem.?"   ,"All complementary recissions ?"  ,"MV_CHV","C" ,01 ,0  ,1  ,"C"  ,""   ,"MV_PAR31","Não"    ,"No"    ,"No" ,""             ,""   ,"Sim"          ,"Si"        ,"Yes"     ,""  ,"","" ,"" ,"" ,"","","" ,"","","","","","","" ,"","","","S",aHelp,aHelpI,aHelpE,cHelp})

If cPaisLoc == "BRA"
	aHelp	:= {"Selecione o grupo de verbas  que"		,;
	           "serão incorporadas ao salario. A "		,;
	           "definição de grupos é feita através"    ,;
	           " do campo 'RV_GRPVERB' no cadastro "	,;
	           " de Verbas                         "    ,;
	           "Obs.: Caso o período de referência "	,;
	           "da geração seja o mesmo da Folha de"	,;
	           "Pagamento em aberto para cálculo, "		,;
	           "as verbas serão pesquisadas nos   "		,;
	           "Lançamentos Mensais."                  }	
	                                                        
	
	aHelpE	:={"Seleccione el grupo de conceptos "		,;
				"que se incorporán al a sueldo.   "		,;
				"La definición de grupos se       "		,;
	            "realiza por medio del campo  "  	   	,;
	            "'RV_GRPVERB' en el  archivo de "	   	,;
	            " Conceptos."							,;
	            " Obs.: Si el período de referencia de"	,;
	            " la generación fuera el mismo de la  "	,;
	            "Planilla de Haberes abierta para "	   	,;
	            "cálculo, los conceptos se buscarán en"	,;
	            " los Registros Mensuales."    		   	}	
	
	aHelpI	:={ "Select budget group to "  				,;
	            "be incorporated into the salary.   " 	,;
	            "Groups are defined through field "   	,;
	            "RV_GRPVERB in the Budget "			  	,;
	            "Registration. "					  	,;	
	            "Obs.: If the generation reference "	,;
	            "period is the same as the Payroll "    ,;
	            " with pending calculation, budgets "	,;
	            "will be searched in Monthly Entries."	}
	
	cHelp := ".GPR14033."
	PutSX1Help("P"+cHelp,aHelp,aHelpI,aHelpE)
	
	
	//          "X1_GRUPO"	,"X1_ORDEM"	,"X1_PERGUNT"			   			,"X1_PERSPA"				   		,"X1_PERENG"						,"X1_VARIAVL"	,"X1_TIPO"	,"X1_TAMANHO"	,"X1_DECIMAL"	,"X1_PRESEL"	,"X1_GSC"	,"X1_VALID"			,"X1_VAR01"	,"X1_DEF01"			,"X1_DEFSPA1"	,"X1_DEFENG1"	,"X1_CNT01"	,"X1_VAR02"	,"X1_DEF02"			,"X1_DEFSPA2"		,"X1_DEFENG2"   ,"X1_CNT02"	,"X1_VAR03"	,"X1_DEF03"			,"X1_DEFSPA3"		,"X1_DEFENG3"		,"X1_CNT03"	,"X1_VAR04"	,"X1_DEF04"	,"X1_DEFSPA4"	,"X1_DEFENG4"	,"X1_CNT04"	,"X1_VAR05"	,"X1_DEF05"	,"X1_DEFSPA5"	,"X1_DEFENG5"	,"X1_CNT05"	,"X1_F3"	,"X1_GRPSXG","X1_PYME"	AHELPPOR	,AHELPENG	,AHELPSPA	,"X1_HELP" }
	aAdd(aRegs,{cPerg 		,"33"		,"Grupo Verbas Comp.Remuneraç."	,"¿Grupo Verbas comp. Remuner.?"	,"Grupo Verbas comp.Remunerac.?"	,"MV_CHY"		,"C"		,10				,0				,0				,"G"		,"fGrpVerba()"		,"mv_par33"	,""					,""				,""				,""    		,""      	,""					,""					,""				,""			,""			,""					,""					,""					,""			,""			,""			,""				,""				,""			,""			,""			,""				,""				,""			,""			,""			,"S"		,aHelp		,aHelpI		,aHelpE		,cHelp	})

Endif               

ValidPerg(aRegs,"GPR140",.T.)

Return nil 

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³GPER140IV ºAutor  ³WAGNER MONTENEGRO   º Data ³  15/12/2010 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³FUNÇÃO DE RETORNO PARA IDENTIFICADOR OU VERBA               º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function GPER140IV(cCampo,nOrdem)
Local aAreaOld:=GetArea()
Local cRet
SRV->(DbSetOrder(1))
IF SRV->(DbSeek(xFilial("SRV")+cCampo))
	If nOrdem==1
		cRet:=SRV->RV_CODFOL
	Else
		cRet:=SRV->RV_HOMOLOG
	Endif	
Endif
RestArea(aAreaOld)
Return(cRet)	

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³GPER140Sum ºAutor  ³WAGNER MONTENEGRO   º Data ³  15/12/2010 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³FUNCAO DE CONTROLE DE SOMA PARA TRCT HOMOLOGNET - BRA.       º±±
±±º          ³                                                             º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³IMPRESH                                                      º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function GPER140Sum(nOp,nColSum,cCond,lCampo,nRef,nType,lPD,cCondID)
//nOp = 1 Proventos 2=Descontos
//nColSum = 1=CODFOL 2=RV_HOMOLOG
Local nX,nY
Local nValor:= 0.00
Local nAvos	:=0     
Local lPagVal := .T.       //Paginar valor
Default lCampo:=.F.
Default nColSum :=0
Default cCond:=""
Default nRef:=3
Default nType:=0
Default lPD:= .F.
Default cCondID:=""
If !lCampo
	If nOp==1
		For nX:=1 to Len(aHomV)
			 If nColSum==1
			 	If Empty(cCondID) .or. ( !Empty(cCondID) .and. aHomV[nX,9] = cCondID )
					 //-- Pela Verba
					 IF lPD
						 If aHomV[nX,4]$cCond
						 	 nValor+=aHomV[nX,nRef] 
						 	 aHomV[nX,7]:=1
						 Endif
					 Else
					 //-- Pelo Identificador     
	 					 If aHomV[nX,5]$cCond
						 	 nValor+=aHomV[nX,nRef] 
						 	 aHomV[nX,7]:=1
					 	 Endif
					 Endif	 			 	 
			 	Endif
			 Endif
			 If nColSum==2
				If aHomV[nX,6]$cCond
					If Empty(cCondID) .or. ( !Empty(cCondID) .and. aHomV[nX,9] = cCondID )
						If aHomV[nX,6]$"013/014" .and. nRef == 2 
							nValor := 0
							lPagVal := .F.
                    	Else	
							nValor+= aHomV[nX,nRef] 
						EndIf	 
						aHomV[nX,7]:=1 
						If aHomV[nX,6]$"013/004/012/014"
							cPercSRV:=TransForm(PosSrv(aHomV[nX,4],xFilial("SRR"),"RV_PERC"),"@E 999.99")
						Endif	 				 	 
					Endif
				Endif
			 Endif
		Next                                                                          	
	Endif	  	 
	If nOp==2
		For nX:=1 to Len(aHomD)
			 If nColSum==1
				 If aHomD[nX,5]$cCond
				 	 nValor+=aHomD[nX,nRef]
				 	 aHomD[nX,7]:=1
				 Endif
			 Endif
			 If nColSum==2
				 If aHomD[nX,6]$cCond
				 	 nValor+=aHomD[nX,nRef]
				 	 aHomD[nX,7]:=1
				 Endif
			 Endif     
			 If nColSum==3
				 If aHomD[nX,4]$cPensao
				 	 nValor+=aHomD[nX,nRef]
				 	 aHomD[nX,7]:=1
				 Endif
			 Endif     
		Next
	Endif	  	 
Else
	If nOp==1
		For nY:= 1 to Len(aCpoForm)
			For nX:=1 to Len(aHomV)
				 If aCpoForm[nY,2]==1
					 If aHomV[nX,5]$aCpoForm[nY,4]
					 	 nValor+=aHomV[nX,3]
					 	 nAvos+=aHomV[nX,2]
					 	 aHomV[nX,7]:=1
					 Endif
				 Endif
				 If aCpoForm[nY,2]==2
					 If aHomV[nX,6]$aCpoForm[nY,4]
					 	 nValor+=aHomV[nX,3]
					 	 nAvos+=aHomV[nX,2]
					 	 aHomV[nX,7]:=1
					 Endif
				 Endif
			Next
			aCpoForm[nY,5]:=Transform(nValor,"@E 99,999,999.99")
			aCpoForm[nY,6]:=Transform(nAvos,"@E 999")
			nValor:=0
			nAvos:=0
		Next	  	 
		For nY:= 1 to Len(aCpoFor1)
			For nX:=1 to Len(aHomV)
				 If aCpoFor1[nY,2]==1
					 If aHomV[nX,5]$aCpoFor1[nY,4]
					 	 nValor+=aHomV[nX,3]
					 	 nAvos+=aHomV[nX,2]
					 	 aHomV[nX,7]:=1
					 Endif
				 Endif
				 If aCpoFor1[nY,2]==2
					 If aHomV[nX,6]$aCpoFor1[nY,4]
					 	 nValor+=aHomV[nX,3]
					 	 nAvos+=aHomV[nX,2]
					 	 aHomV[nX,7]:=1
					 Endif
				 Endif
			Next
			aCpoFor1[nY,5]:=Transform(nValor,"@E 99,999,999.99")
			aCpoFor1[nY,6]:=Transform(nAvos,"@E 999")
			nValor:=0
			nAvos:=0
		Next
	Endif
	If nOp==2
		For nY:= 1 to Len(aCpoFormD)
			For nX:=1 to Len(aHomD)
				If aCpoFormD[nY,1]<>"115"
					 If aCpoFormD[nY,2]==1
						 If aHomD[nX,5]$aCpoFormD[nY,4]
						 	 nValor+=aHomD[nX,3]
						 	 nAvos+=aHomD[nX,2]
						 	 aHomD[nX,7]:=1
						 Endif
					 Endif
					 If aCpoFormD[nY,2]==2
						 If aHomD[nX,6]$aCpoFormD[nY,4]
						 	 nValor+=aHomD[nX,3]
						 	 nAvos+=aHomD[nX,2]
						 	 aHomD[nX,7]:=1
						 Endif
					 Endif
				Endif
			Next
			aCpoFormD[nY,5]:=Transform(nValor,"@E 99,999,999.99")
			aCpoFormD[nY,6]:=Transform(nAvos,"@E 999")
			nValor:=0
			nAvos:=0
		Next
	Endif
Endif	
Return(If(lCampo,.T.,If(nRef=3,If(nType==0,Transform(nValor,"@E 99,999,999.99"),nValor),If(nType==1,If(nValor > 0 .or. lPagVal, nValor,),Transform(nValor,"@E 999")))))		 	



/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ GP140imp ³ Autor ³ R.H. - Mauricio MR    ³ Data ³ 11.01.11 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Calcula Periodo Aquisitivo para TRCT Homolognet - BRA.     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ GPER140(void)                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ IMPRESH 		                                              ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function fPerArquisitivo()                                                                                           
Local aArea			:= GetArea()
Local aAreaSRF		:= SRF->(GetArea())

Local dIPerAqui		:= Ctod("") 
Local dDtBasFim 	:= Ctod("")
Local dSvDtDem		:= dDataDem
Local nDiasVenc 	:= 0
Local nDiasProp 	:= 0      
Private aPerFerias  := {}

//-- Garante a existencia de registro na programacao de ferias
IF !SRF->( dbSeek( SRA->RA_FILIAL + SRA->RA_MAT ) )
	SRF->(RecLock("SRF",.T.))
	SRF->RF_FILIAL := SRA->RA_FILIAL
	SRF->RF_MAT    := SRA->RA_MAT
	SRF->RF_DATABAS:= SRA->RA_ADMISSA
	SRF->(MsUnlock())
Endif

dIPerAqui:=SRF->RF_DATABAS

//Verifica se pagou sabado e domingo e soma dois dias 'a data de demissao se esta for uma sexta feira
If SRG->RG_SABDOM == "1" .And. DoW( dDataDem ) == 6
	dDataDem += 2
EndIf
Calc_Fer(@dIPerAqui,dDataDem,,,,,,.F.)   

//Se houve afastamento R - X - W, altera os periodos no aPerFerias
If ( nPos := aScan(aPerFerias, {|x| x[6] > 0}) ) > 0
	AjPerRXW(nPos)    
Else
	Calc_Fer(dIPerAqui,dDataDem+If(cAvPrevio$ "S*I" .and. nDiasAv > 0,nDiasAv,0),,,,,,.F.)   
Endif

dDataDem := dSvDtDem

RestArea(aAreaSRF)
RestArea(aArea)
Return(aPerFerias)	
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ LoadCodDSR ³ Autor ³ R.H. - Mauricio MR  ³ Data ³ 13.01.11 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Carrega Verbas com incidencia de DSR.	     			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ GPER140(void)                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ IMPRESH 		                                              ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function LoadCodDSR(lLimpa,cFilPar)
Local aArea		:= GetArea() 
Local aSRVArea  := SRV->(GetArea())

Local cFilSRV 
Local cSRV		:= ''


DEFAULT cFilPar	:= cFilAnt
DEFAULT lLimpa	:= .F.

Static cDSRSLV	:= {}

IF lLimpa
	cDSRSLV	:= NIL
Else                       
	cFilSRV:= xFilial('SRV',cFilPar)
    //-- Verifica a necessidade de carregar SRV
    IF ( __SRVKey <> cFilSRV ) 
    	__SRVKey := cFilSRV
		cDSRSLV:=''   //-- Codigos de verbas de DSR s/ Salario Variavel campo 59'
	
		SRV->(Dbseek(cFilSRV))
		While !SRV->(EOF() .and. xFilial('SRV',RV_FILIAL) == cFilSRV ) 
			//-- Verbas com incidencia de DSR (com exceção de horas extras)
			IF !Empty(SRV->RV_CODDSR) .and. ( Upper(Alltrim(SRV->RV_DSRHE)) == "N" )
				cSRV  := Alltrim(Upper(SRV->RV_CODDSR)) 
				IF ! ( cSRV $  cDSRSLV )
					cDSRSLV +='/'+cSRV
				Endif	
			Else
				/*
					035 - DSR Hora Extra
					122 - DSR Sobre Comissoes Rescisao
					166 - DSR Sobre Comissäes
				*/
				IF ( SRV->RV_CODFOL $"035/122/166" )
					cSRV  := Alltrim(Upper(SRV->RV_COD)) 
					cDSRSLV +='/'+cSRV
				Endif
			Endif
			
			SRV->(DbSkip())
			
		Enddo         
    Endif
Endif

RestArea(aSRVArea)	
RestArea(aArea)	

Return(cDSRSLV)	



/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ AjustaSx1³ Autor ³ Mauricio MR		        ³ Data ³ 22/06/11 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Ajusta pergunta GPR140                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³                                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ GPER140                                                        ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/

Static Function AjustaSx1()

Local aArea 	:= GetArea()
Local aSX1Area 	:= SX1->(GetArea())

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Correcao do Pergunte GPR140  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DbSelectarea("SX1")
DbSetOrder(1)   
If SX1->( DbSeek( PADR( "GPR140", LEN(SX1->X1_GRUPO) ) +'32'))
	If UPPER(ALLTRIM(SX1->X1_VAR01)) == 'MV_PAR32'  .AND. (SX1->X1_GSC <> "G")
		RecLock("SX1",.F.)
		SX1->X1_GSC := "G"
		SX1->( MsUnlock() )
	EndIf
EndIf
                 
RestArea( aSX1Area )
RestArea( aArea )

Return Nil




/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³gp140GrpVerb ºAutor ³Mauricio MR       º Data ³  29/03/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Obter os codigos das verbas conforme o grupo selecionado.   º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ GPER140                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function gp140GrpVerb(cFilProc,cVerbas_Aux,cGrpVerbas) 
Local aArea	:= GetArea()
Local aAreaSRV

DEFAULT cFilProc := Space( FWGETTAMFILIAL )

	If ( SRV->( Type("RV_GRPVERB") ) # "U" )
	
		aAreaSRV := SRV->( getArea() )
		dbSelectArea("SRV")
		SRV->( dbGoTop() )
	
		While SRV->(!Eof()) .And. SRV->RV_FILIAL == cFilProc
			IF AllTrim( SRV->RV_GRPVERB ) $ cGrpVerbas .Or. cGrpVerbas $ AllTrim( SRV->RV_GRPVERB )
				cVerbas_Aux += AllTrim( SRV->RV_COD ) + "/" 	
			EndIf
			SRV->( dbSkip() )
		Enddo
		RestArea( aAreaSRV )
	EndIf

RestArea(aArea)	

Return


