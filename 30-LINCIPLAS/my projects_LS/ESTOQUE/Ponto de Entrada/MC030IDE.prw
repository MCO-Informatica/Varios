#INCLUDE "rwmake.ch"

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Programa   	MC030IDE
// Autor 		Alexandre Dalpiaz
// Data 		08/11/12
// Descricao  	PONTOS DE ENTRADA PARA KARDEX DIA - INCLUIR CAMPO NO KARDEX - DANIELJG
// Uso         	LaSelva
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// grava informações no arquivo temporário
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
User Function MC030GRV()
////////////////////////          
If alltrim(KDX->EMISSAO) <> 'Saldos'        
	KDX->DESTINO := paramixb[3,13]
EndIf
Return()

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Adiciona array mantendo tamanho maximo de elementos por  dimensao 
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
User Function MC030ARR()
////////////////////////
If ParamIxb[2] == 'SD2'
	aAdd(ParamIxb[1], iif(SD2->D2_TIPO $ 'DB', 'For - ','Cli - ') + SD2->D2_CLIENTE + '/' + SD2->D2_LOJA + ' - ' + iif(SD2->D2_TIPO $ 'DB', Posicione('SA2',1,xFilial('SA2') + SD2->D2_CLIENTE + SD2->D2_LOJA,'A2_NREDUZ'),Posicione('SA1',1,xFilial('SA1') + SD2->D2_CLIENTE + SD2->D2_LOJA,'A1_NREDUZ')))
ElseIf ParamIxb[2] == 'SD1'
	aAdd(ParamIxb[1], iif(SD1->D1_TIPO $ 'DB', 'Cli - ','For - ') + SD1->D1_FORNECE + '/' + SD1->D1_LOJA + ' - ' + iif(SD1->D1_TIPO $ 'DB', Posicione('SA1',1,xFilial('SA1') + SD1->D1_FORNECE + SD1->D1_LOJA,'A1_NREDUZ'),Posicione('SA2',1,xFilial('SA2') + SD1->D1_FORNECE + SD1->D1_LOJA,'A2_NREDUZ')))
ElseIf ParamIxb[2] == 'SD3'
	aAdd(ParamIxb[1], 'MOVIMENTOS INTERNOS')
Else
	aAdd(ParamIxb[1], ParamIxb[2])
EndIf
Return(ParamIxb[1])

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Ponto de entrada para alteracao da estrutura do arq. dbf temporario
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
User Function MC030IDE()
////////////////////////
Local _aRet := aClone(ParamIxb[1])

aadd(_aRet,{ "DESTINO", "C",33,0 })
aadd(_aRet,{ "CAMPO1", "C",3,0 })
aadd(_aRet,{ "CAMPO2", "C",3,0 })
aadd(_aRet,{ "CAMPO3", "C",3,0 })
aadd(_aRet,{ "CAMPO4", "C",3,0 })

Return(_aRet)

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// preenche aHeader e aCols
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
User Function MC030PRJ()                        
////////////////////////
Local _aHeader := aClone(ParamIxb[2])
Local _aCols   := aClone(ParamIxb[3])

If aScan(_aHeader, {|_1| Upper(AllTrim(_1[1])) == "ORIGEM/DESTINO"   }) == 0
	aAdd(_aHeader, {'Origem/Destino', 'A2_NOME','@!', 33, 0, , '', 'C', 'SA2', ''}) // DESTINO
	aAdd(_aCols,KDX->DESTINO)
Else
	_aCols[len(_aCols)] := KDX->DESTINO
EndIf	                      

Return({_aHeader, _aCols})

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
User Function MTC030MNU()
/////////////////////////
aAdd(aRotina,{"Consulta Laselva"	,"U_COMP001()"		,0,2 })
Return()