#INCLUDE "MATA410.CH"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "XMLXFUN.CH"
#INCLUDE "TBICONN.CH"
#INCLUDE "FWEVENTVIEWCONSTS.CH"       

#DEFINE DIRMASC "\MSXML\"
#DEFINE DIRXMLTMP "\MSXMLTMP\"
#DEFINE ITENSSC6 300                          
//здддддддддддддддддддддддддддддддддддддд©
//ЁDefine para tratamento do IVA AjustadoЁ
//юдддддддддддддддддддддддддддддддддддддды
#DEFINE __UFORI  01
#DEFINE __ALQORI 02
#DEFINE __PROPOR 03

#XCOMMAND CLOSETRANSACTION LOCKIN <aAlias,...>   => EndTran( \{ <aAlias> \}  ); End Sequence
Static aFreteP := {}

/*/
ээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбддддддддддд©╠╠
╠╠ЁFun┤└o    Ё MATA410  Ё Rev.  Ё Eduardo Riera         Ё Data Ё 26.08.2001Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддаддддддддддд╢╠╠
╠╠ЁDescri┤└o Ё Programa de atualizacao de Pedidos de Venda                 Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё Void MATA410(void)                                          Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё Generico                                                    Ё╠╠
╠╠цддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                      Ё╠╠
╠╠цддддддддддддддбддддддддбддддддбддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё PROGRAMADOR  Ё DATA   Ё BOPS Ё  MOTIVO DA ALTERACAO                    Ё╠╠
╠╠цддддддддддддддеддддддддеддддддеддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Andre Veiga  Ё24/01/06Ё089953Ё-Tratamento do codigo do cliente com PadRЁ╠╠
╠╠Ё              Ё        Ё      Ё para completar a posicao do array com o Ё╠╠
╠╠Ё              Ё        Ё      Ё tamanho correto do campo. No portal,    Ё╠╠
╠╠Ё              Ё        Ё      Ё o cliente 169 loja 01 estava sendo      Ё╠╠
╠╠Ё              Ё        Ё      Ё considerado como 16901 loja 01          Ё╠╠
╠╠Ё              Ё        Ё      Ё                                         Ё╠╠
╠╠юддддддддддддддаддддддддаддддддаддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function MATA410(xAutoCab,xAutoItens,nOpcAuto,lSimulacao,cRotina,cCodCli,cLoja,xRatCTBPC,xAdtPC)
//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Define Variaveis                                     Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
Local aCores   := {}
Local cRoda    := ""
Local bRoda    := {|| .T.}
Local xRet     := Nil
Local nPos	   := 0
Local cFilSQL  := Nil

PRIVATE lOnUpdate  := .T.	
PRIVATE l410Auto   := xAutoCab <> NIL  .And. xAutoItens <> NIL
PRIVATE aAutoCab   := {}
PRIVATE aAutoItens := {}
PRIVATE aRotina	 := MenuDef()
	
PRIVATE aRatCTBPC  := If(xRatCTBPC<>NIL,xRatCTBPC,{})
PRIVATE aAdtPC     := If(xAdtPC <> NIL,xAdtPC,{})
PRIVATE nAutoAdt   := If(nOpcAuto <> NIL,nOpcAuto,0)
PRIVATE cCadastro := OemToAnsi(STR0007) //"Atualiza┤└o de Pedidos de Venda"
If ( cPaisLoc != "BRA" )
	PRIVATE aArrayAE:={}
	PRIVATE lImpMsg:=.T.
EndIf
DEFAULT nOpcAuto := 3
DEFAULT lSimulacao := .F.
PRIVATE aHeadLEA :={} //Template GEM - Solidarios
PRIVATE aColsLEA :={} //Template GEM - Solidarios
PRIVATE lShowOpc := .T.

AjustaHelp()
AjustaSX6()	//Ajusta parametros

aCores := {{"Empty(C5_LIBEROK).And.Empty(C5_NOTA) .And. Empty(C5_BLQ)",'ENABLE' },;		//Pedido em Aberto
			{ "!Empty(C5_NOTA).Or.C5_LIBEROK=='E' .And. Empty(C5_BLQ)" ,'DISABLE'},;		   	//Pedido Encerrado
			{ "!Empty(C5_LIBEROK).And.Empty(C5_NOTA).And. Empty(C5_BLQ)",'BR_AMARELO'},;
			{ "C5_BLQ == '1'",'BR_AZUL'},;	//Pedido Bloquedo por regra
			{ "C5_BLQ == '2'",'BR_LARANJA'}}	//Pedido Bloquedo por verba

If cPaisLoc <> "BRA"
	Aadd(aCores,{})
	Ains(aCores,1)
	aCores[1] := {"AllTrim(C5_NOTA)=='REMITO'","BR_CINZA"}
Endif          

//зддддддддддддддддддддддддддддддддддддддддддд©
//ЁAjuste no pergunte MTA410				  Ё
//юддддддддддддддддддддддддддддддддддддддддддды
AjustaSX1()

//зддддддддддддддддддддддддддддддддддддддддддд©
//ЁAjuste no SX3                              Ё
//юддддддддддддддддддддддддддддддддддддддддддды
A410AjuSX3()

//зддддддддддддддддддддддддддддддддддддддддддд©
//ЁTratamento de Rotina Automatica            Ё
//юддддддддддддддддддддддддддддддддддддддддддды

If ValType( cRotina ) == "C"
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Faz tratamento para chamada por outra rotina             Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If !Empty( nScan := AScan( aRotina, { |x| Upper(Alltrim(x[2])) == Upper(Alltrim(cRotina)) } ) )
		bRoda := &( "{ || " + cRotina + "( 'SC5', SC5->( Recno() ), " + Str(nScan,2) + If(ValType(cCodCli)=="C",",nil,nil,nil,nil,nil,cCodCli,cLoja", "" ) + ") } " )
		xRet  := Eval( bRoda )
	EndIf

Else

	If  ( Type("l410Auto") <> "U" .And. l410Auto )
		//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Tratamento do tamanho do campo do C5_CLIENTE. Por algum motivo, Ё
		//Ё a MBrowseAuto sempre retornava este campo concatenado com o     Ё
		//Ё C5_LOJA, por isto, foi feito o tratamento com o PadR para manterЁ
		//Ё o tamanho do campo C5_CLIENTE cf. o SX3                         Ё
		//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		nPos := aScan( xAutoCab, {|x| Alltrim( Upper( x[1] ) ) == "C5_CLIENTE" } )
		If nPos > 0
			xAutoCab[nPos][2] := PadR( xAutoCab[nPos][2], TamSX3("C5_CLIENTE")[1] )
		Endif

		// Valida o array Rateio por CCusto - rotina automatica
		If a410RatAut(aRatCTBPC)
			lOnUpdate  := !lSimulacao
			aAutoCab   := xAutoCab
			aAutoItens := xAutoItens
			MBrowseAuto(nOpcAuto,Aclone(aAutoCab),"SC5")
			xAutoCab   := aAutoCab
			xAutoItens := aAutoItens
		Endif
	Else
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Define variaveis de parametrizacao de lancamentos    Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё MV_PAR01 Sugere Quantidade Liberada ? Sim/Nao        Ё
		//Ё MV_PAR02 Preco Venda Com Substituicao ? Sim?Nao      Ё
		//Ё MV_PAR03 Utiliz.Op.Triangular     ?   Sim/Nao        Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Ativa tecla F-10 para parametros                     Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
		SetKey(VK_F12,{|| a410Ativa()})

		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Ponto de Entrada para alterar cores do Browse do Cadastro    Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If ExistBlock("MA410COR")
			aCores := ExecBlock("MA410COR",.F.,.F.,aCores)
		Endif

		//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Endereca a funcao de BROWSE                          Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If ExistTemplate("MT410BRW")
			ExecTemplate("MT410BRW",.F.,.F.)
		EndIf

		If ExistBlock("MT410BRW")
			ExecBlock("MT410BRW",.F.,.F.)
		EndIf
		#IFDEF TOP
			If ExistBlock("M410FSQL")	// Filtro SQL na MBrowser
				cFilSQL := ExecBlock("M410FSQL",.F.,.F.)
			EndIf
		#ENDIF

		MBrowse( 6,1,22,75,'SC5',,,,,,aCores,,,,,,,,cFilSQL)
	Endif

EndIf

dbSelectArea("SC5")
dbSetOrder(1)
dbClearFilter()
SetKey(VK_F12,Nil)

Return(.T.)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    Ёa410VisualЁ Rev.  ЁEduardo Riera          Ё Data Ё26.08.2001Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁVisualizacao do Pedido de Venda                             Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁExpC1: Alias do cabecalho do pedido de venda                Ё╠╠
╠╠Ё          ЁExpN2: Recno do cabecalho do pedido de venda                Ё╠╠
╠╠Ё          ЁExpN3: Opcao do arotina                                     Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁNenhum                                                      Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁDescri┤└o ЁEsta rotina tem como objetivo efetuar a interface com o usuaЁ╠╠
╠╠Ё          Ёrio e o pedido de vendas                                    Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁObservacaoЁ                                                            Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё Materiais/Distribuicao/Logistica                           Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function A410Visual(cAlias,nReg,nOpc)

Local aArea    := GetArea()
Local aCpos1   := {"C6_QTDVEN ","C6_QTDLIB"}
Local aCpos2   := {}
Local aBackRot := aClone(aRotina)
Local aPosObj  := {}
Local aObjects := {}
Local aSize    := {}
Local aPosGet  := {}
Local aInfo    := {}

Local lContinua:= .T.
Local lGrade   := MaGrade()
Local lQuery   := .F.
Local lFreeze   := (SuperGetMv("MV_PEDFREZ",.F.,0) <> 0)

Local nGetLin  := 0
Local nOpcA    := 0
Local nTotPed  := 0
Local nTotDes  := 0
Local nCntFor  := 0
Local nNumDec  := TamSX3("C6_VALOR")[2]
Local nColFreeze:= SuperGetMv("MV_PEDFREZ",.F.,0)

Local cArqQry  := "SC6"
Local cCadastro:= IIF(cCadastro == Nil,OemToAnsi("STR0007"),cCadastro) //"Atualiza┤└o de Pedidos de Venda"
Local oGetd
Local oSAY1
Local oSAY2
Local oSAY3
Local oSAY4
Local oDlg
Local lMt410Ace:= Existblock("MT410ACE")

Local bCond     := {|| .T. }
Local bAction1  := {|| Mta410Vis(cArqQry,@nTotPed,@nTotDes,lGrade) }	
Local bAction2  := {|| .T. }
Local cSeek     := ""
Local aNoFields := {"C6_NUM","C6_QTDEMP","C6_QTDENT","C6_QTDEMP2","C6_QTDENT2"}		// Campos que nao devem entrar no aHeader e aCols
Local bWhile    := {|| }
Local cQuery    := ""
Local lGrdOk	:= If(lGrade.And.MatOrigGrd()=="SB4",If(FindFunction("VldDocGrd"),VldDocGrd(1,SC5->C5_NUM),.T.),.T.)
Local aRecnoSE1RA
Local aHeadAGG    := {}
Local aColsAGG    := {}

//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Inicializa a Variaveis Privates.                     Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
PRIVATE aTrocaF3  := {}
PRIVATE aTELA[0][0],aGETS[0]
PRIVATE aHeader	  := {}
PRIVATE aCols	  := {}
PRIVATE aHeadFor  := {}
PRIVATE aColsFor  := {}
PRIVATE N         := 1
PRIVATE aGEMCVnd  := {"",{},{}} //Template GEM - Condicao de Venda 
PRIVATE oGetPV	  := Nil

//зддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁVerifica se o campo de codigo de lancamento cat 83 Ё
//Ёdeve estar visivel no acols                        Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддды
If SC6->(FieldPos("C6_CODLAN"))>0 .and. !SuperGetMV("MV_CAT8309",,.F.)
	aAdd(aNoFields,"C6_CODLAN")
EndIf

//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Ponto de entrada para validar acesso do usuario na funcao Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If lMt410Ace
	lContinua := Execblock("MT410ACE",.F.,.F.,{nOpc})
Endif

//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Cria Ambiente/Objeto para tratamento de grade        Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды        
If FindFunction("MsMatGrade") .And. IsAtNewGrd()
	PRIVATE oGrade	  := MsMatGrade():New('oGrade',,"C6_QTDVEN",,"a410GValid()",;
	  						{ 	{VK_F4,{|| A440Saldo(.T.,oGrade:aColsAux[oGrade:nPosLinO][aScan(oGrade:aHeadAux,{|x| AllTrim(x[2])=="C6_LOCAL"})])}} },;
	  						{ 	{"C6_QTDVEN",NIL,NIL},;
	  							{"C6_QTDLIB",NIL,NIL},;
	  							{"C6_QTDENT",NIL,NIL},;
	  							{"C6_ITEM"	,NIL,NIL},;
	  							{"C6_UNSVEN",NIL,NIL},;
	  							{"C6_OPC",NIL,NIL},;
	  							{"C6_BLQ",NIL,NIL}})

	//-- Inicializa grade multicampo
	A410InGrdM()
Else
	PRIVATE aColsGrade:= {}
	PRIVATE aHeadGrade:= {}
EndIf

If Type("Inclui") == "U"
	Inclui := .F.
	Altera := .F.
EndIf
Pergunte("MTA410",.F.)
If ( lGrade )
	aRotina[nOpc][4] := 6
EndIf

If lGrdOk
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Inicializa a Variaveis da Enchoice.                  Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
	RegToMemory( "SC5", .F., .F. )
	
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Filtros para montagem do aCols                       Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
	dbSelectArea("SC6")
	dbSetOrder(1)
	#IFDEF TOP
		lQuery  := .T.
		cQuery := "SELECT * "
		cQuery += "FROM "+RetSqlName("SC6")+" SC6 "
		cQuery += "WHERE SC6.C6_FILIAL='"+xFilial("SC6")+"' AND "
		cQuery += "SC6.C6_NUM='"+SC5->C5_NUM+"' AND "
		cQuery += "SC6.D_E_L_E_T_<>'*' "
		cQuery += "ORDER BY "+SqlOrder(SC6->(IndexKey()))
	
		dbSelectArea("SC6")
		dbCloseArea()
	#ENDIF
	cSeek  := xFilial("SC6")+SC5->C5_NUM
	bWhile := {|| C6_FILIAL+C6_NUM }
	
	//зддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Montagem do aHeader e aCols                           Ё
	//юддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁFillGetDados( nOpcx, cAlias, nOrder, cSeekKey, bSeekWhile, uSeekFor, aNoFields, aYesFields, lOnlyYes,       Ё
	//Ё				  cQuery, bMountFile, lInclui )                                                                Ё
	//ЁnOpcx			- Opcao (inclusao, exclusao, etc).                                                         Ё
	//ЁcAlias		- Alias da tabela referente aos itens                                                          Ё
	//ЁnOrder		- Ordem do SINDEX                                                                              Ё
	//ЁcSeekKey		- Chave de pesquisa                                                                            Ё
	//ЁbSeekWhile	- Loop na tabela cAlias                                                                        Ё
	//ЁuSeekFor		- Valida cada registro da tabela cAlias (retornar .T. para considerar e .F. para desconsiderar Ё
	//Ё				  o registro)                                                                                  Ё
	//ЁaNoFields	- Array com nome dos campos que serao excluidos na montagem do aHeader                         Ё
	//ЁaYesFields	- Array com nome dos campos que serao incluidos na montagem do aHeader                         Ё
	//ЁlOnlyYes		- Flag indicando se considera somente os campos declarados no aYesFields + campos do usuario   Ё
	//ЁcQuery		- Query para filtro da tabela cAlias (se for TOP e cQuery estiver preenchido, desconsidera     Ё
	//Ё	           parametros cSeekKey e bSeekWhiele)                                                              Ё
	//ЁbMountFile	- Preenchimento do aCols pelo usuario (aHeader e aCols ja estarao criados)                     Ё
	//ЁlInclui		- Se inclusao passar .T. para qua aCols seja incializada com 1 linha em branco                 Ё
	//ЁaHeaderAux	-                                                                                              Ё
	//ЁaColsAux		-                                                                                              Ё
	//ЁbAfterCols	- Bloco executado apos inclusao de cada linha no aCols                                         Ё
	//ЁbBeforeCols	- Bloco executado antes da inclusao de cada linha no aCols                                     Ё
	//ЁbAfterHeader -                                                                                              Ё
	//ЁcAliasQry	- Alias para a Query                                                                           Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	FillGetDados(nOPc,"SC6",1,cSeek,bWhile,{{bCond,bAction1,bAction2}},aNoFields,/*aYesFields*/,/*lOnlyYes*/,cQuery,/*bMontCols*/,.F.,/*aHeaderAux*/,/*aColsAux*/,{|| AfterCols(cArqQry) },/*bBeforeCols*/,/*bAfterHeader*/,"SC6")
	
	If "MATA410" $ SuperGetMV("MV_GRDMULT",.F.,"") .And. lGrade
		aCols := aColsGrade(oGrade,aCols,aHeader,"C6_PRODUTO","C6_ITEM","C6_ITEMGRD",aScan(aHeader,{|x| AllTrim(x[2]) == "C6_DESCRI"}))
	EndIf

	If AliasInDic("AGG")
		A410FRat(@aHeadAGG,@aColsAGG)	
	EndIf
	
	If lQuery
		dbSelectArea("SC6")
		dbCloseArea()
		ChkFile("SC6",.F.)
	EndIf
	
	
	For nCntFor := 1 To Len(aHeader)
		If aHeader[nCntFor][8] == "M"
			aadd(aCpos1,aHeader[nCntFor][2])
		EndIf
	Next nCntFor
	
	nTotPed  -= M->C5_DESCONT
	nTotDes  += M->C5_DESCONT
	nTotDes  += A410Arred(nTotPed*M->C5_PDESCAB/100,"C6_VALOR")
	nTotPed  -= A410Arred(nTotPed*M->C5_PDESCAB/100,"C6_VALOR")
	If ( lQuery )
		dbSelectArea(cArqQry)
		dbCloseArea()
		ChkFile("SC6",.F.)
		dbSelectArea("SC6")
	EndIf
	//зддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁMonta o array com as formas de pagamento       Ё
	//юддддддддддддддддддддддддддддддддддддддддддддддды
	Ma410MtFor(@aHeadFor,@aColsFor)
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Caso nao ache nenhum item , abandona rotina.         Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If ( Len(aCols) == 0 )
		Help(" ",1,"A410SEMREG")
		lContinua := .F.
	EndIf
	//зддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Ponto de Entrada para visualizao do pedido de vendas  Ё
	//юддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	
	If ExistBlock("M410VIS")
		ExecBlock("M410VIS",.F.,.F.)
	EndIf
	
	If ( lContinua )
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Faz o calculo automatico de dimensoes de objetos     Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
		aSize := MsAdvSize()
		aObjects := {}
		AAdd( aObjects, { 100, 100, .t., .t. } )
		AAdd( aObjects, { 100, 100, .t., .t. } )
		AAdd( aObjects, { 100, 020, .t., .f. } )
	
		aInfo := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 3, 3 }
		aPosObj := MsObjSize( aInfo, aObjects )
	
		aPosGet := MsObjGetPos(aSize[3]-aSize[1],315,;
			{{003,033,160,200,240,263}} )
	
		DEFINE MSDIALOG oDlg TITLE cCadastro From aSize[7],0 to aSize[6],aSize[5] of oMainWnd PIXEL
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Estabelece a Troca de Clientes conforme o Tipo do Pedido de Venda      Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If ( M->C5_TIPO $ "DB" )
			aTrocaF3 := {{"C5_CLIENTE","SA2"}}
		Else
			aTrocaF3 := {}
		EndIf
		oGetPV:=MSMGet():New( cAlias, nReg, nOpc, , , , , aPosObj[1],aCpos2,3,,,"A415VldTOk")
		nGetLin := aPosObj[3,1]
		//@ nGetLin,aPosGet[1,1]  SAY OemToAnsi(IIF(M->C5_TIPO$"DB",STR0008,STR0009)) SIZE 020,09 PIXEL	//"Fornec.:"###"Cliente: "
		@ nGetLin,aPosGet[1,2]  SAY oSAY1 VAR Space(40)						SIZE 120,09 PICTURE "@!" OF oDlg PIXEL
		@ nGetLin,aPosGet[1,3]  SAY OemToAnsi(STR0011)						SIZE 020,09 OF oDlg PIXEL	//"Total :"
		@ nGetLin,aPosGet[1,4]  SAY oSAY2 VAR 0 PICTURE TM(0,22,Iif(cPaisloc=="CHI" .And. M->C5_MOEDA == 1,NIL,nNumDec))		SIZE 060,09 OF oDlg	PIXEL
		@ nGetLin,aPosGet[1,5]  SAY OemToAnsi(STR0012)						SIZE 030,09 OF oDlg PIXEL	//"Desc. :"
		@ nGetLin,aPosGet[1,6]  SAY oSAY3 VAR 0 PICTURE TM(0,22,Iif(cPaisloc=="CHI" .And. M->C5_MOEDA == 1,NIL,nNumDec))		SIZE 060,09 OF oDlg	PIXEL RIGHT
		@ nGetLin+10,aPosGet[1,5] SAY OemToAnsi("=")							SIZE 020,09 OF oDlg PIXEL
		@ nGetLin+10,aPosGet[1,6] SAY oSAY4 VAR 0								SIZE 060,09 PICTURE TM(0,22,Iif(cPaisloc=="CHI" .And. M->C5_MOEDA == 1,NIL,nNumDec)) OF oDlg PIXEL RIGHT
		oDlg:Cargo	:= {|c1,n2,n3,n4| oSay1:SetText(c1),;
			oSay2:SetText(n2),;
			oSay3:SetText(n3),;
			oSay4:SetText(n4) }
		oGetd   := MsGetDados():New(aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],nOpc,,,"",,aCpos1,nColFreeze,,,"A410FldOk(1)",,,,,lFreeze)	
		A410Bonus(2)
		Ma410Rodap(oGetd,nTotPed,nTotDes)
		ACTIVATE MSDIALOG oDlg ON INIT (A410Limpa(.F.,M->C5_TIPO),Ma410Bar(oDlg,{||nOpcA:=1,oDlg:End()},{||oDlg:End()},nOpc,oGetD,nTotPed,@aRecnoSE1RA,@aHeadAGG,@aColsAGG))
	EndIf
	
	//зддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Ponto de Entrada apos visualizao do pedido de vendas  Ё
	//юддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If ExistBlock("MTA410V")
		ExecBlock("MTA410V",.F.,.F.)
	EndIf
EndIf	

aRotina := aClone(aBackRot)
RestArea(aArea)

Return( nOpcA )

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    Ёa410AlteraЁ Rev.  ЁEduardo Riera          Ё Data Ё26.08.2001Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁAlteracao do Pedido de Venda                                Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁExpC1: Alias do cabecalho do pedido de venda                Ё╠╠
╠╠Ё          ЁExpN2: Recno do cabecalho do pedido de venda                Ё╠╠
╠╠Ё          ЁExpN3: Opcao do arotina                                     Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁNenhum                                                      Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁDescri┤└o ЁEsta rotina tem como objetivo efetuar a interface com o usuaЁ╠╠
╠╠Ё          Ёrio e o pedido de vendas                                    Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁObservacaoЁ                                                            Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё Materiais/Distribuicao/Logistica                           Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function A410Altera(cAlias,nReg,nOpc)

Local aArea     := GetArea()
Local aPosObj   := {}
Local aObjects  := {}
Local aSize     := {}
Local aPosGet   := {}
Local aRegSC6   := {}
Local aRegSCV   := {}
Local aInfo     := {}
Local lLiber 	:= .F.
Local lTransf	:= .F.
Local lGrade	:= MaGrade()
Local lBloqueio := .T.
Local lNaoFatur := .F.
Local lContrat  := .F.
Local lQuery    := .F.
Local lContinua := .T.
Local lMt410Alt := Existblock("MT410ALT")
Local lM410Stts := ExistBlock("M410STTS")
Local lTM410Stts:= ExistTemplate("M410STTS")
Local lM410Bar  := ExistBlock("M410CODBAR")
Local lFreeze   := (SuperGetMv("MV_PEDFREZ",.F.,0) <> 0)
Local lContTPV  := SuperGetMv("MV_TELAPVX",.F.,.F.)
Local lAltPrcCtr:= (SuperGetMv("MV_ALTCTR2",.F.,"2") == "1")
Local nOpcA		:= 0
Local nCntFor   := 0
Local nTotalPed := 0
Local nTotalDes := 0
Local nAux	    := 0
Local nNumDec   := TamSX3("C6_VALOR")[2]
Local nGetLin   := 0
Local nStack    := GetSX8Len()
Local nColFreeze:= SuperGetMv("MV_PEDFREZ",.F.,0)
Local cArqQry   := "SC6"
Local cCadastro := IIF(cCadastro == Nil,OemToAnsi("STR0007"),cCadastro) //"Atualiza┤└o de Pedidos de Venda"
Local oDlg
Local oGetd
Local oSAY1
Local oSAY2
Local oSAY3
Local oSAY4
Local lMt410Ace := Existblock("MT410ACE")
Local nX 		:= 0
//Gestao de Contratos
Local lGCT     := ((SC5->(FieldPos("C5_MDCONTR")) > 0) .And. !Empty(SC5->C5_MDCONTR))
Local aPedCpo  := NIL

Local cSeek     := ""
Local aNoFields := {"C6_NUM","C6_QTDEMP","C6_QTDENT","C6_QTDEMP2","C6_QTDENT2"}		// Campos que nao devem entrar no aHeader e aCols
Local bWhile    := {|| }
Local cQuery    := ""
Local bCond     := {|| .T. }
Local bAction1  := {|| Mta410Alt(cArqQry,@nTotalPed,@nTotalDes,lGrade,@lBloqueio,@lNaoFatur,@lContrat,@aRegSC6) }	
Local bAction2  := {|| .T. }
Local aRecnoSE1RA := {} // Array com os titulos selecionados pelo Adiantamento   
Local aMTA177PER := {}  // Array para carregar as perguntas de central de compras
Local aHeadAGG    := {}
Local aColsAGG    := {}

//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁArray para controlar relacionamento com SD4 (Remessa para Beneficiamento Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
PRIVATE aColsBn := A410CarBen(SC5->C5_NUM)
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁCriar array PRIVATE p/ integracao com sistema de Distribuicao - NAO REMOVER Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
PRIVATE aDistrInd:={}
//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Variaveis utilizadas na LinhaOk                      Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
PRIVATE aCols      := {}
PRIVATE aHeader    := {}
PRIVATE aHeadFor   := {}
PRIVATE aColsFor   := {}
PRIVATE N          := 1
PRIVATE oGetPV		:= nil

//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Monta a entrada de dados do arquivo                  Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
PRIVATE aTELA[0][0],aGETS[0]

PRIVATE aGEMCVnd :={"",{},{}} //Template GEM - Condicao de Venda

//зддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁVerifica se o usuario tem premissao para alterar o Ё
//Ёpedido de venda                                    Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддды
If cPaisLoc <> "BRA" .AND. FieldPos("C5_CATPV") > 0 .AND. !Empty(SC5->C5_CATPV)
	If AliasIndic("AGS") //Tabela que relaciona usuario com os Tipos de Pedidos de vendas que ele tem acesso
		DBSelectArea("AGS")
		DBSetOrder(1)
		If DBSeek(xFilial("AGS") + __cUserId) //Se nЦo encontrar o usuАrio na tabela, permite ele alterar o pedido
			If !DBSeek(xFilial("AGS") + __cUserId + SC5->C5_CATPV) //Verifica se o usuario tem premissao
				MsgStop(STR0167 + " " +STR0004 + " " + STR0168)//"Este usuario nao tem permissao para alterar pedidos de venda com essa categoria."
				lContinua := .F.
			EndIf
		EndIf
	EndIf
EndIf

//зддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁVerifica se o campo de codigo de lancamento cat 83 Ё
//Ёdeve estar visivel no acols                        Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддды
If SC6->(FieldPos("C6_CODLAN"))>0 .and. !SuperGetMV("MV_CAT8309",,.F.)
	aAdd(aNoFields,"C6_CODLAN")
EndIf

//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Ponto de entrada para validar acesso do usuario na funcao Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If lMt410Ace
	lContinua := Execblock("MT410ACE",.F.,.F.,{nOpc})
Endif

//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Cria Ambiente/Objeto para tratamento de grade        Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды        
If FindFunction("MsMatGrade") .And. IsAtNewGrd()
	PRIVATE oGrade	  := MsMatGrade():New('oGrade',,"C6_QTDVEN",,"a410GValid()",;
							{ 	{VK_F4,{|| A440Saldo(.T.,oGrade:aColsAux[oGrade:nPosLinO][aScan(oGrade:aHeadAux,{|x| AllTrim(x[2])=="C6_LOCAL"})])}} },;
	  						{ 	{"C6_QTDVEN",.T., {{"C6_UNSVEN",{|| ConvUm(AllTrim(oGrade:GetNameProd(,nLinha,nColuna)),aCols[nLinha][nColuna],0,2) } }} },;
	  							{"C6_QTDLIB",NIL,NIL},;
	  							{"C6_QTDENT",NIL,NIL},;
	  							{"C6_ITEM"	,NIL,NIL},;
	  							{"C6_UNSVEN",NIL, {{"C6_QTDVEN",{|| ConvUm(AllTrim(oGrade:GetNameProd(,nLinha,nColuna)),0,aCols[nLinha][nColuna],1) }}} },;
	  							{"C6_OPC",NIL,NIL},;
	  							{"C6_BLQ",NIL,NIL}})	  							

	//-- Inicializa grade multicampo
	A410InGrdM(.T.)
Else
	PRIVATE aColsGrade := {}
	PRIVATE aHeadgrade := {}	
EndIf
	
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁCarrega perguntas do MTA177, MATA440 e MATA410                          Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
Pergunte ("MTA177",.F.)
AADD (aMTA177PER,{MV_PAR17,MV_PAR18} )
Pergunte("MTA440",.F.)
lLiber := MV_PAR02 == 1
lTransf:= MV_PAR01 == 1
Pergunte("MTA410",.F.)
//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Variavel utilizada p/definir Op. Triangulares.       Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
IsTriangular( MV_PAR03==1 )
//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Salva a integridade dos campos de Bancos de Dados    Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
dbSelectArea(cAlias)
IF ( (ExistTemplate("M410ALOK")) )
	If (! ExecTemplate("M410ALOK",.F.,.F.) )
		lContinua := .F.
	EndIf
EndIf

IF lContinua .And. ( (ExistBlock("M410ALOK")) )
	If (! ExecBlock("M410ALOK",.F.,.F.) )
		lContinua := .F.
	EndIf
EndIf
//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁEXECUTAR CHAMADA DE FUNCAO p/ integracao com sistema de Distribuicao - NAO REMOVER Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If lContinua .And. SuperGetMv("MV_FATDIST") == "S" // Apenas quando utilizado pelo modulo de Distribuicao
	lContinua:=D410Alok()
EndIf
IF ( SC5->C5_FILIAL <> xFilial("SC5") )
	Help(" ",1,"A000FI")
	lContinua := .F.
EndIf

//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//| Se o Pedido foi originado no SIGAEEC - Nao Altera    |
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
dbSelectArea("SC5")
IF !Empty(SC5->C5_PEDEXP) .And. nModulo != 29 .And. ( Type("l410Auto") == "U" .OR. !l410Auto )
	Help(" ",1,"MTA410ALT")
	lContinua := .F.
EndIf

//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//| Se o Pedido foi originado no SIGATMS - Nao Altera    |
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
If SC5->(FieldPos("C5_SOLFRE")) > 0 .And. !Empty(SC5->C5_SOLFRE)
	Help(" ",1,"A410TMSNAO")
	lContinua := .F.
EndIf

//зддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//| Se o Pedido foi originado no SIGALOJA - Nao Altera    |
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If SC5->(FieldPos("C5_ORCRES")) > 0 .AND. !Empty(SC5->C5_ORCRES) .AND. (Type("l410Auto") == "U" .OR. !l410Auto)
	//"Este Pedido foi gerado atravИs do mСdulo de Controle de Lojas, e nЦo poderА ser alterado."
	MsgAlert(STR0115)
	lContinua := .F.
EndIf

//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//| Verifica se o pedido tem carga montada               |
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
If OmsHasCg(SC5->C5_NUM)
	Help(" ",1,"A410CARGA")
Endif 

//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//| Se o Pedido foi originado no SIGAGCT - Nao Altera    |
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды    
IF lGCT .And. (FunName() != "CNTA120" .And. FunName() != "CNTA150") .And. !ExistBlock("GCTPEDCPO")
	Help(" ",1,"MTA410AGCT")
	lContinua := .F.
EndIf

//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Salva a integridade dos campos de Bancos de Dados    Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
dbSelectArea(cAlias)
If !SoftLock(cAlias)
	lContinua := .F.
EndIf
//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Inicializa desta forma para criar uma nova instancia de variaveis private Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
RegToMemory( "SC5", .F., .F. )

If lContinua
	lContinua := If(lGrade.And.MatOrigGrd()=="SB4",If(FindFunction("VldDocGrd"),VldDocGrd(1,SC5->C5_NUM),.T.),.T.)
EndIf

//-- Como e alteracao so ira mostrar opcionais
//-- caso alterere algo na linha (controle na A410FldOk)
If Type("lShowOpc") == "L"
	lShowOpc := .F.
EndIf

If ( lContinua )
	dbSelectArea("SC6")
	dbSetOrder(1)
	#IFDEF TOP
		If TcSrvType()<>"AS/400" .And. !InTransact() .And. Ascan(aHeader,{|x| x[8] == "M"}) == 0
			lQuery  := .T.
			cQuery := "SELECT SC6.*,SC6.R_E_C_N_O_ SC6RECNO "
			cQuery += "FROM "+RetSqlName("SC6")+" SC6 "
			cQuery += "WHERE SC6.C6_FILIAL='"+xFilial("SC6")+"' AND "
			cQuery += "SC6.C6_NUM='"+SC5->C5_NUM+"' AND "
			cQuery += "SC6.D_E_L_E_T_<>'*' "
			cQuery += "ORDER BY "+SqlOrder(SC6->(IndexKey()))

			dbSelectArea("SC6")
			dbCloseArea()
		EndIf	
	#ENDIF
	cSeek  := xFilial("SC6")+SC5->C5_NUM
	bWhile := {|| C6_FILIAL+C6_NUM }

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Montagem do aHeader e aCols                          Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
	FillGetDados(nOPc,"SC6",1,cSeek,bWhile,{{bCond,bAction1,bAction2}},aNoFields,/*aYesFields*/,/*lOnlyYes*/,cQuery,/*bMontCols*/,Inclui,/*aHeaderAux*/,/*aColsAux*/,{|| AfterCols(cArqQry) },/*bBeforeCols*/,/*bAfterHeader*/,"SC6")	

	If "MATA410" $ SuperGetMV("MV_GRDMULT",.F.,"") .And. lGrade
		aCols := aColsGrade(oGrade,aCols,aHeader,"C6_PRODUTO","C6_ITEM","C6_ITEMGRD",aScan(aHeader,{|x| AllTrim(x[2]) == "C6_DESCRI"}))
	EndIf

 	If AliasInDic("AGG")
		A410FRat(@aHeadAGG,@aColsAGG)	
	EndIf

	nTotalPed  -= M->C5_DESCONT
	nTotalDes  += M->C5_DESCONT
	nTotalDes  += A410Arred(nTotalPed*M->C5_PDESCAB/100,"C6_VALOR")
	nTotalPed  -= A410Arred(nTotalPed*M->C5_PDESCAB/100,"C6_VALOR")
	
	If ( lQuery )
		dbSelectArea(cArqQry)
		dbCloseArea()
		ChkFile("SC6",.F.)
		dbSelectArea("SC6")
	EndIf
EndIf
//зддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁMonta o array com as formas de pagamento do SX5Ё
//юддддддддддддддддддддддддддддддддддддддддддддддды
Ma410MtFor(@aHeadFor,@aColsFor,@aRegSCV)
//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Caso nao ache nenhum item , abandona rotina.         Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
If ( lContinua )
	//
	// Template GEM - Gestao de Empreendimentos Imobiliarios
	//
	// faz a copia da condicao de venda se a mesma tiver 
	// uma vinculacao com a condicao de pagamento
	//
	If ExistBlock("GEM410PV")
		aGEMCVnd := ExecBlock("GEM410PV",.F.,.F.,{ M->C5_NUM ,M->C5_CONDPAG ,M->C5_EMISSAO ,nTotalPed })
	ElseIf ExistTemplate("GEM410PV")
		// Copia a condicao de venda
		aGEMCVnd := ExecTemplate("GEM410PV",.F.,.F.,{ M->C5_NUM ,M->C5_CONDPAG ,M->C5_EMISSAO ,nTotalPed })
	EndIf

	If ( Len(aCols) == 0 )
		lContinua := .F.
		Help(" ",1,"A410S/ITEM")
	EndIf
	If ( (ExistBlock("M410GET")) )
		ExecBlock("M410GET",.F.,.F.)
	EndIf
	If ( lBloqueio )
		Help(" ",1,"A410ELIM")
		lContinua := .F.
	EndIf
	If (!(SuperGetMv("MV_ALTPED")=="S") .And. !lNaoFatur) .And. !(!Empty(SC5->C5_PEDEXP) .And. SuperGetMv("MV_EECFAT") .And. FindFunction("AVINTEMB") .And. AvIntEmb())
		Help(" ",1,"A410PEDFAT")
		lContinua := .F.
	EndIf
	If ( lContrat ) .And. !lAltPrcCtr
		Help(" ",1,"A410CTRPAR")
		lContinua := .F.
	EndIf
EndIf
If ( lContinua )

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Atualiza com cliente original do pedido caso troque  Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
	A410ChgCli(M->C5_CLIENTE+M->C5_LOJACLI)

	If ( Type("l410Auto") == "U" .OR. !l410Auto )
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Faz o calculo automatico de dimensoes de objetos     Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
		aSize := MsAdvSize()
		aObjects := {}
		AAdd( aObjects, { 100, 100, .t., .t. } )
		AAdd( aObjects, { 100, 100, .t., .t. } )
		AAdd( aObjects, { 100, 020, .t., .f. } )
		aInfo := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 3, 3 }
		aPosObj := MsObjSize( aInfo, aObjects )
		aPosGet := MsObjGetPos(aSize[3]-aSize[1],315,{{003,033,160,200,240,263}} )
		nGetLin := aPosObj[3,1]
		If lContTPV		
			DEFINE MSDIALOG oDlg TITLE cCadastro From aSize[7],0 to aSize[6],aSize[5] of oMainWnd PIXEL STYLE nOR( WS_VISIBLE ,DS_MODALFRAME )
		Else 
			DEFINE MSDIALOG oDlg TITLE cCadastro From aSize[7],0 to aSize[6],aSize[5] of oMainWnd PIXEL
		EndIf
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Armazenar dados do Pedido anterior.                  Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
		IF M->C5_TIPO $ "DB"
			aTrocaF3 := {{"C5_CLIENTE","SA2"}}
		Else
			aTrocaF3 := {}
		EndIf

		//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Verifica campos especificos para edicao - SIGAGCT    Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If lGCT
			aPedCpo := {{},{}}
			If ExistBlock("GCTPEDCPO")
				aPedCpo := ExecBlock("GCTPEDCPO",.F.,.F.)
			EndIf
		EndIf

		oGetPV:=MSMGet():New( "SC5", nReg, nOpc, , , , , aPosObj[1],If(lGCT,aPedCpo[1],NIL),3,,,"A415VldTOk")
		//@ nGetLin,aPosGet[1,1]  SAY OemToAnsi(IIF(M->C5_TIPO$"DB",STR0008,STR0009)) SIZE 020,09 PIXEL	//"Fornec.:"###"Cliente: "
		@ nGetLin,aPosGet[1,2]  SAY oSAY1 VAR Space(40)						SIZE 120,09 PICTURE "@!"	OF oDlg PIXEL
		@ nGetLin,aPosGet[1,3]  SAY OemToAnsi(STR0011)						SIZE 020,09 OF oDlg PIXEL	//"Total :"
		@ nGetLin,aPosGet[1,4]  SAY oSAY2 VAR 0 PICTURE IIf(cPaisloc=="CHI",Nil,TM(0,22,nNumDec))	SIZE 060,09 OF oDlg PIXEL
		@ nGetLin,aPosGet[1,5]  SAY OemToAnsi(STR0012)						SIZE 030,09 OF oDlg PIXEL 	//"Desc. :"
		@ nGetLin,aPosGet[1,6]  SAY oSAY3 VAR 0 PICTURE IIf(cPaisloc=="CHI",Nil,TM(0,22,nNumDec))		SIZE 060,09 OF oDlg PIXEL RIGHT
		@ nGetLin+10,aPosGet[1,5]  SAY OemToAnsi("=")							SIZE 020,09 OF oDlg PIXEL
		If cPaisLoc == "BRA"
			@ nGetLin+10,aPosGet[1,6]  SAY oSAY4 VAR 0								SIZE 060,09 PICTURE TM(0,22,2) OF oDlg PIXEL RIGHT
		Else
			@ nGetLin+10,aPosGet[1,6]  SAY oSAY4 VAR 0								SIZE 050,09 PICTURE IIf(cPaisloc=="CHI",Nil,TM(0,22,nNumDec)) OF oDlg PIXEL RIGHT
		EndIf
		oDlg:Cargo	:= {|c1,n2,n3,n4| oSay1:SetText(c1),;
			oSay2:SetText(n2),;
			oSay3:SetText(n3),;
			oSay4:SetText(n4) }
		SetKey(VK_F4,{||A440Stok(NIL,"A410")})
		oGetd:=MsGetDados():New(aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],nOpc,"A410LinOk","A410TudOk","+C6_ITEM/C6_Local/C6_TES/C6_CF/C6_PEDCLI",.T.,If(lGCT,aPedCpo[2],NIL),nColFreeze,,ITENSSC6*IIF(MaGrade(),1,3.33),"A410Blq()",,,"A410ValDel()",,lFreeze)	
		Private oGetDad := oGetD
		If Type("lCodBarra") <> "U"
			oGetd:oBrowse:bGotFocus:={|| IIF(lCodBarra .And. !lM410Bar,a410EntraBarra(oGetD),IIF(lCodBarra .And. lM410Bar,Execblock("M410CODBAR",.F.,.F.,{nOpc,oGetD}),))}
		EndIf
		A410Bonus(2)
		Ma410Rodap(oGetD,nTotalPed,nTotalDes)
		ACTIVATE MSDIALOG oDlg ON INIT (A410Limpa(.F.,M->C5_TIPO),Ma410Bar(oDlg,{||nOpcA:=1,if(A410VldTOk(nOpc, aRecnoSE1RA).And.oGetD:TudoOk(),If(!obrigatorio(aGets,aTela),nOpcA := 0,oDlg:End()),nOpcA := 0)},{||Iif( Ma410VldUs(nOpca), oDlg:End(),)},nOpc,oGetD,nTotalPed,@aRecnoSE1RA,@aHeadAGG,@aColsAGG))
		SetKey(VK_F4,)
	Else
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Permite alterar a data de entrega de um item do pedido, ou   Ё
		//Ё sugerir uma data de entrega a partir da analise do APS       Ё		
		//Ё que somente eh executado via webservice/rotina automatica.   Ё				
		//Ё Valido somente para Integracao APS DRUMMER.                  Ё						
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If SuperGetMv("MV_APS",.F.,"") == "DRUMMER"
			
			/* ---- LOGICA UTILIZADA EM PORTUGUES ESTRUTURADO PARA MAIOR ENTENDIMENTO -----
		
			SE ( NAO EXISTIR O CAMPO SUGERIDO PARA DATA DE ENTREGA) ENTAO
				PROCURA AS REFERENCIAS DO C6_ENTREG NOS ITENS E SE ACHAR APAGA
			SENAO SE ( PARAMETRO QUE INDICA ONDE SERA FEITA A ATUALIZACAO DE ENTREGA FOR INFORMADO FOR PARA GRAVAR NO CAMPO SUGERIDO C6_SUGENTR ) ENTAO
				PROCURA NOS ITEMS DE ARRAY AS REFERENCIAS DE C6_ENTREG E ALTERA PARA C6_SUGENTR
			FIM SE
		    
		    */
		
     		If SuperGetMv("MV_CPOPVEN",.F.,"C6_SUGENTR") == "C6_SUGENTR"
            	For nCntFor:= 1 To Len(aAutoItens)
					nPos := AsCan(aAutoItens[nCntFor],{|x| AllTrim(x[1])=="C6_ENTREG"})            		
					If ( nPos > 0 )
						aAutoItens[nCntFor][nPos,1] := "C6_SUGENTR"
					EndIf
            	Next nCntFor   				     			
			EndIf						
		EndIf
		 
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё validando dados pela rotina automatica                       Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If EnchAuto(cAlias,aAutoCab,{|| Obrigatorio(aGets,aTela)},aRotina[nOpc][4]) .and. MsGetDAuto(aAutoItens,"A410LinOk",{|| A410VldTOk(nOpc) .and. A410TudOk()},aAutoCab,aRotina[nOpc][4])
			nOpcA := 1
		EndIf
	EndIf
	If ( nOpcA == 1 )                           
		//
		// Template GEM - Gestao de Empreendimentos Imobiliarios
		// Gera o contrato baseado nos dados do pedido de venda
		//   
		If HasTemplate("LOT") .AND. ExistTemplate("GEMXPV",,.T.)
			// atualiza o status do empreendimento  
			For nX := 1 to Len(aCols)
				ExecTemplate("GEMXPV",.F.,.F.,{ aCols[nX][Len(aCols[nX])] ,SC6->C6_CODEMPR, 1 })
			Next nX
		EndIf		
		//  Amarracao do Pedido de venda com o pedido de compras 
		//  para a Central de Compras.		
		If nOpcA == 1 .AND. SC6->(FieldPos("C6_PEDCOM")) > 0 .AND.  SC6->(FieldPos("C6_ITPC")) > 0 .AND. SC6->(FieldPos("C6_FILPED")) > 0  
			A410CCPed(aCols,aHeader,aMTA177PER,2)   
		EndIf
		A410Bonus(1)
		If Type("lOnUpDate") == "U" .Or. lOnUpdate
			If a410Trava()
				//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Inicializa a gravacao dos lancamentos do SIGAPCO          Ё
				//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
				PcoIniLan("000100")
				Begin Transaction
					If !A410Grava(lLiber,lTransf,2,aHeadFor,aColsFor,aRegSC6,aRegSCV,nStack,aColsBn,aRecnoSE1RA,aHeadAGG,aColsAGG)
						Help(" ",1,"A410NAOREG")
					EndIf 				
				End Transaction
				//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Finaliza a gravacao dos lancamentos do SIGAPCO            Ё
				//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
				PcoFinLan("000100")

				If lMt410Alt
					Execblock("MT410ALT",.F.,.F.)
				Endif

				If lTM410Stts
					ExecTemplate("M410STTS",.f.,.f.)
				Endif

				If lM410Stts
					ExecBlock("M410STTS",.f.,.f.)
				Endif
			EndIf
		Else
			aAutoCab := MsAuto2Ench("SC5")
			aAutoItens := MsAuto2Gd(aHeader,aCols)
		EndIf
	Else
		If ( (ExistBlock("M410ABN")) )
			ExecBlock("M410ABN",.f.,.f.)
		EndIf
	EndIf
EndIf

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁLimpa cliente anterior para proximo pedido                              Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
a410ChgCli("")

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁDestrava Todos os Registros                                             Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
MsUnLockAll()
RestArea(aArea)
Return( nOpcA )
/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    Ёa410IncluiЁ Rev.  ЁEduardo Riera          Ё Data Ё26.08.2001Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁInclusao do do pedido de venda                              Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁExpC1: Alias do cabecalho do pedido de venda                Ё╠╠
╠╠Ё          ЁExpN2: Recno do cabecalho do pedido de venda                Ё╠╠
╠╠Ё          ЁExpN3: Opcao do arotina                                     Ё╠╠
╠╠Ё          ЁExpL4: Indica se a inclusao vem de um orcamento             Ё╠╠
╠╠Ё          ЁExpL5: Recnos do arquivo SC5 de um orcamento                Ё╠╠
╠╠Ё          ЁExpL6: Indica se a inclusao vem de um contrato de parceria  Ё╠╠
╠╠Ё          ЁExpN7: Tipo do contrato (1=Aprovacao / 2=Remessa)           Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁNenhum                                                      Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁDescri┤└o ЁEsta rotina tem como objetivo efetuar a interface com o usuaЁ╠╠
╠╠Ё          Ёrio e o pedido de vendas                                    Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁObservacaoЁ                                                            Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё Materiais/Distribuicao/Logistica                           Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function A410Inclui(cAlias,nReg,nOpc,lOrcamento,nStack,aRegSCK,lContrat,nTpContr,cCodCli,cLoja,cMedPMS)

Local aPosObj   := {}
Local aObjects  := {}
Local aSize     := {}
Local aPosGet   := {}
Local aInfo     := {}
Local aCpos     := Nil
Local nOpcA 	:= 0
Local nCntFor	:= 0
Local nNumDec   := TamSX3("C6_VALOR")[2]
Local nGetLin   := 0
Local nColFreeze:= SuperGetMv("MV_PEDFREZ",.F.,0)
Local cCadastro := IIF(cCadastro == Nil,OemToAnsi("STR0007"),cCadastro) //"Atualiza┤└o de Pedidos de Venda"
Local cPedido   := "" 
Local cCpoContr := SuperGetMv("MV_CPOCONT",.F.,"C6_QTDVEN|C6_QTDLIB|C6_PRCVEN|C6_PRCUNIT")
Local cHblContr := SuperGetMv("MV_HBLCONT",.F.,"22")
Local cReadBkp  := ReadVar()
Local lHabilOrc := SuperGetMv("MV_HBLORC",.F.,"1") == "1"
Local lMt410Inc := Existblock("MT410INC")
Local lTMt410Inc:= ExistTemplate("MT410INC")
Local lM410Stts := ExistBlock("M410STTS")
Local lM410Bar  := ExistBlock("M410CODBAR")
Local lLiber	:= .F.
Local lTransf	:= .F.
Local lMemory   := .F.
Local lFreeze   := (SuperGetMv("MV_PEDFREZ",.F.,0) <> 0)
Local lContTPV  := SuperGetMv("MV_TELAPVX",.F.,.F.)
Local oDlg
Local oGetD
Local oSAY1
Local oSAY2
Local oSAY3
Local oSAY4
Local aNoFields := {"C6_NUM","C6_QTDEMP","C6_QTDENT","C6_QTDEMP2","C6_QTDENT2","C6_SLDALIB"}		// Campos que nao devem entrar no aHeader e aCols
Local aRecnoSE1RA := {} // Array com os titulos selecionados pelo Adiantamento
Local aCabCN120  := {} //-- Cabecalho para rotina automatica CNTA120
Local aItemCN120 := {} //-- Itens para rotina automatica CNTA120
Local nQUANT	 := 0
Local nTOTAL	 := 0
Local nENTREG	 := 0
Local nITEMED    := 0
Local nITEM	     := 0
Local nPRODUTO   := 0
Local nPRCVEN	 := 0
Local cItem      := ""
Local lCNTA120   := .F.
Local nTotDesc   := 0
Local aHeadAGG    := {}
Local aColsAGG    := {}

DEFAULT nStack := GetSX8Len() 
DEFAULT cCodCli   := ""
DEFAULT cLoja     := ""

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁCriar array PRIVATE p/ integracao com sistema de Distribuicao - NAO REMOVER Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
PRIVATE aDistrInd:={}
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁCarrega perguntas do MATA440 e MATA410                                  Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
Pergunte("MTA440",.F.)
lLiber := MV_PAR02 == 1
lTransf:= MV_PAR01 == 1
Pergunte("MTA410",.F.)
//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Variavel utilizada p/definir Op. Triangulares.       Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
IsTriangular( (MV_PAR03==1) )
//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁArray para controlar relacionamento com SD4 (Remessa para Beneficiamento Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
PRIVATE aColsBn := {}
//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Monta a entrada de dados do arquivo                  Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
PRIVATE aTela[0][0]
PRIVATE aGets[0]
PRIVATE bArqF3
PRIVATE bCpoF3
PRIVATE aTrocaF3  := {}
PRIVATE aHeadFor  := {}
PRIVATE aColsFor  := {}
PRIVATE N         := 1
PRIVATE oGetPV	  := Nil     

//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Variaveis para integraГЦo com SIGAGCT                Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
PRIVATE cContra  := Space(TamSX3("CN9_NUMERO")[1])
PRIVATE cRevisa  := Space(TamSX3("CN9_REVISA")[1])
PRIVATE cCompet  := Space(TamSX3("CNF_COMPET")[1])
PRIVATE cPlan    := Space(TamSX3("CNA_NUMERO")[1])
PRIVATE cParcel  := Space(TamSX3("CNF_PARCEL")[1])
PRIVATE lFixo    := .T. //Contrato Fixo

//зддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁVerifica se o campo de codigo de lancamento cat 83 Ё
//Ёdeve estar visivel no acols                        Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддды
If SC6->(FieldPos("C6_CODLAN"))>0 .and. !SuperGetMV("MV_CAT8309",,.F.)
	aAdd(aNoFields,"C6_CODLAN")
EndIf

//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Cria Ambiente/Objeto para tratamento de grade        Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
If FindFunction("MsMatGrade") .And. IsAtNewGrd()
	PRIVATE oGrade	  := MsMatGrade():New('oGrade',,"C6_QTDVEN",,"a410GValid()",;
	{ 	{VK_F4,{|| A440Saldo(.T.,oGrade:aColsAux[oGrade:nPosLinO][aScan(oGrade:aHeadAux,{|x| AllTrim(x[2])=="C6_LOCAL"})])}} },;
	{ 	{"C6_QTDVEN",.T., {{"C6_UNSVEN",{|| ConvUm(AllTrim(oGrade:GetNameProd(,nLinha,nColuna)),aCols[nLinha][nColuna],0,2) } }} },;
	{"C6_QTDLIB",NIL,NIL},;
	{"C6_QTDENT",NIL,NIL},;
	{"C6_ITEM"	,NIL,NIL},;
	{"C6_OPC",NIL,NIL},;
	{"C6_UNSVEN",NIL, {{"C6_QTDVEN",{|| ConvUm(AllTrim(oGrade:GetNameProd(,nLinha,nColuna)),0,aCols[nLinha][nColuna],1) }}} };
	})

	//-- Inicializa grade multicampo
	A410InGrdM(.T.)
Else
	PRIVATE aColsGrade:= {}
	PRIVATE aHeadGrade:= {}
EndIf

DEFAULT aRegSCK   := {}
DEFAULT nTpContr  := 0

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁO Orcamento de Venda n└o permite grade de produtos                      Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
lOrcamento := If(ValType(lOrcamento)=="L",lOrcamento,.F.)
lContrat   := If(ValType(lContrat)  =="L",lContrat  ,.F.)
l416Auto   := If (Type("l416Auto") == "U",.f.,l416Auto)
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁA inicializacao das variaveis ja foi feita pela rotina de Orcamento     Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If ( !lOrcamento )
	PRIVATE aHeader   := {}
	PRIVATE aCols     := {}

	PRIVATE aGEMCVnd :={"",{},{}} //Template GEM - Condicao de Venda

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Salva a integridade dos campos de Bancos de Dados    Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
	dbSelectArea("SC5")
	dbSetOrder(1)
	//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Inicializa desta forma para criar uma nova instancia de variaveis private Ё
	//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	RegToMemory( "SC5", .T., .F. )

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁMontagem do aHeader e aCols                           Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
	FillGetDados(nOPc,"SC6",1,/*cSeek*/,/*{|| &cWhile }*/,{||.T.},aNoFields,/*aYesFields*/,/*lOnlyYes*/,/*cQuery*/,/*bMontCols*/,.T.)
	aCols[1][1] := "01"
	

	If !Empty(cCodCli) .And. !Empty(cLoja)

		ALTERA   := (aRotina[nOpc,4] == 4)
		INCLUI   := (aRotina[nOpc,4] == 3)
		lRefresh := .T.

		M->C5_CLIENTE := cCodCli
		__ReadVar := "M->C5_CLIENTE"
		lRet := CheckSX3("C5_CLIENTE")

		If lRet
			If ExistTrigger("C5_CLIENTE")
				RunTrigger(1,Nil,Nil,,"C5_CLIENTE")
			Endif


			M->C5_LOJACLI := cLoja
			__ReadVar := "M->C5_LOJACLI"
			lRet := CheckSX3("C5_LOJACLI")

			If lRet
				If ExistTrigger("C5_CLIENTE")
					RunTrigger(1,Nil,Nil,,"C5_LOJACLI")
				Endif
			Endif

		Endif

		__ReadVar := cReadBkp

	Endif

EndIf

//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁVerifica os campos do contrato se serao habilitados      Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If lContrat
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁVerifica os os parametros estao ativos para a movimentacao  Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If (nTpContr == 1 .And. SubStr(cHblContr,1,1) == "1") .Or. (nTpContr == 2 .And. SubStr(cHblContr,2,1) == "1")
		aCpos:= {}
		For nCntFor := 1 To Len(aHeader)
			//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//ЁVerifica o campos esta no parametro MV_CPOCONTR          Ё
			//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			If !(Alltrim(aHeader[nCntFor][2]) $ cCpoContr)
				aadd(aCpos,aHeader[nCntFor][2])
			EndIf
		Next nCntFor
	Endif
Endif

//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁVerifica os campos da Medicao do PMS que serao liberados para visualizacao na getdados Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If cMedPMS <> Nil
	aCpos:= {}
	For nCntFor := 1 To Len(aHeader)
		//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁVerifica o campos esta no parametro MV_CPOCONTR          Ё
		//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If !(Alltrim(aHeader[nCntFor][2]) $ cMedPMS)
			aadd(aCpos,aHeader[nCntFor][2])
		EndIf
	Next nCntFor
EndIf

//зддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁMonta o array com as formas de pagamento do SX5Ё
//юддддддддддддддддддддддддддддддддддддддддддддддды
Ma410MtFor(@aHeadFor,@aColsFor)
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Estabelece a Troca de Clientes conforme o Tipo do Pedido de Venda      Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If ( M->C5_TIPO $ "DB" )
	aTrocaF3 := {{"C5_CLIENTE","SA2"}}
Else
	aTrocaF3 := {}
EndIf
//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁVerifica se eh rotina de inclusao automatica          Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
If ( Type("l410Auto") == "U" .Or. ! l410Auto )
	If !( l416Auto )

		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Caso seja orcamento verifica se o parametro permite a alteracao do     Ё
		//Ё pedido na efetivacao. Caso nao permita troca variaveis de parametro da Ё
		//Ё Enchoice para apenas visualizacao.                                     Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If lOrcamento
			lMemory   := .T.
			If !lHabilOrc .Or. (lContrat .And. SubStr(cHblContr,3,1) == "1")
				Private aRotina := {{"","",0,2}}
				nOpc      := 1
			Else
				Private aRotina := {{"","",0,2},{"","",0,3}}
				nOpc      := 2
			EndIf
		Endif
        //Antes da abertura da Tela de Inclusao
		If ( (ExistBlock("M410INIC")) )
			ExecBlock("M410INIC",.F.,.F.)
		EndIf

		//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Faz o calculo automatico de dimensoes de objetos     Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
		aSize := MsAdvSize()
		aObjects := {}
		AAdd( aObjects, { 100, 100, .t., .t. } )
		AAdd( aObjects, { 100, 100, .t., .t. } )
		AAdd( aObjects, { 100, 020, .t., .f. } )
		aInfo := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 3, 3 }
		aPosObj := MsObjSize( aInfo, aObjects )
		aPosGet := MsObjGetPos(aSize[3]-aSize[1],315,{{003,033,160,200,240,263}} )
		nGetLin := aPosObj[3,1]
		If lContTPV		
			DEFINE MSDIALOG oDlg TITLE cCadastro From aSize[7],0 to aSize[6],aSize[5] of oMainWnd PIXEL STYLE nOR( WS_VISIBLE ,DS_MODALFRAME )
		Else
			DEFINE MSDIALOG oDlg TITLE cCadastro From aSize[7],0 to aSize[6],aSize[5] of oMainWnd PIXEL
		EndIf
		oGetPV:=MSMGet():New( "SC5", nReg, nOpc, , , , , aPosObj[1],,3,,,"A415VldTOk",,,lMemory)
//		@ nGetLin,aPosGet[1,1]  SAY OemToAnsi(IIF(M->C5_TIPO$"DB",STR0008,STR0009)) SIZE 020,09 PIXEL	//"Fornec.:"###"Cliente: "
		@ nGetLin,aPosGet[1,2]  SAY oSAY1 VAR Space(40)						SIZE 120,09 PICTURE "@!"	OF oDlg PIXEL
		@ nGetLin,aPosGet[1,3]  SAY OemToAnsi(STR0011)						SIZE 020,09 OF oDlg	PIXEL //"Total :"
		@ nGetLin,aPosGet[1,4]  SAY oSAY2 VAR 0 PICTURE IIf(cPaisloc=="CHI",Nil,TM(0,22,nNumDec))		SIZE 060,09 OF oDlg PIXEL
		@ nGetLin,aPosGet[1,5]  SAY OemToAnsi(STR0012)						SIZE 030,09 OF oDlg PIXEL	//"Desc. :"
		@ nGetLin,aPosGet[1,6]  SAY oSAY3 VAR 0 PICTURE IIf(cPaisloc=="CHI",Nil,TM(0,22,nNumDec))		SIZE 060,09 OF oDlg	PIXEL	RIGHT
		@ nGetLin+10,aPosGet[1,5]  SAY OemToAnsi("=")							SIZE 020,09 OF oDlg PIXEL
		@ nGetLin+10,aPosGet[1,6]  SAY oSAY4 VAR 0								SIZE 060,09 PICTURE IIf(cPaisloc=="CHI",Nil,TM(0,22,nNumDec)) OF oDlg PIXEL RIGHT
		oDlg:Cargo	:= {|c1,n2,n3,n4| oSay1:SetText(c1),;
			oSay2:SetText(n2),;
			oSay3:SetText(n3),;
			oSay4:SetText(n4) }
		SetKey(VK_F4,{||A440Stok(NIL,"A410")})
		#IFDEF TOP
			SetKey(VK_F9,{||A410KeyF9()})
		#ENDIF
		oGetd:=MsGetDados():New(aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],nOpc,"A410LinOk","A410TudOk","+C6_ITEM/C6_Local/C6_TES/C6_CF/C6_PEDCLI",.T.,aCpos,nColFreeze,,ITENSSC6*IIF(MaGrade(),1,3.33),"A410FldOk(3)",,,,,lFreeze)		
		Private oGetDad:=oGetd
		If Type("lCodBarra") <> "U"
			oGetd:oBrowse:bGotFocus:={|| IIF(lCodBarra .And. !lM410Bar,a410EntraBarra(oGetD),IIF(lCodBarra .And. lM410Bar,Execblock("M410CODBAR",.F.,.F.,{nOpc,oGetD}),))}
		EndIf
		A410Bonus(2)
		Ma410Rodap(oGetd)
		ACTIVATE MSDIALOG oDlg ON INIT (A410Limpa(.F.,M->C5_TIPO),Ma410Bar(oDlg,{||nOpcA:=1,If(A410VldTOk(nOpc, aRecnoSE1RA).And.oGetd:TudoOk(),If(!obrigatorio(aGets,aTela),nOpcA:=0,oDlg:End()),nOpcA:=0)},{||Iif(Ma410VldUs(nOpca),oDlg:End(), ) },nOpc,oGetD,0,@aRecnoSE1RA,@aHeadAGG,@aColsAGG))
		SetKey(VK_F4,)
		SetKey(VK_F9,)
	EndIf
Else
	//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Variaveis utilizadas pela rotina de inclusao automatica     Ё
	//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If EnchAuto(cAlias,aAutoCab,{|| Obrigatorio(aGets,aTela)},aRotina[nOpc][4]) .and. MsGetDAuto(aAutoItens,"A410LinOk",{|| A410VldTOk(nOpc) .and. A410TudOk()},aAutoCab)
		nOpcA := 1
	EndIf
EndIf
If ( l416Auto )
	A410TudOk()
	nOpcA := 1
EndIf
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁEfetua a Gravacao do Pedido de Venda                                    Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If ( nOpcA == 1 )

	If lOrcamento
		a410Trava()
	Endif

	A410Bonus(1)
	cPedido := M->C5_NUM
	
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁVerifica se eh orcamento para realizar a transacao total e nao por item Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If Type("lOnUpDate") == "U" .Or. lOnUpdate
		If lOrcamento

			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Troca o status de visualizacao para inclusao caso nao permita a alte   Ё
			//Ё racao do pedido de venda na efetivacao do orcamento                    Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды

			If !lHabilOrc
				INCLUI := .T.
			Endif

			//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Inicializa a gravacao dos lancamentos do SIGAPCO          Ё
			//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			PcoIniLan("000100")
			Begin Transaction
				If ( !A410Grava(lLiber,lTransf,1,aHeadFor,aColsFor,,,,aColsBn,aRecnoSE1RA,aHeadAGG,aColsAGG) )
					Help(" ",1,"A410NAOREG")
				Else
					If ( cPedido <> M->C5_NUM )
						If !( Type("l410Auto") <> "U" .And. l410Auto )
							Help(" ",1,"NUMSEQ",,M->C5_NUM,4,15)
						EndIf
					EndIf
				EndIf
				//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//ЁChama  funcao de atualizacao do orcamento com os dados do pedido        Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
				M416AtuSCK(aRegSCK)

			End Transaction
			//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Finaliza a gravacao dos lancamentos do SIGAPCO            Ё
			//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			PcoFinLan("000100")

		Else
			//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Inicializa a gravacao dos lancamentos do SIGAPCO          Ё
			//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			PcoIniLan("000100") 
			
			If !Empty(M->C5_MDCONTR) .And. Empty(M->C5_MDNUMED)
				lCNTA120 := .T.
				cNumMed := CriaVar("CND_NUMMED")
				M->C5_MDNUMED := cNumMed
			EndIf

			If ( !A410Grava(lLiber,lTransf,1,aHeadFor,aColsFor,NIL,NIL,nStack,aColsBn,aRecnoSE1RA,aHeadAGG,aColsAGG) )
				Help(" ",1,"A410NAOREG")
			Else	
				If ( cPedido <> M->C5_NUM )
					If !( Type("l410Auto") <> "U" .And. l410Auto )
						Help(" ",1,"NUMSEQ",,M->C5_NUM,4,15)
					EndIf
				EndIf
				
				//здддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Tratamento para, quando PV integrado com GCT, grave a  Ё
				//Ё medicao via rotina automatica (CNTA120).			   Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддды				
				If lCNTA120
					nQUANT   := aScan(aHeader,{|x| AllTrim(x[2]) == "C6_QTDVEN"})
					nTOTAL   := aScan(aHeader,{|x| AllTrim(x[2]) == "C6_VALOR"})
					nENTREG  := aScan(aHeader,{|x| AllTrim(x[2]) == "C6_ENTREG"})
					nITEMED  := aScan(aHeader,{|x| AllTrim(x[2]) == "C6_ITEMED"})
					nITEM	 := aScan(aHeader,{|x| AllTrim(x[2]) == "C6_ITEM"})
					nPRODUTO := aScan(aHeader,{|x| AllTrim(x[2]) == "C6_PRODUTO"})
					nPRCVEN  := aScan(aHeader,{|x| AllTrim(x[2]) == "C6_PRCVEN"})
					nPRUNIT  := aScan(aHeader,{|x| AllTrim(x[2]) == "C6_PRUNIT"})
									
					aAdd(aCabCN120,{"CND_CONTRA",cContra,NIL})
					aAdd(aCabCN120,{"CND_REVISA",cRevisa,NIL})
					aAdd(aCabCN120,{"CND_COMPET",cCompet,NIL})
					aAdd(aCabCN120,{"CND_NUMERO",cPlan,NIL})
					aAdd(aCabCN120,{"CND_NUMMED",cNumMed,NIL})
					If !Empty(CND->(FieldPos("CND_PARCEL")))
						aAdd(aCabCN120,{"CND_PARCEL",cParcel,NIL})
					EndIf
					aAdd(aCabCN120,{"CND_MOEDA",M->C5_MOEDA,NIL})
					//Calcula desconto aplicado
					aEval(aCols,{|x| nTotDesc += x[nPRUNIT]*x[nQUANT] - x[nPRCVEN]*x[nQUANT]})
					If nTotDesc > 0
						aAdd(aCabCN120,{"CND_DESCME",A410Arred(nTotDesc,"C6_VALOR"),NIL})
					EndIf
					aAdd(aCabCN120,{"CND_OBS",STR0136 +cPedido +".",NIL}) //MediГЦo gerada automaticamente a partir da inclusЦo do pedido de venda nЗmero ###.
					aAdd(aCabCN120,{"NUMPED",cPedido,NIL})
					
					For nCntFor := 1 To Len(aCols)
						If !lFixo .And. aCols[nCntFor,Len(aHeader)+1]
							Loop
						ElseIf !lFixo .Or. !Empty(aCols[nCntFor,nITEMED])
							aAdd(aItemCN120,{})
							//-- Preenche item da medicao, de acordo com a existencia de planilha
							If lFixo
								cItem := aCols[nCntFor,nITEMED]
							Else
								cItem := Right(aCols[nCntFor,nITEM],TamSX3("CNB_ITEM")[1])
								cItem := PadL(cItem,TamSX3("CNB_ITEM")[1],"0")
							EndIf
							
							aAdd(aItemCN120[Len(aItemCN120)],{"CNE_ITEM",cItem,NIL})
							aAdd(aItemCN120[Len(aItemCN120)],{"CNE_PRODUT",aCols[nCntFor,nPRODUTO],NIL})
							If !aCols[nCntFor,Len(aHeader)+1]
								aAdd(aItemCN120[Len(aItemCN120)],{"CNE_QUANT",aCols[nCntFor,nQUANT],NIL})
								If !lFixo
									aAdd(aItemCN120[Len(aItemCN120)],{"CNE_VLUNIT",aCols[nCntFor,nPRCVEN],NIL})    	
								EndIf
							Else
								aAdd(aItemCN120[Len(aItemCN120)],{"CNE_QUANT",0,NIL})
								aAdd(aItemCN120[Len(aItemCN120)],{"CNE_VLTOT",0,NIL})
							EndIf
							aAdd(aItemCN120[Len(aItemCN120)],{"CNE_DTENT",aCols[nCntFor,nENTREG],NIL})
						EndIf
					Next nCntFor
					
					//Processa Medicao - CNTA120
					MsgRun(STR0131,STR0127,{|| A410GrvMed(aCabCN120,aItemCN120)}) //SIGAGCT - Aguarde, processsando MediГЦo...
				EndIf				
			EndIf
			
			//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Finaliza a gravacao dos lancamentos do SIGAPCO            Ё
			//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			PcoFinLan("000100")

		Endif
	Else
		aAutoCab := MsAuto2Ench("SC5")
		aAutoItens := MsAuto2Gd(aHeader,aCols)
	EndIf

	If lTMt410Inc
		ExecTemplate("MT410INC",.F.,.F.)
	Endif

	If lMt410Inc
		Execblock("MT410INC",.F.,.F.)
	Endif

	If lM410Stts
		ExecBlock("M410STTS",.f.,.f.)
	EndIf
Else
	If !( nModulo <> 5 .And. Type("l410Auto") <> "U" .And. l410Auto)
		While GetSX8Len() > nStack 
			RollBackSX8()
		EndDo
	EndIf
	If ( (ExistBlock("M410ABN")) )
		ExecBlock("M410ABN",.f.,.f.)
	EndIf
EndIf

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁLimpa cliente anterior para proximo pedido                              Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
a410ChgCli("")

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁRestaura a Integridade da Tela de Entrada                               Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
dbSelectArea(cAlias)
Return( nOpcA )

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    Ёa410DeletaЁ Rev.  ЁEduardo Riera          Ё Data Ё26.08.2001Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁDelecao do Pedido de Venda                                  Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁExpC1: Alias do cabecalho do pedido de venda                Ё╠╠
╠╠Ё          ЁExpN2: Recno do cabecalho do pedido de venda                Ё╠╠
╠╠Ё          ЁExpN3: Opcao do arotina                                     Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁNenhum                                                      Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁDescri┤└o ЁEsta rotina tem como objetivo efetuar a interface com o usuaЁ╠╠
╠╠Ё          Ёrio e o pedido de vendas                                    Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁObservacaoЁ                                                            Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё Materiais/Distribuicao/Logistica                           Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function A410Deleta(cAlias,nReg,nOpc)

Local aArea     := GetArea()
Local aPosObj   := {}
Local aObjects  := {}
Local aSize     := {}
Local aPosGet   := {}
Local aRegSC6   := {}
Local aRegSCV   := {}
Local aInfo     := {}
Local lLiber 	:= .F.
Local lContinua := .T.
Local lGrade	:= MaGrade()
Local lFaturado := .F.
Local lQuery    := .F.
Local lContrat  := .F.
Local lPedTLMK  := .F.
Local lFreeze   := (SuperGetMv("MV_PEDFREZ",.F.,0) <> 0)
Local lContTPV  := SuperGetMv("MV_TELAPVX",.F.,.F.)
Local nOpcA		:= 0
Local nTotalPed := 0
Local nTotalDes := 0
Local nGetLin   := 0
Local nNumDec   := TamSX3("C6_VALOR")[2]
Local nColFreeze:= SuperGetMv("MV_PEDFREZ",.F.,0)
Local cArqQry   := "SC6"
Local cCadastro := IIF(cCadastro == Nil,OemToAnsi("STR0007"),cCadastro) //"Atualiza┤└o de Pedidos de Venda"
Local oDlg
Local oGetd
Local oSAY1
Local oSAY2
Local oSAY3
Local oSAY4
Local lMt410Ace := Existblock("MT410ACE")

//Gestao de Contratos
Local lGCT     := ((SC5->(FieldPos("C5_MDCONTR")) > 0) .And. !Empty(SC5->C5_MDCONTR))

Local cSeek     := ""
Local aNoFields := {"C6_NUM","C6_QTDEMP","C6_QTDENT","C6_QTDEMP2","C6_QTDENT2"}		// Campos que nao devem entrar no aHeader e aCols
Local bWhile    := {|| }
Local cQuery   := ""
Local bCond     := {|| .T. }
Local bAction1  := {|| Mta410Del(cArqQry,@nTotalPed,@nTotalDes,lGrade,@aRegSC6,@lPedTLMK,@lLiber,@lFaturado,lContrat) }	
Local bAction2  := {|| .T. }
Local aRecnoSE1RA := {} // Array com os titulos selecionados pelo Adiantamento     
Local aMTA177PER := {}  // Array para carregar as perguntas de central de compras
Local cEventID   := 0    // Variavel usada para armazenar o ID do EventViewer	  
Local cMessagem  := " " // Variavel para armazenar a mensagem utilizada no eventviewer
Local aHeadAGG    := {}
Local aColsAGG    := {}

//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Variaveis utilizadas na LinhaOk                      Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
PRIVATE aCols      := {}
PRIVATE aHeader    := {}
PRIVATE aHeadFor   := {}
PRIVATE aColsFor   := {}
PRIVATE N          := 1
PRIVATE aOPs       := {}
PRIVATE aTELA[0][0],aGETS[0]
PRIVATE oGetPV		:= Nil

PRIVATE aGEMCVnd :={"",{},{}} //Template GEM - Condicao de Venda

//зддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁVerifica se o usuario tem premissao para alterar o Ё
//Ёpedido de venda                                    Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддды
If cPaisLoc <> "BRA" .AND. FieldPos("C5_CATPV") > 0 .AND. !Empty(SC5->C5_CATPV)
	If AliasIndic("AGS") //Tabela que relaciona usuario com os Tipos de Pedidos de vendas que ele tem acesso
		DBSelectArea("AGS")
		DBSetOrder(1)
		If DBSeek(xFilial("AGS") + __cUserId) //Se nЦo encontrar o usuАrio na tabela, permite ele alterar o pedido
			If !DBSeek(xFilial("AGS") + __cUserId + SC5->C5_CATPV) //Verifica se o usuario tem premissao
				MsgStop(STR0167 + " " + STR0005 + " " + STR0168)//"Este usuario nao tem permissao para excluir pedidos de venda com essa categoria."
				lContinua := .F.
			EndIf
		EndIf
	EndIf
EndIf

//зддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁVerifica se o campo de codigo de lancamento cat 83 Ё
//Ёdeve estar visivel no acols                        Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддды
If SC6->(FieldPos("C6_CODLAN"))>0 .and. !SuperGetMV("MV_CAT8309",,.F.)
	aAdd(aNoFields,"C6_CODLAN")
EndIf

//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Ponto de entrada para validar acesso do usuario na funcao Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If lMt410Ace
	lContinua := Execblock("MT410ACE",.F.,.F.,{nOpc})
Endif

//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Cria Ambiente/Objeto para tratamento de grade        Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды                     
If FindFunction("MsMatGrade") .And. IsAtNewGrd()
	PRIVATE oGrade	  := MsMatGrade():New('oGrade',,"C6_QTDVEN",,"a410GValid()", ,;
	  						{ 	{"C6_QTDVEN",NIL,NIL},;
	  							{"C6_QTDLIB",NIL,NIL},;
	  							{"C6_QTDENT",NIL,NIL},;
	  							{"C6_ITEM"	,NIL,NIL},;
	  							{"C6_UNSVEN",NIL,NIL},;
	  							{"C6_OPC",NIL,NIL},;
	  							{"C6_BLQ",NIL,NIL}	}) 

	//-- Inicializa grade multicampo
	A410InGrdM()
Else
	PRIVATE aColsGrade := {}
	PRIVATE aHeadgrade := {}	
EndIf	 

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁCarrega perguntas do MTA177, MATA440 e MATA410                          Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды    
Pergunte ("MTA177",.F.)
AADD (aMTA177PER,{MV_PAR17,MV_PAR18} )
Pergunte("MTA440",.F.)
lTransf:= MV_PAR01 == 1 
Pergunte("MTA410",.F.)
//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Variavel utilizada p/definir Op. Triangulares.       Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
IsTriangular( MV_PAR03==1 )
//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Executa o ponto de entrada M410ALOK                  Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
dbSelectArea(cAlias)
IF ( (ExistBlock("M410ALOK")) )
	If (! ExecBlock("M410ALOK",.F.,.F.) )
		lContinua := .F.
	EndIf
EndIf
//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁEXECUTAR CHAMADA DE FUNCAO p/ integracao com sistema de Distribuicao - NAO REMOVER Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If lContinua .And. SuperGetMv("MV_FATDIST") == "S" // Apenas quando utilizado pelo modulo de Distribuicao
	lContinua:=D410Alok()
EndIf
IF ( SC5->C5_FILIAL <> xFilial("SC5") )
	Help(" ",1,"A000FI")
	lContinua := .F.
EndIf
//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//| Se o Pedido foi originado no SIGAEEC - Nao Exclui    |
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
dbSelectArea("SC5")
IF !Empty(SC5->C5_PEDEXP)  .AND. NMODULO != 29 .And. ( Type("l410Auto") == "U" .OR. !l410Auto )
	Help(" ",1,"MTA410DEL")
	lContinua := .F.
EndIf

//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//| Se o Pedido foi originado no SIGAGCT - Nao Exclui    |
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
dbSelectArea("SC5")
IF lGCT .And. (FunName() != "CNTA120" .And. FunName() != "CNTA150")
	Help(" ",1,"MTA410GCT")
	lContinua := .F.
EndIf

//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//| Verifica se o pedido tem carga montada               |
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
If OmsHasCg(SC5->C5_NUM)
	Help(" ",1,"A410CARGA")
Endif

//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//| Se o Pedido foi originado no SIGATMS, verificar se   |
//| existe NF de Conhecimento.                           |
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
If SC5->(FieldPos("C5_SOLFRE")) > 0 .And. !Empty(SC5->C5_SOLFRE)
	DT5->(dbSetOrder(1))
	If DT5->(dbSeek(xFilial("SC5")+SC5->C5_SOLFRE+SC5->C5_ITESOL))
		Help(" ",1, "A410NFCON")
		lContinua := .F.
	EndIf
EndIf

//зддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//| Se o Pedido foi originado no SIGALOJA - Nao Exclui    |
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If SC5->(FieldPos("C5_ORCRES")) > 0 .AND. !Empty(SC5->C5_ORCRES) .AND. (Type("l410Auto") == "U" .OR. !l410Auto)
	//"Este Pedido foi gerado atravИs do mСdulo de Controle de Lojas, e somente poderА ser excluМdo atravИs da exclusЦo da Venda que o originou."
	MsgAlert(STR0116)
	lContinua := .F.
EndIf

//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Salva a integridade dos campos de Bancos de Dados    Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
dbSelectArea(cAlias)
If !SoftLock(cAlias)
	lContinua := .F.
EndIf
//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Inicializa desta forma para criar uma nova instancia de variaveis private Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
RegToMemory( "SC5", .F., .F. )

If lContinua
	lContinua := If(lGrade.And.MatOrigGrd()=="SB4",If(FindFunction("VldDocGrd"),VldDocGrd(1,SC5->C5_NUM),.T.),.T.)
EndIf

If ( lContinua )
	dbSelectArea("SC6")
	dbSetOrder(1)
	#IFDEF TOP
		If ( TcSrvType()<>"AS/400"  .And. !InTransact() .And. Ascan(aHeader,{|x| x[8] == "M"}) == 0 )
			lQuery  := .T.
			cQuery := "SELECT SC6.*,R_E_C_N_O_ SC6RECNO "
			cQuery += "FROM "+RetSqlName("SC6")+" SC6 "
			cQuery += "WHERE SC6.C6_FILIAL='"+xFilial("SC6")+"' AND "
			cQuery += "SC6.C6_NUM='"+SC5->C5_NUM+"' AND "
			cQuery += "SC6.D_E_L_E_T_<>'*' "
			cQuery += "ORDER BY "+SqlOrder(SC6->(IndexKey()))

			dbSelectArea("SC6")
			dbCloseArea()
		EndIf	
	#ENDIF
	cSeek  := xFilial("SC6")+SC5->C5_NUM
	bWhile := {|| C6_FILIAL+C6_NUM }


	//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Montagem do aHeader e aCols                          Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
	FillGetDados(nOPc,"SC6",1,cSeek,bWhile,{{bCond,bAction1,bAction2}},aNoFields,/*aYesFields*/,/*lOnlyYes*/,cQuery,/*bMontCols*/,Inclui,/*aHeaderAux*/,/*aColsAux*/,{|| AfterCols(cArqQry) },/*bBeforeCols*/,/*bAfterHeader*/,"SC6")	

	If "MATA410" $ SuperGetMV("MV_GRDMULT",.F.,"") .And. lGrade
		aCols := aColsGrade(oGrade,aCols,aHeader,"C6_PRODUTO","C6_ITEM","C6_ITEMGRD",aScan(aHeader,{|x| AllTrim(x[2]) == "C6_DESCRI"}))
	EndIf

	If AliasInDic("AGG")
		A410FRat(@aHeadAGG,@aColsAGG)	
	EndIf    

	nTotalPed  -= M->C5_DESCONT
	nTotalDes  += M->C5_DESCONT
	nTotalDes  += A410Arred(nTotalPed*M->C5_PDESCAB/100,"C6_VALOR")
	nTotalPed  -= A410Arred(nTotalPed*M->C5_PDESCAB/100,"C6_VALOR")
	
	If ( lQuery )
		dbSelectArea(cArqQry)
		dbCloseArea()
		ChkFile("SC6",.F.)
		dbSelectArea("SC6")
	EndIf
	
EndIf
If ( lContinua )
	//
	// Template GEM - Gestao de Empreendimentos Imobiliarios
	//
	// faz a copia da condicao de venda se a mesma tiver 
	// uma vinculacao com a condicao de pagamento
	//
	If ExistBlock("GEM410PV") 
		aGEMCVnd := ExecBlock("GEM410PV",.F.,.F.,{ M->C5_NUM ,M->C5_CONDPAG ,M->C5_EMISSAO ,nTotalPed })
	ElseIf ExistTemplate("GEM410PV") 
		// Copia a condicao de venda
		aGEMCVnd := ExecTemplate("GEM410PV",.F.,.F.,{ M->C5_NUM ,M->C5_CONDPAG ,M->C5_EMISSAO ,nTotalPed })
	EndIf

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Caso nao ache nenhum item , abandona rotina.         Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If ( Len(aCols)  == 0 )
		Help(" ",1,"A410SEMREG")
		lContinua := .F.
	EndIf
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Caso algum item ja tenha sido FATURADO , impede a exclusao do PV Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If ( lFaturado )
		Help(" ",1,"A410REGFAT")
		lContinua := .F.
	EndIf
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Caso algum item ja tenha sido LIBERADO , impede a exclusao do PV Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If ( lLiber )
		Help(" ",1,"A410LIBER")
		lContinua  := .F.
	EndIf
	If ( ExistBlock("A410EXC") )
		lRdRet := ExecBlock("A410EXC",.F.,.F.)
		If ValType(lRdRet)=="L" .And. !lRdRet
			lContinua  := .F.
		EndIf
	EndIf
	If ( lContrat )
		Help(" ",1,"A410CTRPAR")
		lContinua := .F.
	EndIf

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Se algum item originou-se no telemarketing, impede a exclusao          Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If lPedTLMK
		Help( " ", 1, "A410TLMK" ) // Nao e possivel excluir um pedido com itens originados no TLMK
		lContinua := .F.
	EndIf

EndIf
//зддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁMonta o array com as formas de pagamento do SX5Ё
//юддддддддддддддддддддддддддддддддддддддддддддддды
Ma410MtFor(@aHeadFor,@aColsFor,@aRegSCV)
If ( lContinua )
	If ( Type("l410Auto") == "U" .OR. !l410Auto )
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Faz o calculo automatico de dimensoes de objetos     Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
		aSize := MsAdvSize()
		aObjects := {}
		AAdd( aObjects, { 100, 100, .t., .t. } )
		AAdd( aObjects, { 100, 100, .t., .t. } )
		AAdd( aObjects, { 100, 020, .t., .f. } )
		aInfo := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 3, 3 }
		aPosObj := MsObjSize( aInfo, aObjects )
		aPosGet := MsObjGetPos(aSize[3]-aSize[1],315,{{003,033,160,200,240,263}} )
		nGetLin := aPosObj[3,1]
		If lContTPV		
			DEFINE MSDIALOG oDlg TITLE cCadastro From aSize[7],0 to aSize[6],aSize[5] of oMainWnd PIXEL STYLE nOR( WS_VISIBLE ,DS_MODALFRAME )
		Else
			DEFINE MSDIALOG oDlg TITLE cCadastro From aSize[7],0 to aSize[6],aSize[5] of oMainWnd PIXEL
		EndIf			
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Armazenar dados do Pedido anterior.                  Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
		IF M->C5_TIPO $ "DB"
			aTrocaF3 := {{"C5_CLIENTE","SA2"}}
		Else
			aTrocaF3 := {}
		EndIf
		oGetPV:=MSMGet():New( "SC5", nReg, nOpc, , , , , aPosObj[1],,3,,,"A415VldTOk")
//		@ nGetLin,aPosGet[1,1] SAY OemToAnsi(IIF(M->C5_TIPO$"DB",STR0008,STR0009)) SIZE 020,09 OF oDlg PIXEL	//"Fornec.:"###"Cliente: "
		@ nGetLin,aPosGet[1,2] SAY oSAY1 VAR Space(40)						SIZE 120,09 PICTURE "@!"	OF oDlg PIXEL
		@ nGetLin,aPosGet[1,3] SAY OemToAnsi(STR0011)						SIZE 020,09 OF oDlg PIXEL	//"Total :"
		@ nGetLin,aPosGet[1,4]  SAY oSAY2 VAR 0 PICTURE TM(0,22,Iif(cPaisloc=="CHI" .And. M->C5_MOEDA == 1,NIL,nNumDec))		SIZE 060,09 OF oDlg PIXEL
		@ nGetLin,aPosGet[1,5]  SAY OemToAnsi(STR0012)						SIZE 030,09 OF oDlg PIXEL 	//"Desc. :"
		@ nGetLin,aPosGet[1,6]  SAY oSAY3 VAR 0 PICTURE TM(0,22,Iif(cPaisloc=="CHI" .And. M->C5_MOEDA == 1,NIL,nNumDec))		SIZE 060,09 OF oDlg PIXEL RIGHT
		@ nGetLin+10,aPosGet[1,5]  SAY OemToAnsi("=")							SIZE 020,09 OF oDlg PIXEL
		@ nGetLin+10,aPosGet[1,6]  SAY oSAY4 VAR 0								SIZE 060,09 PICTURE TM(0,22,Iif(cPaisloc=="CHI" .And. M->C5_MOEDA == 1,NIL,nNumDec)) OF oDlg PIXEL	 RIGHT
		oDlg:Cargo	:= {|c1,n2,n3,n4| oSay1:SetText(c1),;
			oSay2:SetText(n2),;
			oSay3:SetText(n3),;
			oSay4:SetText(n4) }
		Set Key VK_F4 to A440Stok(NIL,"A410")
		oGetd:=MsGetDados():New(aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],nOpc,"A410LinOk","A410DelOk","+C6_ITEM/C6_Local/C6_TES/C6_CF/C6_PEDCLI",.T.,,nColFreeze,,ITENSSC6*IIF(MaGrade(),1,3.33),"A410Blq()",,,,,lFreeze)		
		Ma410Rodap(oGetD,nTotalPed,nTotalDes)
		ACTIVATE MSDIALOG oDlg ON INIT (A410Limpa(.F.,M->C5_TIPO),Ma410Bar(oDlg,{||If(A410DelOk().And.A410VldTOk(nOpc, aRecnoSE1RA),(nOpcA:=2,oDlg:End()),nOpcA := 0)},{||Iif( Ma410VldUs( nOpca ), oDlg:End(),  ) },nOpc,oGetD,nTotalPed,@aRecnoSE1RA,@aHeadAGG,@aColsAGG))
	Else
		nOpcA := 2
	EndIf
	If ( nOpcA == 2  )
		If Type("lOnUpDate") == "U" .Or. lOnUpdate
			//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Inicializa a gravacao dos lancamentos do SIGAPCO          Ё
			//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			PcoIniLan("000100") 

			A410Grava(lLiber,lTransf,3,aHeadFor,aColsFor,aRegSC6,aRegSCV,,,aRecnoSE1RA,aHeadAGG,aColsAGG)

            //зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Event Viewer - Envia e-mail ou RSS para exclusao de pedidos de vendas   Ё
			//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	        If GetRpoRelease("R5") // Verifica se o Release e 11.5    
				cEventID  := "031" //Exclusao de pedidos de vendas
				cMessagem := STR0160+SC5->C5_NUM+"."	
				EventInsert(FW_EV_CHANEL_ENVIRONMENT, FW_EV_CATEGORY_MODULES, cEventID,FW_EV_LEVEL_INFO,""/*cCargo*/,STR0159,cMessagem)
		    Else   	
		       	MEnviaMail("031",{SC5->C5_NUM,SC5->C5_CLIENTE,SC5->C5_LOJACLI})
		    End If   	
		       			
			If ( (ExistTemplate("M410STTS") ) )
				ExecTemplate("M410STTS",.f.,.f.)
			Endif

			If ( (ExistBlock("M410STTS") ) )
				ExecBlock("M410STTS",.f.,.f.)
			Endif
            
			//  Amarracao do Pedido de venda com o pedido de compras 
		    //  para a Central de Compras.		
			
			If SC6->(FieldPos("C6_PEDCOM")) > 0 .AND.  SC6->(FieldPos("C6_ITPC")) > 0 .AND. SC6->(FieldPos("C6_FILPED")) > 0 
				A410CCPed(aCols,aHeader,aMTA177PER,1)   
		    EndIf
			//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Finaliza a gravacao dos lancamentos do SIGAPCO            Ё
			//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			PcoFinLan("000100")

		Else
			aAutoCab := MsAuto2Ench("SC5")
			aAutoItens := MsAuto2Gd(aHeader,aCols)
		EndIf
	EndIf
EndIf

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Exclui a amarracao com os conhecimentos                      Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
MsDocument( "SC5", SC5->( RecNo() ), 2, , 3 ) 

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁDestrava Todos os Registros                                             Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
MsUnLockAll()
RestArea(aArea)
Return nOpcA

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    Ёa410PCopiaЁ Autor Ё Henry Fila            Ё Data Ё17/09/2004Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Prepara a funcao de copia para evitar que seja chamada a   Ё╠╠
╠╠Ё          Ё janela de filiais                                          Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁExpC1: Alias do cabecalho do pedido de venda                Ё╠╠
╠╠Ё          ЁExpN2: Recno do cabecalho do pedido de venda                Ё╠╠
╠╠Ё          ЁExpN3: Opcao do arotina                                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё Nenhum                                                     Ё╠╠
╠╠цддддддддддедддддддддддддддбдддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё   DATA   Ё Programador   ЁManutencao Efetuada                         Ё╠╠
╠╠цддддддддддедддддддддддддддедддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё          Ё               Ё                                            Ё╠╠
╠╠юддддддддддадддддддддддддддадддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Function a410PCopia(cAlias,nReg,nOpc)

Local aRotBkp := aClone(aRotina)

aRotina := { { OemToAnsi(STR0001),"AxPesqui"  ,0,1},;		//"Pesquisar"
	{ OemToAnsi(STR0002),"A410Visual",0,2},;		//"Visual"
	{ OemToAnsi(STR0003),"A410Copia",0,3}}		//"Incluir"

a410Copia(calias,nReg,3)

aRotina := aClone(aRotBkp)

Return(.T.)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    Ёa410Copia ЁAutor  ЁEduardo Riera          Ё Data Ё07.09.2001Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁCopia do Pedido de Venda                                    Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁExpC1: Alias do cabecalho do pedido de venda                Ё╠╠
╠╠Ё          ЁExpN2: Recno do cabecalho do pedido de venda                Ё╠╠
╠╠Ё          ЁExpN3: Opcao do arotina                                     Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁNenhum                                                      Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁDescri┤└o ЁEsta rotina tem como objetivo efetuar a interface com o usuaЁ╠╠
╠╠Ё          Ёrio e o pedido de vendas                                    Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁObservacaoЁ                                                            Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё Materiais/Distribuicao/Logistica                           Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function A410Copia(cAlias,nReg,nOpc)

Local aArea     := GetArea()
Local aPosObj   := {}
Local aObjects  := {}
Local aSize     := {}
Local aPosGet   := {}
Local aRegSC6   := {}
Local aRegSCV   := {}
Local aInfo     := {}
Local lLiber 	:= .F.
Local lTransf	:= .F.
Local lGrade	:= MaGrade()
Local lQuery    := .F.
Local lContinua := .T.
Local lFreeze   := (SuperGetMv("MV_PEDFREZ",.F.,0) <> 0)
Local nOpcA		:= 0
Local nTotalPed := 0
Local nTotalDes := 0
Local nNumDec   := TamSX3("C6_VALOR")[2]
Local nGetLin   := 0
Local nStack    := GetSX8Len() 
Local nColFreeze:= SuperGetMv("MV_PEDFREZ",.F.,0)
Local lContTPV  := SuperGetMv("MV_TELAPVX",.F.,.F.)
Local cArqQry   := "SC6"
Local cCadastro := IIF(cCadastro == Nil,OemToAnsi("STR0007"),cCadastro) //"Atualiza┤└o de Pedidos de Venda"
Local cTipoDat  := SuperGetMv("MV_TIPCPDT",.F.,"1")
Local oDlg
Local oGetd
Local dOrig     := Ctod("//")
Local dCopia    := Ctod("//")
Local oSAY1
Local oSAY2
Local oSAY3
Local oSAY4
Local lMt410Ace := Existblock("MT410ACE")

Local cSeek     := ""
Local aNoFields := {"C6_NUM","C6_QTDEMP","C6_QTDENT","C6_QTDEMP2","C6_QTDENT2"}		// Campos que nao devem entrar no aHeader e aCols
Local bWhile    := {|| }
Local cQuery    := ""
Local bCond     := {|| .T. }
Local bAction1  := {|| Mta410Cop(cArqQry,@nTotalPed,@nTotalDes,lGrade) }	
Local bAction2  := {|| .T. }
Local lCopia    := .T.
Local aRecnoSE1RA := {} // Array com os titulos selecionados pelo Adiantamento
Local aHeadAGG    := {}
Local aColsAGG    := {}

//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Variaveis utilizadas na LinhaOk                      Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
PRIVATE aCols      := {}
PRIVATE aHeader    := {}
PRIVATE aHeadFor   := {}
PRIVATE aColsFor   := {}
PRIVATE N          := 1
PRIVATE oGetPV		:= Nil

PRIVATE aGEMCVnd :={"",{},{}} //Template GEM - Condicao de Venda

//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Monta a entrada de dados do arquivo                  Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
PRIVATE aTELA[0][0],aGETS[0]

//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁArray para controlar relacionamento com SD4 (Remessa para Beneficiamento Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
PRIVATE aColsBn := {}

//зддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁVerifica se o campo de codigo de lancamento cat 83 Ё
//Ёdeve estar visivel no acols                        Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддды
If SC6->(FieldPos("C6_CODLAN"))>0 .and. !SuperGetMV("MV_CAT8309",,.F.)
	aAdd(aNoFields,"C6_CODLAN")
EndIf

//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Ponto de entrada para validar acesso do usuario na funcao Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If lMt410Ace
	lContinua := Execblock("MT410ACE",.F.,.F.,{nOpc})
Endif

//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Cria Ambiente/Objeto para tratamento de grade        Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды    
If FindFunction("MsMatGrade") .And. IsAtNewGrd()
	PRIVATE oGrade	  := MsMatGrade():New('oGrade',,"C6_QTDVEN",,"a410GValid()",;
	  						{ 	{VK_F4,{|| A440Saldo(.T.,oGrade:aColsAux[oGrade:nPosLinO][aScan(oGrade:aHeadAux,{|x| AllTrim(x[2])=="C6_LOCAL"})])}} },;
	  						{ 	{"C6_QTDVEN",.T.,{{"C6_UNSVEN",{|| ConvUm(AllTrim(oGrade:GetNameProd(,nLinha,nColuna)),aCols[nLinha][nColuna],0,2) } }} },;
	  							{"C6_QTDLIB",NIL,NIL},;
	  							{"C6_QTDENT",NIL,NIL},;
	  							{"C6_ITEM"	,NIL,NIL},;
	  							{"C6_UNSVEN", {{"C6_QTDVEN",{|| ConvUm(AllTrim(oGrade:GetNameProd(,nLinha,nColuna)),0,aCols[nLinha][nColuna],1) }}} },;
	  							{"C6_OPC",NIL,NIL},;
	  							{"C6_BLQ",NIL,NIL};
	  						}) 

	//-- Inicializa grade multicampo
	A410InGrdM(.T.)
Else
	PRIVATE aColsGrade := {}
	PRIVATE aHeadgrade := {}
EndIf	

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁCarrega perguntas do MATA440 e MATA410                                  Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
INCLUI := .T.
ALTERA := .F.

Pergunte("MTA440",.F.)
lLiber := MV_PAR02 == 1
lTransf:= MV_PAR01 == 1
Pergunte("MTA410",.F.)
//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Variavel utilizada p/definir Op. Triangulares.       Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
IsTriangular( MV_PAR03==1 )
//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Salva a integridade dos campos de Bancos de Dados    Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
dbSelectArea(cAlias)
IF ( (ExistBlock("M410ALOK")) )
	If (! ExecBlock("M410ALOK",.F.,.F.) )
		lContinua := .F.
	EndIf
EndIf
IF ( SC5->C5_FILIAL <> xFilial("SC5") )
	Help(" ",1,"A000FI")
	lContinua := .F.
EndIf
//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//| Se o Pedido foi originado no SIGATMS - Nao Copia     |
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
If SC5->(FieldPos("C5_SOLFRE")) > 0 .And. !Empty(SC5->C5_SOLFRE)
	Help(" ",1,"A410TMSNAO")
	lContinua := .F.
EndIf

//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Inicializa desta forma para criar uma nova instancia de variaveis private Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
RegToMemory( "SC5", .F., .F. )

dOrig  := M->C5_EMISSAO
dCopia := CriaVar("C5_EMISSAO",.T.)

//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Limpa as variaveis que possuem amarracoes do pedido anterior              Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
M->C5_NOTA  := Space(Len(SC5->C5_NOTA))
M->C5_SERIE := Space(Len(SC5->C5_SERIE))
M->C5_OS    := Space(Len(SC5->C5_OS))

If lContinua
	lContinua := If(lGrade.And.MatOrigGrd()=="SB4",If(FindFunction("VldDocGrd"),VldDocGrd(1,SC5->C5_NUM),.T.),.T.)
EndIf

If ( lContinua )
	dbSelectArea("SC6")
	dbSetOrder(1)
	#IFDEF TOP
		If TcSrvType()<>"AS/400" .And. !InTransact() .And. Ascan(aHeader,{|x| x[8] == "M"}) == 0
			lQuery  := .T.
			cQuery := "SELECT SC6.*,SC6.R_E_C_N_O_ SC6RECNO "
			cQuery += "FROM "+RetSqlName("SC6")+" SC6 "
			cQuery += "WHERE SC6.C6_FILIAL='"+xFilial("SC6")+"' AND "
			cQuery += "SC6.C6_NUM='"+SC5->C5_NUM+"' AND "
			cQuery += "SC6.D_E_L_E_T_<>'*' "
			cQuery += "ORDER BY "+SqlOrder(SC6->(IndexKey()))

			dbSelectArea("SC6")
			dbCloseArea()
		EndIf
	#ENDIF
	cSeek  := xFilial("SC6")+SC5->C5_NUM
	bWhile := {|| C6_FILIAL+C6_NUM }
	
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Montagem do aHeader e aCols                          Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
	FillGetDados(7,"SC6",1,cSeek,bWhile,{{bCond,bAction1,bAction2}},aNoFields,/*aYesFields*/,/*lOnlyYes*/,cQuery,/*bMontCols*/,.F.,/*aHeaderAux*/,/*aColsAux*/,{|| AfterCols(cArqQry,cTipoDat,dCopia,dOrig,lCopia) },/*bBeforeCols*/,/*bAfterHeader*/,"SC6")	
	
	//здддддддддддддддддддддддддд©
	//ЁCarrega os dados do rateioЁ
	//юдддддддддддддддддддддддддды
	If AliasInDic("AGG")
		A410FRat(@aHeadAGG,@aColsAGG)	
	EndIf
	
	nTotalPed  -= M->C5_DESCONT
	nTotalDes  += M->C5_DESCONT
	nTotalDes  += A410Arred(nTotalPed*M->C5_PDESCAB/100,"C6_VALOR")
	nTotalPed  -= A410Arred(nTotalPed*M->C5_PDESCAB/100,"C6_VALOR")
	If ( lQuery )
		dbSelectArea(cArqQry)
		dbCloseArea()
		ChkFile("SC6",.F.)
		dbSelectArea("SC6")
	EndIf
EndIf
//зддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁMonta o array com as formas de pagamento do SX5Ё
//юддддддддддддддддддддддддддддддддддддддддддддддды
Ma410MtFor(@aHeadFor,@aColsFor)
//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Caso nao ache nenhum item , abandona rotina.         Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
If ( lContinua )
	If ( Len(aCols) == 0 )
		lContinua := .F.
	EndIf
EndIf
//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Ajusta as variaveis para copia                                            Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
M->C5_NUM := CriaVar("C5_NUM",.T.)
M->C5_EMISSAO := CriaVar("C5_EMISSAO",.T.)
aRegSC6 := {}
aRegSCV := {}
//
// Template GEM - Gestao de Empreendimentos Imobiliarios
//
// Carrega a condicao de venda se a mesma tiver 
// uma vinculacao com a pedido/condicao de pagamento
//
If ExistBlock("GEM410PV") 
	aGEMCVnd := ExecBlock("GEM410PV",.F.,.F.,{ SC5->C5_NUM ,SC5->C5_CONDPAG ,M->C5_EMISSAO ,nTotalPed })
ElseIf ExistTemplate("GEM410PV") 
	// Copia a condicao de venda
	aGEMCVnd := ExecTemplate("GEM410PV",.F.,.F.,{ SC5->C5_NUM ,SC5->C5_CONDPAG ,M->C5_EMISSAO ,nTotalPed })
EndIf

If ExistBlock("MT410CPY")
	ExecBlock("MT410CPY",.F.,.F.)
EndIf
If ( lContinua )
	If ( Type("l410Auto") == "U" .OR. !l410Auto )
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Faz o calculo automatico de dimensoes de objetos     Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
		aSize := MsAdvSize()
		aObjects := {}
		AAdd( aObjects, { 100, 100, .t., .t. } )
		AAdd( aObjects, { 100, 100, .t., .t. } )
		AAdd( aObjects, { 100, 020, .t., .f. } )
		aInfo := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 3, 3 }
		aPosObj := MsObjSize( aInfo, aObjects )
		aPosGet := MsObjGetPos(aSize[3]-aSize[1],315,{{003,033,160,200,240,263}} )
		nGetLin := aPosObj[3,1]
		If lContTPV
			DEFINE MSDIALOG oDlg TITLE cCadastro From aSize[7],0 to aSize[6],aSize[5] of oMainWnd PIXEL STYLE nOR( WS_VISIBLE ,DS_MODALFRAME )
		Else
			DEFINE MSDIALOG oDlg TITLE cCadastro From aSize[7],0 to aSize[6],aSize[5] of oMainWnd PIXEL
		EndIf
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Armazenar dados do Pedido anterior.                  Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
		IF M->C5_TIPO $ "DB"
			aTrocaF3 := {{"C5_CLIENTE","SA2"}}
		Else
			aTrocaF3 := {}
		EndIf
		oGetPV:=MSMGet():New( "SC5", nReg, nOpc, , , , , aPosObj[1],,3,,,"A415VldTOk")
//		@ nGetLin,aPosGet[1,1]  SAY OemToAnsi(IIF(M->C5_TIPO$"DB",STR0008,STR0009)) SIZE 020,09 PIXEL	//"Fornec.:"###"Cliente: "
		@ nGetLin,aPosGet[1,2]  SAY oSAY1 VAR Space(40)						SIZE 120,09 PICTURE "@!"	OF oDlg PIXEL
		@ nGetLin,aPosGet[1,3]  SAY OemToAnsi(STR0011)						SIZE 020,09 OF oDlg PIXEL	//"Total :"
		@ nGetLin,aPosGet[1,4]  SAY oSAY2 VAR 0 PICTURE IIf(cPaisloc=="CHI",Nil,TM(0,22,nNumDec))	SIZE 060,09 OF oDlg PIXEL
		@ nGetLin,aPosGet[1,5]  SAY OemToAnsi(STR0012)						SIZE 030,09 OF oDlg PIXEL 	//"Desc. :"
		@ nGetLin,aPosGet[1,6]  SAY oSAY3 VAR 0 PICTURE IIf(cPaisloc=="CHI",Nil,TM(0,22,nNumDec))		SIZE 060,09 OF oDlg PIXEL RIGHT
		@ nGetLin+10,aPosGet[1,5]  SAY OemToAnsi("=")							SIZE 020,09 OF oDlg PIXEL
		If cPaisLoc == "BRA"
			@ nGetLin+10,aPosGet[1,6]  SAY oSAY4 VAR 0								SIZE 060,09 PICTURE TM(0,22,2) OF oDlg PIXEL RIGHT
		Else
			@ nGetLin+10,aPosGet[1,6]  SAY oSAY4 VAR 0								SIZE 050,09 PICTURE IIf(cPaisloc=="CHI",Nil,TM(0,22,nNumDec)) OF oDlg PIXEL RIGHT
		EndIf
		oDlg:Cargo	:= {|c1,n2,n3,n4| oSay1:SetText(c1),;
			oSay2:SetText(n2),;
			oSay3:SetText(n3),;
			oSay4:SetText(n4) }
		Set Key VK_F4 to A440Stok(NIL,"A410")
		oGetd:=MsGetDados():New(aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],nOpc,"A410LinOk","A410TudOk","+C6_ITEM/C6_Local/C6_TES/C6_CF/C6_PEDCLI",.T.,,nColFreeze,,ITENSSC6*IIF(MaGrade(),1,3.33),"A410Blq()",,,,,lFreeze)		
		Private oGetDad:=oGetd
		A410Bonus(2)
		Ma410Rodap(oGetD,nTotalPed,nTotalDes)
		ACTIVATE MSDIALOG oDlg ON INIT (A410Limpa(.F.,M->C5_TIPO),Ma410Bar(oDlg,{||nOpcA:=1,if(A410VldTOk(nOpc, aRecnoSE1RA).And.oGetd:TudoOk(),If(!obrigatorio(aGets,aTela),nOpcA := 0,oDlg:End()),nOpcA := 0)},{||oDlg:End()},nOpc,oGetD,nTotalPed,@aRecnoSE1RA,@aHeadAGG,@aColsAGG))
		SetKey(VK_F4,)
	Else
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё validando dados pela rotina automatica                       Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If EnchAuto(cAlias,aAutoCab,{|| Obrigatorio(aGets,aTela)},aRotina[nOpc][4]) .and. MsGetDAuto(aAutoItens,"A410LinOk",{|| A410VldTOk(nOpc) .and. A410TudOk()},aAutoCab)
			nOpcA := 1
		EndIf
	EndIf
	If ( nOpcA == 1 )
		A410Bonus(1)
		If Type("lOnUpDate") == "U" .Or. lOnUpdate
			//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Inicializa a gravacao dos lancamentos do SIGAPCO          Ё
			//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			PcoIniLan("000100")

			If !A410Grava(lLiber,lTransf,1,aHeadFor,aColsFor,aRegSC6,aRegSCV,nStack,aColsBn,aRecnoSE1RA,aHeadAGG,aColsAGG)
				Help(" ",1,"A410NAOREG")
			EndIf
			If ( (ExistBlock("M410STTS") ) )
				ExecBlock("M410STTS",.f.,.f.)
			EndIf

			//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Finaliza a gravacao dos lancamentos do SIGAPCO            Ё
			//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			PcoFinLan("000100")

		Else
			aAutoCab := MsAuto2Ench("SC5")
			aAutoItens := MsAuto2Gd(aHeader,aCols)
		EndIf
	Else
		While GetSX8Len() > nStack
			RollBackSX8()
		EndDo
		If ( (ExistBlock("M410ABN")) )
			ExecBlock("M410ABN",.f.,.f.)
		EndIf
	EndIf
EndIf
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁDestrava Todos os Registros                                             Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
MsUnLockAll()
RestArea(aArea)
Return( nOpcA )
/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    Ёa410Barra Ё Rev.  ЁEduardo Riera          Ё Data Ё26.08.2001Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁInclusao via codigo de barra                                Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁExpC1: Alias do cabecalho do pedido de venda                Ё╠╠
╠╠Ё          ЁExpN2: Recno do cabecalho do pedido de venda                Ё╠╠
╠╠Ё          ЁExpN3: Opcao do arotina                                     Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁNenhum                                                      Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁDescri┤└o ЁEsta rotina tem como objetivo efetuar a interface com o usuaЁ╠╠
╠╠Ё          Ёrio e o pedido de vendas                                    Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁObservacaoЁ                                                            Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё Materiais/Distribuicao/Logistica                           Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function A410Barra(cAlias,nReg,nOpc)

LOCAL lRetorno := .F.
PRIVATE lCodBarra := .T.
If aRotina[nOpc][4] == 3
	lRetorno := A410Inclui("SC5",SC5->(Recno()),nOpc)
Else
	lRetorno := A410Altera(cAlias,nReg,nOpc)
EndIf
lCodBarra := .F.
Return(lRetorno)

/*/
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбддддддддддд©╠╠
╠╠ЁFun┤└o    ЁA410LegendЁAutor  Ё Eduardo Riera         Ё Data Ё13.09.2000 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддаддддддддддд╢╠╠
╠╠Ё          ЁDemonstra a legenda das cores da mbrowse                     Ё╠╠
╠╠Ё          Ё                                                             Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁNenhum                                                       Ё╠╠
╠╠Ё          Ё                                                             Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁNenhum                                                       Ё╠╠
╠╠Ё          Ё                                                             Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁDescri┤└o ЁEsta rotina monta uma dialog com a descricao das cores da    Ё╠╠
╠╠Ё          ЁMbrowse.                                                     Ё╠╠
╠╠Ё          Ё                                                             Ё╠╠
╠╠Ё          Ё                                                             Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё Materiais                                                   Ё╠╠
╠╠юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function A410Legend()
Local aCores := {}

aCores := {{"ENABLE",STR0033},;      //Pedido em aberto
{"DISABLE",STR0034},;    //Pedido encerrado
{"BR_AMARELO",STR0035}}

If !__lPyme
	Aadd(aCores,{"BR_AZUL",STR0058})
	Aadd(aCores,{"BR_LARANJA",STR0059})  //Pedido Liberado
EndIf

If cPaisLoc <> "BRA"
	Aadd(aCores,{"BR_CINZA",STR0034 + " (" + AllTrim(GetDescRem()) + ")"}) //Pedido finalizado (Remito)
Endif
//зддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Ponto de Entrada para alterar cores da legenda    Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддды
If ExistBlock("MA410LEG")
	aCores := ExecBlock("MA410LEG",.F.,.F.,aCores)
Endif
BrwLegenda(cCadastro,STR0032,aCores)

Return(.T.)
/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    Ёa410LinOk Ё Rev.  ЁEduardo Riera          Ё Data Ё26.08.2001Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁValidacao da Linha da Getdados                              Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁExpO1: Objeto da GetDados                                   Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁNenhum                                                      Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁDescri┤└o ЁEsta rotina tem como objetivo efetuar a validacao da linhaOkЁ╠╠
╠╠Ё          Ё da getdados                                                Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁObservacaoЁ                                                            Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё Materiais/Distribuicao/Logistica                           Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function A410LinOk(o)

Local aArea     := GetArea()
Local aPedido   := {}
Local aVlrDev   := {}

Local aContrato := {}
Local lRetorno 	:= .T.
Local lGrade   	:= MaGrade()
Local lQuery    := .F.
Local lGradeReal:= .F.
Local lIntACD	:= SuperGetMV("MV_INTACD",.F.,"0") == "1"

Local cAviso    := ""
Local cItemSC6 	:= ""
Local cProduto 	:= ""
Local cTes     	:= ""
Local cNumRes  	:= ""
Local cLocal   	:= ""
Local cLoteCtl 	:= ""
Local cNumLote 	:= ""
Local cLocaliza  := Space(TamSX3("C0_LOCALIZ")[1])
Local cNumSerie	:= Space(TamSX3("C0_NUMSERI")[1])
Local cNfOrig  	:= ""
Local cSerieOri	:= ""
Local cItemOri	:= ""
Local cItemGrad	:= ""
Local cIdentB6 	:= ""
Local cServico	:= ''
Local cArqQry   := ""
Local cOpc      := ""
Local cOpcional := ""

Local nQtdRese 	:= 0
Local nCntFor  	:= 0
Local nQtdVen  	:= 0
Local nQtdLib  	:= 0
Local nPrcVen  	:= 0
Local nValor   	:= 0
Local nSaldo   	:= 0
Local nPosIdB6 	:= 0
Local nPosQtdVen:= 0
Local nPosQtdLib:= 0
Local nPosLocal := 0
Local nPosProd  := 0
Local nPosNfOrig:= 0
Local nPosSerOri:= 0
Local nPosItemOr:= 0
Local nPosServ  := 0
Local nPrUnit   := 0
Local nPosTes   := aScan(aHeader,{|x| AllTrim(x[2])=="C6_TES"})
Local nPContrat := aScan(aHeader,{|x| AllTrim(x[2])=="C6_CONTRAT"})
Local nPItContr := aScan(aHeader,{|x| AllTrim(x[2])=="C6_ITEMCON"})
Local nPItem	:= aScan(aHeader,{|x| AllTrim(x[2])=="C6_ITEM" })
Local nPQtdVen  := aScan(aHeader,{|x| AllTrim(x[2])=="C6_QTDVEN" })
Local nPLocal   := aScan(aHeader,{|x| AllTrim(x[2])=="C6_LOCAL"})
Local nPEntreg  := aScan(aHeader,{|x| AllTrim(x[2])=="C6_ENTREG" })
Local nPOpcional:= aScan(aHeader,{|x| AllTrim(x[2])=="C6_OPC"})
Local nPPrcVen  := aScan(aHeader,{|x| AllTrim(x[2])=="C6_PRCVEN"})
Local nPPrUnit  := aScan(aHeader,{|x| AllTrim(x[2])=="C6_PRUNIT"})
Local nPValor	:= aScan(aHeader,{|x| AllTrim(x[2])=="C6_VALOR"})
Local nUsado    := Len(aHeader)
Local nPIdentB6 := aScan(aHeader,{|x| AllTrim(x[2])=="C6_IDENTB6"})
Local nValDesc	:= 0
Local nX        := 0
Local nY        := 0
Local nPNumOrc    := aScan(aHeader,{|x| AllTrim(x[2])=="C6_NUMORC"})
Local nValorTot   := 0
Local nLinha      := 0
Local nColuna     := 0

Local nPProduto := aScan(aHeader,{|x| AllTrim(x[2])=="C6_PRODUTO"})
Local nPQtdLib  := aScan(aHeader,{|x| AllTrim(x[2])=="C6_QTDLIB"})
Local nPNfOrig  := aScan(aHeader,{|x| AllTrim(x[2])=="C6_NFORI"})
Local nPSerOrig := aScan(aHeader,{|x| AllTrim(x[2])=="C6_SERIORI"})
Local nPItOrig  := aScan(aHeader,{|x| AllTrim(x[2])=="C6_ITEMORI"})

Local nPC6_PROJPMS := aScan(aHeader,{|x| AllTrim(x[2])=="C6_PROJPMS"})
Local nPC6_EDTPMS  := aScan(aHeader,{|x| AllTrim(x[2])=="C6_EDTPMS"})
Local nPC6_TASKPMS := aScan(aHeader,{|x| AllTrim(x[2])=="C6_TASKPMS"})

// Indica se o preco unitario sera arredondado em 0 casas decimais ou nao. Se .T. respeita MV_CENT (Apenas Chile).
Local lPrcDec   := SuperGetMV("MV_PRCDEC",,.F.)
Local aQtdP3 := {}
#IFDEF TOP 
	Local cQuery    := ""
#ENDIF 	

Local lAltCtr3   := SuperGetMV("MV_ALTCTR3",.F.,.F.)
Local lGrdMult	 := "MATA410" $ SuperGetMV("MV_GRDMULT",.F.,"")

//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё verifica se linha do acols foi preenchida            Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
If ( !CheckCols(n,aCols) )
	lRetorno := .F.
EndIf
//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё  Caso o item nao esteja deletado                     Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
If ( !aCols[n][Len(aCols[n])] .And. lRetorno )
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁVerifica se os campos obrigatorios nao estao em branco                  Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	For nCntFor := 1 To nUsado
		Do Case
		Case ( AllTrim(aHeader[nCntFor][2]) == "C6_QTDVEN" )
			nQtdVen		:= aCols[n][nCntFor]
			nPosQtdVen	:= nCntFor
		Case ( AllTrim(aHeader[nCntFor][2]) == "C6_ITEM" )
			cItemSC6	:= aCols[n][nCntFor]
		Case ( AllTrim(aHeader[nCntFor][2]) == "C6_QTDLIB" )
			nQtdLib 	:= aCols[n][nCntFor]
			nPosQtdLib  := nCntFor
		Case ( AllTrim(aHeader[nCntFor][2]) == "C6_PRCVEN" )
			nPrcVen 	:= aCols[n][nCntFor]
		Case ( AllTrim(aHeader[nCntFor][2]) == "C6_VALOR" )
			nValor 	:= aCols[n][nCntFor]
		Case ( AllTrim(aHeader[nCntFor][2]) == "C6_PRUNIT" )
			nPrUnit 	:= aCols[n][nCntFor]
		Case ( AllTrim(aHeader[nCntFor][2]) == "C6_VALDESC" )
			nValDesc	:= aCols[n][nCntFor]
		Case ( AllTrim(aHeader[nCntFor][2]) == "C6_PRODUTO" )
			cProduto	:= aCols[n][nCntFor]
			nPosProd	:= nCntFor
			//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//ЁVerifica se a grade esta ativa, e se o produto digita-Ё
			//Ёdo e' uma referencia                                  Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
			If ( lGrade ) .And. MatGrdPrRf(@cProduto)
				lGradeReal := .T.
			EndIf
		Case ( AllTrim(aHeader[nCntFor][2]) == "C6_LOCAL" )
			cLocal   := aCols[n][nCntFor]
			nPosLocal:= nCntFor
		Case ( AllTrim(aHeader[nCntFor][2]) == "C6_TES" )
			cTes     := aCols[n][nCntFor]
		Case ( AllTrim(aHeader[nCntFor][2]) == "C6_NUMLOTE" )
			cNumLote := aCols[n][nCntFor]
		Case ( AllTrim(aHeader[nCntFor][2]) == "C6_LOTECTL" )
			cLoteCtl	:= aCols[n][nCntFor]
		Case ( AllTrim(aHeader[nCntFor][2]) == "C6_LOCALIZ" )
			cLocaliza := aCols[n][nCntFor]
		Case ( AllTrim(aHeader[nCntFor][2]) == "C6_NUMSERI" )
			cNumSerie := aCols[n][nCntFor]
		Case ( AllTrim(aHeader[nCntFor][2]) == "C6_IDENTB6" )
			cIdentB6  := aCols[n][nCntFor]
			nPosIdB6  := nCntFor
		Case ( AllTrim(aHeader[nCntFor][2]) == "C6_NFORI" )
			cNfOrig		:= aCols[n][nCntFor]
			nPosNfOrig	:= nCntFor
		Case ( AllTrim(aHeader[nCntFor][2]) == "C6_SERIORI" )
			cSerieOri	:= aCols[n][nCntFor]
			nPosSerOri  := nCntFor
		Case ( AllTrim(aHeader[nCntFor][2]) == "C6_ITEMORI" )
			cItemOri 	:= aCols[n][nCntFor]
			nPosItemOr 	:= nCntFor
		Case ( AllTrim(aHeader[nCntFor][2]) == "C6_GRADE" )
			cItemGrad:= aCols[n][nCntFor]
		Case ( AllTrim(aHeader[nCntFor][2]) == "C6_RESERVA" )
			cNumRes := aCols[n][nCntFor]
		Case ( AllTrim(aHeader[nCntFor][2]) == "C6_SERVIC" )
			nPosServ  := nCntFor
			cServico := aCols[n][nCntFor]
		EndCase

		If ( Empty(aCols[n][nCntFor]) )
			If ( lRetorno .And. AT(M->C5_TIPO,"CIP")==0 )
				If (	(AllTrim(aHeader[nCntFor][2]) == "C6_QTDVEN" .And. !MaTesSel(aCols[n][nPosTes])).Or.;
						AllTrim(aHeader[nCntFor][2]) == "C6_PRCVEN" .Or.;
						AllTrim(aHeader[nCntFor][2]) == "C6_VALOR"  .Or.;
						AllTrim(aHeader[nCntFor][2]) == "C6_TES" )
					Help(" ",1,"A410VZ")
					lRetorno := .F.
				EndIf
			EndIf
			If ( lRetorno .And. AT(M->C5_TIPO,"CIP") <> 0 )
				If (	AllTrim(aHeader[nCntFor][2]) == "C6_PRCVEN" .Or.;
						AllTrim(aHeader[nCntFor][2]) == "C6_VALOR"  .Or.;
						AllTrim(aHeader[nCntFor][2]) == "C6_TES" )
					Help(" ",1,"A410VZ2")
					lRetorno := .F.
				EndIf
			EndIf
			//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//ЁVerifica se o pedido e uma devolucao de compra, um    Ё
			//Ёcomplemento de ICMS ou IPI, para validar a nota fiscalЁ
			//Ёde origem.                                            Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
			If ( lRetorno .And. At(M->C5_TIPO,"CIPD") <> 0 )
				If ( AllTrim(aHeader[nCntFor][2]) == "C6_NFORI" )
					If ( At(M->C5_TIPO,"CIP") <> 0 )
						Help(" ",1,"A410COMPIP")
					Else
						Help(" ",1,"A410NFORI")
					EndIf
					lRetorno := .F.
				EndIf
			EndIf
			If M->C5_TIPO $ "C" .And. Str(nPrcVen,15,2) <> Str(nValor,15,2);
					.And. nCntFor == nUsado //so testar na ultima vez
				If cPaisLoc != "BRA"
					Help(" ",1,"A410TOTAL")
					lRetorno := .F.
				EndIf
			EndIf

			//зддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Analisa se o tipo do armazem permite a movimentacao |
			//юддддддддддддддддддддддддддддддддддддддддддддддддддддды
			If lRetorno .And. FindFunction('AvalBlqLoc') .And. AvalBlqLoc(aCols[n,nPProduto],aCols[n,nPLocal],aCols[n,nPosTes])
				lRetorno := .F.
			EndIf
			
			//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//ЁVerifica se o pedido e uma devolucao de compra,e se   Ё
			//Ёo produto possui rastro, se positivo o numero do lote Ё
			//Ёe' obrigatorio                                        Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
			If ( lRetorno .And. M->C5_TIPO == "D" .And. AvalTes(cTes,"S"))
				If ( 	( AllTrim(aHeader[nCntFor][2]) == "C6_NUMLOTE" .And.;
						Rastro(cProduto,"S") ) .Or.;
						( AllTrim(aHeader[nCntFor][2]) == "C6_LOTECTL" .And.;
						Rastro(cProduto,"L")) )
					HELP(" ",1,"A100S/LOT")
					lRetorno := .F.
				EndIf
			EndIf
		Else
			If ( AllTrim(aHeader[nCntFor][2]) == "C6_QTDVEN" .And. MaTesSel(aCols[n][nPosTes]) )
				aCols[n][nPosQtdVen] := 0
			EndIf
		EndIf
		If ( !lRetorno )
			nCntFor := nUsado + 1
		EndIf
	Next nCntFor
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁVerifica se o produto nao esta preenchido.                              Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If ( Empty(cProduto) )
		lRetorno := .F.
	EndIf
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁPosiciona Registros.                                                    Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If lRetorno
		dbSelectArea("SC6")
		dbSetOrder(1)
		MsSeek(xFilial("SC6")+M->C5_NUM+cItemSC6+cProduto)

		dbSelectArea("SF4")
		dbSetOrder(1)
		MsSeek(xFilial("SF4")+cTes)

		dbSelectArea("SC0")
		dbSetOrder(1)
		MsSeek(xFilial("SC0")+cNumRes+cProduto+cLocal)

		If M->C5_TIPO == "D"
			dbSelectArea("SD1")
			dbSetOrder(1)
			MsSeek(xFilial("SD1")+cNfOrig+cSerieOri+M->C5_CLIENTE+M->C5_LOJACLI+cProduto+cItemOri)
		Else
			If SF4->F4_PODER3=="D" .And. nPIdentB6 <> 0
				If !Empty(aCols[n][nPIdentB6])
					dbSelectArea("SD1")
					dbSetOrder(4)
					MsSeek(xFilial("SD1")+aCols[n][nPIdentB6])
				Else
					Help(" ",1,"A100USARF4")
					lRetorno := .F.
				EndIf
			EndIf
		EndIf
    EndIf
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁVerifica se o cliente ou fornecedor И valido.                           Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If lRetorno
		dbSelectArea(IIF(M->C5_TIPO$"DB","SA2","SA1"))
		dbSetOrder(1)
		If MsSeek(xFilial(IIF(M->C5_TIPO$"DB","SA2","SA1"))+M->C5_CLIENTE+M->C5_LOJACLI)
			If !RegistroOk(IIF(M->C5_TIPO$"DB","SA2","SA1"))
				lRetorno	 := .F.
			Endif
		Endif
	Endif
	
	If lRetorno
		dbSelectArea(IIF(M->C5_TIPO$"DB","SA2","SA1"))
		dbSetOrder(1)
		If MsSeek(xFilial(IIF(M->C5_TIPO$"DB","SA2","SA1"))+IIf(!Empty(M->C5_CLIENT),M->C5_CLIENT,M->C5_CLIENTE)+M->C5_LOJAENT)
			If !RegistroOk(IIF(M->C5_TIPO$"DB","SA2","SA1"))
				lRetorno	 := .F.
			Endif
		Endif
	Endif	
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Verifica a quantidade do pedido em relacao a quanti- Ё
	//Ё dade reservada.                                      Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If ( lRetorno .And. !Empty(cNumRes) )
		If ( !INCLUI )
			If ( SC6->(Found()) )
				nQtdRese += SC6->C6_QTDRESE
			EndIf
		EndIf
		If ( SC0->(Found()) )
			If ( SC0->C0_QUANT+nQtdRese < 0 )
				Help(" ",1,"A410RESERV")
				lRetorno := .F.
			EndIf
			If lretorno .And. GetNewPar("MV_CHCLRES",.F.)
				If SC0->C0_TIPO == "CL" .And. SC0->C0_DOCRES <> M->C5_CLIENTE
					lRetorno := .F.
					MsgAlert(STR0093 + Alltrim(cNumRes) + STR0094 + SC0->C0_DOCRES)
				Endif
			Endif
			If ( lRetorno .And. (  SF4->F4_ESTOQUE=="N" .Or. M->C5_TIPO$"CIP") )
				Help(" ",1,"A410RESERV")
				lRetorno := .F.
			EndIf
			If ( (SC0->C0_LOTECTL <> cLoteCtl	.Or.;
					SC0->C0_NUMLOTE <> cNumLote	.Or.;
					SC0->C0_LOCALIZ <> cLocaliza	.Or.;
					SC0->C0_NUMSERI <> cNumSerie) )
				Help(" ",1,"A410RESERV")
				lRetorno := .F.
			EndIf
		Else
			Help(" ",1,"A410RESERV")
			lRetorno := .F.
		EndIf
	EndIf
	If ( lRetorno )
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁVerifica se eh grade para calcular o valor total por item da gradeЁ
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		nValorTot := 0
        
		If FindFunction("MsMatGrade") .And. IsAtNewGrd()
			If lGrade .And. lGradeReal  .And. Type("oGrade")=="O" .And. Len(oGrade:aColsGrade) > 0
				If lGrdMult
					nValorTot := a410Arred(oGrade:SomaGrade("C6_VALOR",n),"C6_VALOR",If(cPaisLoc=="CHI" .And. lPrcDec,M->C5_MOEDA,NIL))
				Else
					For nLinha := 1 To Len(oGrade:aColsGrade[n])
						For nColuna := 2 To Len(oGrade:aHeadGrade[n])
							If ( oGrade:aColsFieldByName("C6_QTDVEN",n,nLinha,nColuna) <> 0 )
								nValorTot += a410Arred(oGrade:aColsFieldByName("C6_QTDVEN",n,nLinha,nColuna)*nPrcVen,"C6_VALOR",If(cPaisLoc=="CHI" .And. lPrcdec,M->C5_MOEDA,NIL))
							Endif
						Next nColuna
					Next nLinha
				EndIf
			Else
				nValorTot := A410Arred(nPrcVen*nQtdVen,"C6_VALOR",If(cPaisLoc=="CHI" .And. lPrcdec,M->C5_MOEDA,NIL))
			Endif 
		Else
			If lGrade .And. lGradeReal  .And. Len(aColsGrade) > 0
				For nLinha := 1 To Len(aColsGrade[n])
					For nColuna := 2 To Len(aHeadGrade[n])
						If ( aColsGrade[n][nLinha][nColuna][1] <> 0 )
							nValorTot += a410Arred(aColsGrade[n][nLinha][nColuna][1]*nPrcVen,"C6_VALOR",If(cPaisLoc=="CHI" .And. lPrcdec,M->C5_MOEDA,NIL))
						Endif
					Next nColuna
				Next nLinha
			Else
				nValorTot := A410Arred(nPrcVen*nQtdVen,"C6_VALOR",If(cPaisLoc=="CHI" .And. lPrcdec,M->C5_MOEDA,NIL))
			Endif			
		EndIf

		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁConsiste o valor total do pedido de venda                               Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		Do Case
		Case ( AT(M->C5_TIPO,"DCIP") == 0 .AND.  SF4->F4_PODER3<>"D" ) .Or.;
				((M->C5_TIPO == "D" .Or. SF4->F4_PODER3=="D").And. QtdComp(nQtdVen)<>QtdComp(SD1->D1_QUANT))
			If (nValor <> nValorTot .And. !MaTesSel(aCols[n][nPosTES])) .Or.;
					(nValor <> A410Arred(nPrcVen,"C6_VALOR") .And. MaTesSel(aCols[n][nPosTES]))
				Help(" ",1,"A410TOTAL")
				lRetorno := .F.
			EndIf
			If !SD1->(Found()) .And. SF4->F4_PODER3=="D"
				Help(" ",1,"A100USARF4")
				lRetorno := .F.
			EndIf
		Case M->C5_TIPO == "D" .OR. SF4->F4_PODER3== "D"
			If A410Arred(nValor,"C6_VALOR") <> A410Arred(SD1->D1_TOTAL-SD1->D1_VALDESC-SD1->D1_VALDEV,"C6_VALOR") .And.;
				!MaTesSel(aCols[n][nPosTES]).And.(!SC6->(Found()).Or.SC6->C6_QTDVEN-SC6->C6_QTDENT > 0)
				Help(" ",1,"A410TOTAL")
				lRetorno := .F.
			EndIf
			If !SD1->(Found()) .And. SF4->F4_PODER3=="D"
				Help(" ",1,"A100USARF4")
				lRetorno := .F.
			EndIf
		Case AT(M->C5_TIPO,"CIP") <> 0
			If ( A410Arred(nValor,"C6_VALOR") <> A410Arred(nPrcVen,"C6_VALOR") )
				Help(" ",1,"A410TOTAL")
				lRetorno := .F.
			EndIf
		EndCase
	EndIf
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁVerifica o TES                                                          Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If ( lRetorno )
		If (n > 1 .And. !aCols[n-1][Len(aCols[n-1])])  //verifica se esta deletado
			lRetorno := A410ValTES(cTes,IIf(n > 1 ,aCols[n-1][nPosTes],Nil))
		Else
			lRetorno := A410ValTES(cTes,Nil)
		EndIf
	EndIf
	If ( lRetorno .And. Empty(cNumRes) )
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁConsiste o item quanto a Rastro  ou Localizacao Fisica.                 Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If ( SF4->F4_ESTOQUE=="N" .And. (!Empty(cLoteCtl) .Or. !Empty(cNumLote)) )
			Help(" ",1,"A410TEEST")
			lRetorno := .F.
		Else
			If ( SF4->F4_ESTOQUE =="S" .And. !(M->C5_TIPO $ "CIP") .And. SuperGetMv("MV_GERABLQ")=="N" )
				nSaldo := SldAtuEst(cProduto,cLocal,nQtdVen,cLoteCtl,cNumLote,cLocaliza,cNumSerie,cNumRes ,nil,nil,nil,nil,cServico)
				nSaldo += SC6->C6_QTDEMP
				If ( Localiza(cProduto)  )
					If ( M->C5_TIPO == "D" )
						If ( nSaldo < nQtdVen )
							Help(" ",1,"SALDOLOCLZ")
							lRetorno:=.F.
						EndIf
					Else
						If ( nSaldo < nQtdLib )
							If  !( Type("l410Auto") <> "U" .And. l410Auto )
								Help(" ",1,"SALDOLOCLZ")
							EndIf
							nQtdLib := nSaldo
							aCols[n][nPosQtdLib] := nQtdLib
						EndIf
					EndIf
				EndIf
				If ( Rastro(cProduto) )
					If ( M->C5_TIPO == "D" )
						If ( nQtdVen > nSaldo )
							Help(" ",1,"A440ACILOT")
							lRetorno := .F.
						EndIf
					Else
						If ( nQtdLib > nSaldo )
							Help(" ",1,"A440ACILOT")
							nQtdLib := nSaldo
							aCols[n][nPosQtdLib] := nQtdLib
						EndIf
					EndIf
				EndIf
			EndIf
		EndIf
	EndIf
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁVerifica se o pedido se trata de poder de terceiros   Ё
	//Ёse positivo, verifica se e' um item de grade          Ё
	//Ёse for informa que a grade nao esta disponivel para   Ё
	//Ёpoder de terceiros                                    Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If ( lRetorno .And. SF4->F4_PODER3 $ "RD" )
		If ( cItemGrad == "S" )
			Help(" ",1,"A410GRATER")
			lRetorno:=.F.
		EndIf
	EndIf
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁVerifica o saldo do Poder de Terceiro                                   Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If ( lRetorno .And. SF4->F4_PODER3=="D" )
		aQtdP3 := A410SNfOri(M->C5_CLIENTE,M->C5_LOJACLI,cNfOrig,cSerieOri,"",cProduto,cIdentB6,aCols[n][nPosLocal],,@aPedido)
		If ( aQtdP3[1] < 0 )
			If !Empty(aPedido)
				cAviso := ""
				For nX:=1 To Len(aPedido)
					cAviso += aPedido[nX] + " | "
				Next nX
				Aviso( STR0038, STR0087+cAviso, { "Ok" } )
				lRetorno := .F.
			Else
				Help(" ",1,"A100USARF4")
				lRetorno := .F.
			EndIf
		Else
			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//ЁVerifica o saldo da Liberacao de CQ                                     Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			If aQtdP3[4][6] > 0 
				If (aQtdP3[5]+aQtdP3[6]) > (aQtdP3[4][1]-aQtdP3[4][6])
					Aviso( STR0038, STR0113, { "Ok" } )
					lRetorno := .F.				
				Endif
			Endif			
		EndIf
	EndIf
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Nao permite a inclusao do produto se o almoxarifado  Ё
	//Ё for igual o do CQ e o tipo do pedido for NORMAL.     Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддLarsonддды
	If lRetorno .And. ( M->C5_TIPO $ "NB" .And. SF4->F4_PODER3<>"D" )
		If cLocal == GetMv("MV_CQ")
			Help(" ",1,"ARMZCQ",,GetMv("MV_CQ"),2,15)
			lRetorno := .F.
		EndIf
	EndIf
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁQuando devolucao verifica se a nota fiscal de origem  Ё
	//Ёexiste                                                Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If ( lRetorno .And. M->C5_TIPO == "D" )
		dbSelectArea("SD1")
		dbSetOrder(1)
		If ( MsSeek(xFilial("SD1")+cNfOrig+cSerieOri+M->C5_CLIENTE+M->C5_LOJACLI+cProduto) .And. Empty(cItemOri) )
			Help(" ",1,"A410S/ITDE")
			lRetorno := .F.
		Else
			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//ЁConsiste no acols a saldo em quantidade e valor da devolucao            Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			If lRetorno .And. !Empty(cItemOri)
				aVlrDev := a410SNfOri(M->C5_CLIENTE,M->C5_LOJACLI,cNfOrig,cSerieOri,cItemOri,cProduto,Nil,aCols[n][nPosLocal])
				If nValor > aVlrDev[2]
					Help(" ",1,"A410UNIDIF")
					lRetorno := .F.
				Else
					If ( nQtdVen > aVlrDev[1] )
						Help(" ",1,"A410NSALDO")
						lRetorno := .F.
					EndIf
				EndIf
			EndIf
		EndIf
	EndIf
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁVerificar se o servico digitado,tem alguma Tarefa com Tipo de TransporteЁ
	//Ёinformado (DC5).                                                        Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If  ( lRetorno .And. nPosServ > 0 .And. !Empty(aCols[n][nPosServ]) )
		If !Empty(cLocaliza)
			//-- Verifica se o servico diferente de conferencia 000005 - DLCONFEREN
			DC5->(DbSetOrder(1)) //DC5_FILIAL+DC5_SERVIC+DC5_ORDEM
			If	DC5->(MsSeek(xFilial('DC5')+aCols[n,nPosServ]) .And. DC5_FUNEXE<>'000005')
				Aviso('SigaWMS', STR0104, {'Ok'}) //"Nao informar servico WMS para itens com endereco informado."
				lRetorno := .F.
			EndIf
		EndIf
		If lRetorno
			dbSelectArea("DC5")
			dbSetOrder(1)
			#IFDEF TOP
				If TcSrvType()<>"AS/400" .And. !InTransact()
					aStruDC5 := DC5->(dbStruct())
					cArqQry := "DC5"
					lQuery  := .T.
					cQuery := "SELECT DC5.*,DC5.R_E_C_N_O_ DC5RECNO "
					cQuery += "FROM "+RetSqlName("DC5")+" DC5 "
					cQuery += "WHERE DC5.DC5_FILIAL='"+xFilial("DC5")+"' AND "
					cQuery += "DC5.DC5_SERVIC='"+aCols[n][nPosServ]+"' AND "
					cQuery += "DC5.DC5_TIPTRA <>'"+Criavar("DC5_TIPTRA",.F.)+"' AND "
					cQuery += "DC5.D_E_L_E_T_<>'*' "
					cQuery += "ORDER BY "+SqlOrder(DC5->(IndexKey()))
					cQuery := ChangeQuery(cQuery)
					dbSelectArea("DC5")
					dbCloseArea()
					dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cArqQry,.T.,.T.)
					For nCntFor := 1 To Len(aStruDC5)
						If ( aStruDC5[nCntFor,2]<>"C" )
							TcSetField(cArqQry,aStruDC5[nCntFor,1],aStruDC5[nCntFor,2],aStruDC5[nCntFor,3],aStruDC5[nCntFor,4])
						EndIf
					Next nCntFor
				Else
			#ENDIF
				cArqQry := "DC5"
				MsSeek(xFilial("DC5")+aCols[n][nPosServ])
				#IFDEF TOP
				EndIf
				#ENDIF
			
			Do While !Eof() .And. (cArqQry)->DC5_FILIAL+(cArqQry)->DC5_SERVIC==xFilial('DC5')+aCols[n,nPosServ]
				If !Empty(DC5->DC5_TIPTRA)
					Help(' ',1, 'A410SERTMS')
					lRetorno := .F.
					Exit
				EndIf
				dbSkip()
			EndDo
			
		EndIf
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁFecha query criada                                                      Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If lQuery
			dbSelectArea(cArqQry)
			dbCloseArea()
		EndIf
		//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁImpede a alteracao de Itens do Pedido com Servico de WMS jah executado Ё
		//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If lRetorno .And. IntDL(aCols[n, nPosProd]) .And. ALTERA
			SC9->(dbSetOrder(2)) //-- C9_FILIAL+C9_CLIENTE+C9_LOJA+C9_PEDIDO+C9_ITEM
			If	SC9->(MsSeek(xFilial('SC9')+SC5->C5_CLIENTE+SC5->C5_LOJACLI+SC5->C5_NUM+aCols[n, nPItem]))
				If	FindFunction("WmsChkDCF")
					If	Empty(SC9->C9_NFISCAL) .And. WmsChkDCF('SC9',SC9->C9_CARGA,,SC9->C9_SERVIC,'2,3',,SC9->C9_PEDIDO,SC9->C9_ITEM,SC9->C9_CLIENTE,SC9->C9_LOJA,SC9->C9_LOCAL,SC9->C9_PRODUTO,'','',SC9->C9_SEQUEN,SC9->C9_ITEM)
						Aviso('SigaWMS',STR0066+AllTrim(aCols[n,nPItem])+STR0062+" ( "+AllTrim(Transform(SC9->C9_QTDLIB, PesqPict('SC9', 'C9_QTDLIB')))+" "+STR0065, {'Ok'}) //'O Item '###" nao pode ser alterado porque possui servicos de WMS pendentes. Estorne estes servicos para proceder com a alteracao."###"na 1a UM ) nao pode ser alterada."
						lRetorno := .F.
    				EndIf
				Else
					If	Empty(SC9->C9_NFISCAL) .And. SC9->C9_BLWMS == '05' //-- Liberado para WMS (Servico jah executado)
						Aviso('SigaWMS',STR0066+AllTrim(aCols[n,nPItem])+STR0062+" ( "+AllTrim(Transform(SC9->C9_QTDLIB, PesqPict('SC9', 'C9_QTDLIB')))+" "+STR0065, {'Ok'}) //'O Item '###" nao pode ser alterado porque possui servicos de WMS pendentes. Estorne estes servicos para proceder com a alteracao."###"na 1a UM ) nao pode ser alterada."
						lRetorno := .F.
					EndIf
				EndIf
			EndIf
			If	lRetorno .And. SC5->(FieldPos('C5_GERAWMS'))>0
				//-- C5_TPCARGA -> 1=Utiliza;2=Nao Utiliza;3=Utiliza sem Unitizacao
				//-- C5_GERAWMS -> 1=no Pedido;2=na Montagem da Carga;3=na Unitizacao da Carga
				If	M->C5_TPCARGA == "2" .And. M->C5_GERAWMS <> "1"
					Aviso('SigaWMS', STR0111, {'Ok'}) //"Nao sera gerado OS.WMS. Pedido nao utiliza carga"
					lRetorno := .F.
				EndIf
			EndIf
		EndIf
	EndIf
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁVerifica a integridade do contrato de parceira                          Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If lRetorno .And. nPContrat > 0 .And. nPItContr > 0 .And. ADB->(LastRec())<>0
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Verifica o saldo de contratos deste pedido de venda  Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
		For nY := 1 To Len(aCols)
			If !aCols[nY][nUsado+1] .And. N <> nY .And. !Empty(aCols[nY][nPContrat])
				nX := aScan(aContrato,{|x| x[1] == aCols[nY][nPContrat] .And. x[2] == aCols[nY][nPItContr]})
				If nX == 0
					aadd(aContrato,{aCols[nY][nPContrat],aCols[nY][nPItContr],aCols[nY][nPQtdVen]})
					nX := Len(aContrato)
				Else
					aContrato[nX][3] += aCols[nY][nPQtdVen]
				EndIf
			EndIf
			dbSelectArea("SC6")
			dbSetOrder(1)
			If ALTERA .And. MsSeek(xFilial("SC6")+M->C5_NUM+aCols[nY][nPItem]) .And. !Empty(SC6->C6_CONTRAT)
				nX := aScan(aContrato,{|x| x[1] == SC6->C6_CONTRAT .And. x[2] == SC6->C6_ITEMCON})
				If nX == 0
					aadd(aContrato,{SC6->C6_CONTRAT,SC6->C6_ITEMCON,0})
					nX := Len(aContrato)
				EndIf
				aContrato[nX][3] -= SC6->C6_QTDVEN
			EndIf
		Next nY
		dbSelectArea("ADB")
		dbSetOrder(1)
		If MsSeek(xFilial("ADB")+aCols[n][nPContrat]+aCols[n][nPItContr])
			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//ЁVerifica a quantidade                                                   Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			If Empty(ADB->ADB_PEDCOB) .And. !Empty(ADB->ADB_TESCOB)
				If nQtdVen <> ADB->ADB_QUANT .Or. aCols[n][nPosTES] <> ADB->ADB_TESCOB
					Help(" ",1,"A410CTRQT1")
					lRetorno := .F.
				EndIf
			Else
				nX := aScan(aContrato,{|x| x[1] == aCols[n][nPContrat] .And. x[2] == aCols[n][nPItContr]})
				If nQtdVen > ADB->ADB_QUANT - ADB->ADB_QTDEMP-If(nX>0,aContrato[nX][3],0) .And. (nPNumOrc==0 .Or. Empty(aCols[n][nPNumOrc]))
					Help(" ",1,"A410CTRQT2")
					lRetorno := .F.
				EndIf
				//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//ЁVerifica o preco de venda                                               Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
				If lRetorno .And. ADB->ADB_PRCVEN>nPrcVen .And. !lAltCtr3
					Aviso(STR0038, STR0079, {'Ok'})
					lRetorno := .F.
				EndIf
			EndIf
		Else
			aCols[n][nPContrat] := CriaVar("C6_CONTRAT",.F.)
			aCols[n][nPItContr] := CriaVar("C6_ITEMCON",.F.)
		EndIf
	EndIf

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Valida as colunas do browse referente ao SIGAPMS                       Ё
	//Ё Colunas: C6_PROJPMS, C6_EDTPMS, C6_TASKPMS                             Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If lRetorno .AND. ( nPC6_PROJPMS > 0 .AND. nPC6_EDTPMS > 0 .AND. nPC6_TASKPMS > 0 )
		lRetorno := a410VldPMS(aCols[n][nPC6_PROJPMS],aCols[n][nPC6_EDTPMS],aCols[n][nPC6_TASKPMS],SF4->F4_MOVPRJ,M->C5_TIPO )
	EndIf

	//
	// Template GEM 
	// valida o empreendimento e o codigo do produto
	//
	If lRetorno 
		If ExistBlock("GEM410LI") 
			lRetorno := ExecBlock("GEM410LI",.F.,.F.,{N})
		ElseIf ExistTemplate("GEM410LI") 
			lRetorno := ExecTemplate("GEM410LI",.F.,.F.,{N})
		Endif
	EndIf

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁAtualiza os Opcionais                                                   Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If lRetorno .And. If(Type("lShowOpc")=="L",lShowOpc,.F.) .And. !( Type("l410Auto") != "U" .And. l410Auto ) .And. nPOpcional > 0
		//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Funcao utilizada para verificar a ultima versao do fonte		Ё
		//Ё SIGACUSB.PRX aplicado no rpo do cliente, assim verificando		|
		//| a necessidade de uma atualizacao neste fonte. NAO REMOVER !!!	Ё
		//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If !(FindFunction("SIGACUSB_V") .and. SIGACUSB_V() >= 20090204)
			Final(STR0122)	//"Atualizar SIGACUSB.PRX !!!"
		EndIf
		lRetorno := SeleOpc(2,"MATA410",cProduto,,,aCols[n][nPOpcional],"M->C6_PRODUTO",,aCols[n,nPQtdVen],aCols[n,nPEntreg])
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Aqui ┌ efetuado o tratamento diferencial de Precos para os   Ё
		//Ё Opcionais do Produto.                                        Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		dbSelectArea("SGA")
		dbSetOrder(1)
		cOpcional := aCols[n][nPOpcional]
		While !Empty(cOpcional)
			cOpc      := SubStr(cOpcional,1,At("/",cOpcional)-1)
			cOpcional := SubStr(cOpcional,At("/",cOpcional)+1)
			If ( MsSeek(xFilial("SGA")+cOpc) )
				If AT(M->C5_TIPO,"CIP") == 0 
					aCols[n][nPPrcVen] += SGA->GA_PRCVEN
					If QtdComp(aCols[n][nPPrUnit]) != QtdComp(0)
						aCols[n][nPPrUnit] := aCols[n][nPPrcVen]
					EndIf
					aCols[n][nPValor]  := (nQtdVen * aCols[n][nPPrcVen])
				EndIf

			EndIf
		EndDo
		If lRetorno
			lShowOpc := .F.
		EndIf
	EndIf
EndIf

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁVerificar se for devolucao e o produto for quality, se o mesmo ja foi   Ё
//Ёliberado do estoque na qualidade.                                       Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If ( lRetorno .And. !aCols[n][Len(aCols[n])] .and. M->C5_TIPO == "D" )
	lRetorno := Ma410VldQEK( M->C5_CLIENTE,M->C5_LOJACLI,aCols[n][nPNfOrig],aCols[n][nPSerOrig],aCols[n][nPItOrig],aCols[n][nPProduto]) 
EndIF

If ( aCols[n][Len(aCols[n])] .And. lRetorno )
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁPosiciona Registros.                                                    Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	dbSelectArea("SC6")
	dbSetOrder(1)
	MsSeek(xFilial("SC6")+M->C5_NUM+aCols[n][nPItem])
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁQdo um item possuir quantidade entregue nao deve ser permitida a        Ё
	//Ёexclusao neste item.                                                    Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If ( lRetorno .And. SC6->(Found()) .And. SC6->C6_QTDENT <> 0 )
		Help(" ",1,"A410FAT")
		lRetorno := .F.
	EndIf
	//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁImpede a exclusao de Itens do Pedido com Servico de WMS jah executado  Ё
	//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	nPosProd := aScan(aHeader,{|x| AllTrim(x[2])=="C6_PRODUTO" })
	If lRetorno .And. IntDL(aCols[n, nPosProd]) .And. ALTERA
		SC9->(dbSetOrder(2)) //-- C9_FILIAL+C9_CLIENTE+C9_LOJA+C9_PEDIDO+C9_ITEM
		If	SC9->(MsSeek(xFilial('SC9')+SC5->C5_CLIENTE+SC5->C5_LOJACLI+SC5->C5_NUM+aCols[n, nPItem]))
			If	FindFunction("WmsChkDCF")
				If	Empty(SC9->C9_NFISCAL) .And. WmsChkDCF('SC9',SC9->C9_CARGA,,SC9->C9_SERVIC,'2,3',,SC9->C9_PEDIDO,SC9->C9_ITEM,SC9->C9_CLIENTE,SC9->C9_LOJA,SC9->C9_LOCAL,SC9->C9_PRODUTO,'','',SC9->C9_SEQUEN,SC9->C9_ITEM)
					Aviso('SigaWMS',STR0066+AllTrim(aCols[n,nPItem])+STR0062+" ( "+AllTrim(Transform(SC9->C9_QTDLIB, PesqPict('SC9', 'C9_QTDLIB')))+" "+STR0065, {'Ok'}) //'O Item '###" nao pode ser alterado porque possui servicos de WMS pendentes. Estorne estes servicos para proceder com a alteracao."###"na 1a UM ) nao pode ser alterada."
					lRetorno := .F.
   				EndIf
			Else
				If	Empty(SC9->C9_NFISCAL) .And. SC9->C9_BLWMS == '05'
					Aviso('SigaWMS',STR0066+AllTrim(aCols[n,nPItem])+STR0062,{'Ok'}) //"O item"###" nao pode ser alterado porque possui servicos de WMS pendentes. Estorne estes servicos para proceder com a alteracao."
					lRetorno := .F.
				EndIf
			EndIf
		EndIf
	EndIf
EndIf

dbSelectArea("SC6")

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Pontos de Entrada 				                                       Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If lRetorno .And. ExistTemplate("M410LIOK")
	lRetorno := ExecTemplate("M410LIOK",.F.,.F.,o)
EndIf
If lRetorno .And. ExistBlock("M410LIOK")
	lRetorno := ExecBlock("M410LIOK",.F.,.F.,o)
EndIf

//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Integracao com o ACD - Faz validacao dos itens dos PV's Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If lRetorno .And. lIntACD .And. FindFunction("CBM410ACDL")
	lRetorno := CBM410ACDL()
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Ponto de Entrada M410ACDL - Utilizado para validar os itens liberados que possuam Ordem de Separacao (SIGAACD) Ё
//Ё Obs.: Criado este novo P.E. para nao conflitar com outro modulo Template que possivelmente use o P.E. anterior Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
ElseIf lRetorno .And. ExistTemplate("M410ACDL")
	lRetorno := ExecTemplate("M410ACDL",.F.,.F.)
EndIf
If lRetorno .And. ExistBlock("M410ACDL")
	lRetorno := ExecBlock("M410ACDL",.F.,.F.)
EndIf

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Restaura a integridade da rotina                                       Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
Ma410Rodap(o)
RestArea(aArea)
Return(lRetorno)
/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    Ёa410TudOk Ё Rev.  ЁEduardo Riera          Ё Data Ё26.08.2001Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁValidacao de toda a GetDados                                Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁExpO1: Objeto da GetDados                                   Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁNenhum                                                      Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁDescri┤└o ЁEsta rotina tem como objetivo efetuar a validacao em toda a Ё╠╠
╠╠Ё          ЁGetdados                                                    Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁObservacaoЁ                                                            Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё Materiais/Distribuicao/Logistica                           Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function A410TudOk(o)

Local aArea      := GetArea()
Local aChkPMS	 := {}
Local aHandFat	 := {}
Local aContrato  := {}
Local aInfo			 := {{"Projeto","Tarefa","Faturamento","Remessas","Saldo Faturam.","Saldo Rem."}}
Local nPProduto	 := aScan(aHeader,{|x| AllTrim(x[2])=="C6_PRODUTO"})
Local nPTes		 := aScan(aHeader,{|x| AllTrim(x[2])=="C6_TES"})
Local nPItem	 := aScan(aHeader,{|x| AllTrim(x[2])=="C6_ITEM"})
Local nPQtdVen	 := aScan(aHeader,{|x| AllTrim(x[2])=="C6_QTDVEN"})
Local nPValor	 := aScan(aHeader,{|x| AllTrim(x[2])=="C6_VALOR"})
Local nPOper	 := aScan(aHeader,{|x| AllTrim(x[2])=="C6_OPER"})
Local nPPrj		 := aScan(aHeader,{|x| AllTrim(x[2])=="C6_PROJPMS"})
Local nPTsk		 := aScan(aHeader,{|x| AllTrim(x[2])=="C6_TASKPMS"})
Local nPEDT		 := aScan(aHeader,{|x| AllTrim(x[2])=="C6_EDTPMS"})
Local nPQtdLib	 := aScan(aHeader,{|x| AllTrim(x[2])=="C6_QTDLIB"})
Local nPContrat  := aScan(aHeader,{|x| AllTrim(x[2])=="C6_CONTRAT"})
Local nPItemCon  := aScan(aHeader,{|x| AllTrim(x[2])=="C6_ITEMCON"})
Local nPNfOrig   := aScan(aHeader,{|x| AllTrim(x[2])=="C6_NFORI"})
Local nPSerOrig  := aScan(aHeader,{|x| AllTrim(x[2])=="C6_SERIORI"})
Local nPItOrig   := aScan(aHeader,{|x| AllTrim(x[2])=="C6_ITEMORI"})
Local nPNumOrc   := aScan(aHeader,{|x| AllTrim(x[2])=="C6_NUMORC"})
Local nPReserva  := aScan(aHeader,{|x| AllTrim(x[2])=="C6_RESERVA"})
Local nPLocal    := aScan(aHeader,{|x| AllTrim(x[2])=="C6_LOCAL"})
Local nPprcven   := aScan(aHeader,{|x| AllTrim(x[2])=="C6_PRCVEN"})
Local nPprunit   := aScan(aHeader,{|x| AllTrim(x[2])=="C6_PRUNIT"})
Local nPosServ   := aScan(aHeader,{|x| AllTrim(x[2])=="C6_SERVIC"})
Local nMaxArray	 := Len(aCols)
Local nCntFor	 := 0
Local lRetorna	 := .T.
Local nQtdDel	 := 0
Local nX	     := 0
Local nSldPms	 := 0
Local nSldPmsR	 := 0
Local nTotPed    := 0
Local cAuxPrj	 := ""
Local cTesAux    := ""

If ( nMaxArray == 1  )
	If	(  Empty(aCols[nMaxArray][nPProduto]) )
		Help(" ",1,"A410SEMREG")
		lRetorna	 := .F.
	EndIf
EndIf

If M->C5_TIPO $ "DB" .And. M->C5_ACRSFIN <> 0
	Help(" ",1,"A410DEVACR")
	lRetorna := .F.
Endif

//зддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁVerifica se o usuario tem premissao para alterar o Ё
//Ёpedido de venda                                    Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддды
If cPaisLoc <> "BRA" .AND. SC5->(FieldPos("C5_CATPV")) > 0 .AND. !Empty(M->C5_CATPV)
	If AliasIndic("AGS") //Tabela que relaciona usuario com os Tipos de Pedidos de vendas que ele tem acesso
		DBSelectArea("AGS")
		DBSetOrder(1)
		If DBSeek(xFilial("AGS") + __cUserId) //Se nЦo encontrar o usuАrio na tabela, permite ele alterar o pedido
			If !DBSeek(xFilial("AGS") + __cUserId + M->C5_CATPV) //Verifica se o usuario tem premissao
				MsgStop(STR0167 + " " + STR0003 + " " + STR0168)//"Este usuario nao tem permissao para incluir pedidos de venda com essa categoria."
				lRetorna := .F.
			EndIf
		EndIf
	EndIf
EndIf


//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Valida se a nota ainda esta no CQ.                   Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
If M->C5_TIPO == "D"
	For nCntFor := 1 to nMaxArray
		If !aCols[nCntFor][Len(aCols[nCntFor])]
			lRetorna := Ma410VldQEK( M->C5_CLIENTE,M->C5_LOJACLI,aCols[nCntFor][nPNfOrig],aCols[nCntFor][nPSerOrig],aCols[nCntFor][nPItOrig],aCols[nCntFor][nPProduto]) 
  	 	 	If !lRetorna
   			 	Exit
   		 	EndIf
		EndIF
	Next nCntFor
EndIf

//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё verifica se o ultimo elemento do array esta em brancoЁ
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
If ( lRetorna )
	For nCntFor := 1 to nMaxArray
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁDeleta os itens com  produto em branco                                  Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If ( Empty(aCols[nCntFor,nPProduto]) )
			aCols[nCntFor,Len(aCols[nCntFor])] := .T.
		EndIf
		If ( !aCols[nCntFor][Len(aCols[nCntFor])] )//Deletado
			If Empty(cTesAux)
			   cTesAux:= aCols[nCntFor][nPTes] 
			EndIf
			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//ЁAvalia o Tes                                                            Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			If (nCntFor > 1 .And. !aCols[nCntFor-1][Len(aCols[nCntFor-1])])  //verifica se esta deletado
				lRetorna	:= A410ValTES(aCols[nCntFor][nPTes],IIf(nCntFor > 1,aCols[nCntFor-1][nPTes],NIL))
			Else
				lRetorna	:= A410ValTES(aCols[nCntFor][nPTes],cTesAux)
			EndIf
			If ( NoRound(aCols[nCntFor][nPQtdLib],aHeader[nPQtdLib,4]) > NoRound(aCols[nCntFor][nPQtdVen],aHeader[nPQtdVen,4]) .And. SuperGetMv("MV_LIBACIM") )
				Help(" ",1,"QTDLIBMAI")
				lRetorna := .F.
			EndIf
			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Verifica os campos C6_PRCVEN, C6_VALOR e C6_PRUNIT se estao em branco|
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			If ( lRetorna .And. AT(M->C5_TIPO,"CIP")==0 )
				If ( Empty(aCols[nCntFor,nPPrcven]) ) .or. ( Empty(aCols[nCntFor,nPValor]) )
					Help(" ",1,"A410VZ")
					lRetorna := .F.
				EndIf
			EndIf
			If ( lRetorna .And. AT(M->C5_TIPO,"CIP") <> 0 )
				If ( Empty(aCols[nCntFor,nPPrcven]) ) .or. ( Empty(aCols[nCntFor,nPValor]) )
					Help(" ",1,"A410VZ2")
					lRetorna := .F.
				EndIf
			EndIf
			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Verifica o contrato de parceria                              Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			If nPContrat<>0 .And. nPItemCon<>0 .And. !Empty(aCols[nCntFor][nPContrat])
				nX := aScan(aContrato,{|x| x[1] == aCols[nCntFor][nPContrat] .And. x[2] == aCols[nCntFor][nPItemCon]})
				If nX == 0
					aadd(aContrato,{aCols[nCntFor][nPContrat],aCols[nCntFor][nPItemCon],aCols[nCntFor][nPQtdVen]})
					nX := Len(aContrato)
				Else
					aContrato[nX][3] += aCols[nCntFor][nPQtdVen]
				EndIf
			EndIf
			//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//ЁImpede a alteracao de Itens do Pedido com Servico de WMS jah executado Ё
			//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			If	lRetorna .And. IntDL(aCols[nCntFor,nPProduto]) .And. ALTERA
				SC9->(dbSetOrder(2)) //-- C9_FILIAL+C9_CLIENTE+C9_LOJA+C9_PEDIDO+C9_ITEM
				If	SC9->(MsSeek(xFilial('SC9')+SC5->C5_CLIENTE+SC5->C5_LOJACLI+SC5->C5_NUM+aCols[nCntFor,nPItem]))
					If	FindFunction("WmsChkDCF")
						If	Empty(SC9->C9_NFISCAL) .And. !(aCols[nCntFor, nPQtdLib]==SC9->C9_QTDLIB) .And.;
							WmsChkDCF('SC9',SC9->C9_CARGA,,SC9->C9_SERVIC,'2,3',,SC9->C9_PEDIDO,SC9->C9_ITEM,SC9->C9_CLIENTE,SC9->C9_LOJA,SC9->C9_LOCAL,SC9->C9_PRODUTO,'','',SC9->C9_SEQUEN,SC9->C9_ITEM)
							Aviso('SigaWMS',STR0066+AllTrim(aCols[nCntFor,nPItem])+STR0062+" ( "+AllTrim(Transform(SC9->C9_QTDLIB, PesqPict('SC9', 'C9_QTDLIB')))+" "+STR0065, {'Ok'}) //'O Item '###" nao pode ser alterado porque possui servicos de WMS pendentes. Estorne estes servicos para proceder com a alteracao."###"na 1a UM ) nao pode ser alterada."
							lRetorna := .F.
						EndIf
					Else
						If	Empty(SC9->C9_NFISCAL) .And. SC9->C9_BLWMS == '05' .And. !(aCols[nCntFor, nPQtdLib]==SC9->C9_QTDLIB) //-- Liberado para WMS (Servico jah executado)
							Aviso('SigaWMS',STR0066+AllTrim(aCols[nCntFor,nPItem])+STR0062+" ( "+AllTrim(Transform(SC9->C9_QTDLIB, PesqPict('SC9', 'C9_QTDLIB')))+" "+STR0065, {'Ok'}) //'O Item '###" nao pode ser alterado porque possui servicos de WMS pendentes. Estorne estes servicos para proceder com a alteracao."###"na 1a UM ) nao pode ser alterada."
							lRetorna := .F.
						EndIf
					EndIf
				EndIf
				If	lRetorna .And. SC5->(FieldPos('C5_GERAWMS'))>0
					If  nPosServ > 0 .And. !Empty(aCols[nCntFor][nPosServ])
						//-- C5_TPCARGA -> 1=Utiliza;2=Nao Utiliza;3=Utiliza sem Unitizacao
						//-- C5_GERAWMS -> 1=no Pedido;2=na Montagem da Carga;3=na Unitizacao da Carga
						If	M->C5_TPCARGA == "2" .And. M->C5_GERAWMS <> "1"
							Aviso('SigaWMS', STR0111, {'Ok'}) //"Nao sera gerado OS.WMS. Pedido nao utiliza carga"
							lRetorna := .F.
						EndIf
					EndIf
				EndIf
			EndIf
			//здддддддддддддддддддддддддддддддддддддддддддддддддд©
			//ЁVerifica se os projetos possuem saldo para faturarЁ
			//юдддддддддддддддддддддддддддддддддддддддддддддддддды  
			If lRetorna .And. SuperGetMv("MV_PMSBLQF",,"0") == "1"
				If !Empty(aCols[nCntFor][nPPrj])
					If !Empty(aCols[nCntFor][nPEDT])
						Aviso(STR0072,STR0073+aCols[nCntFor][nPItem]+".",{STR0074},2)
						lRetorna := .F.
					Else
						nPosChk := aScan(aChkPMS,{|x| x[2]+x[3]==aCols[nCntFor][nPPrj]+aCols[nCntFor][nPTsk]})
						If nPosChk > 0 
							If aCols[nCntFor][nPTes] $ SuperGetMv("MV_PMSTSV",,"")
								aChkPMS[nPosChk][1] += aCols[nCntFor][nPValor]
							EndIf
							If aCols[nCntFor][nPTes] $ SuperGetMv("MV_PMSTSR",,"")
								aChkPMS[nPosChk][4] += aCols[nCntFor][nPValor]
							EndIf
						Else
							If aCols[nCntFor][nPTes] $ SuperGetMv("MV_PMSTSV",,"")
								aAdd(aChkPMS,{aCols[nCntFor][nPValor],aCols[nCntFor][nPPrj],aCols[nCntFor][nPTsk],0})
							EndIf
							If aCols[nCntFor][nPTes] $ SuperGetMv("MV_PMSTSR",,"")
								aAdd(aChkPMS,{0,aCols[nCntFor][nPPrj],aCols[nCntFor][nPTsk],aCols[nCntFor][nPValor]})
							EndIf
						EndIf
					EndIf
				EndIf
			EndIf
			//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//ЁVerifica se o pedido e uma devolucao de compra, um    Ё
			//Ёcomplemento de ICMS ou IPI, para validar a nota fiscalЁ
			//Ёde origem.                                            Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
			If ( lRetorna .And. At(M->C5_TIPO,"CIPD") <> 0 .And. Empty( aCols[nCntFor,nPNFOrig] ) ) 
				If ( At(M->C5_TIPO,"CIP") <> 0 )
					Help(" ",1,"A410COMPIP")
				Else
					Help(" ",1,"A410NFORI")
				EndIf
				lRetorna := .F.
			EndIf
			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Verifica as faixas da condicao de pagamento                  Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			nTotPed += aCols[nCntFor][nPValor]
			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Verifica o contrato de parceria                              Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			If nPContrat<>0 .And. nPItemCon<>0
				dbSelectArea("SC6")
				dbSetOrder(1)
				If MsSeek(xFilial("SC6")+M->C5_NUM+aCols[nCntFor][nPItem]) .And. !Empty(SC6->C6_CONTRAT)
					nX := aScan(aContrato,{|x| x[1] == SC6->C6_CONTRAT .And. x[2] == SC6->C6_ITEMCON})
					If nX == 0
						aadd(aContrato,{SC6->C6_CONTRAT,SC6->C6_ITEMCON,0})
						nX := Len(aContrato)
					EndIf
					aContrato[nX][3] -= SC6->C6_QTDVEN
				EndIf
			EndIf
			If lRetorna .And. GetNewPar("MV_CHCLRES",.F.)
				If nPReserva>0 .And. !Empty(aCols[nCntFor][nPReserva])
					If SC0->(MsSeek(xFilial("SC0")+aCols[nCntFor][nPReserva]+aCols[nCntFor][nPProduto]+aCols[nCntFor][nPLocal]))
						If SC0->C0_DOCRES <> M->C5_CLIENTE
							MsgAlert(STR0093 + Alltrim(aCols[nCntFor][nPReserva]) + STR0094 + SC0->C0_DOCRES)
							lRetorna := .F.
						Endif
					Endif
				Endif
			Endif
		Else
			nQtdDel++
		EndIf
		//
		// Template GEM - Gestao de Empreendimentos Imobiliarios
		//
		// Valida a linha do browse
		//
		If lRetorna 
			If ExistBlock("GEM410LI") 
				lRetorna := ExecBlock("GEM410LI",.F.,.F.,{ nCntFor })
			ElseIf ExistTemplate("GEM410LI") 
				lRetorna := ExecTemplate("GEM410LI",.F.,.F.,{ nCntFor })
			Endif
		EndIf

		If ( !lRetorna )
			Exit
		EndIf
	Next nCntFor
	If ( nQtdDel >= nMaxArray .And. ALTERA )
		Help(" ",1,"EXCLTODOS")
		lRetorna := .F.
	EndIf
EndIf

If lRetorna
	aChkPMS := aSort(aChkPMS,,,{|x,y| x[2] < y[2] })
	cAuxPrj	:= ""
	For nX := 1 to Len(aChkPMS)
		If cAuxPrj <> aChkPMS[nX,2]
			AF8->(dbSetOrder(1))
			AF8->(MsSeek(xFilial()+aChkPMS[nX,2]))
			aHandFat := PmsIniFat(AF8->AF8_PROJET,AF8->AF8_REVISA,AF8->AF8_PROJET+SPACE(2))
		EndIf
		If !PmsChkSldF(aHandFat,M->C5_MOEDA,aChkPMS[nX,1],aChkPMS[nX,2],"",aChkPMS[nX,3],Altera,M->C5_EMISSAO,@nSldPms,aChkPMS[nX,4],nSldPmsR)
			aAdd(aInfo,{aChkPMS[nX,2],aChkPMS[nX,3],TransForm(aChkPMS[nX,1],"@E 999,999,999,999.99"),TransForm(aChkPMS[nX,4],"@E 999,999,999,999.99"),TransForm(nSldPms,"@E 999,999,999,999.99"),TransForm(nSldPmsR,"@E 999,999,999,999.99")})
			lRetorna := .F.
		EndIf
	Next nX
	If !lRetorna
		If Aviso(STR0075,STR0076,{STR0077,STR0074},2)==1
			PmsDispBox(aInfo,6,STR0078,{30,60,50,50,50,50},,1)
		EndIf
	EndIf
EndIf
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Verifica o contrato de parceria                              Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If lRetorna
	For nX := 1 To Len(aContrato)
		dbSelectArea("ADB")
		dbSetOrder(1)
		MsSeek(xFilial("ADB")+aContrato[nX][1]+aContrato[nX][2])
		If aContrato[nX][3] > ADB->ADB_QUANT-ADB->ADB_QTDEMP .And. (nPNumOrc==0 .And. Empty(aCols[nX][nPNumOrc]))
			Help(" ",1,"A410QTDCTR2")
			lRetorna := .F.
		EndIf
	Next nX
EndIf
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁVerifica a Condicao de Pagamento Tipo 9                                 Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If !A410Tipo9()
	lRetorna  := .F.
EndIf
//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁVerifica se a tabela de precos eh valida                 Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If lRetorna
	lRetorna := MaVldTabPrc(M->C5_TABELA,M->C5_CONDPAG,,M->C5_EMISSAO)
EndIf

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Verifica as faixas da condicao de pagamento                  Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If lRetorna
	dbSelectArea("SE4")
	dbSetOrder(1)
	MsSeek(xFilial("SE4")+M->C5_CONDPAG)
	If nTotPed > SE4->E4_SUPER .AND. SE4->E4_SUPER <> 0 .And. GetNewPar("MV_CNDPLIM","1")=="1"
		Help(" ","1","LJLIMSUPER")
		lRetorna := .F.
	ElseIf nTotPed < SE4->E4_INFER .AND. SE4->E4_INFER <> 0 .And. GetNewPar("MV_CNDPLIM","1")=="1"
		Help(" ","1","LJLIMINFER")
		lRetorna := .F.
	Endif
EndIf
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁVerifica a Validacao dos Pontos de Entrada                              Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If ( lRetorna )
	If ( ExistTemplate("MTA410",,.T.) )
		lRetorna  := ExecTemplate("MTA410",.F.,.F.)
	EndIf
EndIf
If ( lRetorna )
	If ( ExistBlock("MTA410",,.T.) )
		lRetorna  := ExecBlock("MTA410",.F.,.F.)
	EndIf
EndIf
RestArea(aArea)
Return( lRetorna )

/*
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤└o    ЁA410ValDelЁ Autor Ё Aline Correa do Vale  Ё Data Ё05/03/02  Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤└o Ё Valida a exclusao de itens com OP na alteracao do PV       Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё Generico                                                   Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
*/
Function A410ValDel()
Local lRet		:= .T.
Local nPosItem  := aScan(aHeader,{|x| AllTrim(x[2])=="C6_ITEM"})
Local nPosProd  := aScan(aHeader,{|x| AllTrim(x[2])=="C6_PRODUTO"})
Local lAtuSGJ	:= SuperGetMV("MV_PVCOMOP",.F.,.F.) .And. FindFunction("ALIASINDIC") .And. AliasIndic("SGJ")
//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Trata se exclui ou nao itens que geraram OPs         Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
If !aCols[n][Len(aCols[n])]
	SC6->(dbSetOrder(1))
	If SC6->(MsSeek(xFilial("SC6")+SC5->C5_NUM+aCols[n][nPosItem]+aCols[n][nPosProd]))
		If SC6->C6_OP $ "01/03"
			If !lAtuSGJ .And. SuperGetMv("MV_DELPVOP",.F.,.T.)
				lRet:=(Aviso(OemToAnsi(STR0014),STR0027+SC6->C6_ITEM+" - "+SC6->C6_PRODUTO+STR0028+SC6->C6_NUMOP+" "+SC6->C6_ITEMOP+"."+STR0029,{STR0030,STR0031}) == 1) //"Aten┤└o"###"O item "###" gerou a Ordem de Producao "###"Confirma Exclusao ?"###"Sim"###"Nao"
			Else
				Aviso(OemToAnsi(STR0014),STR0060,{STR0040})
				lRet := .F.
			Endif
		EndIf
	EndIf
EndIf
Return lRet

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁMa410Bar  Ё Autor Ё Eduardo Riera         Ё Data Ё 18.02.99 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё EnchoiceBar especifica do Mata410                          Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё Nenhum                                                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ oDlg: 	Objeto Dialog                                     Ё╠╠
╠╠Ё          Ё bOk:  	Code Block para o Evento Ok                       Ё╠╠
╠╠Ё          Ё bCancel: Code Block para o Evento Cancel                   Ё╠╠
╠╠Ё          Ё nOpc:    nOpc transmitido pela mbrowse                     Ё╠╠
╠╠Ё          Ё aForma: Array com as formas de pagamento                   Ё╠╠
╠╠Ё          Ё nTotalPed: Inteiro com o valor do Pedido                   Ё╠╠
╠╠Ё          Ё aRecnoSE1: Array com os titulos de Adiantamento            Ё╠╠
╠╠цддддддддддедддддддддддддддбдддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё   DATA   Ё Programador   ЁManutencao Efetuada                         Ё╠╠
╠╠цддддддддддедддддддддддддддедддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё          Ё               Ё                                            Ё╠╠
╠╠юддддддддддадддддддддддддддадддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Static Function Ma410Bar(oDlg,bOk,bCancel,nOpc,oGetD,nTotalPed,aRecnoSE1,aHeadAGG,aColsAGG,cCondPAdt)

Local aButtons  := {}
Local aButtonUsr:= {}
Local nI        := 0
LOCAL lOpcPadrao:= GetNewPar("MV_REPGOPC","N") == "N"
Local nEmpBn 	:= A410CtEmpBN()
Local nPProduto	:= aScan(aHeader,{|x| AllTrim(x[2])=="C6_PRODUTO"})
Local nPQtdVen	:= aScan(aHeader,{|x| AllTrim(x[2])=="C6_QTDVEN"})
Local nPEntreg	:= aScan(aHeader,{|x| AllTrim(x[2])=="C6_ENTREG"})
Local nPOpcional:= If(lOpcPadrao,aScan(aHeader,{|x| AllTrim(x[2])=="C6_OPC"}),aScan(aHeader,{|x| AllTrim(x[2])=="C6_MOPC"}))
Local lAltRat	:= .F.
Local nLAutoAdt := 0

Default cCondPAdt :="0"
Default aHeadAGG := {}
Default aColsAGG := {}

If Type( "nAutoAdt" ) == "N" .AND. nAutoAdt > 0
	nLAutoAdt := nAutoAdt
EndIf

If ( nOpc == 3 .Or. nOpc == 4 .Or. (nOpc == 2 .And. !AtIsRotina("A450F4CON")) )
	aadd(aButtons,{"POSCLI",{|| If(M->C5_TIPO=="N".And.!Empty(M->C5_CLIENTE),a450F4Con(),.F.),Pergunte("MTA410",.F.)},STR0022,STR0067 }) 	//"Posi┤└o de Cliente"
EndIf
aadd(aButtons,{"BUDGET",{|| Ma410ForPg(nOpc)},STR0041, STR0068 }) //"Formas de Pagamento"

//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁPonto de entrada para verificar se o usuАrio pode acessar o botЦo Planilha Financeira    Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If ExistBlock("A410PLAN")
	If Execblock("A410PLAN",.F.,.F.)
    	aadd(aButtons,{"RELATORIO",{|| Pergunte("MTA410",.F.), Ma410Impos(aRotina[ nOpc, 4 ]) },STR0043, STR0069 })	//"Planilha Financeira"
    Endif
Else
	aadd(aButtons,{"RELATORIO",{|| Pergunte("MTA410",.F.), Ma410Impos(aRotina[ nOpc, 4 ]) },STR0043, STR0069 })	//"Planilha Financeira"
Endif                                                                                                                            

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁPonto de entrada para verificar se o usuario pode acessar a formacao    Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If VerSenha(107) //Permite consulta a Formacao de Precos
	If ExistBlock("A410BPRC")
		If ExecBlock("A410BPRC",.F.,.F.)
			aadd(aButtons,{"AUTOM",{||Ma410Forma()},STR0056,STR0070 })	//"Formacao de Precos"
		Endif
	Else
		aadd(aButtons,{"AUTOM",{||Ma410Forma()},STR0056,STR0070 })	//"Formacao de Precos"
	Endif
EndIf

If ( aRotina[ nOpc, 4 ] == 2 .Or. aRotina[ nOpc, 4 ] == 6 ) .And. !AtIsRotina("A410TRACK")
	AAdd(aButtons,{ "BMPVISUAL", {|| A410Track() }, STR0050, STR0071 } )  // "System Tracker"
EndIf
If !( nOpc == 2 .Or. nOpc == 5 ) .And. AtIsRotina("MATA410")
	If ExistBlock("A410BPRO")
		If ExecBlock("A410BPRO",.F.,.F.)
			Aadd(aButtons,{"PRODUTO", {|| Ma410BOM(aHeader,aCols,N) } ,STR0085,STR0086}) //"Estrutura de Produto"###"Estr.Prod."
		EndIf
	Else
		Aadd(aButtons,{"PRODUTO", {|| Ma410BOM(aHeader,aCols,N) } ,STR0085,STR0086}) //"Estrutura de Produto"###"Estr.Prod."	
	EndIf
EndIf
Aadd(aButtons,{"PESQUISA",{|| GdSeek(oGetD,STR0001,,,.F.)},STR0001,STR0001}) //"Pesquisar"
If ( nOpc == 1 .Or. nOpc == 2 .Or. nOpc == 5 ) .And. nPOpcional > 0
	Aadd(aButtons,{"SDUCOUNT", {|| SeleOpc(2,"MATA410",aCols[n][nPProduto],,,Ma410Opc(lOpcPadrao,nPOpcional),"M->C6_PRODUTO",.T.,aCols[n,nPQtdVen],aCols[n,nPEntreg]) } ,STR0106,STR0107}) //"Opcionais Selecionados"###"Opcionais"
EndIf
If (nEmpBn != 0) .And. (nOpc == 3)
	Aadd(aButtons,{"UP_MDI",{|| A410RemBen()},STR0120,STR0121}) //"Remessa para Beneficiamento"###"Beneficiamento"
EndIf

//зддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁO processo de Recebimento Antecipado estarА disponivel Ё
//Ёapenas para TOP no Financeiro.                         Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддды
#IFDEF TOP 
	If cPaisLoc $ "ANG|BRA|MEX" .and. AliasInDic("FIE")
		aRecnoSE1 := FPedAdtPed( "R", {M->C5_NUM},,nLAutoAdt )
		If (nOpc == 3 .OR. nOpc == 4 .OR. nOpc == 6) .And. (Empty(M->C5_NOTA))  
			Aadd(aButtons,{"FINIMG32",{|| A410Adiant(M->C5_NUM, M->C5_CONDPAG, 0/*nTotalPed*/, @aRecnoSE1, , M->C5_CLIENTE, M->C5_LOJACLI,,aRatCTBPC,aAdtPC,@cCondPAdt)},STR0123,STR0124}) //"Recebimento antecipado"##"Adiantamento"
		Else
			Aadd(aButtons,{"FINIMG32",{|| FPDxADTREL("R", M->C5_NUM, 0, @aRecnoSE1, M->C5_CLIENTE, M->C5_LOJACLI, .T.)},STR0123,STR0124}) //"Recebimento antecipado"##"Adiantamento"		
		EndIf
	EndIf
#ENDIF

//Rateio por item
If AliasInDic("AGG") // verifica se existe a nova tabela, para habilitar o buttom.
	lAltRat := nOpc == 3 .OR. nOpc == 4 .OR. nOpc == 6
	Aadd(aButtons , {'S4WB013N' ,{|| a410RatCC(@aHeadAGG,@aColsAGG,lAltRat,N) },STR0145,STR0144} ) //"Rateio por Item do pedido de venda"##"Rateio"
EndIf 

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁPontos de Entrada 													   Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If ExistTemplate("A410CONS",,.T.)
	aButtonUsr := ExecTemplate("A410CONS",.F.,.F.)
	If ValType(aButtonUsr) == "A"
		For nI   := 1  To  Len(aButtonUsr)
			Aadd(aButtons,aClone(aButtonUsr[nI]))
		Next nI
	EndIf
EndIf
If ExistBlock("A410CONS",,.T.)
	aButtonUsr := ExecBlock("A410CONS",.F.,.F.)
	If ValType(aButtonUsr) == "A"
		For nI   := 1  To  Len(aButtonUsr)
			Aadd(aButtons,aClone(aButtonUsr[nI]))
		Next nI
	EndIf
EndIf

//
// Template GEM - Gestao de Empreendimentos Imobiliarios
//
// Adiciona botoes na enchoice
// 
aButtonUsr:= {}
If ExistBlock("GMMA410BUT",,.T.)
	aButtonUsr := ExecBlock("GMMA410BUT",.F.,.F.,{nOpc ,M->C5_NUM ,M->C5_CLIENTE ,M->C5_LOJACLI})
ElseIf ExistTemplate("GMMA410BUT",,.T.)
	aButtonUsr := ExecTemplate("GMMA410BUT",.F.,.F.,{nOpc ,M->C5_NUM ,M->C5_CLIENTE ,M->C5_LOJACLI})
Endif	
If ValType(aButtonUsr) == "A"
	For nI := 1 To Len(aButtonUsr)
		Aadd(aButtons,aClone(aButtonUsr[nI]))
	Next nI
EndIf
Return (EnchoiceBar(oDlg,bOK,bcancel,,aButtons))
/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁMatGrdSomaЁ Autor ЁEduardo Riera          Ё Data Ё 26.02.99 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁAtualizar a Quantidade Vendida com as Quantidades da Grade  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁExpN1: Quantidade digitada na Grade                         Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁExpN1: Linha do aCols Principal                             Ё╠╠
╠╠Ё          ЁExpN2: Quantidade digitada na Acols Principal               Ё╠╠
╠╠цддддддддддедддддддддддддддбдддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё   DATA   Ё Programador   ЁManutencao Efetuada                         Ё╠╠
╠╠цддддддддддедддддддддддддддедддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё          Ё               Ё                                            Ё╠╠
╠╠юддддддддддадддддддддддддддадддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Static Function MatGrdSoma(nLinAcols,nQtdInf,nColQtd)

Local nColunas	:= 0
Local nLinhas	:= 0
Local nSoma		:= 0

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁSoma a Quantidade digitada na grade                                     Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
For nColunas:=2 to Len(aHeadGrade[nLinAcols])
	For nLinhas:=1 to Len(aColsGrade[nLinAcols])
		nSoma+=aColsGrade[nLinAcols][nLinhas][nColunas][nColQtd]
	Next nLinhas
Next nColunas
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁVerifica se ha divergencias                                             Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If ( nSoma <> nQtdInf .And. nQtdInf <> 0 )
	Help(" ",1,"A410QTDDIF")
EndIf
Return(nSoma)
/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁA410gValidЁ Autor ЁEduardo Riera          Ё Data Ё26.02.99  Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁValidacao da Grade de Produtos                              Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁExpL1: Indica se os valores digitados na grade sao validos  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁExpN1: Linha do aCols                                       Ё╠╠
╠╠Ё          ЁExpL2: Indica se foi alterada a quantidade vendida          Ё╠╠
╠╠цддддддддддедддддддддддддддбдддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё   DATA   Ё Programador   ЁManutencao Efetuada                         Ё╠╠
╠╠цддддддддддедддддддддддддддедддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё          Ё               Ё                                            Ё╠╠
╠╠юддддддддддадддддддддддддддадддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function a410GValid(nLinAcols,lQtdVen)     // --> Parametros usados para manter legado

Local lRetorno	:=.T.
Local nColuna	:= aScan(aHeader,{|x| AllTrim(x[2]) == AllTrim(Substr(Readvar(),4))})
Local cProdGrd	:= ""
Local xConteudo	:= &(ReadVar())
Local nPDescon  := aScan(oGrade:aHeadAux,{|x| AllTrim(x[2])=="C6_DESCONT" })
Local nPEntreg  := aScan(oGrade:aHeadAux,{|x| AllTrim(x[2])=="C6_ENTREG" })
Local nPOpc     := aScan(oGrade:aHeadAux,{|x| AllTrim(x[2])=="C6_OPC" })
Local aHeadBkp  := {}
Local aColsBkp  := {}
Local nNBkp 	:= 0
Local cOpcMark  := ""
Local lGrdMult  := "MATA410" $ SuperGetMV("MV_GRDMULT",.F.,"")

If FindFunction("MsMatGrade") .And. IsAtNewGrd()
	lQtdVen		:= If(lQtdVen==NIL,(oGrade:cCpo<>"C6_QTDLIB"),lQtdVen)
	nLinAcols	:= oGrade:oGetDados:oBrowse:nAt
	cProdGrd	:= oGrade:GetNameProd(,n,nColuna)
	
	If Posicione("SX3",2,oGrade:cCpo,"X3_TIPO") == "N"
		lRetorno := Positivo()
	EndIf
	
	If lRetorno .And. lGrdMult .And. oGrade:cCpo == "C6_PRCVEN" .And. aCols[n,nColuna] <> xConteudo .And. !Empty(oGrade:aColsAux[oGrade:nPosLinO,nPDescon])
		Help(" ",1,"A410PRCD")
		lRetorno := .F.
	EndIf
	                             
	If lRetorno .And. lQtdVen
		lRetorno := RegistroOk("SB1")
	EndIf         
	
	If lRetorno
	 	lRetorno := A410PedFat(cProdGrd,.T.,xConteudo,lQtdVen) 
	EndIf
	
	If ( lRetorno )	
		If ( ExistBlock("A410GVLD") )    
			//ATENCAO -> TRATAR ESTE PONTO DE ENTRADA E VER SE SERA NECESSARIO CRIAR VARIAVEIS PARA MANTER LEGADO    
			If Valtype('aHeadGrade')<>'A' .And. Valtype('aColsGrade')<>'A'
				PRIVATE aHeadGrade := {}
		   		PRIVATE aColsGrade := {}
		  	EndIf
			aHeadGrade := aClone(oGrade:aHeadGrade)
	   		aColsGrade := aClone(oGrade:aColsGrade) 		
			
			ExecBlock("A410GVLD",.F.,.F.,{nLinAcols,n,nColuna})
	
			If Valtype('aHeadGrade')=='A' .And. Valtype('aColsGrade')=='A'
				oGrade:aHeadGrade := aClone(aHeadGrade)	
				oGrade:aColsGrade := aClone(aColsGrade)
			EndIf
		
		EndIf
	EndIf
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁVerifica a quantidade Liberada                                          Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If ( !lQtdVen )
		If ( SuperGetMv("MV_LIBACIM") )
			If  ( xConteudo > (oGrade:aColsFieldByName("C6_QTDVEN",,n,nColuna) ))
				Help(" ",1,"A410LIB")
				lRetorno := .F.
			EndIf
			If ( lRetorno .And. xConteudo > (oGrade:aColsFieldByName("C6_QTDVEN",,n,nColuna)  - oGrade:aColsFieldByName("C6_QTDENT",,n,nColuna) ) )
				HELP(" ",1,"A440QTDL")
				lRetorno := .F.
			EndIf
		EndIf
	EndIf
	                                                                                          
	If lRetorno .And. Inclui .And. oGrade:cCpo == "C6_QTDVEN"
		cOpcMark := oGrade:aColsGrade[oGrade:nPosLinO][n][nColuna][oGrade:GetFieldGrdPos("C6_OPC")] 
		
		//Retorna aHeader, aCols e n para chamada da SeleOpc
		aHeadBkp := aClone(aHeader)
		aColsBkp := aClone(aCols)
		nNBkp	 := n
		aHeader  := aClone(oGrade:aHeadAux)
		aCols	 := aClone(oGrade:aColsAux)
		n		 := oGrade:nPosLinO
		
		//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Funcao utilizada para verificar a ultima versao do fonte		Ё
		//Ё SIGACUSB.PRX aplicado no rpo do cliente, assim verificando		|
		//| a necessidade de uma atualizacao neste fonte. NAO REMOVER !!!	Ё
		//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If !(FindFunction("SIGACUSB_V") .and. SIGACUSB_V() >= 20090204)
			Final(STR0122)	//"Atualizar SIGACUSB.PRX !!!"
		EndIf

		lRetorno := SeleOpc(2,"MATA410",cProdGrd,,,cOpcMark,"M->C6_PRODUTO",,xConteudo,aCols[oGrade:nPosLinO,nPEntreg])
		
		n		 := nNBkp
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Adiciona o opcional do produto no aCols                                Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If !Empty(aCols[oGrade:nPosLinO,nPOpc])
			oGrade:aColsGrade[oGrade:nPosLinO][n][nColuna][oGrade:GetFieldGrdPos("C6_OPC")] := aCols[oGrade:nPosLinO,nPOpc]
			aCols[oGrade:nPosLinO,nPOpc] := ""
		EndIf
		
		aHeader	 := aClone(aHeadBkp)
		aCols	 := aClone(aColsBkp)
	EndIf

	If lRetorno .And. oGrade:cCpo == "C6_BLQ"
		If Empty(oGrade:aColsFieldByName("C6_QTDVEN",,n,nColuna))
			Aviso(STR0014,STR0169,{"Ok"}) // Este item nao teve quantidade informada
			lRetorno := .F.
		EndIf
        
		If lRetorno
			lRetorno := Empty(xConteudo) .Or. ExistCpo("SX5","F1"+xConteudo)
		EndIf

		If lRetorno
			oGrade:ZeraGrade("C6_QTDLIB",oGrade:nPosLinO)
		EndIf
	EndIf
Else
	lRetorno :=	_a410GValid(nLinAcols,lQtdVen)
EndIf
Return(lRetorno)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁA410QtdGraЁ Autor Ё Eduardo Riera         Ё Data Ё 23.02.99 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁEfetua a entrada de dados da quantidade quando a grade esta Ё╠╠
╠╠Ё          Ёativa.                                                      Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁSempre .T.                                                  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁNenhum                                                      Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддбдддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё   DATA   Ё Programador   ЁManutencao Efetuada                         Ё╠╠
╠╠цддддддддддедддддддддддддддедддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё          Ё               Ё                                            Ё╠╠
╠╠юддддддддддадддддддддддддддадддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function A410QtdGra()
Local nPProduto:= aScan(aHeader,{|x| AllTrim(x[2])=="C6_PRODUTO"})
Local nPQtdVen := aScan(aHeader,{|x| AllTrim(x[2])=="C6_QTDVEN"})
Local nPPrcVen := aScan(aHeader,{|x| AllTrim(x[2])=="C6_PRCVEN"})
Local nPQtdLib := aScan(aHeader,{|x| AllTrim(x[2])=="C6_QTDLIB"})
Local nPLocal  := aScan(aHeader,{|x| AllTrim(x[2])=="C6_LOCAL"})
Local nPQtdVen2 := aScan(aHeader,{|x| AllTrim(x[2])=="C6_UNSVEN"})
Local nPBlq    := aScan(aHeader,{|x| AllTrim(x[2])=="C6_BLQ"})
Local nOpcA 	:= 0
Local nCntFor	:= 0
Local nColuna 	:= 0
Local nLinha	:= 0
Local lGrade	:= MaGrade() 
Local cCpoName	:= StrTran(ReadVar(),"M->","")
Local lRet 		:= .T.
Local aCampos	:= {"C6_QTDVEN","C6_PRCVEN"}
Local aTotais	:= {}
Local lGrdMult	:= "MATA410" $ SuperGetMV("MV_GRDMULT",.F.,"")

If FindFunction("MsMatGrade") .And. IsAtNewGrd()
	If lGrdMult .And. !(FindFunction("MATA410A_V") .And. MATA410A_V() >= 20110131)
	    Final("Atualizar MATA410A.PRX !!!") //"Atualizar MATA410A.PRX !!!"
	Endif
	
	If lGrdMult .And. aScan(aCampos,{|x| x == cCpoName}) > 0
		aAdd(aTotais,"C6_VALOR")
	ElseIf cCpoName # "C6_PRCVEN"
		aCampos := {cCpoName}
	Else
		lGrade := .F.
	EndIf
	
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Verifica se a grade esta ativa                       Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If ( lGrade )
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Arrays auxiliares para armazenar a getdados principalЁ
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
		oGrade:cProdRef	:= aCols[n][nPProduto]
		oGrade:nPosLinO	:= n   
		If oGrade:Show(aCampos,aTotais) .And. oGrade:lOk
			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//ЁAtualiza a quantidade do acols original                                 Ё  
			//ЁATENCAO: a variavel nQtdInformada foi alimentada dentro do objeto com   Ё 
			//Ё         ReadVar(), mas o programador pode alimentala quando desejar.   Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды	          
			DO CASE    
				//POSICIONADO NA QUANTIDADE VENDIDA
				CASE "C6_QTDVEN" $ cCpoName    
					A410MultT("C6_QTDVEN",oGrade:nQtdInformada,.F.)
					aCols[n][nPQtdVen]	:= oGrade:nQtdInformada
					M->C6_QTDVEN		:= oGrade:nQtdInformada
					If ( nPQtdVen2 > 0 )
						oGrade:nQtdInformada := 0
						oGrade:nQtdInformada := oGrade:SomaGrade("C6_UNSVEN",oGrade:nPosLinO,oGrade:nQtdInformada)
						A410MultT("C6_UNSVEN",oGrade:nQtdInformada)
						aCols[n][nPQtdVen2] := oGrade:nQtdInformada
						M->C6_UNSVEN        := oGrade:nQtdInformada				
					EndIf   
				//POSICIONADO NO PRECO UNITARIO
				CASE "C6_PRCVEN" $ cCpoName    
					A410MultT("C6_PRCVEN",oGrade:nQtdInformada,.F.)
 					aCols[n][nPPrcVen]	:= oGrade:nQtdInformada
					M->C6_PRCVEN		:= oGrade:nQtdInformada
				//POSICIONADO NA SEGUNDA UNIDADE DE MEDIDA DA QUANTIDADE					
				CASE "C6_UNSVEN" $ cCpoName  
					A410MultT("C6_UNSVEN",oGrade:nQtdInformada)
					aCols[n][nPQtdVen2]	:= oGrade:nQtdInformada
					M->C6_UNSVEN		:= oGrade:nQtdInformada
					oGrade:nQtdInformada:= 0
					oGrade:nQtdInformada:= oGrade:SomaGrade("C6_QTDVEN",oGrade:nPosLinO,oGrade:nQtdInformada)
					A410MultT("C6_QTDVEN",oGrade:nQtdInformada,.F.)
					aCols[n][nPQtdVen]	:= oGrade:nQtdInformada
					M->C6_QTDVEN		:= oGrade:nQtdInformada
				//POSICIONADO NA QUANTIDADE LIBERADA
				CASE "C6_QTDLIB" $ cCpoName
					aCols[n][nPQtdLib]	:= oGrade:nQtdInformada
					M->C6_QTDLIB		:= oGrade:nQtdInformada
				//POSICIONADO NO BLOQUEIO
				CASE "C6_BLQ" $ cCpoName
					aCols[n][nPBlq]	 := PadR("N",Len(SC6->C6_BLQ))
					M->C6_BLQ 		 := PadR("N",Len(SC6->C6_BLQ))
			END CASE
		ElseIf MatGrdPrRf(aCols[n,nPProduto])
			nColuna := aScan(aHeader,{|x| AllTrim(x[2]) == cCpoName})
			&(ReadVar()) := aCols[n,nColuna]
		EndIf
	EndIf
Else
	_A410QtdGra()
EndIf	
Return lRet

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁMa410RodapЁ Autor Ё Eduardo Riera         Ё Data Ё12.02.99  Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁFuncao para preenchimento do Rodape.                        Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁSempre .T.                                                  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁExpO1: Objeto da Getdados                                   Ё╠╠
╠╠Ё          ЁExpN1: Total do Pedido                                      Ё╠╠
╠╠Ё          ЁExpN2: Total do Desconto                                    Ё╠╠
╠╠цддддддддддедддддддддддддддбдддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё   DATA   Ё Programador   ЁManutencao Efetuada                         Ё╠╠
╠╠цддддддддддедддддддддддддддедддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё          Ё               Ё                                            Ё╠╠
╠╠юддддддддддадддддддддддддддадддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function Ma410Rodap(oGetDad,nTotPed,nTotDes)
Local aSvArea   := GetArea()
Local oDlg
Local nX     	:= 0
Local nY        := 0
Local nMaxFor	:= Len(aCols)
Local nDescCab  := 0
Local nUsado    := Len(aHeader)
Local lTestaDel := nUsado <> Len(aCols[1])
Local nPosTotal := aScan(aHeader,{|x| AllTrim(x[2])=="C6_VALOR"})
Local nPosDesc  := aScan(aHeader,{|x| AllTrim(x[2])=="C6_VALDESC"})
Local nPPrUnit  := aScan(aHeader,{|x| AllTrim(x[2])=="C6_PRUNIT"})
Local nPQtdVen  := aScan(aHeader,{|x| AllTrim(x[2])=="C6_QTDVEN"})
Local nPPrcVen  := aScan(aHeader,{|x| AllTrim(x[2])=="C6_PRCVEN"})
Local nPProduto := aScan(aHeader,{|x| AllTrim(x[2])=="C6_PRODUTO"})
Local cCliente	:= Space(30)
Local nLin		:= 0
Local nCol		:= 0
Local nPrcTab   := 0

l416Auto := If (Type("l416Auto") == "U",.f.,l416Auto)

If ( Type("l410Auto") == "U" .or. ! l410Auto) .and. !(l416Auto)
	nTotPed	:= If(nTotPed==Nil,0,nTotPed)
	nTotDes	:= If(nTotDes==Nil,0,nTotDes)
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁPosiciona o Cliente                                                     Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If ( M->C5_TIPO $ "DB" )
		dbSelectArea("SA2")
		dbSetOrder(1)
		If ( MsSeek(xFilial("SA2")+M->C5_CLIENTE+M->C5_LOJACLI) )
			cCliente	:= SA2->A2_NOME
		EndIf
	Else
		dbSelectArea("SA1")
		dbSetOrder(1)
		If ( MsSeek(xFilial("SA1")+M->C5_CLIENTE+M->C5_LOJACLI) )
			cCliente	:= SA1->A1_NOME
		EndIf
	EndIf
	//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁCaso nao seja passado o objeto da getdados deve-se pegar a janela defaultЁ
	//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If ( oGetDad == Nil )
		oDlg		:= GetWndDefault()
		If ( ValType(oDlg:Cargo)<>"B" )
			oDlg := oDlg:oWnd
		EndIf
	Else
		oDlg := oGetDad:oWnd
	EndIf
	//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁSoma as variaveis do Rodape                                    Ё
	//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If ( nTotPed == 0 )
		//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁSoma as variaveis do aCols                                     Ё
		//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		For nY := 1 To 2
			For nX := 1 To nMaxFor
				If ( (lTestaDel .And. !aCols[nX][nUsado+1]) .Or. !lTestaDel )
					If ( nPosDesc > 0 .And. nPPrUnit > 0 .And. nPPrcVen > 0 .And. nPQtdVen > 0)
						If "MATA410" $ SuperGetMV("MV_GRDMULT",.F.,"") .And. FindFunction("MsMatGrade") .And. IsAtNewGrd() .And. MatGrdPrRf(aCols[nX,nPProduto])
							For nLin := 1 To Len(oGrade:aColsGrade[nX])
								For nCol := 2 To Len(oGrade:aHeadGrade[nx])
									If (nPrcTab := oGrade:aColsFieldByName("C6_PRUNIT",nX,nLin,nCol)) > 0
										nTotDes += A410Arred(nPrcTab*oGrade:aColsFieldByName("C6_QTDVEN",nX,nLin,nCol),"C6_VALDESC")-;
												A410Arred(oGrade:aColsFieldByName("C6_PRCVEN",nX,nLin,nCol)*oGrade:aColsFieldByName("C6_QTDVEN",nX,nLin,nCol),"C6_VALDESC")
									EndIf
								Next nCol
							Next nLin
						ElseIf ( aCols[nX][nPPrUnit]==0 )
							nTotDes	+= aCols[nX][nPosDesc ]
						Else
							nTotDes += A410Arred(aCols[nX][nPPrUnit]*aCols[nX][nPQtdVen],"C6_VALDESC")-;
								A410Arred(aCols[nX][nPPrcVen]*aCols[nX][nPQtdVen],"C6_VALDESC")
						EndIf
					EndIf
					If ( nPosTotal > 0 )
						nTotPed	+=	aCols[nX][nPosTotal]
					EndIf
				EndIf
			Next nX
			nDescCab := M->C5_DESC4
			nTotPed  -= M->C5_DESCONT
			nTotDes  += M->C5_DESCONT
			nTotDes  += A410Arred(nTotPed*M->C5_PDESCAB/100,"C6_VALOR")
			nTotPed  -= A410Arred(nTotPed*M->C5_PDESCAB/100,"C6_VALOR")
			If nY == 1
				If FtRegraDesc(3,nTotPed+nTotDes,@M->C5_DESC4) == nDescCab
					Exit
				Else
					nTotPed	:=	0
					nTotDes	:=	0
				EndIf
			EndIf
		Next nY	
	EndIf
	//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁSoma as variaveis da Enchoice                                  Ё
	//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	nTotPed += M->C5_FRETE
	nTotPed += M->C5_SEGURO
	nTotPed += M->C5_DESPESA
	nTotPed += M->C5_FRETAUT
	If cPaisLoc == "PTG"    
		nTotPed += M->C5_DESNTRB
		nTotPed += M->C5_TARA
	Endif

	// Inserido esta linha para arrendondar o total do pedido
	If cPaisLoc=="CHI"
		nTotPed  := A410Arred(nTotPed,"C6_VALOR",M->C5_MOEDA)
		nTotDes  := A410Arred(nTotDes,"C6_VALOR",M->C5_MOEDA)	
	EndIf
	
   	If ValType(oDlg) == "O"	
		If ( ValType(oDlg:Cargo)=="B" )
			If ExistBlock("MT410ROD")
				ExecBlock("MT410ROD",.F.,.F.,{oDlg:Cargo,SubStr(cCliente,1,40),nTotPed+nTotDes,nTotDes,nTotPed}) 
			Else
				If ( cPaisLoc=="CHI" .And. (INCLUI .Or. ALTERA) .And. M->C5_MOEDA > 1 )
					Eval(oDlg:Cargo,SubStr(cCliente,1,40),;
									Transform(nTotPed+nTotDes,x3picture("C6_VALOR")),;
									Transform(nTotDes,x3picture("C6_VALOR")),;
									Transform(nTotPed,x3picture("C6_VALOR")))
				Else
					//3 parametro passado para EVAL desta maneira ,pois adicionava ao SAY valor incorreto ( FNC: 00000026096/2010 )			
					Eval(oDlg:Cargo,SubStr(cCliente,1,40),IIF(nTotDes!=0,nTotPed+nTotDes,nTotPed),nTotDes,nTotPed)
				EndIf 
			EndIf
		EndIf
	Endif      
EndIf
RestArea(aSvArea)
Return(.T.)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁA410Grava Ё Autor ЁEduardo Riera          Ё Data Ё17.03.99  Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁEfetua a Gravacao de um pedido de Vendas                    Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁExpL1: Indica se houve gravacao de itens                    Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁExpL1: Liberacao Parcial                                    Ё╠╠
╠╠Ё          ЁExpL2: Transfere Locais                                     Ё╠╠
╠╠Ё          ЁExpN3: Tipo de Operacao a ser executada  ( Opcional )       Ё╠╠
╠╠Ё          Ё       [1] Inclusao                                         Ё╠╠
╠╠Ё          Ё       [2] Alteracao                                        Ё╠╠
╠╠Ё          Ё       [3] Exclusao                                         Ё╠╠
╠╠Ё          Ё       [4] Inclusao via XML                                 Ё╠╠
╠╠Ё          ЁExpA4: aHeader das formas de pagamento ( Opcional )         Ё╠╠
╠╠Ё          ЁExpA5: aCols das formas de pagamento   ( Opcional )         Ё╠╠
╠╠Ё          ЁExpA6: Registros do SC6                ( Opcional )         Ё╠╠
╠╠Ё          ЁExpA7: Registros do SCV                ( Opcional )         Ё╠╠
╠╠Ё          ЁExpN8: Tamanho da pilha do semaforo    ( Opcional )         Ё╠╠
╠╠Ё          ЁExpN8: Array com relacionamento entre SD4 X SC6( Opcional ) Ё╠╠
╠╠Ё          ЁExpA9: Array com Adiantamentos relacionado ao Pedido (Opc)  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁObservacaoЁNecessita das variaveis: aHeader,aCols,aHeadGrade,aColsGradeЁ╠╠
╠╠Ё          Ёe INCLUI                                                    Ё╠╠
╠╠цддддддддддедддддддддддддддбдддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё   DATA   Ё Programador   ЁManutencao Efetuada                         Ё╠╠
╠╠Ё16/03/2010Ё Marcos Justo  ЁIncluida a contabilizaГЦo do pedido on-line Ё╠╠
╠╠цддддддддддедддддддддддддддедддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠юддддддддддадддддддддддддддадддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function A410Grava(lLiber,lTransf,nOpcao,aHeadFor,aColsFor,aRegSC6,aRegSCV,nStack,aEmpBn,aRecnoSE1,aHeadAGG,aColsAGG)
Local aArea     := GetArea("SC5")
Local aRegLib   := {}
Local bCampo 	:= {|nCPO| Field(nCPO) }
Local lTravou   := .F.
Local lTravou2  := .F.
Local lLiberou  := .F.
Local lLiberOk	:= .T.
Local lResidOk	:= .T.
Local lFaturOk	:= .F.
Local lGravou	:= .F.
Local lContinua := .F.
Local lQuery    := .F.
Local lXml      := .F.
Local lMta410I  := ExistBlock("MTA410I")
Local lMta410E  := ExistBlock("MTA410E")
Local cPedido   := ""
Local cMay      := ""
Local cArqQry   := "SC6"
Local cProdRef	:= "" 
//---------- Variavel existente somente para manter legado ate R4              
Local cMascara	:= SuperGetMv("MV_MASCGRD")                       
//------------------------------------------------------------------
Local nTamRef	:= If(FindFunction("MsMatGrade") .And. IsAtNewGrd(),0,Val(Substr(cMascara,1,2)))
Local nMaxFor	:= Len(aCols)
Local nMaxFor2	:= 0
Local nPItem    := aScan(aHeader,{|x| AllTrim(x[2]) == "C6_ITEM"})
Local nPProduto := aScan(aHeader,{|x| AllTrim(x[2]) == "C6_PRODUTO"})
Local nVlrCred  := 0
Local nX        := 0
Local nY        := 0
Local nZ        := 0
Local nW        := 0
Local xZ			 := 0
Local nDeleted  := Len(aHeader)+1
Local nDeleted2 := 0
Local nMoedaOri := 1
Local nCntForma := 0
Local nCount    := 0
Local aSaldoSDC := {} 
Local aRegStatus:= {}   

Local lCtbOnLine := .F.
Local lDigita := .F.
Local lAglutina	 := .F.
Local aCtbDia	 := {}  
Local bCtbOnLine := {|| .T.}
Local cArqCtb    := ""       
Local nTotalCtb  := 0            
Local c612       := ""  
Local c621       := "" 
Local c632       := "" 
Local c636       := "" 
Local aCt5       := {}           
Local nHdlPrv    := 0
Local aAreaSX1   := {}
Local lMata410	 := IIF(FUNNAME()=="MATA410",.T.,.F.)

Local lAtuSGJ	 := SuperGetMV("MV_PVCOMOP",.F.,.F.) .And. FindFunction("ALIASINDIC") .And. AliasIndic("SGJ")
Local nUsadoAGG  := 0
LOCAL cCondPOld  := ""
Local nTpCtlBN   := A410CtEmpBN()

#IFDEF TOP 
	Local cQuery    := ""
#ENDIF

//-- Variaveis utilizadas pela funcao wmsexedcf
Private aLibSDB	:= {}
Private aWmsAviso:= {}
Private nValItPed := 0
PRIVATE cCondPAdt   := "0" //Controle p/ cond. pgto. com aceite de Adt. 0=normal 1=Adt

DEFAULT nOpcao     := 0
DEFAULT aHeadFor   := {}
DEFAULT aColsFor   := {}
DEFAULT aRegSC6    := {}
DEFAULT aRegSCV    := {}
DEFAULT nStack     := 0 
DEFAULT aEmpBn	   := {}
DEFAULT aRecnoSE1  := {}
DEFAULT aHeadAGG   := {}
DEFAULT aColsAGG   := {} 


If Type( "nAutoAdt" ) == "N" .AND. nAutoAdt == 4
	cCondPOld := SC5->C5_CONDPAG
EndIf

nMaxFor2  := Len(aColsFor)
nDeleted2 := Len(aHeadFor)+1

aRegStatus := Array( Len( aRegSC6 ) )
AFill( aRegStatus, .T. )

// NЦo contabiliza a alteraГЦo - !ALTERA
If nOpcao == 1 .OR. nOpcao == 3
	aAreaSX1 := SX1->(GetArea())
	SaveInter()
	Pergunte("MTA410",.F.)    
		If nOpcao == 1                        
			lCtbOnLine 	:= lMata410 .And. MV_PAR05==1 
		Else      
			lCtbOnLine 	:= lMata410 .And. MV_PAR05==1 .And. !Empty( SC5->( FieldPos( "C5_DTLANC" ) ) )
		Endif
	lAglutina  	:= MV_PAR06==1
	lDigita 	:= MV_PAR07==1
	RestInter()
	RestArea(aAreaSX1)
Endif

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Ponto de entrada antes de iniciar a manutencao do pedido               Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If ExistBlock("M410AGRV")
	ExecBlock("M410AGRV",.f.,.f.,{ nOpcao })
EndIf

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Ponto de entrada para pegar os registros de SDC para reconstruir as    Ё
//Ё as liberaГУes na alteraГЦo dos Itens do Pedidos.                       Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If ( (ExistBlock("M410PSDC") ) )
	aSaldoSDC := ExecBlock("M410PSDC",.f.,.f.)
EndIf

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Se Grade estiver ativa, grava Acols conf.AcolsGrade  para depois       Ё
//Ё continuar a gravar como um pedido comum.                               Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If ( MaGrade().And. If(FindFunction("MsMatGrade") .And. IsAtNewGrd(),Type("oGrade")=="O",Type('aHeadGrade')<>'U') )
	Ma410GraGr()
	nMaxFor	:= Len(aCols)
EndIf
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Verifica se ha itens a serem gravados                                  Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
For nX := 1 To nMaxFor
	If nOpcao == 3
		aCols[nX][nDeleted] := .T.
	EndIf
	If !aCols[nX][nDeleted]
		lGravou   := .T.
		lContinua := .T.
		Exit
	EndIf
Next nX
If !lGravou .And. !INCLUI
	nOpcao := 3
	lContinua := .T.
EndIf
If nOpcao == 3
	For nX := 1 To nMaxFor2
		aColsFor[nX][nDeleted2] := .T.
	Next nX
EndIf
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Verifica se a gravacao via JOB XML esta ativa                          Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If lContinua .And. nOpcao == 1 .And. GetNewPar("MV_MSPVXML",.F.)
	lXml := Ma410GrXml()
EndIf
If nOpcao == 4
	nOpcao :=1
EndIf

nMoedaOri := M->C5_MOEDA

//Montagem dos dados da execauto de rateio
If Type( "nAutoAdt" ) == "N" .AND. nAutoAdt > 0 .And. Len(aRatCTBPC) > 0  
   aHeadAGG:={}
   aColsAGG:={}
   DbSelectArea("SX3")
	dbSetOrder(1)
	MsSeek("AGG")
	While !EOF() .And. (SX3->X3_ARQUIVO == "AGG")
 	  							    IF X3USO(SX3->X3_USADO) .AND. cNivel >= SX3->X3_NIVEL .And. !"AGG_CUSTO"$SX3->X3_CAMPO
			AADD(aHeadAGG,{ TRIM(x3Titulo()),;
			SX3->X3_CAMPO,;
			SX3->X3_PICTURE,;
			SX3->X3_TAMANHO,;
			SX3->X3_DECIMAL,;
			SX3->X3_VALID,;
			SX3->X3_USADO,;
			SX3->X3_TIPO,;
			SX3->X3_F3,;
			SX3->X3_CONTEXT } )
		EndIf
		dbSelectArea("SX3")
		dbSkip()
	EndDo
	aColsAGG := M410AutRat(aRatCTBPC)	
Endif
nUsadoAGG := Len(aHeadAGG)
		
//Caso AlteraГЦo Automatica deleta os rateios
If Type( "nAutoAdt" ) == "N" .AND. nAutoAdt==4 .And. Len(aRatCTBPC) > 0
	aAreaAGG := GetArea()
	AGG->(DbSetOrder(1)) //CH_FILIAL+CH_PEDIDO+CH_FORNECE+CH_LOJA+CH_ITEMPD+CH_ITEM
	If AGG->(MsSeek(xFilial("AGG")+SC5->C5_NUM+SC5->C5_CLIENTE+SC5->C5_LOJACLI)) .and. nX==1
		While !AGG->(EOF()).and. (SC5->C5_FILIAL+SC5->C5_NUM+SC5->C5_CLIENTE+SC5->C5_LOJACLI==;
			AGG->AGG_FILIAL+AGG->AGG_PEDIDO+AGG->AGG_FORNECE+AGG->AGG_LOJA)
			RecLock("AGG",.F.)
			AGG->(dbDelete())
			MsUnlock()
			AGG->(DbSkip())
		Enddo
	EndIf
	RestArea(aAreaAGG)
Endif

If !lXml
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Verifica a Numeracao do pedido de venda                                Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	dbSelectArea("SC5")
	cPedido := M->C5_NUM
	If ( INCLUI )
		cMay := "SC5"+ Alltrim(xFilial("SC5"))
		SC5->(dbSetOrder(1))
		While ( DbSeek(xFilial("SC5")+cPedido) .or. !MayIUseCode(cMay+cPedido) )
			cPedido := Soma1(cPedido,Len(M->C5_NUM))
		EndDo
	EndIf
	M->C5_NUM := cPedido
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁGuarda o numero do registro do itens que serao alterados                Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If Empty(aRegSC6) .And. !INCLUI
		dbSelectArea("SC6")
		dbSetOrder(1)
		#IFDEF TOP
			If TcSrvType()<>"AS/400"
				cArqQry := "A410GRAVA"
				lQuery  := .T.
				cQuery := "SELECT SC6.R_E_C_N_O_ SC6RECNO "
				cQuery += "FROM "+RetSqlName("SC6")+" SC6 "
				cQuery += "WHERE SC6.C6_FILIAL='"+xFilial("SC6")+"' AND "
				cQuery += "SC6.C6_NUM='"+M->C5_NUM+"' AND "
				cQuery += "SC6.D_E_L_E_T_=' ' "

				cQuery := ChangeQuery(cQuery)

				dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cArqQry,.T.,.T.)
			Else
		#ENDIF
			MsSeek(xFilial("SC6")+M->C5_NUM)
			#IFDEF TOP
			EndIf
			#ENDIF
		While ( !Eof() .And. xFilial("SC6")==(cArqQry)->C6_FILIAL .And.(cArqQry)->C6_NUM==M->C5_NUM )

			aadd(aRegSC6,If(lQuery,(cArqQry)->SC6RECNO,(cArqQry)->(Recno())))

			dbSelectArea(cArqQry)
			dbSkip()

		EndDo
		If lQuery
			dbSelectArea(cArqQry)
			dbCloseArea()
			dbSelectArea("SC6")	
		EndIf
	EndIf
	If Empty(aRegSCV) .And. !INCLUI
		dbSelectArea("SCV")
		dbSetOrder(1)
		#IFDEF TOP
			If TcSrvType()<>"AS/400"
				cArqQry := "A410GRAVA"
				lQuery  := .T.
				cQuery := "SELECT SCV.R_E_C_N_O_ SCVRECNO,SCV.CV_FILIAL,SCV.CV_PEDIDO "
				cQuery += "FROM "+RetSqlName("SCV")+" SCV "
				cQuery += "WHERE SCV.CV_FILIAL='"+xFilial("SCV")+"' AND "
				cQuery += "SCV.CV_PEDIDO='"+M->C5_NUM+"' AND "
				cQuery += "SCV.D_E_L_E_T_=' ' "

				cQuery := ChangeQuery(cQuery)

				dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cArqQry,.T.,.T.)
			Else
		#ENDIF
			MsSeek(xFilial("SCV")+M->C5_NUM)
			#IFDEF TOP
			EndIf
			#ENDIF
		While ( !Eof() .And. xFilial("SCV")==(cArqQry)->CV_FILIAL .And.(cArqQry)->CV_PEDIDO=M->C5_NUM )

			aadd(aRegSCV,If(lQuery,(cArqQry)->(Recno()),(cArqQry)->SCVRECNO))

			dbSelectArea(cArqQry)
			dbSkip()

		EndDo
		If lQuery
			dbSelectArea(cArqQry)
			dbCloseArea()
			dbSelectArea("SCV")	
		EndIf
	EndIf
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Atualiza os dados do pedido do venda                                   Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If lContinua                 
    
    
    				//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					//Ё Prepara a contabilizacao On-Line do Pedido              Ё
					//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
					If lCtbOnLine

						dbSelectArea("SX5")
						dbSetOrder(1)
						If MsSeek(xFilial()+"09FAT")          // Verifica o numero do lote contabil
							cLoteCtb := AllTrim(X5Descri())
						Else
							cLoteCtb := "FAT "
						EndIf		

						If At(UPPER("EXEC"),X5Descri()) > 0   // Executa um execblock
							cLoteCtb := &(X5Descri())
						EndIf				

						nHdlPrv:=HeadProva(cLoteCtb,"MATA410",Subs(cUsuario,7,6),@cArqCtb) // Inicializa o arquivo de contabilizacao
						If nHdlPrv <= 0
							HELP(" ",1,"SEM_LANC")
							lCtbOnLine := .F.
						EndIf
                   Endif

		
		
		For nX := 1 To nMaxFor
			Begin Transaction
				//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Se for o primeiro item e nao for exclusao, grava o cabecalho           Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
				If nX == 1 .And. nOpcao <> 3
					//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					//Ё Estorna  o cabecalho do pedido de venda                                Ё
					//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
					If !INCLUI
						//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
						//Ё Armazena a moeda original do pedido de venda                           Ё
						//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
						nMoedaOri := SC5->C5_MOEDA
						MaAvalSC5("SC5",2,Nil,Nil,Nil,Nil,Nil,Nil,Nil,Nil,Nil,Nil,Nil,@nVlrCred)
					EndIf
					//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					//Ё Atualiza o cabecalho do pedido de venda                                Ё
					//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
					If lGravou
						RecLock("SC5",INCLUI)
						For nY := 1 TO FCount()
							If ("FILIAL" $ FieldName(nY) )
								FieldPut(nY,xFilial("SC5"))
							ElseIf (("TABELA" $ FieldName(nY)) .And. (M->&(EVAL(bCampo,nY)) == PadR("1",Len(DA0->DA0_CODTAB))))
								FieldPut(nY,"")
							Else
								FieldPut(nY,M->&(EVAL(bCampo,nY)))
							EndIf
						Next nY
						SC5->C5_BLQ := ""

						//
						// Template GEM - Gestao de Empreendimentos Imobiliarios
						// Gravacao dos solidarios do cliente do pedido de venda
						//
						If ExistTemplate("GEMXGRSOL",,.T.)
							ExecTemplate("GEMXGRSOL",.F.,.F.,{nOpcao ,M->C5_NUM})
						EndIf

						//
						// Template GEM - Gestao de Empreendimentos Imobiliarios
						// Gravacao da condicao de venda "personalizada"
						//
						If ExistBlock("GEMXGRCVND",,.T.)
							ExecBlock("GEMXGRCVND",.F.,.F.,{nOpcao ,M->C5_NUM ,M->C5_CONDPAG})
						ElseIf ExistTemplate("GEMXGRCVND",,.T.)
							ExecTemplate("GEMXGRCVND",.F.,.F.,{nOpcao ,M->C5_NUM ,M->C5_CONDPAG})
						EndIf  
						
						// Contabiliza cabeГalho - LanГamento PadrЦo 621					
						If lCtbOnLine
							nTotalCtb+=DetProva(nHdlPrv,"621","MATA410",cLoteCtb)
						EndIf

					EndIf

					//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					//Ё Atualiza as formas de pagamento                                        Ё
					//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
					If Len(aColsFor) >= 1 .And. !Empty(aColsFor[1][1])
						SC5->(FkCommit())
						For nY := 1 To nMaxFor2
							//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
							//ЁVerifica se sera alteracao ou inclusao                                  Ё
							//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
							If ( Len(aRegSCV) >= nY )
								dbSelectArea("SCV")
								MsGoto(aRegSCV[nY])
								RecLock("SCV",.F.)
								lTravou2 := .T.
							Else
								If ( !aColsFor[nY][nDeleted2] )
									RecLock("SCV",.T.)
									lTravou2 := .T.
								Else
									lTravou2 := .F.
								EndIf
							EndIf
							If aColsFor[nY][nDeleted2]
								If lTravou2
									SCV->(dbDelete())
								EndIf
							Else
								For nZ := 1 To Len(aHeadFor)
									If aHeadFor[nZ][10] <> "V"
										SCV->(FieldPut(FieldPos(aHeadFor[nZ][2]),aColsFor[nY][nZ]))
									EndIf
								Next nZ
								SCV->CV_FILIAL := xFilial("SCV")
								SCV->CV_PEDIDO := M->C5_NUM
								SCV->(MsUnLock())
							EndIf
						Next nY
					EndIf
					
					//здддддддддддддддддддддддддддддддддддддддд©
					//ЁGrava o relacionamento com AdiantamentosЁ
					//юдддддддддддддддддддддддддддддддддддддддды
					If cPaisLoc $ "ANG|BRA|MEX" .and. AliasInDic("FIE") .and. Type( "nAutoAdt" ) == "N" .AND. (nAutoAdt==3 .OR. nAutoAdt==4) //.OR. nAutoAdt==5 
				       If A410UsaAdi( SC5->C5_CONDPAG )
						  IF Len(aAdtPC)>0 
						     If nAutoAdt==3 
						        If a410AdtSld(SC5->C5_NUM,aAdtPC,nAutoAdt)>0
						           FPedAdtGrv("R", 2, SC5->C5_NUM, aRecnoSE1,,,,aAdtPC,nAutoAdt)					
						           FPedAdtGrv("R", 1, SC5->C5_NUM, aRecnoSE1,,,,aAdtPC,nAutoAdt)					
						        Endif   
						      Else
						        If lCkAdtFR3(SC5->C5_NUM,nAutoAdt)==0
						        If a410AdtSld(SC5->C5_NUM,aAdtPC,nAutoAdt,0)>0 //Verifica saldo sem apresentar HELP
						           FPedAdtGrv("R", 2, SC5->C5_NUM, aRecnoSE1,,,,aAdtPC,nAutoAdt)					
						           FPedAdtGrv("R", 1, SC5->C5_NUM, aRecnoSE1,,,,aAdtPC,nAutoAdt)
						          Else
						           If a410AdtSld(SC5->C5_NUM,aAdtPC,nAutoAdt,2)>0 //Verifica se ao excluir ADT haverА saldo para nova inclusao
						              FPedAdtGrv("R", 2, SC5->C5_NUM, aRecnoSE1,,,,aAdtPC,nAutoAdt)					
						              If a410AdtSld(SC5->C5_NUM,aAdtPC,nAutoAdt)>0
						                 FPedAdtGrv("R", 1, SC5->C5_NUM, aRecnoSE1,,,,aAdtPC,nAutoAdt)
						              Endif
						           Endif   
						       Endif
							      Else
							        Help(" ",1,"A410ADTEMUSO") //"Pedido possui compensaГЦo por RA, nЦo pode ser alterado ou excluido!"
							     Endif  
						     Endif
						   Else 
						     If nAutoAdt==4 
							     If lCkAdtFR3(SC5->C5_NUM,nAutoAdt)==0
						        aRecnoSE1 := FPedAdtPed("R",{SC5->C5_NUM}, .F.,0)
						        If Len(aRecnoSE1)<>0
						           FPedAdtGrv("P", 2, SC5->C5_NUM, aRecnoSE1)					
						        Endif   
							       Else
							        Help(" ",1,"A410ADTEMUSO") //"Pedido possui compensaГЦo por RA, nЦo pode ser alterado ou excluido!"
							     Endif   
						     Endif   
						  Endif
					    Else
						  If nAutoAdt==4
						     If lCkAdtFR3(SC5->C5_NUM,nAutoAdt)==0
						     If A410UsaAdi( cCondPOld ) 
						        FPedAdtGrv("R", 2, SC5->C5_NUM, aRecnoSE1,,,,aAdtPC,nAutoAdt)					
						     Endif   					   						     
								 Else
								  Help(" ",1,"A410ADTEMUSO") //"Pedido possui compensaГЦo por RA, nЦo pode ser alterado ou excluido!"  					   						     
							  Endif	  
						  Endif
		   		       Endif   
  					 Else
  				       If cPaisLoc $ "ANG|BRA|MEX" .and. AliasInDic("FIE")  
					If A410UsaAdi( SC5->C5_CONDPAG )
						FPedAdtGrv( "R", 1, SC5->C5_NUM, aRecnoSE1 )
					EndIf					
  				       Endif	  
					EndIf					
				EndIf
				//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Atualiza os itens do pedido de venda                                   Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
				//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//ЁVerifica se sera alteracao ou inclusao de um item do PV                 Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
				If ( Len(aRegSC6) >= nX )
					dbSelectArea("SC6")
					
					If aRegStatus[ nX ]
						MsGoto(aRegSC6[nX])
					Endif
					//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					//ЁVerifica se o item (acols) corresponde ao item gravado.Quando utiliza-seЁ
					//Ёgrade de produtos os itens podem ser adicionados no em qualquer ordem   Ё
					//Ёprejudicando a atualizacao dos campos Qtd.Entregue e Empenhada          Ё
					//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
					
					//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					//ЁNa situacao da troca de um produto que pertence a grade, os registros   Ё
					//Ёposteriores nao podem ser reaproveitados, tendo que ser excluidos e in  Ё
					//Ёseridos novamente                                                       Ё
					//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
					If FindFunction("MsMatGrade") .And. IsAtNewGrd()
						If ( aCols[nX][nPItem] == SC6->C6_ITEM .And. SC6->C6_GRADE=="S" .And. aRegStatus[nX])
							cProdRef := aCols[nX][nPProduto]
							MatGrdPrRf(@cProdRef,.T.)
						    nTamRef	:= Len(cProdRef)
							If SubStr(aCols[nX][nPProduto],1,nTamRef) <> SubStr(SC6->C6_PRODUTO,1,nTamRef) 
								AFill( aRegStatus, .F., nX ) 
		
								For nCount := nX to Len( aRegSC6 ) 
		
									SC6->(MsGoto(aRegSC6[nCount]))
		
									//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
									//ЁEfetua o estorno dos itens do pedido de venda                           Ё
									//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
									RecLock("SC6")
									MaAvalSC6("SC6",2,"SC5",lLiber,lTransf,@lLiberOk,@lResidOk,@lFaturOk,Nil,@nVlrCred,Nil,Nil,nMoedaOri)
									If !GetMv("MV_AVALCRD")
										nVlrCred := 0
									EndIf
									lTravou := .T.
									
									//-- Libera empenhos vinculados ao item do pedido
									If nTpCtlBN == 1 // metodo antigo - unico envio: gravacao na SD4
										dbSelectArea("SD4")
										dbSetOrder(6)
										If dbSeek(xFilial("SD4")+SC6->(C6_NUM+C6_ITEM))
											RecLock("SD4",.F.)
											Replace D4_NUMPVBN With CriaVar("D4_NUMPVBN",.F.)
											Replace D4_ITEPVBN With CriaVar("D4_ITEPVBN",.F.)
											MsUnLock()
										EndIf
										dbSelectArea("SDC")
										dbSetOrder(1)
										If dbSeek(xFilial("SDC")+SC6->(C6_PRODUTO+C6_LOCAL)+"SC2"+SC6->(C6_NUM+C6_ITEM))
											RecLock("SDC",.F.)
											Replace DC_PEDIDO With CriaVar("DC_PEDIDO",.F.)
											Replace DC_ITEM	With CriaVar("DC_ITEM",.F.)
											MsUnLock()
										EndIf
									ElseIf nTpCtlBN == 2 // metodo novo - multiplos envios: gravacao na SGO
										dbSelectArea("SGO")
										dbSetOrder(2) // GO_FILIAL+GO_NUMPV+GO_ITEMPV+GO_OP+GO_COD+GO_LOCAL
										dbSeek(xFilial("SGO")+SC6->C6_NUM+SC6->C6_ITEM)
										If ( GO_FILIAL+GO_NUMPV+GO_ITEMPV == SC6->C6_FILIAL+SC6->C6_NUM+SC6->C6_ITEM )
											RecLock("SGO", .F.)
											dbDelete()
											MsUnLock()
										EndIf
										dbSelectArea("SDC")
										dbSetOrder(1)
										If dbSeek(xFilial("SDC")+SC6->(C6_PRODUTO+C6_LOCAL)+"SC2"+SC6->(C6_NUM+C6_ITEM))
											RecLock("SDC",.F.)
											Replace DC_PEDIDO With CriaVar("DC_PEDIDO",.F.)
											Replace DC_ITEM	With CriaVar("DC_ITEM",.F.)
											MsUnLock()
										EndIf
									EndIf
									SC6->( dbDelete() )
									aRegStatus[ nCount ] := .F.
								Next nCount
							EndIf
						Endif    
					Else
						If ( aCols[nX][nPItem] == SC6->C6_ITEM .And. SubStr(aCols[nX][nPProduto],1,nTamRef) <> SubStr(SC6->C6_PRODUTO,1,nTamRef) .And.;
							SC6->C6_GRADE=="S"  .And. aRegStatus[ nX ] )
							AFill( aRegStatus, .F., nX ) 
	
							For nCount := nX to Len( aRegSC6 ) 
	
								SC6->(MsGoto(aRegSC6[nCount]))
	
								//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
								//ЁEfetua o estorno dos itens do pedido de venda                           Ё
								//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
								RecLock("SC6")
								MaAvalSC6("SC6",2,"SC5",lLiber,lTransf,@lLiberOk,@lResidOk,@lFaturOk,Nil,@nVlrCred,Nil,Nil,nMoedaOri)
								If !GetMv("MV_AVALCRD")
									nVlrCred := 0
								EndIf
								lTravou := .T.
								
								//-- Libera empenhos vinculados ao item do pedido
								If nTpCtlBN == 1 // metodo antigo - unico envio: gravacao na SD4
									dbSelectArea("SD4")
									dbSetOrder(6)
									If dbSeek(xFilial("SD4")+SC6->(C6_NUM+C6_ITEM))
										RecLock("SD4",.F.)
										Replace D4_NUMPVBN With CriaVar("D4_NUMPVBN",.F.)
										Replace D4_ITEPVBN With CriaVar("D4_ITEPVBN",.F.)
										MsUnLock()
									EndIf
									dbSelectArea("SDC")
									dbSetOrder(1)
									If dbSeek(xFilial("SDC")+SC6->(C6_PRODUTO+C6_LOCAL)+"SC2"+SC6->(C6_NUM+C6_ITEM))
										RecLock("SDC",.F.)
										Replace DC_PEDIDO With CriaVar("DC_PEDIDO",.F.)
										Replace DC_ITEM	With CriaVar("DC_ITEM",.F.)
										MsUnLock()
									EndIf
								ElseIf nTpCtlBN == 2 // metodo novo - multiplos envios: gravacao na SGO
									dbSelectArea("SGO")
									dbSetOrder(2) // GO_FILIAL+GO_NUMPV+GO_ITEMPV+GO_OP+GO_COD+GO_LOCAL
									dbSeek(xFilial("SGO")+SC6->C6_NUM+SC6->C6_ITEM)
									If ( GO_FILIAL+GO_NUMPV+GO_ITEMPV == SC6->C6_FILIAL+SC6->C6_NUM+SC6->C6_ITEM )
										RecLock("SGO", .F.)
										dbDelete()
										MsUnLock()
									EndIf
									dbSelectArea("SDC")
									dbSetOrder(1)
									If dbSeek(xFilial("SDC")+SC6->(C6_PRODUTO+C6_LOCAL)+"SC2"+SC6->(C6_NUM+C6_ITEM))
										RecLock("SDC",.F.)
										Replace DC_PEDIDO With CriaVar("DC_PEDIDO",.F.)
										Replace DC_ITEM	With CriaVar("DC_ITEM",.F.)
										MsUnLock()
									EndIf
								EndIf
								SC6->( dbDelete() )
								aRegStatus[ nCount ] := .F.
							Next nCount
						Endif
					
					EndIf
	
					If aRegStatus[ nX ]  
						If SC6->C6_GRADE=="S" .And. ( FindFunction("MsMatGrade") .And. IsAtNewGrd() )
							cProdRef := aCols[nX][nPProduto]
							MatGrdPrRf(@cProdRef,.T.)
						    nTamRef	:= Len(cProdRef)			
						EndIf                       
						
						If ( aCols[nX][nPItem] <> SC6->C6_ITEM .Or. (aCols[nX][nPProduto] <> SC6->C6_PRODUTO .And. SubStr(aCols[nX][nPProduto],1,nTamRef) == SubStr(SC6->C6_PRODUTO,1,nTamRef)) .And.;
							SC6->C6_GRADE=="S" )
							If ( !aCols[nX][nDeleted] )
								RecLock("SC6",.T.)
								lTravou := .T.
							Else
								lTravou := .F.
							EndIf
							//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
							//ЁMove os Recnos do SC6 para posterior atualizacao                        Ё
							//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
							aadd(aRegSC6,0)
							aadd(aRegStatus,.T.)
							For nZ := Len(aRegSC6) To nX+1 STEP -1
								aRegSC6[nZ] := aRegSC6[nZ-1]
							Next nZ
						Else
							//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
							//ЁEfetua o estorno dos itens do pedido de venda                           Ё
							//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
							RecLock("SC6")
							MaAvalSC6("SC6",2,"SC5",lLiber,lTransf,@lLiberOk,@lResidOk,@lFaturOk,Nil,@nVlrCred,Nil,Nil,nMoedaOri)

							If !GetMv("MV_AVALCRD")
								nVlrCred := 0
							EndIf
							lTravou := .T.
						EndIf
					Else
						//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
						//ЁCaso o produto tenha sido trocado sera estornado o registro e incluido  Ё
						//Ёnovamente. Somsnte quando a troca for por produto de grade              Ё
						//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
							
						RecLock( "SC6", .T. )

						//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
						//ЁAtualiza os itens do pedido de venda                                    Ё
						//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
						For nY := 1 to Len(aHeader)
							If aHeader[nY][10] <> "V"
								SC6->(FieldPut(FieldPos(aHeader[nY][2]),aCols[nX][nY]))
							EndIf
						Next nY
						If SC6->C6_QTDLIB > 0 .Or. SC5->C5_TIPO $ "CIP"
							lLiberou := .T.
						EndIf
						MaAvalSC6("SC6",1,"SC5",lLiber,lTransf,@lLiberOk,@lResidOk,@lFaturOk,Nil,@nVlrCred)
						
						//Grava relacionamento entre SC6 e SD4,SDC
						If !Empty(aEmpBn)
							nY := aScan(aEmpBn, {|x| x[3] == SC6->C6_ITEM})
							While !Empty (nY) .AND. nY <= Len(aEmpBn) .And. aEmpBn[nY,3] == SC6->C6_ITEM
								(aEmpBn[nY,1])->(dbGoTo(aEmpBn[nY,2]))
								If nTpCtlBN == 1 // metodo antigo - unico envio: gravacao na SD4
									RecLock(aEmpBn[nY,1],.F.)
									If aEmpBn[nY,1] == "SD4"
										Replace D4_NUMPVBN With SC6->C6_NUM
										Replace D4_ITEPVBN With SC6->C6_ITEM
									Else
										Replace DC_PEDIDO With SC6->C6_NUM
										Replace DC_ITEM   With SC6->C6_ITEM
									EndIf
									MsUnLock()
								ElseIf nTpCtlBN == 2 // metodo novo - multiplos envios: gravacao na SGO
									If aEmpBn[nY,1] == "SDC"
										RecLock("SDC",.F.)
										Replace DC_PEDIDO With SC6->C6_NUM
										Replace DC_ITEM   With SC6->C6_ITEM
									ElseIf aEmpBn[nY,1] == "SD4"
										SGO->(dbSetOrder(2)) // GO_FILIAL+GO_NUMPV+GO_ITEMPV+GO_OP+GO_COD+GO_LOCAL
										If !(SGO->(dbSeek(xFilial("SGO")+SC6->C6_NUM+SC6->C6_ITEM+SD4->D4_OP+SD4->D4_COD+SD4->D4_LOCAL)))
											RecLock("SGO",.T.)
											Replace GO_FILIAL  With xFilial("SGO")
											Replace GO_OP      With SD4->D4_OP
											Replace GO_COD     With SD4->D4_COD
											Replace GO_LOCAL   With SD4->D4_LOCAL
											Replace GO_NUMPV   With SC6->C6_NUM
											Replace GO_ITEMPV  With SC6->C6_ITEM
											Replace GO_TRT     With SD4->D4_TRT
											Replace GO_RECNOD4 With SD4->(Recno())
										Else
											RecLock("SGO", .F.)
										EndIf
										Replace GO_QUANT   With SC6->C6_QTDVEN
										Replace GO_QTSEGUM With ConvUM(SD4->D4_COD, SC6->C6_QTDVEN, 0, 2)
									EndIf
									MsUnLock()
								EndIf
								nY++
							End
						EndIf
					Endif
				Else
					If ( !aCols[nX][nDeleted] )
						RecLock("SC6",.T.)
						lTravou := .T.
					Else
						lTravou := .F.
					EndIf
				EndIf
				If aCols[nX][nDeleted]
					If lTravou
						//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
						//Ё Grava os lancamentos nas contas orcamentarias SIGAPCO    Ё
						//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
						PcoDetLan("000100","02","MATA410")
						
						//-- Libera empenhos vinculados ao item do pedido
						If nTpCtlBN == 1 // metodo antigo - unico envio: gravacao na SD4
							dbSelectArea("SD4")
							dbSetOrder(6)
							If dbSeek(xFilial("SD4")+SC6->(C6_NUM+C6_ITEM))
								RecLock("SD4",.F.)
								Replace D4_NUMPVBN With CriaVar("D4_NUMPVBN",.F.)
								Replace D4_ITEPVBN With CriaVar("D4_ITEPVBN",.F.)
								MsUnLock()
							EndIf
							dbSelectArea("SDC")
							dbSetOrder(1)
							If dbSeek(xFilial("SDC")+SC6->(C6_PRODUTO+C6_LOCAL)+"SC2"+SC6->(C6_NUM+C6_ITEM))
								RecLock("SDC",.F.)
								Replace DC_PEDIDO With CriaVar("DC_PEDIDO",.F.)
								Replace DC_ITEM	With CriaVar("DC_ITEM",.F.)
								MsUnLock()
							EndIf
						ElseIf nTpCtlBN == 2 // metodo novo - multiplos envios: gravacao na SGO
							dbSelectArea("SGO")
							dbSetOrder(2) // GO_FILIAL+GO_NUMPV+GO_ITEMPV+GO_OP+GO_COD+GO_LOCAL
							dbSeek(xFilial("SGO")+SC6->C6_NUM+SC6->C6_ITEM)
							If ( GO_FILIAL+GO_NUMPV+GO_ITEMPV == SC6->C6_FILIAL+SC6->C6_NUM+SC6->C6_ITEM )
								RecLock("SGO", .F.)
								dbDelete()
								MsUnLock()
							EndIf
							dbSelectArea("SDC")
							dbSetOrder(1)
							If dbSeek(xFilial("SDC")+SC6->(C6_PRODUTO+C6_LOCAL)+"SC2"+SC6->(C6_NUM+C6_ITEM))
								RecLock("SDC",.F.)
								Replace DC_PEDIDO With CriaVar("DC_PEDIDO",.F.)
								Replace DC_ITEM	With CriaVar("DC_ITEM",.F.)
								MsUnLock()
							EndIf
						EndIf

						//зддддддддддддддддддддддддддддддддддддддддддддддддддддд©
						//Ё Executa a exclusao da tabela SGJ                    Ё
						//юддддддддддддддддддддддддддддддддддддддддддддддддддддды
						If lAtuSGJ .And. FindFunction("A650DelSGJ")
							A650DelSGJ("I")		//Por Item
						Endif

						//зддддддддддддддддддддддддддд©
						//ЁEfetua a ExclusЦo do RateioЁ
						//юддддддддддддддддддддддддддды
		                If AliasInDic("AGG")
		    				aAreaAGG := GetArea()
							If (nY	:= aScan(aColsAGG,{|x| AllTrim(x[1]) == AllTrim(SC6->C6_ITEM) })) > 0
								For nZ := 1 To Len(aColsAGG[nY][2])                             				
										AGG->(DbSetOrder(1)) //AGG_FILIAL+AGG_PEDIDO+AGG_FORNEC+AGG_LOJA+AGG_ITEMPD+AGG_ITEM
										If AGG->(MsSeek(xFilial("AGG")+SC5->C5_NUM+SC5->C5_CLIENTE+SC5->C5_LOJACLI+SC6->C6_ITEM+GdFieldGet("AGG_ITEM",nz,NIL,aHeadAGG,ACLONE(aColsAGG[NY,2]))))
											RecLock("AGG",.F.)
											AGG->(dbDelete())
											MsUnlock()
		                                EndIf
								Next nZ
							EndIf
							RestArea(aAreaAGG)
		                EndIf						                        

						dbSelectArea("SC6")
						dbDelete()
						MsUnLock()
						
						// Verifica se o C5_DTLANC esta preenchido, se estiver preenchido contabiliza a exclusЦo dos itens.
						If lCtbOnLine 
							If !Empty(SC5->C5_DTLANC)
								nTotalCtb+=DetProva(nHdlPrv,"632","MATA410",cLoteCtb)
							Endif
						EndIf
						
						
						If lMta410E
							ExecBlock("MTA410E",.f.,.f.)
						EndIf
					EndIf
				Else
             
					//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					//ЁAltera o campo C6_OP para permitir que a rotina de geracao de OP's por  Ё
					//Ёvenda seja executada novamente para este item                           Ё
					//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
					If nOpcao == 2 .And. SC6->C6_PRODUTO <> aCols[nX][nPProduto] .And. !(SC6->C6_OP $ '01#03')
						SC6->C6_OP := ""
					EndIf

					//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					//ЁAtualiza os itens do pedido de venda                                    Ё
					//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
					For nY := 1 to Len(aHeader)
						If aHeader[nY][10] <> "V"
							SC6->(FieldPut(FieldPos(aHeader[nY][2]),aCols[nX][nY]))
						EndIf
					Next nY 

					// Se for alteracao, e mudaram o cliente do pedido, nao considerar o valor o estorno
					// como um credito, pois o estorno considera um cliente e a gravacao considera outro.
					If nOpcao == 2 .And. (SC6->C6_CLI+SC6->C6_LOJA <> M->C5_CLIENTE+M->C5_LOJACLI)
						nVlrCred := 0
					EndIf
					SC6->C6_FILIAL	:= xFilial("SC6")
					SC6->C6_NUM		:= M->C5_NUM
					SC6->C6_CLI		:= M->C5_CLIENTE
					SC6->C6_LOJA 	:= M->C5_LOJACLI
                                                            
					// Contabiliza itens do pedido de venda 
					If lCtbOnLine
						nTotalCtb+=DetProva(nHdlPrv,"612","MATA410",cLoteCtb)
					EndIf
					
					If SC6->C6_QTDLIB > 0.Or. SC5->C5_TIPO $ "CIP"
						lLiberou := .T.
					EndIf
					
					//Grava relacionamento entre SC6 e SD4,SDC
					If !Empty(aEmpBn)
						nY := aScan(aEmpBn, {|x| x[3] == SC6->C6_ITEM})
						While !Empty (nY) .AND. nY <= Len(aEmpBn) .And. aEmpBn[nY,3] == SC6->C6_ITEM
							(aEmpBn[nY,1])->(dbGoTo(aEmpBn[nY,2]))
							If nTpCtlBN == 1 // metodo antigo - unico envio: gravacao na SD4
								RecLock(aEmpBn[nY,1],.F.)
								If aEmpBn[nY,1] == "SD4"
									Replace D4_NUMPVBN With SC6->C6_NUM
									Replace D4_ITEPVBN With SC6->C6_ITEM
								Else
									Replace DC_PEDIDO With SC6->C6_NUM
									Replace DC_ITEM   With SC6->C6_ITEM
								EndIf
								MsUnLock()
							ElseIf nTpCtlBN == 2 // metodo novo - multiplos envios: gravacao na SGO
								If aEmpBn[nY,1] == "SDC"
									RecLock("SDC",.F.)
									Replace DC_PEDIDO With SC6->C6_NUM
									Replace DC_ITEM   With SC6->C6_ITEM
								ElseIf aEmpBn[nY,1] == "SD4"
									SGO->(dbSetOrder(2)) // GO_FILIAL+GO_NUMPV+GO_ITEMPV+GO_OP+GO_COD+GO_LOCAL
									If !(SGO->(dbSeek(xFilial("SGO")+SC6->C6_NUM+SC6->C6_ITEM+SD4->D4_OP+SD4->D4_COD+SD4->D4_LOCAL)))
										RecLock("SGO",.T.)
										Replace GO_FILIAL  With xFilial("SGO")
										Replace GO_OP      With SD4->D4_OP
										Replace GO_COD     With SD4->D4_COD
										Replace GO_LOCAL   With SD4->D4_LOCAL
										Replace GO_NUMPV   With SC6->C6_NUM
										Replace GO_ITEMPV  With SC6->C6_ITEM
										Replace GO_TRT     With SD4->D4_TRT
										Replace GO_RECNOD4 With SD4->(Recno())
									Else
										RecLock("SGO", .F.)
									EndIf
									Replace GO_QUANT   With SC6->C6_QTDVEN
									Replace GO_QTSEGUM With ConvUM(SD4->D4_COD, SC6->C6_QTDVEN, 0, 2)
								EndIf
								MsUnLock()
							EndIf
							nY++
						End
					EndIf
					MaAvalSC6("SC6",1,"SC5",lLiber,lTransf,@lLiberOk,@lResidOk,@lFaturOk,Nil,@nVlrCred)

					If lAtuSGJ .And. FindFunction("A650AvalPV")
						A650AvalPV()
					Endif

					If AliasInDic("AGG")
						AGG->(DbSetOrder(1)) //AGG_FILIAL+AGG_PEDIDO+AGG_FORNEC+AGG_LOJA+AGG_ITEMPD+AGG_ITEM
						If (nY	:= aScan(aColsAGG,{|x| Alltrim(x[1]) == Alltrim(SC6->C6_ITEM) })) > 0
							For nZ := 1 To Len(aColsAGG[nY][2])
								If Type( "nAutoAdt" ) == "N" .AND. nAutoAdt == 0
		                        	cItemSCH := GdFieldGet("AGG_ITEM",nz,NIL,aHeadAGG,ACLONE(aColsAGG[NY,2]))
		                        Else
                        	 		cItemSCH := aRatCTBPC[nY][2][nZ][aScan(aRatCTBPC[nY][2][nZ],{|x| x[1] == "AGG_ITEM"})][2]
		                        EndIf 
								lAchou:=AGG->(MsSeek(xFilial("AGG")+SC5->C5_NUM+SC5->C5_CLIENTE+SC5->C5_LOJACLI+SC6->C6_ITEM+cItemSCH) )
								If !aColsAGG[nY][2][nZ][nUsadoAGG+1]
									RecLock("AGG",!lAchou)
									For nW := 1 To nUsadoAGG
										If aHeadAGG[nW][10] <> "V"
											AGG->(FieldPut(FieldPos(aHeadAGG[nW][2]),aColsAGG[nY][2][nZ][nW]))
										EndIf
									Next nW
									AGG->AGG_FILIAL	:= xFilial("AGG")
									AGG->AGG_PEDIDO	:= SC5->C5_NUM
									AGG->AGG_FORNEC:= SC5->C5_CLIENTE
									AGG->AGG_LOJA	:= SC5->C5_LOJACLI
									AGG->AGG_ITEMPD	:= SC6->C6_ITEM
									MsUnlock()
								ElseIf lAchou
									RecLock("AGG",.F.)
									AGG->(dbDelete())
									MsUnlock()
								EndIf
							Next nZ
						EndIf
					EndIf
				EndIf

				//
				// Template GEM - Gestao de Empreendimentos Imobiliarios
				// Gera o contrato baseado nos dados do pedido de venda
				// 
				
				If  HasTemplate("LOT") .AND. ExistTemplate("GEMXPV",,.T.)
					// atualiza o status do empreendimento
					ExecTemplate("GEMXPV",.F.,.F.,{ aCols[nX][nDeleted] ,SC6->C6_CODEMPR, 2 })
				EndIf
            
				If lMta410I
					ExecBlock("MTA410I",.f.,.f.,nX)
				EndIf

				//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Caso seja o ultimo item e exclusao, exclui o cabecalho                 Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
				If nX == nMaxFor .And. nOpcao == 3

					If Len(aRegSCV) > 0
						dbSelectArea("SCV")
						For nCntForma := 1 to Len(aColsFor)
	 						dbSelectArea("SCV")
							MsGoto(aRegSCV[nCntForma])
							RecLock("SCV")
								dbDelete()
							MsUnLock()
						Next
					Endif	

					dbSelectArea("SC5")
					//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					//ЁAtualiza os acumulados do SC5                                           Ё
					//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
					MaAvalSC5("SC5",2,lLiber,lTransf,@lLiberOk,@lResidOk,@lFaturOk,Nil,Nil,Nil,Nil,Nil,Nil,@nVlrCred)	
					//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					//Ё Grava os lancamentos nas contas orcamentarias SIGAPCO    Ё
					//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
					PcoDetLan("000100","04","MATA410")

					//
					// Template GEM - Gestao de Empreendimentos Imobiliarios
					// Exclui os solidarios do cliente gravados no pedido de venda
					//
					If ExistTemplate("GEMXGRSOL",,.T.)
						ExecTemplate("GEMXGRSOL",.F.,.F.,{nOpcao ,SC5->C5_NUM})
					EndIf

					//
					// Template GEM - Gestao de Empreendimentos Imobiliarios
					// Exclui a condicao de venda "personalizada" do cliente gravado no pedido de venda
					//
					If ExistBlock("GEMXGRCVND",,.T.)
						ExecBlock("GEMXGRCVND",.F.,.F.,{nOpcao ,SC5->C5_NUM ,SC5->C5_CONDPAG})
					ElseIf ExistTemplate("GEMXGRCVND",,.T.)
						ExecTemplate("GEMXGRCVND",.F.,.F.,{nOpcao ,SC5->C5_NUM ,SC5->C5_CONDPAG})
					EndIf

					//зддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					//Ё Executa a exclusao da tabela SGJ                    Ё
					//юддддддддддддддддддддддддддддддддддддддддддддддддддддды
					If lAtuSGJ .And. FindFunction("A650DelSGJ")
						A650DelSGJ("T")		//Por Total
					Endif

					//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					//Ё Exclui o SC5                                                           Ё
					//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
					dbSelectArea("SC5")
					RecLock("SC5")
					dbDelete()
					MsUnLock()
					    // Verifica se o C5_DTLANC esta preenchido, se estiver preenchido contabiliza cabeГalho do PV.
						If lCtbOnLine 
							If !Empty(SC5->C5_DTLANC)
								nTotalCtb+=DetProva(nHdlPrv,"636","MATA410",cLoteCtb)
							Endif
						EndIf
					
					If (ExistBlock("MA410DEL"))
						ExecBlock("MA410DEL",.F.,.F.)
					EndIf
					                                            
					//зддддддддддддддддддддддддддддддддддддддддддддддддд©
					//ЁApaga o SALDO do relacionamento com AdiantamentosЁ
					//юддддддддддддддддддддддддддддддддддддддддддддддддды
					If A410UsaAdi( SC5->C5_CONDPAG )					
						aRecnoSE1 := FPedAdtPed( "R", { SC5->C5_NUM }, .F. ) 
						FPedAdtGrv( "R", 2, SC5->C5_NUM, aRecnoSE1 )					
					EndIf
				EndIf

			CLOSETRANSACTION LOCKIN "SC5"
			If SC5->C5_TIPLIB=="2" .And. !aCols[nX][nDeleted]
				aadd(aRegLib,SC6->(RecNo()))
			EndIf
		Next nX
		Begin Transaction
			If ( lGravou )
				//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//ЁVerifica a liberacao por pedido de venda                                Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
				If ( SC5->C5_TIPLIB=="2" .And. lLiberou )
					MaAvalSC5("SC5",3,lLiber,lTransf,@lLiberOk,@lResidOk,@lFaturOk,Nil,Nil,Nil,aRegLib,Nil,Nil,@nVlrCred)
				EndIf
				//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//ЁAtualiza os acumulados do SC5                                           Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
				MaAvalSC5("SC5",1,lLiber,lTransf,@lLiberOk,@lResidOk,@lFaturOk,Nil,Nil,Nil,Nil,Nil,Nil,@nVlrCred)
				If INCLUI
					While GetSX8Len() > nStack 
						ConfirmSX8()
					EndDo
				EndIf
				//зддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Pontos de entrada para todos os itens do pedido.    Ё
				//юддддддддддддддддддддддддддддддддддддддддддддддддддддды
				If ExistTemplate("MTA410T")
					ExecTemplate("MTA410T",.F.,.F.)
				EndIf
                
				If nModulo == 72
					KEXF920()
				EndIf
				
				If ExistBlock("MTA410T")
					ExecBlock("MTA410T",.F.,.F.)
				EndIf
				//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё  Processa Gatilhos                                   Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
				EvalTrigger()
				//-- Integrado ao wms devera avaliar as regras para convocacao do servico e disponibilizar os 
				//-- registros do SDB para convocacao
				If	IntDL() .And. !Empty(aLibSDB)
					WmsExeDCF('2')
				EndIf
			EndIf
			//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//ЁEXECUTAR CHAMADA DE FUNCAO p/ integracao com sistema de Distribuicao - NAO REMOVER Ё
			//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			If SuperGetMv("MV_FATDIST") == "S" // Apenas quando utilizado pelo modulo de Distribuicao
				D630Descon(cPedido)
			EndIf          
			
					//зддддддддддддддддддддддддддддддддддддддддддддддддддд©
					//Ё Envia os dados para o modulo contabil             Ё
					//юддддддддддддддддддддддддддддддддддддддддддддддддддды
					 
					If lCtbOnLine
						RodaProva(nHdlPrv,nTotalCtb)
						If nTotalCtb > 0
							cA100Incl(cArqCtb,nHdlPrv,1,cLoteCtb,lDigita,lAglutina)  
						EndIf
	        		EndIf                  
	        		          
	        		// Flag de contabilizaГЦo on-line.                     
	        		If lCtbOnLine
	        		RecLock("SC5")
							SC5->C5_DTLANC := dDataBase
					MsUnlock()   
					Endif

			
		End Transaction
	EndIf
Else
	lGravou := lXml
EndIf
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Ponto de entrada para refazer as liberaГУes de estoque considerando o  Ё
//Ё os registros de SDC da liberaГЦo anterior...                           Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If ( (ExistBlock("M410RLIB") ) )
	aSaldoSDC := ExecBlock("M410RLIB",.f.,.f.,aSaldoSDC)
EndIf

RestArea( aArea )
Return(lGravou)


/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁMa410GraGrЁ Autor ЁEduardo Riera          Ё Data Ё26.02.99  Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁEsta funcao compatibiliza o acols da grade de produtos com  Ё╠╠
╠╠Ё          Ёo acols original do pedido de vendas assim nao eh necessarioЁ╠╠
╠╠Ё          Ёqualquer modificacao na funcao de gravacao do Pedido de     Ё╠╠
╠╠Ё          ЁVenda                                                       Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁNenhum                                                      Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁNenhum                                                      Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддбдддддддбдддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё   DATA   Ё Author  Ё BOPS  Ё Manutencao Efetuada                      Ё╠╠
╠╠цддддддддддедддддддддедддддддедддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠юддддддддддадддддддддадддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function Ma410GraGr()

Local aColsOrig:= aClone(aCols)  //aCols Original
Local nMaxFor  := Len(aColsOrig)
Local nCntFor  := 0     // Contador
Local nPProduto:= aScan(aHeader,{|x| AllTrim(x[2])=="C6_PRODUTO"})
Local nPItem   := aScan(aHeader,{|x| AllTrim(x[2])=="C6_ITEM"})
Local nPItGrade:= aScan(aHeader,{|x| AllTrim(x[2])=="C6_ITEMGRD"})
Local nPPrcVen := aScan(aHeader,{|x| AllTrim(x[2])=="C6_PRCVEN"})
Local nPQtdVen := aScan(aHeader,{|x| AllTrim(x[2])=="C6_QTDVEN"})
Local nPQtdVen2:= aScan(aHeader,{|x| AllTrim(x[2])=="C6_UNSVEN"})
Local nPQtdLib := aScan(aHeader,{|x| AllTrim(x[2])=="C6_QTDLIB"})
Local nPValor  := aScan(aHeader,{|x| AllTrim(x[2])=="C6_VALOR"})
Local nPGrade  := aScan(aHeader,{|x| AllTrim(x[2])=="C6_GRADE"})
Local nPValDesc := aScan(aHeader,{|x| AllTrim(x[2])=="C6_VALDESC"})
Local nPOpc    := aScan(aHeader,{|x| AllTrim(x[2])=="C6_OPC"})
Local nPDescri := aScan(aHeader,{|x| AllTrim(x[2])=="C6_DESCRI"})
Local nPPrUnit := aScan(aHeader,{|x| AllTrim(x[2])=="C6_PRUNIT"})
Local nPBlq    := aScan(aHeader,{|x| AllTrim(x[2])=="C6_BLQ"})
Local nLinha   := 0     // Contador de Linhas
Local nColuna  := 0     // Contador de Colunas
Local nAcols   := 0     // Numero de Elementos do Acols
Local cProdRef := ""    // Codigo do Produto Grade
Local cItem    := "00"  // Controle de Itens do Pedido de Venda
Local aColsGrd := {} 	//montara aCols de produtos da grade aqui para poder ordenar pelo codigo

Local lTestaDel:= If(Len(aColsOrig[1])==Len(aHeader),.F.,.T.)
Local lGrdMult := "MATA410" $ SuperGetMV("MV_GRDMULT",.F.,"")

If FindFunction("MsMatGrade") .And. IsAtNewGrd()
	If MaGrade() .And. Type("oGrade")=="O" .And. Len(oGrade:aColsGrade)>0
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁInicializa o Acols para posterior atualizacao                           Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		aCols := {}
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁVarre o acols original para atualizar a variavel aCols                  Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		For nCntFor := 1 To nMaxFor
			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//ЁAtualiza o Controle de Itens do Pedido de Venda e da Grade              Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			cItem    := aColsOrig[nCntFor][nPitem] //Soma1(cItem,Len(SC6->C6_ITEM))
			cItemGrd := StrZero(0,TamSX3("C6_ITEMGRD")[1])
			cProdRef := aColsOrig[nCntFor][nPProduto]
			If ( !Empty(cProdRef) )                   
				oGrade:nPosLinO := nCntFor
				//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//ЁVerifica se foi digitado uma referencia                                 Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
				If ( Len(oGrade:aHeadGrade)>0 .And. oGrade:aHeadGrade[nCntFor][1] == "R" )
					For nLinha := 1 To Len(oGrade:aColsGrade[nCntFor])
						For nColuna := 2 To Len(oGrade:aHeadGrade[nCntFor])
							//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
							//ЁVerifica se a valor a ser gravado                                       Ё
							//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
							If ( oGrade:aColsFieldByName("C6_QTDVEN",nCntFor,nLinha,nColuna) <> 0 .And.;
									If(lTestaDel,!aColsOrig[nCntFor][Len(aHeader)+1],.T.) )
								cItemGrd := Soma1(cItemGrd,Len(SC6->C6_ITEMGRD))
								cProdRef := aColsOrig[nCntFor][nPProduto]
								MatGrdPrRf(@cProdRef)                
								cProdRef := oGrade:GetNameProd(cProdRef,nLinha,nColuna) 
								aadd(aColsGrd,aClone(aColsOrig[nCntFor]))
								nAcols := Len(aColsGrd)
								aColsGrd[nAcols][nPProduto ]  := PadR(cProdRef,Len(SB1->B1_COD))
								aColsGrd[nAcols][nPItem    ]  := cItem
								If ( nPItGrade <> 0 )
									aColsGrd[nAcols][nPItGrade ]  := cItemGrd
								EndIf
								If ( nPQtdVen <> 0 )
									aColsGrd[nAcols][nPQtdVen  ]  := oGrade:aColsFieldByName("C6_QTDVEN",nCntFor,nLinha,nColuna)
								EndIf
								If ( nPQtdVen2 <> 0 )
									aColsGrd[nAcols][nPQtdVen2 ]  := oGrade:aColsFieldByName("C6_UNSVEN",nCntFor,nLinha,nColuna)
								EndIf
								If ( nPQtdLib <> 0 )
									aColsGrd[nAcols][nPQtdLib  ]  := oGrade:aColsFieldByName("C6_QTDLIB",nCntFor,nLinha,nColuna)
								EndIf
								If ( nPValor <> 0 )
									If lGrdMult
										aColsGrd[nAcols][nPValor   ]  := oGrade:aColsFieldByName("C6_VALOR",nCntFor,nLinha,nColuna)
									Else
										aColsGrd[nAcols][nPValor   ]  := a410Arred(oGrade:aColsFieldByName("C6_QTDVEN",nCntFor,nLinha,nColuna)*aColsOrig[nCntFor][nPPrcVen],"C6_VALOR")
									EndIf
								EndIf
								If  ( nPGrade <>  0 )
									aColsGrd[nAcols][nPGrade   ]  := "S"
								Endif
								If  ( nPValDesc !=  0 )
									If lGrdMult
										aColsGrd[nAcols][nPValDesc]	:= oGrade:aColsFieldByName("C6_VALDESC",nCntFor,nLinha,nColuna)
									Else
										aColsGrd[nAcols][nPValDesc]	:= (aColsOrig[nCntFor][nPValDesc]/aColsOrig[nCntFor][nPQtdVen])*aColsGrd[nAcols][nPQtdVen	]
									EndIf
								EndIf
								If  ( nPOpc !=  0 )
									aColsGrd[nAcols][nPOpc]	:= oGrade:aColsFieldByName("C6_OPC",nCntFor,nLinha,nColuna)
								EndIf
								If lGrdMult
									If  ( nPPrcVen != 0 )
										aColsGrd[nAcols][nPPrcVen]	:= oGrade:aColsFieldByName("C6_PRCVEN",nCntFor,nLinha,nColuna)
									EndIf
									If  ( nPDescri != 0 )
										aColsGrd[nAcols][nPDescri]	:= oGrade:aColsFieldByName("C6_DESCRI",nCntFor,nLinha,nColuna)
									EndIf
									If	( nPPrUnit != 0 )
										aColsGrd[nAcols][nPPrUnit]	:= oGrade:aColsFieldByName("C6_PRUNIT",nCntFor,nLinha,nColuna)
									EndIf
									If	(nPBlq != 0 )
										aColsGrd[nAcols][nPBlq]	:= oGrade:aColsFieldByName("C6_BLQ",nCntFor,nLinha,nColuna)
									EndIf
								EndIf
							Else
								//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
								//ЁVerifica se o item ja foi gravado para deleta-lo                        Ё
								//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
								If ( !Empty(oGrade:aColsFieldByName("C6_ITEM",nCntFor,nLinha,nColuna)) )
									cProdRef := aColsOrig[nCntFor][nPProduto]
									cItemGrd := Soma1(cItemGrd,Len(SC6->C6_ITEMGRD))
									MatGrdPrRf(@cProdRef) 
									cProdRef := oGrade:GetNameProd(cProdRef,nLinha,nColuna) 
									aadd(aColsGrd,aClone(aColsOrig[nCntFor]))
									nAcols := Len(aColsGrd)
									aColsGrd[nAcols][nPProduto ]  := PadR(cProdRef,Len(SB1->B1_COD))
									aColsGrd[nAcols][nPItem    ]  := cItem
									If ( nPItGrade <> 0 )
										aColsGrd[nAcols][nPItGrade ]  := cItemGrd
									EndIf
									If ( nPQtdVen <> 0 )
										aColsGrd[nAcols][nPQtdVen  ]  := oGrade:aColsFieldByName("C6_QTDVEN",nCntFor,nLinha,nColuna) 
									EndIf
									If ( nPQtdLib <> 0 )
										aColsGrd[nAcols][nPQtdLib  ]  := oGrade:aColsFieldByName("C6_QTDLIB",nCntFor,nLinha,nColuna) 
									EndIf
									If ( nPValor <> 0 )
										If lGrdMult
											aColsGrd[nAcols][nPValor   ]  := oGrade:aColsFieldByName("C6_VALOR",nCntFor,nLinha,nColuna) 
										Else
											aColsGrd[nAcols][nPValor   ]  := a410Arred(oGrade:aColsFieldByName("C6_QTDVEN",nCntFor,nLinha,nColuna)*aColsOrig[nCntFor][nPPrcVen],"C6_VALOR")
										EndIf
									EndIf
									If  ( nPGrade <>  0 )
										aColsGrd[nAcols][nPGrade   ]  := "S"
									EndIf
									If  ( nPValDesc !=  0 )
										If lGrdMult
											aColsGrd[nAcols][nPValDesc]	:= oGrade:aColsFieldByName("C6_VALDESC",nCntFor,nLinha,nColuna) 
										Else
											aColsGrd[nAcols][nPValDesc]	:= (aColsOrig[nCntFor][nPValDesc]/aColsOrig[nCntFor][nPQtdVen])*aColsGrd[nAcols][nPQtdVen	]
										EndIf
									EndIf
									If  ( nPOpc !=  0 )
										aColsGrd[nAcols][nPOpc]	:= oGrade:aColsFieldByName("C6_OPC",nCntFor,nLinha,nColuna)
									EndIf
									If lGrdMult
										If  ( nPPrcVen != 0 )
											aColsGrd[nAcols][nPPrcVen]	:= oGrade:aColsFieldByName("C6_PRCVEN",nCntFor,nLinha,nColuna)
										EndIf
										If  ( nPDescri != 0 )
											aColsGrd[nAcols][nPDescri]	:= oGrade:aColsFieldByName("C6_DESCRI",nCntFor,nLinha,nColuna)
										EndIf
										If	( nPPrUnit != 0 )
											aColsGrd[nAcols][nPPrUnit]	:= oGrade:aColsFieldByName("C6_PRUNIT",nCntFor,nLinha,nColuna)
										EndIf
										If	(nPBlq != 0 )
											aColsGrd[nAcols][nPBlq]	:= oGrade:aColsFieldByName("C6_BLQ",nCntFor,nLinha,nColuna)
										EndIf
									EndIf
									aColsGrd[nAcols][Len(aHeader)+1] := .T.
								EndIf
							EndIf
						Next nColuna
					Next nLinha
					
					//--Ordena itens da grade pelo codigo para evitar erro durante alteracoes e liberacoes do PV
					aSort(aColsGrd,,,{|x,y| x[2] < y[2]})
					
					For nAcols := 1 To Len(aColsGrd)
						aColsGrd[nAcols][nPItGrade] := StrZero(nAcols,TamSX3("C6_ITEMGRD")[1])
						aAdd(aCols, aClone(aColsGrd[nAcols]) )
					Next nAcols
				Else
					aadd(aCols,aClone(aColsOrig[nCntFor]))
					nAcols := Len(aCols)
					aCols[nAcols][nPItem    ]  := cItem
				EndIf
				//--Limpa array temporario utilizado para o sort
				aColsGrd := {}
			EndIf
		Next nCntFor
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁOrdena o aCols                                                          Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		aCols       := aSort(aCols,,,{|x,y| x[nPItem]+x[nPItGrade] < y[nPItem]+y[nPItGrade] })
		oGrade:aColsGrade  := {}
		oGrade:aHeadGrade  := {}
	EndIf
Else
	_Ma410GraGr()
EndIf
Return(Nil)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁA410VldTOk  Ё Autor ЁEduardo Riera        Ё Data Ё01/06/2001Ё╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁRotina de validacao da tudoOk da Enchoice                   Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ Nenhum                                                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё ExpL1: Dados validos?                                      Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁObservacaoЁEsta rotina efetua a validacao da TudoOk da Enchoice        Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё MATA410                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function A410VldTOk(nOpc, aRecnoSE1RA)

Local lRet      := .T.
Local lMt410TOK := Existblock("MT410TOK")  
Local cString   := ""

If nOpc = 4
	cString   := STR0141 //"Este Documento nЦo poderА ser alterado pois existe um vinculo com pedido de compras ja recebido"
Else	
    cString   := STR0142 //"Este Documento nЦo poderА ser excluido pois existe um vinculo com pedido de compras ja recebido"
EndIf    

Default aRecnoSE1RA := {}

If !__lPyme

	//
	// Template GEM
	//	
	If lRet 
		If ExistBlock("GMCVndVLD")
			lRet := ExecBlock("GMCVndVLD",.F.,.F.,{ lRet ,aHeader ,aCols ,aGEMCVnd })
		ElseIf ExistTemplate("GMCVndVLD",,.T.)
			//
			// executa a validacao da condicao de venda
			//
			lRet := ExecTemplate("GMCVndVLD",.F.,.F.,{ lRet ,aHeader ,aCols ,aGEMCVnd })
		Endif	

	EndIf

Endif

//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Ponto de entrada para validar ao clicar botao OK          Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If lMt410TOK
	lRet := Execblock("MT410TOK",.F.,.F.,{nOpc})
Endif

//зддддддддддддддддддддддддддддддддддддддддддддд©
//ЁValida se hА relacionamentos de AdiantamentosЁ
//юддддддддддддддддддддддддддддддддддддддддддддды
If 	( Type("l410Auto") == "U" .OR. !l410Auto ) .AND.; 
	A410UsaAdi( M->C5_CONDPAG ) .AND.; 
	Len(aRecnoSE1RA) <= 0 .AND.;
	lRet
	
	lRet := MsgYesNo( STR0125 ) // "NЦo foram relacionados Adiantamentos para este pedido. Deseja prosseguir?"
EndIf

If lRet .AND. Len(aCols) > 0
	//здддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁVerifica se existe Vinculo com SC6 E SC7, se  Ё
	//Ёhouve recebimento do SC7 O sc6 nao podera ser Ё
	//Ёalterado.									 Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддды
   	lRet :=	A410VerISC6(SC5->C5_FILIAL,SC5->C5_NUM ,aCols,aHeader)
	If !lRet 
		Aviso(STR0140,cString,{"Ok"}) //  "ATENгцO" | "Este Documento nЦo poderА ser alterado/excluido pois existe um vinculo com pedido de compras ja recebido"				
	EndIf	
EndIf	
Return(lRet)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁA410Bonus Ё Autor ЁEduardo Riera          Ё Data Ё16.06.2001Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁRotina de tratamento da regra de bonificacao para interface Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁExpN1: Tipo de operacao                                     Ё╠╠
╠╠Ё          Ё       [1] Inclusao do bonus                                Ё╠╠
╠╠Ё          Ё       [2] Exclusao do bonus                                Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁNenhum                                                      Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁDescri┤└o ЁEsta rotina tem como objetivo avaliar a regra de bonificacaoЁ╠╠
╠╠Ё          Ёe adicionar na respectiva interface                         Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁObservacaoЁ                                                            Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё Materiais/Distribuicao/Logistica                           Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Static Function A410Bonus(nTipo)

Local aArea     := GetArea()
Local aBonus    := {}
Local lA410BLCo := ExistBlock("A410BLCO")
Local nX        := 0
Local nY        := 0
Local nW        := 0 
Local nZ        := Len(aCols)
Local nUsado    := Len(aHeader)
Local nPProd    := aScan(aHeader,{|x| AllTrim(x[2])=="C6_PRODUTO" })
Local nPQtdVen  := aScan(aHeader,{|x| AllTrim(x[2])=="C6_QTDVEN" })
Local nPPrcVen  := aScan(aHeader,{|x| AllTrim(x[2])=="C6_PRCVEN" })
Local nPValor   := aScan(aHeader,{|x| AllTrim(x[2])=="C6_VALOR" })
Local nPTES		:= aScan(aHeader,{|x| AllTrim(x[2])=="C6_TES" })                
Local nPItem	:= aScan(aHeader,{|x| AllTrim(x[2])=="C6_ITEM" })
Local nPQtdLib  := aScan(aHeader,{|x| AllTrim(x[2])=="C6_QTDLIB" })
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁVerifica os bonus                                       Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If nTipo == 1
	Ma410GraGr()
	If M->C5_TIPO=="N"
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁVerifica os bonus por item de venda                     Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If ExistBlock('A410BONU')
			aBonus	:=	ExecBlock('A410BONU',.F.,.F.,{aCols,{nPProd,nPQtdVen,nPTES}})
		Else
			aAreaSE4 := SE4->(GetArea())
			SE4->(DbSetOrder(1))
			SE4->(DbSeek( xFilial("SE4") + M->C5_CONDPAG ))
			aBonus   := FtRgrBonus(aCols,{nPProd,nPQtdVen,nPTES},M->C5_CLIENTE,M->C5_LOJACLI,M->C5_TABELA,M->C5_CONDPAG,SE4->E4_FORMA)
			SE4->(RestArea(aAreaSE4))
		Endif
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁRecupera os bonus ja existentes                         Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		aBonus   := FtRecBonus(aCols,{nPProd,nPQtdVen,nPTES,nUsado+1},aBonus)
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁGrava os novos bonus                                    Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		nY := Len(aBonus)
		If nY > 0
			cItem := aCols[nZ,nPItem]
			For nX := 1 To nY
				cItem := Soma1(cItem)
				aadd(aCols,Array(nUsado+1))
				nZ++
				N := nZ
				For nW := 1 To nUsado
					If (aHeader[nW,2] <> "C6_REC_WT") .And. (aHeader[nW,2] <> "C6_ALI_WT")
						aCols[nZ,nW] := CriaVar(aHeader[nW,2],.T.)
					EndIf	
				Next nW
				aCols[nZ,nUsado+1] := .F.
				aCols[nZ,nPItem  ] := cItem
				A410Produto(aBonus[nX][1],.F.)
				A410MultT("M->C6_PRODUTO",aBonus[nX][1])
				A410MultT("M->C6_TES",aBonus[nX][3])
				aCols[nZ,nPProd  ] := aBonus[nX][1]
				
 				If ExistTrigger("C6_PRODUTO")
   					RunTrigger(2,Len(aCols))
				Endif

				aCols[nZ,nPQtdVen] := aBonus[nX][2]
				aCols[nZ,nPTES   ] := aBonus[nX][3]
				If ( aCols[nZ,nPPrcVen] == 0 )
					aCols[nZ,nPPrcVen] := 1
					aCols[nZ,nPValor ] := aCols[nZ,nPQtdVen]
				Else
					aCols[nZ,nPValor ] := A410Arred(aCols[nZ,nPQtdVen]*aCols[nZ,nPPrcVen],"C6_VALOR")
				EndIf
				
 				If ExistTrigger("C6_TES    ")
   					RunTrigger(2,Len(aCols))
				Endif

				If mv_par01 == 1 
					aCols[nZ,nPQtdLib ] := aCols[nZ,nPQtdVen ]
				Endif

				If lA410BLCo
					aCols[nZ] := ExecBlock("A410BLCO",.F.,.F.,{aHeader,aCols[nZ]})
				Endif
			Next nX
		EndIf
	EndIf
Else
	FtDelBonus(aCols,{nPProd,nPQtdVen,nPTES,nUsado+1})	
EndIf
RestArea(aArea)
Return(.T.)
/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁMA410ForPgЁ Autor ЁHenry Fila             Ё Data Ё17.08.2001Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁInterface com o usuario das formas de pagamento             Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁExpN1: Opcao do aRotina                                     Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁNenhum                                                      Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁDescri┤└o ЁEsta rotina tem como objetivo controlar a interface com o   Ё╠╠
╠╠Ё          Ёusuario das formas de pagamento                             Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁObservacaoЁ                                                            Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё Materiais/Distribuicao/Logistica                           Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Static Function Ma410ForPg(nOpcX)

Local aArea     := GetArea()
Local aPosObj   := {}
Local aObjects  := {}
Local aSize     := MsAdvSize()
Local nOpcA     := 0
Local oDlg
Local oGetDad

PRIVATE N := 1
PRIVATE aHeader := aClone(aHeadFor)
PRIVATE aCols   := aClone(aColsFor)

AAdd( aObjects, { 100, 100, .t., .t. } )

aSize[ 3 ] -= 50
aSize[ 4 ] -= 50 	

aSize[ 5 ] -= 100
aSize[ 6 ] -= 100

aInfo   := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 3, 2 }
aPosObj := MsObjSize( aInfo, aObjects )

dbSelectArea("SCV")
DEFINE MSDIALOG oDlg TITLE OemToAnsi(STR0041) From aSize[7],00 to aSize[6],aSize[5] Of oMainWnd PIXEL
oGetDad := MsGetDados():New(aPosObj[1,1],aPosObj[1,2],aPosObj[1,3],aPosObj[1,4],nOpcX,"M410FmLok()","M410FmTOk()",,.T.,,,,99)
ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{|| nOpcA := 1,If(oGetDad:TudoOk(),oDlg:End(),nOpcA := 0)},{||oDlg:End()}) CENTERED
If nOpcA == 1
	aHeadFor := aClone(aHeader)
	aColsFor   := aClone(aCols)
EndIf
RestArea(aArea)
Return(.T.)
/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁA410FmTOk Ё Autor ЁHenry Fila             Ё Data Ё23.08.2001Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁValidacao da TudoOk da Getdados das formas de pagamento     Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ                                                            Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁExpL1: Indica se todos os itens sao validos                 Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁDescri┤└o ЁEsta rotina tem como objetivo efetuar a validacao da TudoOk Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁObservacaoЁ                                                            Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё Materiais/Distribuicao/Logistica                           Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function M410FmTok()

Local lRet     := .T.
Local nX       := 0
Local nPercent := 0
Local nPosPer  := Ascan(aHeader,{|x| Alltrim(x[2]) == "CV_RATFOR"})
Local lValida  := .F.

For nX := 1 to Len(aCols)
	If !aCols[nX][Len(aHeader)+1]
		nPercent += aCols[nX][nPosPer]
		lValida  := .T.
	Endif
Next nX
If nPercent <> 100 .And. lValida
	Help(" ",1,"M410FRATEI")
	lRet := .F.
EndIf
Return(lRet)
/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁA410FmLOk Ё Autor ЁHenry Fila             Ё Data Ё23.08.2001Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁValidacao da Linha Ok da Getdados das formas de pagamento   Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁNenhum                                                      Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁExpL1: Indica se a linha e valida                           Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁDescri┤└o ЁEsta rotina tem como objetivo efetuar a validacao da linhaOkЁ╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁObservacaoЁ                                                            Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё Materiais/Distribuicao/Logistica                           Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function M410FmLok()

Local lRet     := .T.
Local nX       := 0

Local nPosFor  := Ascan(aHeader,{|x| Alltrim(x[2]) == "CV_FORMAPG"})

If !aCols[n][Len(aHeader)+1]

	For nX := 1 to Len(aCols)
		If !aCols[nX][Len(aHeader)+1] .And. n <> nX
			If aCols[nX][nPosFor] == aCols[n][nPosFor]
				Help(" ",1,"M410FORMA")
				lRet := .F.
			Endif
		Endif
	Next nX
EndIf
Return(lRet)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁMa410MtForЁ Autor ЁHenry Fila             Ё Data Ё17.08.2001Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁMontagem das formas de pagamento                            Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁExpA1: Array onde sera montado o aHeader do SCV             Ё╠╠
╠╠Ё          ЁExpA2: Array onde sera montado o aCols do SCV               Ё╠╠
╠╠Ё          ЁExpA3: Array com os recnos do SCV                           Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁNenhum                                                      Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁDescri┤└o ЁEsta rotina tem como objetivo efetuar a montagem do aheader Ё╠╠
╠╠Ё          Ёe acols da tabela SCV para uso na GetDados.                 Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁObservacaoЁ                                                            Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё Materiais/Distribuicao/Logistica                           Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function Ma410MtFor(aHeadFor,aColsFor,aRegSCV)

Local cArqQry    := "SCV"
Local lQuery     := .F.
Local nUsado     := 0
Local nX         := 0

#IFDEF TOP
	Local aStruSCV   := {}
	Local cQuery     := ""
#ENDIF 	

DEFAULT aRegSCV  := {}
DEFAULT aHeadFor := {}
DEFAULT aColsFor := {}
//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Montagem do aHeadFor                                 Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
dbSelectArea("SX3")
dbSetOrder(1)
dbSeek("SCV")
While !Eof() .And. SX3->X3_ARQUIVO == "SCV"
	If X3Uso(SX3->X3_USADO) .And. cNivel >= SX3->X3_NIVEL
		nUsado++
		Aadd(aHeadFor, { AllTrim(X3Titulo()),;
			SX3->X3_CAMPO,;
			SX3->X3_PICTURE,;
			SX3->X3_TAMANHO,;
			SX3->X3_DECIMAL,;
			SX3->X3_VALID,;
			SX3->X3_USADO,;
			SX3->X3_TIPO,;
			SX3->X3_ARQUIVO,;
			SX3->X3_CONTEXT } )
	EndIf
	dbSelectArea("SX3")
	dbSkip()
EndDo
If  !INCLUI
	SCV->(DbSetOrder(1))
	#IFDEF TOP
		If TcSrvType()<>"AS/400" .And. !InTransact()
			lQuery  := .T.
			cArqQry := "SCV"
			aStruSCV:= SCV->(dbStruct())

			cQuery := "SELECT SCV.*,SCV.R_E_C_N_O_ SCVRECNO "
			cQuery += "FROM "+RetSqlName("SCV")+" SCV "
			cQuery += "WHERE "
			cQuery += "SCV.CV_FILIAL='"+xFilial("SCV")+"' AND "
			cQuery += "SCV.CV_PEDIDO='"+M->C5_NUM+"' AND "
			cQuery += "SCV.D_E_L_E_T_=' ' "
			cQuery += "ORDER BY "+SqlOrder(SCV->(IndexKey()))

			cQuery := ChangeQuery(cQuery)

			dbSelectArea("SCV")
			dbCloseArea()
			dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cArqQry,.T.,.T.)

			For nX := 1 To Len(aStruSCV)
				If	aStruSCV[nX,2] <> "C"
					TcSetField(cArqQry,aStruSCV[nX,1],aStruSCV[nX,2],aStruSCV[nX,3],aStruSCV[nX,4])
				EndIf
			Next nX
		Else
	#ENDIF
		SCV->(DbSeek(xFilial("SCV")+M->C5_NUM))
		#IFDEF TOP
		EndIf
		#ENDIF
	While (cArqQry)->(!Eof() .And. (cArqQry)->CV_FILIAL==xFilial("SCV") .And.;
			(cArqQry)->CV_PEDIDO==M->C5_NUM)
		aadd(aColsFor,Array(nUsado+1))	
		For nX := 1 To nUsado
			If ( aHeadFor[nX][10] <> "V" )
				aColsFor[Len(aColsFor)][nX] := (cArqQry)->(FieldGet(FieldPos(aHeadFor[nX][2])))
			Else
				aColsFor[Len(aColsFor)][nX] := CriaVar(aHeadFor[nX,2])
			EndIf
		Next
		aColsFor[Len(aColsFor)][nUsado+1] := .F.

		aadd(aRegSCV,If(lQuery,(cArqQry)->SCVRECNO,(cArqQry)->(RecNo())))

		dbSelectArea(cArqQry)
		dbSkip()
	EndDo
	If lQuery
		dbSelectArea(cArqQry)
		dbCloseArea()
		ChkFile("SCV",.F.)
		dbSelectArea("SCV")
	EndIf
Endif

If Empty(aColsFor)
	aadd(aColsFor,Array(nUsado+1))

	For nX := 1 To nUsado
		aColsFor[1][nX] := CriaVar(aHeadFor[nX][2])
	Next nX
	aColsFor[Len(aColsFor)][nUsado+1] := .F.
Endif

Return(.T.)

/*
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤└o    ЁA410DelOk Ё Autor ЁRodrigo de A. Sartorio Ё Data Ё21/08/00  Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤└o Ё Consistencia geral dos itens de Pedidos de Venda antes da  Ё╠╠
╠╠Ё          Ё exclusao.                                                  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё Generico                                                   Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
*/
Function A410DelOk()
Local lRet    := .T.
Local z       := 0
Local lAtuSGJ := SuperGetMV("MV_PVCOMOP",.F.,.F.) .And. FindFunction("ALIASINDIC") .And. AliasIndic("SGJ")
Local lCanDel := SuperGetMv("MV_DELPVOP",.F.,.T.)

//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Trata se exclui ou nao itens que geraram OPs         Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Estrutura do Array aOPs :                            Ё
//Ё aOPs[z,1] := Item do Pedido de Vendas                Ё
//Ё aOPs[z,2] := Produto do Pedido de Vendas             Ё
//Ё aOPs[z,3] := No. da OP gerada para o PV              Ё
//Ё aOPs[z,4] := Item da OP gerada para o PV             Ё
//Ё aOPs[z,5] := No. da SC gerada para o PV              Ё
//Ё aOPs[z,6] := Item da SC gerada para o PV             Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
For z:=1 to Len(aOps)
	If lCanDel
	    If ValType(aOps[z,5])=="C" .And. !Empty(aOps[z,5]) .And. !Empty(aOps[z,6])
    		lRet:=(Aviso(OemToAnsi(STR0014),STR0027+aOps[z,1]+" - "+aOps[z,2]+ " " + OemToAnsi(STR0108)+ " : " + aOps[z,5]+"/"+aOps[z,6]+"."+STR0029,{STR0030,STR0031}) == 1) //"Aten┤└o"###"O item "###" gerou a Solicitacao de Compras "###"Confirma Exclusao ?"###"Sim"###"Nao"
		ElseIf lAtuSGJ
			Aviso(OemToAnsi(STR0014),STR0060,{STR0040})
			lRet := .F.
    	Else
			lRet:=(Aviso(OemToAnsi(STR0014),STR0027+aOps[z,1]+" - "+aOps[z,2]+STR0028+aOps[z,3]+"/"+aOps[z,4]+"."+STR0029,{STR0030,STR0031}) == 1) //"Aten┤└o"###"O item "###" gerou a Ordem de Producao "###"Confirma Exclusao ?"###"Sim"###"Nao"
		EndIf
		If !lRet
			Exit
		EndIf
	Else
		Aviso(OemToAnsi(STR0014),STR0060,{STR0040})
		lRet := .F.
		Exit
	Endif
Next z

Return lRet

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤└o    Ёa410IncRapЁ Autor Ё Eduardo Riera         Ё Data Ё          Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤└o Ё Programa exclusivo para inclusao no MATA410                Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe e Ё Void a410IncRap(ExpC1,ExpN1,,lOrcamento)                   Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpC1 = Alias do arquivo                                   Ё╠╠
╠╠Ё          Ё ExpN1 = Numero do registro                                 Ё╠╠
╠╠Ё          Ё ExpL1 = Define se a inclusao foi efetuado pelo MATA416     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё Generico                                                   Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Function A410IncRap(cAlias,nReg,nOpc)

Local nOpcA 	:= 0

Local nCntFor	:= 0
Local nStack    := GetSX8Len() 


Local nUsado	:= 0

Local lLiber	:= .F.
Local lTransf	:= .F.
Local lGravou	:= .F.
Local nPProduto:= 0
Local bCampo	:= { |nCPO| Field(nCPO) }
Local oDlg
Local oGetD
Local oSAY1
Local oSAY2
Local oSAY3
Local oSAY4
Local nNumDec := TamSX3("C6_VALOR")[2]

Local aPosObj   := {}
Local aObjects  := {}
Local aSize     := {}
Local aPosGet   := {}
Local nGetLin   := 0
Local lM410Bar  := ExistBlock("M410CODBAR")
Local aRecnoSE1RA := {} // Array com os titulos selecionados pelo Adiantamento
Local aHeadAGG    := {}
Local aColsAGG    := {}

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁCarrega perguntas do MATA440 e MATA410                                  Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
Pergunte("MTA440",.F.)
lLiber := MV_PAR02 == 1
lTransf:= MV_PAR01 == 1
Pergunte("MTA410",.F.)
//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Variavel utilizada p/definir Op. Triangulares.       Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
IsTriangular( (MV_PAR03==1) )
//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Monta a entrada de dados do arquivo                  Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
PRIVATE aTela[0][0]
PRIVATE aGets[0]
PRIVATE bArqF3
PRIVATE bCpoF3
PRIVATE aTrocaF3	:= {}
PRIVATE aValidGet	:= {}   
PRIVATE oGetPV		:= Nil

PRIVATE cCondPAdt   := "0" //Controle p/ cond. pgto. com aceite de Adt. 0=normal 1=Adt
If !FindFunction("MsMatGrade") .And. IsAtNewGrd()
	PRIVATE aColsGrade := {}
	PRIVATE aHeadGrade := {}
EndIf

If (! MaGrade() )
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Salva a integridade dos campos de Bancos de Dados    Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
	dbSelectArea("SC5")
	dbSetOrder(1)

	RegToMemory( "SC5", .T., .F. )

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Salva a integridade dos campos de Bancos de Dados    Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
	PRIVATE aHeader[0]
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁMontagem do aHeader                                   Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
	dbSelectArea("SX3")
	dbSetOrder(1)
	MsSeek("SC6")
	While ( !Eof() .And. (SX3->X3_ARQUIVO == "SC6") )
		IF ( X3USO(SX3->X3_USADO) .And. cNivel >= SX3->X3_NIVEL ) .And.;
				(	!AllTrim(SX3->X3_CAMPO)=="C6_NUM" 	.And.;
				!SX3->X3_CAMPO$"C6_QTDEMP #C6_QTDENT " .And.;
				!SX3->X3_CAMPO$"C6_RESERVA#C6_BLQ    " .And.;
				!SX3->X3_CAMPO$"C6_NFORI  #C6_SERIORI#C6_ITEMORI" .And.;
				!SX3->X3_CAMPO$"C6_LOTECTL#C6_NUMLOTE#C6_DTVALID#C6_NUMSERI#C6_LOCALIZ" )
			nUsado++
			Aadd(aHeader,{ AllTrim(X3Titulo()),;
				SX3->X3_CAMPO	,;
				SX3->X3_PICTURE,;
				SX3->X3_TAMANHO,;
				SX3->X3_DECIMAL,;
				SX3->X3_VALID	,;
				SX3->X3_USADO	,;
				SX3->X3_TIPO	,;
				SX3->X3_ARQUIVO,;
				SX3->X3_CONTEXT } )
		EndIf
		dbSelectArea("SX3")
		dbSkip()
	EndDo
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁMontagem do aCols                                     Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
	PRIVATE aCOLS[1][nUsado+1]
	For nCntFor	:= 1 To nUsado
		aCols[1][nCntFor] := CriaVar(aHeader[nCntFor][2])
		If ( AllTrim(aHeader[nCntFor][2]) == "C6_ITEM" )
			aCols[1][nCntFor] := "01"
		EndIf
	Next nCntFor
	aCOLS[1][Len(aHeader)+1] := .F.
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁRotina de inclusao automatica                         Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If ( Type("l410Auto") <> "U" .And. l410Auto )
		MsAuto2Acols()
	EndIf
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Estabelece a Troca de Clientes conforme o Tipo do Pedido de Venda      Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If ( M->C5_TIPO $ "DB" )
		aTrocaF3 := {{"C5_CLIENTE","SA2"}}
	Else
		aTrocaF3 := {}
	EndIf
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁVerifica se eh rotina de inclusao automatica          Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If ( Type("l410Auto") == "U" .or. ! l410Auto )
		aSize := MsAdvSize()
		aObjects := {}
		AAdd( aObjects, { 100, 100, .T., .T. } )
		AAdd( aObjects, { 100, 100, .T., .T. } )
		AAdd( aObjects, { 100, 015, .T., .F. } )
		aInfo := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 3, 3 }
		aPosObj := MsObjSize( aInfo, aObjects )
		aPosGet := MsObjGetPos(aSize[3]-aSize[1],315,{{003,033,160,200,240,263}})
		nGetLin := aPosObj[3,1]
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Armazenar dados do Pedido anterior.                  Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
		DEFINE MSDIALOG oDlg TITLE cCadastro From aSize[7],0 to aSize[6],aSize[5] of oMainWnd PIXEL

		oGetPV:=MSMGet():New( "SC5", nReg, nOpc, , , , , APOSOBJ[1],,3)
//		@ nGetLin,aPosGet[1,1] SAY OemToAnsi(IIF(M->C5_TIPO$"DB",STR0008,STR0009)) SIZE 020,09 PIXEL //"Fornec.:"###"Cliente: "
		@ nGetLin,aPosGet[1,2] SAY oSAY1 VAR Space(40)						SIZE 120,09 PICTURE "@!"	OF oDlg PIXEL
		@ nGetLin,aPosGet[1,3] SAY OemToAnsi(STR0011)						SIZE 020,09 OF oDlg PIXEL	//"Total :"
		@ nGetLin,aPosGet[1,4] SAY oSAY2 VAR 0 PICTURE TM(0,16,Iif(cPaisloc=="CHI",NIL,nNumDec))		SIZE 050,09 OF oDlg PIXEL
		@ nGetLin,aPosGet[1,5] SAY OemToAnsi(STR0012)						SIZE 030,09 OF oDlg PIXEL 	//"Desc. :"
		@ nGetLin,aPosGet[1,6] SAY oSAY3 VAR 0 PICTURE TM(0,16,Iif(cPaisloc=="CHI",NIL,nNumDec))		SIZE 050,09 OF oDlg PIXEL RIGHT
		@ nGetLin+10,aPosGet[1,5] SAY OemToAnsi("=")							SIZE 020,09 OF oDlg PIXEL
		@ nGetLin+10,aPosGet[1,6] SAY oSAY4 VAR 0								SIZE 050,09 PICTURE TM(0,16,Iif(cPaisloc=="CHI",NIL,nNumDec)) OF oDlg PIXEL	 RIGHT
		oDlg:Cargo	:= {|c1,n2,n3,n4| oSay1:SetText(c1),;
			oSay2:SetText(n2),;
			oSay3:SetText(n3),;
			oSay4:SetText(n4) }
		SetKey(VK_F4,{||A440Stok(NIL,"A410")})
		oGetd:=MsGetDados():New(aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],nOpc,"Ma410GrvIt","A410TudOk","+C6_ITEM/C6_Local/C6_TES/C6_CF/C6_PEDCLI",.T.,,1,,1200)
		If Type("lCodBarra")<>"U"
			oGetd:oBrowse:bGotFocus:={|| IIF(lCodBarra .And. !lM410Bar,a410EntraBarra(oGetD),IIF(lCodBarra .And. lM410Bar,Execblock("M410CODBAR",.F.,.F.,{nOpc,oGetD}),))}
		EndIf
		Private oGetDad:=oGetd
		Ma410Rodap(oGetd)
		ACTIVATE MSDIALOG oDlg ON INIT Ma410Bar(oDlg,{||nOpcA:=1,If(oGetd:TudoOk(),If(!obrigatorio(aGets,aTela),nOpcA:=0,oDlg:End()),nOpcA:=0)},{||oDlg:End()},nOpc,oGetD,nTotalPed,@aRecnoSE1RA,@aHeadAGG,@aColsAGG)
		SetKey(VK_F4,)
	Else
		//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Variaveis utilizadas pela rotina de inclusao automatica     Ё
		//Ё aValidGet                                                   Ё
		//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		aValidGet := {}
		nOpcA := 1
		If ! SC5->(MsVldGAuto(aValidGet)) // consiste os gets
			nOpcA := 0
		EndIf
		If (nOpcA == 0) .or. !(SC6->(MsVldAcAuto(aValidGet,"A410LinOk(o)","A410TudOk()")))   // consiste o campos do Acols
			nOpcA := 0
		EndIf
	EndIf
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁEfetua a Gravacao do Pedido de Venda                                    Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	nPProduto := aScan(aHeader,{|x| AllTrim(x[2])=="C6_PRODUTO"})
	For nCntfor := 1 To Len(aCols)
		If ( Empty(aCols[nCntFor][nPProduto]) )
			aCols[nCntFor][nUsado+1] := .T.
		EndIf
		If ( !aCols[nCntfor][nUsado+1] )
			lGravou := .T.
		EndIf
	Next nCntFor
	If ( nOpcA == 1 .And. lGravou )
		While GetSX8Len() > nStack 
			ConfirmSX8()
		EndDo
		EvalTrigger()
	Else
		For nCntFor := 1 To Len(aCols)
			If ( !aCols[nCntFor][nUsado+1] )
				aCols[nCntFor][nUsado+1] := .T.
				n := nCntFor
				Ma410GrvIt()
			EndIf
		Next nCntFor
		While GetSX8Len() > nStack 
			RollBackSx8()
		EndDo
	EndIf
	If ( lGravou )
		//зддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Grava arquivo SC5 (Cabec.do pedido de Venda)        Ё
		//юддддддддддддддддддддддддддддддддддддддддддддддддддддды
		dbSelectArea("SC5")
		dbSetOrder(1)
		If ( !MsSeek(xFilial("SC5")+M->C5_NUM) )
			RecLock("SC5",.T.)
		Else
			RecLock("SC5")
		EndIf
		For nCntFor := 1 TO FCount()
			If "FILIAL"$FieldName(nCntFor)
				FieldPut(nCntFor,xFilial("SC5"))
			ElseIf (("TABELA" $ FieldName(nCntFor)) .And. (M->&(EVAL(bCampo,nCntFor)) == PadR("1",Len(DA0->DA0_CODTAB))))
				FieldPut(nCntFor,"")
			Else
				FieldPut(nCntFor,M->&(EVAL(bCampo,nCntFor)))
			EndIf
		Next nCntFor
		MsUnLock()
	EndIf
	dbSelectArea("SC6")
	dbSetOrder(1)
	If ( !MsSeek(xFilial("SC6")+M->C5_NUM) )
		dbSelectArea("SC5")
		dbSetOrder(1)
		If ( MsSeek(xFilial("SC5")+M->C5_NUM) )
			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Grava os lancamentos nas contas orcamentarias SIGAPCO    Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			PcoDetLan("000100","04","MATA410")

			RecLock("SC5")
			dbDelete()
			MsUnLock()
		EndIf
	EndIf
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁRestaura a Integridade da Tela de Entrada                               Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	dbSelectArea(cAlias)
EndIf
Return( nOpcA )

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤└o    ЁA410AltRapЁ Autor Ё Claudinei M. Benzi    Ё Data Ё          Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤└o Ё Programa exclusivo ao MATA410 para alteracao               Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe e Ё Void A410AltRap(ExpC1,ExpN1)                               Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpC1 = Alias do arquivo                                   Ё╠╠
╠╠Ё          Ё ExpN1 = Numero do registro                                 Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё Mata410                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Function A410AltRap(cAlias,nReg,nOpc)

Local bCampo	:= { |nCPO| Field(nCPO) }
Local nOpcA		:= 0

Local lLiber 	:= .F.
Local lTransf	:= .F.
Local cCampo	:=""
Local lContinua:= .T.
Local nUsado   := 0
Local nCntFor  := 0
Local nTotalPed:= 0
Local nTotalDes:= 0
Local lGrade	:= MaGrade()
Local lBloqueio:= .T.
Local aLiberado:= {}
Local nPIdentB6:= 0
Local nPNfOrig := 0
Local nPSerOrig:= 0
Local nPItOrig := 0
Local nAux		:= 0
Local lNaoFatur:= .F.

Local lQuery   := .F.
Local cArqQry  := ""
Local oDlg
Local oGetd
Local oSAY1
Local oSAY2
Local oSAY3
Local oSAY4
Local nNumDec := TamSX3("C6_VALOR")[2]
Local aPosObj   := {}
Local aObjects  := {}
Local aSize     := {}
Local aPosGet   := {}
Local nGetLin   := 0
Local aRecnoSE1RA := {} // Array com os titulos selecionados pelo Adiantamento
Local aHeadAGG    := {}
Local aColsAGG    := {}

#IFDEF TOP
	Local cQuery     := ""
#ENDIF 	

//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Variaveis utilizadas na LinhaOk                      Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
PRIVATE aCols    	:= {}
PRIVATE aHeader  	:= {}
PRIVATE aAlterado := {}  
If FindFunction("MsMatGrade") .And. IsAtNewGrd()
	PRIVATE oGrade	  := MsMatGrade():New('oGrade',,"C6_QTDVEN", ,"a410GValid()",{ {VK_F4,{|| A440Saldo(.T.,oGrade:aColsAux[oGrade:nPosLinO][aScan(oGrade:aHeadAux,{|x| AllTrim(x[2])=="C6_LOCAL"})] )}} }) 
Else
	PRIVATE aColsGrade:= {}
	PRIVATE aHeadgrade:= {}
EndIf

PRIVATE oGetPV 	:= Nil

//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Monta a entrada de dados do arquivo                  Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
PRIVATE aTELA[0][0],aGETS[0]
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁCarrega perguntas do MATA440 e MATA410                                  Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
Pergunte("MTA440",.F.)
lLiber := MV_PAR02 == 1
lTransf:= MV_PAR01 == 1  
Pergunte("MTA410",.F.)
//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Variavel utilizada p/definir Op. Triangulares.       Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
IsTriangular( MV_PAR03==1 )
//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Salva a integridade dos campos de Bancos de Dados    Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
dbSelectArea(cAlias)
IF ( (ExistBlock("M410ALOK")) )
	If (! ExecBlock("M410ALOK",.F.,.F.) )
		lContinua := .F.
	EndIf
EndIf
//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁEXECUTAR CHAMADA DE FUNCAO p/ integracao com sistema de Distribuicao - NAO REMOVER Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If lContinua .And. SuperGetMv("MV_FATDIST") == "S" // Apenas quando utilizado pelo modulo de Distribuicao
	lContinua:=D410Alok()
EndIf

IF ( SC5->C5_FILIAL <> xFilial("SC5") )
	Help(" ",1,"A000FI")
	lContinua := .F.
EndIf
//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Salva a integridade dos campos de Bancos de Dados    Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
dbSelectArea(cAlias)
#IFDEF TOP
	TcSrvMap("SC5")
	dbGoto(RecNo())
#ENDIF
If !SoftLock(cAlias)
	lContinua := .F.
EndIf

//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Inicializa desta forma para criar uma nova instancia de variaveis private Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
RegToMemory( "SC5", .F., .F. )

dbSelectArea("SX3")
dbSetOrder(1)
MsSeek("SC6")
//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Montagem do aHeader                                  Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
While ( !EOF() .And. (SX3->X3_ARQUIVO == "SC6") )
	If (	X3USO(SX3->X3_USADO) .And.;
			!( Trim(SX3->X3_CAMPO) == "C6_NUM" );
			.And. Trim(x3_campo) <> "C6_QTDEMP";
			.And. Trim(x3_campo) <> "C6_QTDENT";
			.And. cNivel >= SX3->X3_NIVEL )
		nUsado++
		Aadd(aHeader,{ TRIM(X3Titulo()),;
			SX3->X3_CAMPO,;
			SX3->X3_PICTURE,;
			SX3->X3_TAMANHO,;
			SX3->X3_DECIMAL,;
			SX3->X3_VALID,;
			SX3->X3_USADO,;
			SX3->X3_TIPO,;
			SX3->X3_ARQUIVO,;
			SX3->X3_CONTEXT } )
		If ( AllTrim(SX3->X3_CAMPO)=="C6_IDENTB6" )
			nPIdentB6 := nUsado
		EndIf
		If ( AllTrim(SX3->X3_CAMPO)=="C6_NFORI" )
			nPNfOrig := nUsado
		EndIf
		If ( AllTrim(SX3->X3_CAMPO)=="C6_SERIORI" )
			nPSerOrig:= nUsado
		EndIf
		If ( AllTrim(SX3->X3_CAMPO)=="C6_ITEMORI" )
			nPItOrig := nUsado
		EndIf
	EndIf
	dbSelectArea("SX3")
	dbSkip()
EndDo
If ( lContinua )
	dbSelectArea("SC6")
	dbSetOrder(1)
	#IFDEF TOP
		If ( TcSrvType()<>"AS/400" )
			cArqQry := "MATA410"
			lQuery  := .T.
			cQuery := "SELECT SC6.*,R_E_C_N_O_ SC6RECNO "
			cQuery += "FROM "+RetSqlName("SC6")+" SC6 "
			cQuery += "WHERE SC6.C6_FILIAL='"+xFilial("SC6")+"' AND "
			cQuery += "SC6.C6_NUM='"+SC5->C5_NUM+"' AND "
			cQuery += "SC6.D_E_L_E_T_<>'*' "
			cQuery += "ORDER BY "+SqlOrder(SC6->(IndexKey()))

			cQuery := ChangeQuery(cQuery)

			dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cArqQry,.T.,.T.)
			For nCntFor := 1 To Len(aHeader)
				If ( aHeader[nCntFor,8]<>"C" .And. aHeader[nCntFor,10] <>  "V" )
					TcSetField(cArqQry,AllTrim(aHeader[nCntFor][2]),aHeader[nCntFor,8],aHeader[nCntFor,4],aHeader[nCntFor,5])
				EndIf
			Next nCntFor
		Else
	#ENDIF
		cArqQry := "SC6"
		MsSeek(xFilial("SC6")+SC5->C5_NUM)
		#IFDEF TOP
		EndIf
		#ENDIF
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Montagem do aCols                                    Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
	While ( !Eof() .And. (cArqQry)->C6_FILIAL	== xFilial("SC6") .And.;
			(cArqQry)->C6_NUM 	== SC5->C5_NUM		.And.;
			lContinua )
		
		dbSelectArea("SF4")
		dbSetOrder(1)
		dbSeek(xFilial("SF4")+(cArqQry)->C6_TES)
		If ( (cArqQry)->C6_QTDENT < (cArqQry)->C6_QTDVEN .AND. SF4->F4_QTDZERO <> "1" ) .OR. ;
		   ((cArqQry)->C6_QTDENT == (cArqQry)->C6_QTDVEN .AND. SF4->F4_QTDZERO == "1" .AND. Empty((cArqQry)->C6_NOTA))
	  		lNaoFatur := .T.
		EndIf
		If !(("R"$Alltrim((cArqQry)->C6_BLQ)).And.(SuperGetMv("MV_RSDOFAT")=="N")) .And. (cArqQry)->C6_QTDENT < (cArqQry)->C6_QTDVEN
			lBloqueio := .F.
		EndIf
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Verifica se este item foi digitada atraves de uma    Ё
		//Ё grade, se for junta todos os itens da grade em uma   Ё
		//Ё referencia , abrindo os itens so quando teclar enter Ё
		//Ё na quantidade                                        Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If ( (cArqQry)->C6_GRADE == "S" .And. lGrade )
			a410Grade(.T.,,cArqQry)
		Else
			AADD(aCols,Array(Len(aHeader)+1))
			For nCntFor := 1 To Len(aHeader)
				cCampo := Alltrim(aHeader[nCntFor,2])
				If ( aHeader[nCntFor,10] # "V" )
					aCols[Len(aCols)][nCntFor] := FieldGet(FieldPos(cCampo))
				Else
					If ( lQuery )
						SC6->(MsGoto((cArqQry)->SC6RECNO))
					EndIf
					aCols[Len(aCols)][nCntFor] := CriaVar(cCampo)
				EndIf
			Next nCntFor
			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Mesmo nao sendo um item digitado atraves de grade e' necessa-Ё
			//Ё rio criar o Array referente a este item para controle da     Ё
			//Ё grade                                                        Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды   
			If FindFunction("MsMatGrade") .And. IsAtNewGrd()
				oGrade:MatGrdMont(Len(aCols))       
			Else
				MatGrdMont(Len(aCols))
			EndIf
		EndIf
		If ( SC5->C5_TIPO <> "D" )
			nAux := aScan(aLiberado,{|x| x[2] == aCols[Len(aCols)][nPIdentB6]})
			If ( nAux == 0 )
				aadd(aLiberado,{ (cArqQry)->C6_ITEM , aCols[Len(aCols)][nPIdentB6] , (cArqQry)->C6_QTDEMP ,(cArqQry)->C6_QTDENT})
			Else
				aLiberado[nAux][3] += (cArqQry)->C6_QTDEMP
				aLiberado[nAux][4] += (cArqQry)->C6_QTDENT
			EndIf
		Else
			nAux := aScan(aLiberado,{|x| x[1] == (cArqQry)->C6_SERIORI .And.;
				x[2] == (cArqQry)->C6_NFORI   .And.;
				x[3] == (cArqQry)->C6_ITEMORI })
			If ( nAux == 0 )
				aadd(aLiberado,{ (cArqQry)->C6_SERIORI , (cArqQry)->C6_NFORI , (cArqQry)->C6_ITEMORI , (cArqQry)->C6_QTDEMP })
			Else
				aLiberado[nAux][4] += (cArqQry)->C6_QTDEMP
			EndIf
		EndIf
		nTotalPed += (cArqQry)->C6_VALOR
		If ( (cArqQry)->C6_PRUNIT = 0 )
			nTotalDes += (cArqQry)->C6_VALDESC
		Else
			nTotalDes += ((cArqQry)->C6_PRUNIT*(cArqQry)->C6_QTDVEN)-((cArqQry)->C6_PRCVEN*(cArqQry)->C6_QTDVEN)
		EndIf
		aCols[Len(aCols)][Len(aHeader)+1] := .F.
		dbSelectArea(cArqQry)
		dbSkip()
	EndDo
	If ( lQuery )
		dbSelectArea(cArqQry)
		dbCloseArea()
		dbSelectArea("SC6")
	EndIf
EndIf
//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Caso nao ache nenhum item , abandona rotina.         Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
If ( Len(aCols) == 0 )
	lContinua := .F.
	Help(" ",1,"A410S/ITEM")
EndIf
If ( (ExistBlock("M410GET")) )
	ExecBlock("M410GET",.F.,.F.)
EndIf
If ( lBloqueio )
	Help(" ",1,"A410ELIM")
	lContinua := .F.
EndIf
If ((!(SuperGetMv("MV_ALTPED")=="S") .And. !lNaoFatur ) .And. !(SC5->C5_TIPO $ "CIP")) .And. !(!Empty(SC5->C5_PEDEXP) .And. SuperGetMv("MV_EECFAT") .And. FindFunction("AVINTEMB") .And. AvIntEmb())
	Help(" ",1,"A410PEDFAT")
	lContinua := .F.
EndIf

If ( lContinua )

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Faz o calculo automatico de dimensoes de objetos     Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды

	aSize := MsAdvSize()

	aObjects := {}
	AAdd( aObjects, { 100, 100, .t., .t. } )
	AAdd( aObjects, { 100, 100, .t., .t. } )
	AAdd( aObjects, { 100, 015, .t., .f. } )	

	aInfo := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 3, 3 }
	aPosObj := MsObjSize( aInfo, aObjects )

	aPosGet := MsObjGetPos(aSize[3]-aSize[1],315,;
		{{003,033,160,200,240,263}} )

	nGetLin := aPosObj[3,1]

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Armazenar dados do Pedido anterior.                  Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
	DEFINE MSDIALOG oDlg TITLE cCadastro From aSize[7],0 to aSize[6],aSize[5] of oMainWnd PIXEL

	IF M->C5_TIPO $ "DB"
		aTrocaF3 := {{"C5_CLIENTE","SA2"}}
	Else
		aTrocaF3 := {}
	EndIf
	oGetPV:=MSMGet():New( "SC5", nReg, nOpc, , , , , aPosObj[1], , 3)
//	@ nGetLin,aPosGet[1,1] SAY OemToAnsi(IIF(M->C5_TIPO$"DB",STR0008,STR0009))		//"Fornec.:"###"Cliente: "
//	@ nGetLin,aPosGet[1,1] SAY OemToAnsi(IIF(M->C5_TIPO$"DB",STR0008,STR0009)) SIZE 020,09 PIXEL	//"Fornec.:"###"Cliente: "
	@ nGetLin,aPosGet[1,2] SAY oSAY1 VAR Space(40)						SIZE 120,09 PICTURE "@!"	OF oDlg PIXEL
	@ nGetLin,aPosGet[1,3] SAY OemToAnsi(STR0011)						SIZE 020,09 OF oDlg PIXEL	//"Total :"
	@ nGetLin,aPosGet[1,4] SAY oSAY2 VAR 0 PICTURE TM(0,16,Iif(cPaisloc=="CHI",NIL,nNumDec))		SIZE 050,09 OF oDlg PIXEL
	@ nGetLin,aPosGet[1,5] SAY OemToAnsi(STR0012)						SIZE 030,09 OF oDlg PIXEL 	//"Desc. :"
	if cPaisLoc == "BRA"	
		@ nGetLin,aPosGet[1,6] SAY oSAY3 VAR 0 PICTURE TM(0,16,2)		SIZE 050,09 OF oDlg PIXEL RIGHT
	Else
		@ nGetLin,aPosGet[1,6] SAY oSAY3 VAR 0 PICTURE TM(0,16,Iif(cPaisloc=="CHI",NIL,nNumDec))		SIZE 050,09 OF oDlg	PIXEL RIGHT
	EndIf	
	@ nGetLin+10,aPosGet[1,5] SAY OemToAnsi("=")							SIZE 020,09 OF oDlg PIXEL
	if cPaisLoc == "BRA"
		@ nGetLin+10,aPosGet[1,6] SAY oSAY4 VAR 0								SIZE 050,09 PICTURE TM(0,16,2) OF oDlg PIXEL RIGHT
	Else
		@ nGetLin+10,aPosGet[1,6] SAY oSAY4 VAR 0								SIZE 050,09 PICTURE TM(0,16,Iif(cPaisloc=="CHI",NIL,nNumDec)) OF oDlg PIXEL RIGHT	
	EndIf
	oDlg:Cargo	:= {|c1,n2,n3,n4| oSay1:SetText(c1),;
		oSay2:SetText(n2),;
		oSay3:SetText(n3),;
		oSay4:SetText(n4) }
	Set Key VK_F4 to A440Stok(NIL,"A410")
	oGetd:=MsGetDados():New(aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],nOpc,"A410LinOk","A410TudOk","+C6_ITEM/C6_Local/C6_TES/C6_CF/C6_PEDCLI",.T.,,1,,1200,"Ma410ItAlt()")
	Private oGetDad:=oGetd	
	Ma410Rodap(oGetD,nTotalPed,nTotalDes)
	ACTIVATE MSDIALOG oDlg ON INIT Ma410Bar(oDlg,{||nOpcA:=1,if(oGetd:TudoOk(),If(!obrigatorio(aGets,aTela),nOpcA := 0,oDlg:End()),nOpcA := 0)},{||oDlg:End()},nOpc,oGetD,nTotalPed,@aRecnoSE1RA,@aHeadAGG,@aColsAGG)
	SetKey(VK_F4,)
	If ( nOpcA == 1 )
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁEfetua as Alteracoes                                                    Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		For nCntFor := 1 To Len(aAlterado)

			N := aAlterado[nCntFor]
			Ma410GrvIt()

		Next nCntFor
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁEfetua as Delecoes                                                      Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		For nCntFor := 1 To Len(aCols)
			If ( aCols[nCntfor][nUsado+1] )
				N := nCntFor
				Ma410GrvIt()
			EndIf
		Next nCntFor
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁEfetua a Gravacao SC5                                                   Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		RecLock("SC5")
		For nCntFor := 1 TO FCount()
			If "FILIAL"$FieldName(nCntFor)
				FieldPut(nCntFor,xFilial("SC5"))
			ElseIf (("TABELA" $ FieldName(nCntFor)) .And. (M->&(EVAL(bCampo,nCntFor)) == PadR("1",Len(DA0->DA0_CODTAB))))
				FieldPut(nCntFor,"")
			Else
				FieldPut(nCntFor,M->&(EVAL(bCampo,nCntFor)))
			EndIf
		Next nCntFor
		SC5->(MsUnLock())
	Else
		If ( (ExistBlock("M410ABN")) )
			ExecBlock("M410ABN",.f.,.f.)
		EndIf
	EndIf
EndIf
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁDestrava Todos os Registros                                             Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
MsUnLockAll()
dbSelectArea(cAlias)

Return( nOpcA )

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤└o    ЁMa410GrvItЁ Autor Ё Eduardo Riera         Ё Data Ё          Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤└o Ё Programa exclusivo para inclusao no MATA410                Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe e Ё Void Ma410GrvIt(o)                                         Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpO1 = Objeto da getdados                                 Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё Generico                                                   Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Function Ma410GrvIt(o)

Local nPItem   := aScan(aHeader,{|x| AllTrim(x[2])=="C6_ITEM"})
Local nPProduto:= aScan(aHeader,{|x| AllTrim(x[2])=="C6_PRODUTO"})
Local aArea    := GetArea()
Local lRetorno := If ( o<>Nil , a410LinOk(o) , .T. )
Local lLiber   := MV_PAR02 == 1
Local lTransf  := MV_PAR01 == 1
Local nCntFor  := 0
Local bCampo   := {|n| FieldName(n)}
Local nMCusto  := 0
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁVerifica se o Produto foi prenchido                                     Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If ( Empty(aCols[n][nPProduto]) )
	aCols[n][Len(aHeader)+1] := .T.
EndIf

If ( lRetorno )
	If __lSx8
		ConfirmSX8()
	EndIf
	//зддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Grava arquivo SC5 (Cabec.do pedido de Venda)        Ё
	//юддддддддддддддддддддддддддддддддддддддддддддддддддддды
	dbSelectArea("SC5")
	dbSetOrder(1)
	If ( !MsSeek(xFilial("SC5")+M->C5_NUM) )
		RecLock("SC5",.T.)
		For nCntFor := 1 To FCount()
			If "FILIAL"$FieldName(nCntFor)
				FieldPut(nCntFor,xFilial("SC5"))
			ElseIf (("TABELA" $ FieldName(nCntFor)) .And. (M->&(EVAL(bCampo,nCntFor)) == PadR("1",Len(DA0->DA0_CODTAB))))
				FieldPut(nCntFor,"")
			Else
				FieldPut(nCntFor,M->&(EVAL(bCampo,nCntFor)))
			EndIf
		Next nCntFor
		MsUnLock()
	EndIf

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁPosiciona Clientes                                                      Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	dbSelectArea("SA1")
	dbSetOrder(1)
	MsSeek(xFilial("SA1")+SC5->C5_CLIENTE+SC5->C5_LOJACLI)

	nMCusto := If(SA1->A1_MOEDALC > 0, SA1->A1_MOEDALC, Val(SuperGetMv("MV_MCUSTO")))
	//зддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Estorna o Item do Pedido de Venda                   Ё
	//юддддддддддддддддддддддддддддддддддддддддддддддддддддды

	dbSelectArea("SC6")
	dbSetOrder(1)
	If ( MsSeek(xFilial("SC6")+SC5->C5_NUM+aCols[n][nPItem]) )

		dbSelectArea("SF4")
		dbSetOrder(1)
		MsSeek(xFilial("SF4")+SC6->C6_TES)

		RecLock("SC6")

		If ( SF4->F4_ESTOQUE == "S" )
			dbSelectArea("SB2")
			dbSetOrder(1)
			MsSeek( xFilial( "SB2" ) + SC6->C6_PRODUTO + SC6->C6_LOCAL )

			RecLock("SB2")
			If cPaisLoc == "BRA"
				SB2->B2_QPEDVEN -= (SC6->C6_QTDVEN-SC6->C6_QTDENT-SC6->C6_QTDEMP-SC6->C6_QTDRESE	)
				SB2->B2_QPEDVE2 -= ConvUM(SB2->B2_COD, SC6->C6_QTDVEN-SC6->C6_QTDENT-SC6->C6_QTDEMP-SC6->C6_QTDRESE, 0, 2)
			Else
				If SA1->A1_TIPO <> "E"
					SB2->B2_QPEDVEN -= (SC6->C6_QTDVEN-SC6->C6_QTDENT-SC6->C6_QTDEMP-SC6->C6_QTDRESE	)
				EndIf
			EndIf
			MsUnLock()

		EndIf

		If ( SF4->F4_DUPLIC == "S" )
			nQtdVen := SC6->C6_QTDVEN - SC6->C6_QTDEMP - SC6->C6_QTDENT
			If ( nQtdVen > 0 )
				RecLock("SA1")
				SA1->A1_SALPED -= xMoeda( nQtdVen * SC6->C6_PRCVEN , SC5->C5_MOEDA , nMCusto , SC5->C5_EMISSAO )
				MsUnLock()
			EndIf
		EndIf

		dbSelectArea("SC9")
		dbSetOrder(1)
		MsSeek(xFilial("SC9")+SC6->C6_NUM+SC6->C6_ITEM)

		While ( !Eof() .And. SC9->C9_FILIAL == xFilial("SC9") .And.;
				SC9->C9_PEDIDO == SC6->C6_NUM .And.;
				SC9->C9_ITEM   == SC6->C6_ITEM )

			If ( SC9->C9_BLCRED <> "10"  .And. SC9->C9_BLEST <> "10" .And. SC9->C9_BLCRED <> "ZZ"  .And. SC9->C9_BLEST <> "ZZ")
				Begin Transaction
					a460Estorna(.T.)
				End Transaction
			EndIf

			dbSelectArea("SC9")
			dbSkip()

		EndDo

		dbSelectArea("SC6")
		If ( aCols[n][Len(aHeader)+1] )
			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Grava os lancamentos nas contas orcamentarias SIGAPCO    Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			PcoDetLan("000100","02","MATA410")
			RecLock("SC6")
			dbDelete()
		EndIf

	Else

		If (!aCols[n][Len(aHeader)+1] )
			RecLock("SC6",.T.)
		EndIf

	EndIf

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁEfetua a gravacao do Item do Pedido de Venda                            Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If ( !SC6->(Deleted()) .And. (!aCols[n][Len(aHeader)+1] ) )
		RecLock("SC6")
		For nCntfor := 1 To Len(aHeader)
			FieldPut(FieldPos(aHeader[nCntFor][2]),aCols[n][nCntFor])
		Next nCntFor
		SC6->C6_FILIAL := xFilial("SC6")
		SC6->C6_NUM    := SC5->C5_NUM
		SC6->C6_CLI    := SC5->C5_CLIENTE
		SC6->C6_LOJA   := SC5->C5_LOJACLI

		dbSelectArea("SF4")
		dbSetOrder(1)
		MsSeek(xFilial("SF4")+SC6->C6_TES)

		CriaSB2( SC6->C6_PRODUTO, SC6->C6_LOCAL )

		dbSelectArea("SB2")
		dbSetOrder(1)
		MsSeek(xFilial("SB2")+SC6->C6_PRODUTO+SC6->C6_LOCAL)

		If ( SF4->F4_ESTOQUE == "S" )
			RecLock("SB2")
			SB2->B2_QPEDVEN += (SC6->C6_QTDVEN-SC6->C6_QTDENT-SC6->C6_QTDEMP-SC6->C6_QTDRESE	)
			SB2->B2_QPEDVE2 += ConvUM(SB2->B2_COD, SC6->C6_QTDVEN-SC6->C6_QTDENT-SC6->C6_QTDEMP-SC6->C6_QTDRESE, 0, 2)
			MsUnLock()
		EndIf
		If ( SF4->F4_DUPLIC == "S" )
			nQtdVen := SC6->C6_QTDVEN - SC6->C6_QTDEMP - SC6->C6_QTDENT
			If ( nQtdVen > 0 )
				RecLock("SA1")
				SA1->A1_SALPED += xMoeda( nQtdVen * SC6->C6_PRCVEN , SC5->C5_MOEDA , nMCusto , SC5->C5_EMISSAO )
				MsUnLock()
			EndIf
		EndIf
		If ( SC6->C6_QTDLIB > 0 )
			Begin Transaction
				MaLibDoFat(SC6->(RecNo()),SC6->C6_QTDLIB,,,.T.,.T.,lLiber,lTransf)
			End Transaction
		EndIf
	EndIf
	SC6->(MsUnLock())
EndIf

RestArea(aArea)

Return(lRetorno)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁMa410ItAltЁ Autor ЁEduardo Riera          Ё Data Ё20.08.1999Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁVerifica os Itens que foram alterados                       Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁNenhum                                                      Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ                                                            Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддбдддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё   DATA   Ё Programador   ЁManutencao Efetuada                         Ё╠╠
╠╠цддддддддддедддддддддддддддедддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё          Ё               Ё                                            Ё╠╠
╠╠юддддддддддадддддддддддддддадддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function Ma410ItAlt()

Local nScan := aScan(aAlterado,N)

If ( nScan == 0 )
	aadd(aAlterado,N)
EndIf

Return(.T.)

/*/
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбддддддддддд©╠╠
╠╠ЁFun┤└o    ЁMA410ImposЁ Autor Ё Eduardo Riera         Ё Data Ё06.12.2001 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддаддддддддддд╢╠╠
╠╠Ё          ЁMa410Impos( nOpc)                                            Ё╠╠
╠╠Ё          ЁFuncao de calculo dos impostos contidos no pedido de venda   Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ nOpc                                                        Ё╠╠
╠╠Ё          Ё                                                             Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁNenhum                                                       Ё╠╠
╠╠Ё          Ё                                                             Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁDescri┤└o ЁEsta funcao efetua os calculos de impostos (ICMS,IPI,ISS,etc)Ё╠╠
╠╠Ё          Ёcom base nas funcoes fiscais, a fim de possibilitar ao usua- Ё╠╠
╠╠Ё          Ёrio o valor de desembolso financeiro.                        Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё Generico                                                    Ё╠╠
╠╠юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/

Static Function Ma410Impos( nOpc )

Local aArea		:= GetArea()
Local aAreaSA1	:= SA1->(GetArea())
Local aFisGet	:= {}
Local aFisGetSC5:= {}
Local aTitles   := {STR0044,STR0045,STR0080} //"Nota Fiscal"###"Duplicatas"###"Rentabilidade"
Local aDupl     := {}
Local aVencto   := {}
Local aFlHead   := { STR0046,STR0047,STR0063 } //"Vencimento"###"Valor"
Local aEntr     := {}
Local aDuplTmp  := {}
Local aNfOri    := {}
Local aRFHead   := { RetTitle("C6_PRODUTO"),RetTitle("C6_VALOR"),STR0081,STR0082,STR0083,STR0084} //"C.M.V"###"Vlr.Presente"###"Lucro Bruto"###"Margem de ContribuiГЦo(%)"
Local aRentab   := {}
Local nPLocal   := aScan(aHeader,{|x| AllTrim(x[2])=="C6_LOCAL"})
Local nPTotal   := aScan(aHeader,{|x| AllTrim(x[2])=="C6_VALOR"})
Local nPValDesc := aScan(aHeader,{|x| AllTrim(x[2])=="C6_VALDESC"})
Local nPPrUnit  := aScan(aHeader,{|x| AllTrim(x[2])=="C6_PRUNIT"})
Local nPPrcVen  := aScan(aHeader,{|x| AllTrim(x[2])=="C6_PRCVEN"})
Local nPQtdVen  := aScan(aHeader,{|x| AllTrim(x[2])=="C6_QTDVEN"})
Local nPDtEntr  := aScan(aHeader,{|x| AllTrim(x[2])=="C6_ENTREG"})
Local nPProduto := aScan(aHeader,{|x| AllTrim(x[2])=="C6_PRODUTO"})
Local nPTES     := aScan(aHeader,{|x| AllTrim(x[2])=="C6_TES"})
Local nPCodRet  := Iif(cPaisLoc=="EQU",aScan(aHeader,{|x| AllTrim(x[2])=="C6_CONCEPT"}),"")
Local nPNfOri   := aScan(aHeader,{|x| AllTrim(x[2])=="C6_NFORI"})
Local nPSerOri  := aScan(aHeader,{|x| AllTrim(x[2])=="C6_SERIORI"})
Local nPItemOri := aScan(aHeader,{|x| AllTrim(x[2])=="C6_ITEMORI"})
Local nPIdentB6 := aScan(aHeader,{|x| AllTrim(x[2])=="C6_IDENTB6"})
Local nPItem    := aScan(aHeader,{|x| AllTrim(x[2])=="C6_ITEM"})
Local nPProvEnt := aScan(aHeader,{|x| AllTrim(x[2])=="C6_PROVENT"})
Local nPosCfo	:= aScan(aHeader,{|x| AllTrim(x[2])=="C6_CF"})
Local nPSuframa := 0      
Local nUsado    := Len(aHeader)
Local nX        := 0
Local nX1       := 0
Local nAcerto   := 0
Local nPrcLista := 0
Local nValMerc  := 0
Local nDesconto := 0
Local nAcresFin := 0	// Valor do acrescimo financeiro do total do item
Local nQtdPeso  := 0
Local nRecOri   := 0
Local nPosEntr  := 0
Local nItem     := 0
Local nY        := 0 
Local nPosCpo   := 0
Local nPropLot  := 0
Local lDtEmi    := SuperGetMv("MV_DPDTEMI",.F.,.T.)
Local dDataCnd  := M->C5_EMISSAO
Local oDlg
Local oDupl
Local oFolder
Local oRentab
Local lCondVenda := .F. // Template GEM
Local aRentabil := {}
Local cProduto  := ""
Local nTotDesc  := 0
Local lSaldo    := MV_PAR04 == 1 .And. !INCLUI
Local nQtdEnt   := 0
Local lM410Ipi	:= ExistBlock("M410IPI")
Local lM410Icm	:= ExistBlock("M410ICM")
Local lM410Soli	:= ExistBlock("M410SOLI")
Local lUsaVenc  := .F.
Local lIVAAju   := .F.
Local lRastro	 := ExistBlock("MAFISRASTRO")
Local lRastroLot := .F.
Local lPParc	:=.F.
Local aSolid	:= {}
Local nLancAp	:=	0
Local aHeadCDA		:=	{}
Local aColsCDA		:=	{}
Local aTransp	:= {"",""}
Local aSaldos	:= {}
Local aInfLote	:= {}
Local nAcresUnit:= 0	// Valor do acrescimo financeiro do valor unitario
Local nAcresTot := 0	// Somatoria dos Valores dos acrescimos financeiros dos itens
Local dIni		:= Ctod("//") 
Local cEstado	:= SuperGetMv("MV_ESTADO") 
Local cTesVend  :=  SuperGetMv("MV_TESVEND",,"")
Local cCliPed   := "" 
Local lCfo      := .F.
Local nlValor	:= 0
Local nValRetImp:= 0
Local cImpRet 	:= ""
Local cNatureza :="" 
PRIVATE oLancApICMS
PRIVATE _nTotOper_ := 0		//total de operacoes (vendas) realizadas com um cliente - calculo de IB - Argentina
Private _aValItem_ := {}

//зддддддддддддддддддддддддддддддддддддддддддддд©
//ЁBusca referencias no SC6                     Ё
//юддддддддддддддддддддддддддддддддддддддддддддды
aFisGet	:= {}
dbSelectArea("SX3")
dbSetOrder(1)
MsSeek("SC6")
While !Eof().And.X3_ARQUIVO=="SC6"
	cValid := UPPER(X3_VALID+X3_VLDUSER)
	If 'MAFISGET("'$cValid
		nPosIni 	:= AT('MAFISGET("',cValid)+10
		nLen		:= AT('")',Substr(cValid,nPosIni,Len(cValid)-nPosIni))-1
		cReferencia := Substr(cValid,nPosIni,nLen)
		aAdd(aFisGet,{cReferencia,X3_CAMPO,MaFisOrdem(cReferencia)})
	EndIf
	If 'MAFISREF("'$cValid
		nPosIni		:= AT('MAFISREF("',cValid) + 10
		cReferencia	:=Substr(cValid,nPosIni,AT('","MT410",',cValid)-nPosIni)
		aAdd(aFisGet,{cReferencia,X3_CAMPO,MaFisOrdem(cReferencia)})
	EndIf
	dbSkip()
EndDo
aSort(aFisGet,,,{|x,y| x[3]<y[3]})

//зддддддддддддддддддддддддддддддддддддддддддддд©
//ЁBusca referencias no SC5                     Ё
//юддддддддддддддддддддддддддддддддддддддддддддды
aFisGetSC5	:= {}
dbSelectArea("SX3")
dbSetOrder(1)
MsSeek("SC5")
While !Eof().And.X3_ARQUIVO=="SC5"
	cValid := UPPER(X3_VALID+X3_VLDUSER)
	If 'MAFISGET("'$cValid
		nPosIni 	:= AT('MAFISGET("',cValid)+10
		nLen		:= AT('")',Substr(cValid,nPosIni,Len(cValid)-nPosIni))-1
		cReferencia := Substr(cValid,nPosIni,nLen)
		aAdd(aFisGetSC5,{cReferencia,X3_CAMPO,MaFisOrdem(cReferencia)})
	EndIf
	If 'MAFISREF("'$cValid
		nPosIni		:= AT('MAFISREF("',cValid) + 10
		cReferencia	:=Substr(cValid,nPosIni,AT('","MT410",',cValid)-nPosIni)
		aAdd(aFisGetSC5,{cReferencia,X3_CAMPO,MaFisOrdem(cReferencia)})
	EndIf
	dbSkip()
EndDo
aSort(aFisGetSC5,,,{|x,y| x[3]<y[3]})

SA4->(dbSetOrder(1))
If SA4->(dbSeek(xFilial("SA4")+M->C5_TRANSP)) 
	aTransp[01] := SA4->A4_EST
	aTransp[02] := Iif(SA4->(FieldPos("A4_TPTRANS")) > 0,SA4->A4_TPTRANS,"")
Endif
//зддддддддддддддддддддддддддддддддддддддддддддд©
//ЁInicializa a funcao fiscal                   Ё
//юддддддддддддддддддддддддддддддддддддддддддддды  

//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁA Consultoria TributАria, por meio da Resposta Ю Consulta n╨ 268/2004, determinou a aplicaГЦo das seguintes alМquotas nas Notas Fiscais de venda emitidas pelo vendedor remetente:                                                                         Ё
//Ё1) no caso previsto na letra "a" (venda para SP e entrega no PR) - aplicaГЦo da alМquota interna do Estado de SЦo Paulo, visto que a operaГЦo entre o vendedor remetente e o adquirente originАrio И interna;                                              Ё
//Ё2) no caso previsto na letra "b" (venda para o DF e entrega no PR) - aplicaГЦo da alМquota interestadual prevista para as operaГУes com o ParanА, ou seja, 12%, visto que a circulaГЦo da mercadoria se dА entre os Estado de SЦo Paulo e do ParanА.       Ё
//Ё3) no caso previsto na letra "c" (venda para o RS e entrega no SP) - aplicaГЦo da alМquota interna do Estado de SЦo Paulo, uma vez que se considera interna a operaГЦo, quando nЦo se comprovar a saМda da mercadoria do territСrio do Estado de SЦo Paulo,Ё
//Ё conforme previsto no art. 36, ╖ 4╨ do RICMS/SP                                                                                                                                                                                                            Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If len(aCols) > 0
	If cEstado == 'SP'
		If !Empty(M->C5_CLIENT) .And. M->C5_CLIENT <> M->C5_CLIENTE
			For nX := 1 To Len(aCols)
		   		If Alltrim(aCols[nX][nPTES])$ Alltrim(cTesVend)
		 			lCfo:= .T.
		 		EndIf
		   	Next		   	
		   	If lCfo		
				dbSelectArea(IIF(M->C5_TIPO$"DB","SA2","SA1"))
				dbSetOrder(1)           
				MsSeek(xFilial()+M->C5_CLIENTE+M->C5_LOJAENT)
				If Iif(M->C5_TIPO$"DB", SA2->A2_EST,SA1->A1_EST) == 'SP'
					cCliPed := M->C5_CLIENTE
				Else
					cCliPed := M->C5_CLIENT
				EndIf
			EndIf
		EndIf
	EndIf
EndIf

MaFisSave()
MaFisEnd()
MaFisIni(IIf(!Empty(cCliPed),cCliPed,Iif(Empty(M->C5_CLIENT),M->C5_CLIENTE,M->C5_CLIENT)),;// 1-Codigo Cliente/Fornecedor
	M->C5_LOJAENT,;		// 2-Loja do Cliente/Fornecedor
	IIf(M->C5_TIPO$'DB',"F","C"),;				// 3-C:Cliente , F:Fornecedor
	M->C5_TIPO,;				// 4-Tipo da NF
	M->C5_TIPOCLI,;		// 5-Tipo do Cliente/Fornecedor
	Nil,;
	Nil,;
	Nil,;
	Nil,;
	"MATA461",;
	Nil,;
	Nil,;
	Nil,;
	Nil,;
	Nil,;
	Nil,;
	Nil,;
	aTransp)

//зддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁRealiza alteracoes de referencias do SC5         Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддды
If Len(aFisGetSC5) > 0
	dbSelectArea("SC5")
	For nY := 1 to Len(aFisGetSC5)
		If !Empty(&("M->"+Alltrim(aFisGetSC5[ny][2])))
			MaFisAlt(aFisGetSC5[ny][1],&("M->"+Alltrim(aFisGetSC5[ny][2])),,.F.)
		EndIf
	Next nY
Endif

//Na argentina o calculo de impostos depende da serie.
If cPaisLoc == 'ARG'
	SA1->(DbSetOrder(1))
	SA1->(MsSeek(xFilial()+IIf(!Empty(M->C5_CLIENT),M->C5_CLIENT,M->C5_CLIENTE)+M->C5_LOJAENT))
	MaFisAlt('NF_SERIENF',LocXTipSer('SA1',MVNOTAFIS))
	/*
	зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Tratamento de IB para monotributistas - Argentina           Ё
	Ё AGIP 177/2009                                               Ё
	юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	If SA1->A1_TIPO == "M"
		dIni := (dDatabase + 1) - 365
		_nTotOper_ := RetTotOper(SA1->A1_COD,SA1->A1_LOJA,"C",dIni,dDatabase,1)
	Endif 
ElseIf cPaisLoc=="EQU"   
	SA1->(DbSetOrder(1))
	SA1->(MsSeek(xFilial()+IIf(!Empty(M->C5_CLIENT),M->C5_CLIENT,M->C5_CLIENTE)+M->C5_LOJAENT))
	cNatureza:=SA1->A1_NATUREZ
	
	lPParc:=Posicione("SED",1,xFilial("SED")+cNatureza,"ED_RATRET")=="1"	
Endif
//зддддддддддддддддддддддддддддддддддддддддддддд©
//ЁAgrega os itens para a funcao fiscal         Ё
//юддддддддддддддддддддддддддддддддддддддддддддды
If nPTotal > 0 .And. nPValDesc > 0 .And. nPPrUnit > 0 .And. nPProduto > 0 .And. nPQtdVen > 0 .And. nPTes > 0
	For nX := 1 To Len(aCols)
		nQtdPeso := 0

			nItem++
			/*
			зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			Ё Tratamento de IB para monotributistas - Argentina           Ё
			Ё AGIP 177/2009                                               Ё
			юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
			If cPaisLoc == "ARG"
				If SA1->A1_TIPO == "M"
					Aadd(_aValItem_,{nItem,.F.,xmoeda(aCols[nX][nPPrcVen],SC5->C5_MOEDA ,1,)})
				Endif
			Endif
			
			//зддддддддддддддддддддддддддддддддддддддддддддд©
			//ЁPosiciona Registros                          Ё
			//юддддддддддддддддддддддддддддддддддддддддддддды
			If lSaldo .And. nPItem > 0
				dbSelectArea("SC6")
				dbSetOrder(1)
				MsSeek(xFilial("SC6")+M->C5_NUM+aCols[nX][nPItem]+aCols[nX][nPProduto])
				nQtdEnt := IIf(!SubStr(SC6->C6_BLQ,1,1)$"RS" .And. Empty(SC6->C6_BLOQUEI),SC6->C6_QTDENT,SC6->C6_QTDVEN)
			Else
				lSaldo := .F.
			EndIf
			
			cProduto := aCols[nX][nPProduto]
			MatGrdPrRf(@cProduto)
			SB1->(dbSetOrder(1))
			If SB1->(MsSeek(xFilial("SB1")+cProduto))
				nQtdPeso := If(lSaldo,aCols[nX][nPQtdVen]-nQtdEnt,aCols[nX][nPQtdVen])*SB1->B1_PESO
			EndIf
        	If nPIdentB6 <> 0 .And. !Empty(aCols[nX][nPIdentB6])
				SD1->(dbSetOrder(4))
				If SD1->(MSSeek(xFilial("SD1")+aCols[nX][nPIdentB6]))
					nRecOri := SD1->(Recno())
				EndIf
        	ElseIf nPNfOri > 0 .And. nPSerOri > 0 .And. nPItemOri > 0
				If !Empty(aCols[nX][nPNfOri]) .And. !Empty(aCols[nX][nPItemOri])
					SD1->(dbSetOrder(1))
					If SD1->(MSSeek(xFilial("SD1")+aCols[nX][nPNfOri]+aCols[nX][nPSerOri]+M->C5_CLIENTE+M->C5_LOJACLI+aCols[nX][nPProduto]+aCols[nX][nPItemOri]))
						nRecOri := SD1->(Recno())
					EndIf
				EndIf
			EndIf
            SB2->(dbSetOrder(1))
            SB2->(MsSeek(xFilial("SB2")+SB1->B1_COD+aCols[nX][nPLocal]))
            SF4->(dbSetOrder(1))
            SF4->(MsSeek(xFilial("SF4")+aCols[nX][nPTES]))
			//зддддддддддддддддддддддддддддддддддддддддддддд©
			//ЁCalcula o preco de lista                     Ё
			//юддддддддддддддддддддддддддддддддддддддддддддды
			nValMerc  := If(aCols[nX][nPQtdVen]==0,aCols[nX][nPTotal],If(lSaldo,(aCols[nX][nPQtdVen]-nQtdEnt)*aCols[nX][nPPrcVen],aCols[nX][nPTotal]))
			nPrcLista := aCols[nX][nPPrUnit]
			If ( nPrcLista == 0 )
				nValMerc  := If(aCols[nX][nPQtdVen]==0,aCols[nX][nPTotal],If(lSaldo,(aCols[nX][nPQtdVen]-nQtdEnt)*aCols[nX][nPPrcVen],aCols[nX][nPTotal]))
			EndIf
			nAcresUnit:= A410Arred(aCols[nX][nPPrcVen]*M->C5_ACRSFIN/100,"D2_PRCVEN")
			nAcresFin := A410Arred(If(lSaldo,aCols[nX][nPQtdVen]-nQtdEnt,aCols[nX][nPQtdVen])*nAcresUnit,"D2_TOTAL")
			nAcresTot += nAcresFin
			nValMerc  += nAcresFin
			nDesconto := a410Arred(nPrcLista*If(lSaldo,aCols[nX][nPQtdVen]-nQtdEnt,aCols[nX][nPQtdVen]),"D2_DESCON")-nValMerc
			nDesconto := IIf(nDesconto<=0,aCols[nX][nPValDesc],nDesconto)
			nDesconto := Max(0,nDesconto)
			nPrcLista += nAcresUnit
			//Para os outros paises, este tratamento e feito no programas que calculam os impostos.
			If cPaisLoc=="BRA" .or. GetNewPar('MV_DESCSAI','1') == "2"
				nValMerc  += nDesconto
			Endif
			//зддддддддддддддддддддддддддддддддддддддддддддд©
			//ЁVerifica a data de entrega para as duplicatasЁ
			//юддддддддддддддддддддддддддддддддддддддддддддды
			If ( nPDtEntr > 0 )
				If ( dDataCnd > aCols[nX][nPDtEntr] .And. !Empty(aCols[nX][nPDtEntr]) )
					dDataCnd := aCols[nX][nPDtEntr]
				EndIf
			Else
				dDataCnd  := M->C5_EMISSAO
			EndIf
			//зддддддддддддддддддддддддддддддддддддддддддддд©
			//ЁTratamento do IVA Ajustado                   Ё
			//юддддддддддддддддддддддддддддддддддддддддддддды			
			SB1->(dbSetOrder(1))
			If SB1->(MsSeek(xFilial("SB1")+cProduto))
               lIVAAju := IIF(SB1->(FieldPos("B1_IVAAJU")) > 0 .And. SB1->B1_IVAAJU == '1' .And. (IIF(lRastro,lRastroLot := ExecBlock("MAFISRASTRO",.F.,.F.),Rastro(cProduto,"S"))),.T.,.F.)			   
			EndIf
			dbSelectArea("SC6")
			dbSetOrder(1)
			MsSeek(xFilial("SC6")+M->C5_NUM)
			If lIVAAju
				dbSelectArea("SC9")
				dbSetOrder(1)
				If MsSeek(xFilial("SC9")+SC6->C6_NUM+SC6->C6_ITEM)
					If ( SC9->C9_BLCRED $ "  10"  .And. SC9->C9_BLEST $ "  10")
						While ( !Eof() .And. SC9->C9_FILIAL == xFilial("SC9") .And.;
								SC9->C9_PEDIDO == SC6->C6_NUM .And.;
								SC9->C9_ITEM   == SC6->C6_ITEM )
				
							aadd(aSaldos,{SC9->C9_LOTECTL,SC9->C9_NUMLOTE,,,SC9->C9_QTDLIB})	
		
							dbSelectArea("SC9")
							dbSkip()
						EndDo
					Else
						dbSelectArea("SC6")
						dbSetOrder(1)
						MsSeek(xFilial("SC6")+M->C5_NUM)
						lUsaVenc:= If(!Empty(SC6->C6_LOTECTL+SC6->C6_NUMLOTE),.T.,(SuperGetMv('MV_LOTVENC')=='S'))
						aSaldos := SldPorLote(aCols[nX][nPProduto],aCols[nX][nPLocal],aCols[nX][nPQtdVen]/* nQtdLib*/,0/*nQtdLib2*/,SC6->C6_LOTECTL,SC6->C6_NUMLOTE,SC6->C6_LOCALIZ,SC6->C6_NUMSERI,NIL,NIL,NIL,lUsaVenc,nil,nil,dDataBase)					
					EndIf
				Else
					dbSelectArea("SC6")
					dbSetOrder(1)
					MsSeek(xFilial("SC6")+M->C5_NUM)
					lUsaVenc:= If(!Empty(SC6->C6_LOTECTL+SC6->C6_NUMLOTE),.T.,(SuperGetMv('MV_LOTVENC')=='S'))
					aSaldos := SldPorLote(aCols[nX][nPProduto],aCols[nX][nPLocal],aCols[nX][nPQtdVen]/* nQtdLib*/,0/*nQtdLib2*/,SC6->C6_LOTECTL,SC6->C6_NUMLOTE,SC6->C6_LOCALIZ,SC6->C6_NUMSERI,NIL,NIL,NIL,lUsaVenc,nil,nil,dDataBase)									
				EndIf
				For nX1 := 1 to Len(aSaldos)
					nPropLot := aSaldos[nX1][5]
					If lRastroLot
						dbSelectArea("SB8")
						dbSetOrder(5)
						If MsSeek(xFilial("SB8")+cProduto+aSaldos[nX][01])
							aadd(aInfLote,{SB8->B8_DOC,SB8->B8_SERIE,SB8->B8_CLIFOR,SB8->B8_LOJA,nPropLot})
						EndIf		
					Else				
						dbSelectArea("SB8")
						dbSetOrder(2)
						If MsSeek(xFilial("SB8")+aSaldos[nX][02]+aSaldos[nX][01])
							aadd(aInfLote,{SB8->B8_DOC,SB8->B8_SERIE,SB8->B8_CLIFOR,SB8->B8_LOJA,nPropLot})
						EndIf
					EndIf
					dbSelectArea("SF3")
					dbSetOrder(4)
					If !Empty(aInfLote)
						If MsSeek(xFilial("SF3")+aInfLote[nX1][03]+aInfLote[nX1][04]+aInfLote[nX1][01]+aInfLote[nX1][02])
							aadd(aNfOri,{SF3->F3_ESTADO,SF3->F3_ALIQICM,aInfLote[nX1][05],0})
						EndIf
					EndIf
				Next nX1
			EndIf						
			//зддддддддддддддддддддддддддддддддддддддддддддд©
			//ЁAgrega os itens para a funcao fiscal         Ё
			//юддддддддддддддддддддддддддддддддддддддддддддды
			MaFisAdd(cProduto,;   	// 1-Codigo do Produto ( Obrigatorio )
				aCols[nX][nPTES],;	   	// 2-Codigo do TES ( Opcional )
				If(lSaldo,aCols[nX][nPQtdVen]-nQtdEnt,aCols[nX][nPQtdVen]),;  	// 3-Quantidade ( Obrigatorio )
				nPrcLista,;		  	// 4-Preco Unitario ( Obrigatorio )
				nDesconto,; 	// 5-Valor do Desconto ( Opcional )
				"",;	   			// 6-Numero da NF Original ( Devolucao/Benef )
				"",;				// 7-Serie da NF Original ( Devolucao/Benef )
				nRecOri,;					// 8-RecNo da NF Original no arq SD1/SD2
				0,;					// 9-Valor do Frete do Item ( Opcional )
				0,;					// 10-Valor da Despesa do item ( Opcional )
				0,;					// 11-Valor do Seguro do item ( Opcional )
				0,;					// 12-Valor do Frete Autonomo ( Opcional )
				nValMerc,;			// 13-Valor da Mercadoria ( Obrigatorio )
				0,;					// 14-Valor da Embalagem ( Opiconal )	
				,;					// 15
				,;					// 16
				Iif(nPItem>0,aCols[nX,nPItem],""),; //17
				0,;					// 18-Despesas nao tributadas - Portugal
				0,;					// 19-Tara - Portugal
				aCols[nX,nPosCfo],; // 20-CFO
				aNfOri,;            // 21-Array para o calculo do IVA Ajustado (opcional)	
				Iif(cPaisLoc=="EQU",aCols[nX,nPCodRet],""))// 22-Codigo Retencao - Equador
			//зддддддддддддддддддддддддддддддддддддддддддддд©
			//ЁTratamento do IVA Ajustado                   Ё
			//юддддддддддддддддддддддддддддддддддддддддддддды
			If lIVAAju
				For nX1 := 1 To Len(aNfOri)
					MaFisAddIT("IT_ANFORI2",{aNfOri[nX1][__UFORI],aNfOri[nX1][__ALQORI],aNfOri[nX1][__PROPOR],0},nItem,nX1==1)
				Next nX1			
				aSaldos :={}
				aNfOri  :={}
			EndIf				
			//зддддддддддддддддддддддддддддддддддддддддддддд©
			//ЁProvincia de entrega - Ingresos Brutos       Ё
			//юддддддддддддддддддддддддддддддддддддддддддддды
			If cPaisLoc == "ARG"
				If nPProvEnt > 0
					MaFisAlt("IT_PROVENT",aCols[nX,nPProVent],nItem)
				Endif
			Endif
			//зддддддддддддддддддддддддддддддддддддддддддддд©
			//ЁCalculo do ISS                               Ё
			//юддддддддддддддддддддддддддддддддддддддддддддды
			SF4->(dbSetOrder(1))
			SF4->(MsSeek(xFilial("SF4")+aCols[nX][nPTES]))
			If ( M->C5_INCISS == "N" .And. M->C5_TIPO == "N")
				If ( SF4->F4_ISS=="S" )
					nPrcLista := a410Arred(nPrcLista/(1-(MaAliqISS(nItem)/100)),"D2_PRCVEN")
					nValMerc  := a410Arred(nValMerc/(1-(MaAliqISS(nItem)/100)),"D2_PRCVEN")
					MaFisAlt("IT_PRCUNI",nPrcLista,nItem)
					MaFisAlt("IT_VALMERC",nValMerc,nItem)
				EndIf
			EndIf
			//зддддддддддддддддддддддддддддддддддддддддддддд©
			//ЁAltera peso para calcular frete              Ё
			//юддддддддддддддддддддддддддддддддддддддддддддды
			MaFisAlt("IT_PESO",nQtdPeso,nItem)
			MaFisAlt("IT_PRCUNI",nPrcLista,nItem)
			MaFisAlt("IT_VALMERC",nValMerc,nItem)
			//зддддддддддддддддддддддддддддддддддддддддддддд©
			//ЁAnalise da Rentabilidade                     Ё
			//юддддддддддддддддддддддддддддддддддддддддддддды
			If SF4->F4_DUPLIC=="S"
				nTotDesc += MaFisRet(nItem,"IT_DESCONTO")
				nY := aScan(aRentab,{|x| x[1] == aCols[nX][nPProduto]})
				If nY == 0
					aadd(aRenTab,{aCols[nX][nPProduto],0,0,0,0,0})
					nY := Len(aRenTab)
				EndIf
				If cPaisLoc=="BRA"
					aRentab[nY][2] += (nValMerc - nDesconto)
				Else
					aRentab[nY][2] += nValMerc
				Endif
				aRentab[nY][3] += If(lSaldo,aCols[nX][nPQtdVen]-nQtdEnt,aCols[nX][nPQtdVen])*SB2->B2_CM1
			Else
				If GetNewPar("MV_TPDPIND","1")=="1"
					nTotDesc += MaFisRet(nItem,"IT_DESCONTO")
				EndIf
			EndIf

	  		If aCols[nX][nUsado+1]
				MaFisDel(nItem,aCols[nX][nUsado+1])	
	        EndIf

	Next nX
EndIf
//зддддддддддддддддддддддддддддддддддддддддддддд©
//ЁIndica os valores do cabecalho               Ё
//юддддддддддддддддддддддддддддддддддддддддддддды]
If ( ( cPaisLoc == "PER" .Or. cPaisLoc == "COL" ) .And. M->C5_TPFRETE == "F" ) .Or. ( cPaisLoc != "PER" .And. cPaisLoc != "COL" )
	MaFisAlt("NF_FRETE",M->C5_FRETE)
EndIf
MaFisAlt("NF_VLR_FRT",M->C5_VLR_FRT)
MaFisAlt("NF_SEGURO",M->C5_SEGURO)
MaFisAlt("NF_AUTONOMO",M->C5_FRETAUT)
MaFisAlt("NF_DESPESA",M->C5_DESPESA)                 
If cPaisLoc == "PTG"
	MaFisAlt("NF_DESNTRB",M->C5_DESNTRB)
	MaFisAlt("NF_TARA",M->C5_TARA)	
Endif
//зддддддддддддддддддддддддддддддддддддддддддддд©
//ЁIndenizacao por valor                        Ё
//юддддддддддддддддддддддддддддддддддддддддддддды
If M->C5_DESCONT > 0
	MaFisAlt("NF_DESCONTO",Min(MaFisRet(,"NF_VALMERC")-0.01,nTotDesc+M->C5_DESCONT),/*nItem*/,/*lNoCabec*/,/*nItemNao*/,GetNewPar("MV_TPDPIND","1")=="2" )
EndIf

If M->C5_PDESCAB > 0
	MaFisAlt("NF_DESCONTO",A410Arred(MaFisRet(,"NF_VALMERC")*M->C5_PDESCAB/100,"C6_VALOR")+MaFisRet(,"NF_DESCONTO"))
EndIf

If lM410Ipi .Or. lM410Icm .Or. lM410Soli
	nItem := 0
	For nX := 1 To Len(aCols)
		nItem++
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁPonto de Entrada M410IPI para alterar os valores do IPI referente a palnilha financeira           Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If lM410Ipi 
			VALORIPI    := MaFisRet(nItem,"IT_VALIPI")
			BASEIPI     := MaFisRet(nItem,"IT_BASEIPI")
			QUANTIDADE  := MaFisRet(nItem,"IT_QUANT")
			ALIQIPI     := MaFisRet(nItem,"IT_ALIQIPI")
			BASEIPIFRETE:= MaFisRet(nItem,"IT_FRETE")
			MaFisAlt("IT_VALIPI",ExecBlock("M410IPI",.F.,.F.,{ nItem }),nItem,.T.)
			MaFisLoad("IT_BASEIPI",BASEIPI ,nItem)
			MaFisLoad("IT_ALIQIPI",ALIQIPI ,nItem)
			MaFisLoad("IT_FRETE"  ,BASEIPIFRETE,nItem,"11")
			MaFisEndLoad(nItem,1)
		EndIf
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁPonto de Entrada M410ICM para alterar os valores do ICM referente a palnilha financeira           Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If lM410Icm
			_BASEICM    := MaFisRet(nItem,"IT_BASEICM")
			_ALIQICM    := MaFisRet(nItem,"IT_ALIQICM")
			_QUANTIDADE := MaFisRet(nItem,"IT_QUANT")
			_VALICM     := MaFisRet(nItem,"IT_VALICM")
			_FRETE      := MaFisRet(nItem,"IT_FRETE")
			_VALICMFRETE:= MaFisRet(nItem,"IT_ICMFRETE")
			_DESCONTO   := MaFisRet(nItem,"IT_DESCONTO")
			ExecBlock("M410ICM",.F.,.F., { nItem } )
			MaFisLoad("IT_BASEICM" ,_BASEICM    ,nItem)
			MaFisLoad("IT_ALIQICM" ,_ALIQICM    ,nItem)
			MaFisLoad("IT_VALICM"  ,_VALICM     ,nItem)
			MaFisLoad("IT_FRETE"   ,_FRETE      ,nItem)
			MaFisLoad("IT_ICMFRETE",_VALICMFRETE,nItem)
			MaFisLoad("IT_DESCONTO",_DESCONTO   ,nItem)
			MaFisEndLoad(nItem,1)
		EndIf
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁPonto de Entrada M410SOLI para alterar os valores do ICM Solidario referente a palnilha financeiraЁ
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If lM410Soli
			ICMSITEM    := MaFisRet(nItem,"IT_VALICM")		// variavel para ponto de entrada
			QUANTITEM   := MaFisRet(nItem,"IT_QUANT")		// variavel para ponto de entrada
			BASEICMRET  := MaFisRet(nItem,"IT_BASESOL")	    // criado apenas para o ponto de entrada
			MARGEMLUCR  := MaFisRet(nItem,"IT_MARGEM")		// criado apenas para o ponto de entrada
			aSolid := ExecBlock("M410SOLI",.f.,.f.,{nItem}) 
			aSolid := IIF(ValType(aSolid) == "A" .And. Len(aSolid) == 2, aSolid,{})
			If !Empty(aSolid)
				MaFisLoad("IT_BASESOL",NoRound(aSolid[1],2),nItem)
				MaFisLoad("IT_VALSOL" ,NoRound(aSolid[2],2),nItem)
				MaFisEndLoad(nItem,1)
			Endif
		EndIf
	Next
EndIf

//зддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁRealiza alteracoes de referencias do SC6         Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддды
dbSelectArea("SC6")
If Len(aFisGet) > 0
	For nX := 1 to Len(aCols)
		If Len(aCols[nX])==nUsado .Or. !aCols[nX][Len(aHeader)+1]
			For nY := 1 to Len(aFisGet)
				nPosCpo := aScan(aHeader,{|x| AllTrim(x[2])==Alltrim(aFisGet[ny][2])})
				If nPosCpo > 0
					If !Empty(aCols[nX][nPosCpo])
						MaFisAlt(aFisGet[ny][1],aCols[nX][nPosCpo],nX,.F.)
					Endif
				EndIf
			Next nX
		Endif
	Next nY
EndIf
//зддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁRealiza alteracoes de referencias do SC5 Suframa Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддды
nPSuframa:=aScan(aFisGetSC5,{|x| x[1] == "NF_SUFRAMA"})
If !Empty(nPSuframa)
	dbSelectArea("SC5")
	If !Empty(&("M->"+Alltrim(aFisGetSC5[nPSuframa][2])))
		MaFisAlt(aFisGetSC5[nPSuframa][1],Iif(&("M->"+Alltrim(aFisGetSC5[nPSuframa][2])) == "1",.T.,.F.),nItem,.F.)
	EndIf
Endif
If ExistBlock("M410PLNF")
	ExecBlock("M410PLNF",.F.,.F.)
EndIf
MaFisWrite(1)
//
// Template GEM - Gestao de Empreendimentos Imobiliarios
//
// Verifica se a condicao de pagamento tem vinculacao com uma condicao de venda
//
If ExistTemplate("GMCondPagto")
	lCondVenda := .F.
	lCondVenda := ExecTemplate("GMCondPagto",.F.,.F.,{M->C5_CONDPAG,} )
EndIf
//зддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁCalcula os venctos conforme a condicao de pagto  Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддды
If !M->C5_TIPO == "B"
	If lDtEmi
		dbSelectarea("SE4")
		dbSetOrder(1)
		MsSeek(xFilial("SE4")+M->C5_CONDPAG)
		If ((SE4->E4_TIPO=="9".AND.!(INCLUI.OR.ALTERA)).OR.SE4->E4_TIPO<>"9")				
		
			If SFB->FB_JNS == 'J' .And. cPaisLoc == 'COL'
			    dbSelectArea("SFC")
				dbSetOrder(2)
				If dbSeek(xFilial("SFC") + SF4->F4_CODIGO + "RV0" )
					nValRetImp 	:= MaFisRet(,"NF_VALIV2")
					Do Case
						Case FC_INCDUPL == '1'
							nlValor := MaFisRet(,"NF_BASEDUP") - nValRetImp
						Case FC_INCDUPL == '2'
							nlValor :=MaFisRet(,"NF_BASEDUP") + nValRetImp
						Otherwise
							nlValor :=MaFisRet(,"NF_BASEDUP")
					EndCase
				Elseif dbSeek(xFilial("SFC") + SF4->F4_CODIGO + "RF0" )
					nValRetImp 	:= MaFisRet(,"NF_VALIV4")
					Do Case
						Case FC_INCDUPL == '1'
							nlValor := MaFisRet(,"NF_BASEDUP") - nValRetImp
						Case FC_INCDUPL == '2'
							nlValor :=MaFisRet(,"NF_BASEDUP") + nValRetImp
						Otherwise
							nlValor :=MaFisRet(,"NF_BASEDUP")
					EndCase
				Elseif dbSeek(xFilial("SFC") + SF4->F4_CODIGO + "RC0" )
					nValRetImp 	:= MaFisRet(,"NF_VALIV7")
					Do Case
						Case FC_INCDUPL == '1'
							nlValor := MaFisRet(,"NF_BASEDUP") - nValRetImp
						Case FC_INCDUPL == '2'
							nlValor :=MaFisRet(,"NF_BASEDUP") + nValRetImp
						Otherwise
							nlValor :=MaFisRet(,"NF_BASEDUP")
					EndCase
				Endif
			Else
				nlValor := MaFisRet(,"NF_BASEDUP")
			EndIf				
		
			aDupl := Condicao(nlValor,M->C5_CONDPAG,MaFisRet(,"NF_VALIPI"),dDataCnd,MaFisRet(,"NF_VALSOL"),,,nAcresTot)
			If Len(aDupl) > 0
				If ! lCondVenda
					For nX := 1 To Len(aDupl)
						nAcerto += aDupl[nX][2]
					Next nX
					aDupl[Len(aDupl)][2] += MaFisRet(,"NF_BASEDUP") - nAcerto
				EndIf
	
				aVencto := aClone(aDupl)
				For nX := 1 To Len(aDupl)
					aDupl[nX][2] := TransForm(aDupl[nX][2],PesqPict("SE1","E1_VALOR"))
				Next nX
			Endif
		Else
			aDupl := {{Ctod(""),TransForm(MaFisRet(,"NF_BASEDUP"),PesqPict("SE1","E1_VALOR"))}}
			aVencto := {{dDataBase,MaFisRet(,"NF_BASEDUP")}}
		EndIf
	Else
		nItem := 0	
		For nX := 1 to Len(aCols)
			If Len(aCols[nX])==nUsado .Or. !aCols[nX][nUsado+1]
				If nPDtEntr > 0
					nItem++
					nPosEntr := Ascan(aEntr,{|x| x[1] == aCols[nX][nPDtEntr]})
	 				If nPosEntr == 0
						Aadd(aEntr,{aCols[nX][nPDtEntr],MaFisRet(nItem,"IT_BASEDUP"),MaFisRet(nItem,"IT_VALIPI"),MaFisRet(nItem,"IT_VALSOL")})
					Else    
						aEntr[nPosEntr][2]+= MaFisRet(nItem,"IT_BASEDUP")
						aEntr[nPosEntr][3]+= MaFisRet(nItem,"IT_VALIPI")
						aEntr[nPosEntr][4]+= MaFisRet(nItem,"IT_VALSOL")
					EndIf
				Endif
			Endif
	    Next
		dbSelectarea("SE4")
		dbSetOrder(1)
		MsSeek(xFilial("SE4")+M->C5_CONDPAG)
		If !(SE4->E4_TIPO=="9")
			For nY := 1 to Len(aEntr)
				nAcerto  := 0
				
				If SFB->FB_JNS $ 'J/S' .And. cPaisLoc == 'COL'
				    
				    dbSelectArea("SFC")
					dbSetOrder(2)
					If dbSeek(xFilial("SFC") + SF4->F4_CODIGO + "RV0" )
						nValRetImp 	:= MaFisRet(,"NF_VALIV2")
						Do Case
							Case FC_INCDUPL == '1'
								nlValor := aEntr[nY][2] - nValRetImp
							Case FC_INCDUPL == '2'
								nlValor :=aEntr[nY][2] + nValRetImp
							Otherwise
								nlValor :=aEntr[nY][2]
						EndCase
					Elseif dbSeek(xFilial("SFC") + SF4->F4_CODIGO + "RF0" )
						nValRetImp 	:= MaFisRet(,"NF_VALIV4")
						Do Case
							Case FC_INCDUPL == '1'
								nlValor := aEntr[nY][2] - nValRetImp
							Case FC_INCDUPL == '2'
								nlValor :=aEntr[nY][2] + nValRetImp
							Otherwise
								nlValor :=aEntr[nY][2]
						EndCase
					Elseif dbSeek(xFilial("SFC") + SF4->F4_CODIGO + "RC0" )
						nValRetImp 	:= MaFisRet(,"NF_VALIV7")
						Do Case
							Case FC_INCDUPL == '1'
								nlValor := aEntr[nY][2] - nValRetImp
							Case FC_INCDUPL == '2'
								nlValor :=aEntr[nY][2] + nValRetImp
							Otherwise
								nlValor :=aEntr[nY][2]
						EndCase
					Endif
				ElseIf cPaisLoc=="EQU" .And. lPParc
					DbSelectArea("SFC")
					SFC->(dbSetOrder(2))
					If DbSeek(xFilial("SFC") + SF4->F4_CODIGO + "RIR") //RetenГЦo IVA
						cImpRet		:= SFC->FC_IMPOSTO
						DbSelectArea("SFB")
						SFB->(dbSetOrder(1))
						If SFB->(DbSeek(xFilial("SFB")+AvKey(cImpRet,"FB_CODIGO")))
							nValRetImp 	:= MaFisRet(,"NF_VALIV"+SFB->FB_CPOLVRO)
					    Endif       
					    DbSelectArea("SFC")
						If SFC->FC_INCDUPL == '1'
							nlValor	:=aEntr[nY][2] - nValRetImp				
						ElseIf SFC->FC_INCDUPL == '2'
							nlValor :=aEntr[nY][2] + nValRetImp
						EndIf   
				    Endif
				Else
					nlValor := aEntr[nY][2]
				EndIf
				
				
				aDuplTmp := Condicao(nlValor,M->C5_CONDPAG,aEntr[nY][3],aEntr[nY][1],aEntr[nY][4],,,nAcresTot)
				If Len(aDuplTmp) > 0
					If ! lCondVenda
						If cPaisLoc=="EQU"
							For nX := 1 To Len(aDuplTmp)
								If nX==1                            
									If SFC->FC_INCDUPL == '1'
										aDuplTmp[nX][2]+= nValRetImp
									ElseIf SFC->FC_INCDUPL == '2'
										aDuplTmp[nX][2]-= nValRetImp
									Endif										
								Endif	
							Next nX
						Else
							For nX := 1 To Len(aDuplTmp)
								nAcerto += aDuplTmp[nX][2]
							Next nX
							aDuplTmp[Len(aDuplTmp)][2] += aEntr[nY][2] - nAcerto
						Endif
					EndIf
	
					aVencto := aClone(aDuplTmp)
					For nX := 1 To Len(aDuplTmp)
						aDuplTmp[nX][2] := TransForm(aDuplTmp[nX][2],PesqPict("SE1","E1_VALOR"))
					Next nX
					aEval(aDuplTmp,{|x| Aadd(aDupl,{aEntr[nY][1],x[1],x[2]})})
				EndIf
			Next
		Else
			aDupl := {{Ctod(""),TransForm(MaFisRet(,"NF_BASEDUP"),PesqPict("SE1","E1_VALOR"))}}
			aVencto := {{dDataBase,MaFisRet(,"NF_BASEDUP")}}
		EndIf
	EndIf
Else
	aDupl := {{Ctod(""),TransForm(0,PesqPict("SE1","E1_VALOR"))}}
	aVencto := {{dDataBase,0}}
EndIf
//
// Template GEM - Gestao de empreendimentos Imobiliarios
// Gera os vencimentos e valores das parcelas conforme a condicao de venda
//
If lCondVenda 
	If ExistBlock("GMMA410Dupl")
		aVencto := ExecBlock("GMMA410Dupl",.F.,.F.,{M->C5_NUM ,M->C5_CONDPAG,dDataCnd,,MaFisRet(,"NF_BASEDUP") ,aVencto}, .F., .F.) 
	ElseIf ExistTemplate("GMMA410Dupl")
		aVencto := ExecTemplate("GMMA410Dupl",.F.,.F.,{M->C5_NUM ,M->C5_CONDPAG,dDataCnd,,MaFisRet(,"NF_BASEDUP") ,aVencto}) 
	Endif	
	aDupl := {}
	aEval(aVencto ,{|aTitulo| aAdd( aDupl ,{transform(aTitulo[1],x3Picture("E1_VENCTO")) ,transform(aTitulo[2],x3Picture("E1_VALOR"))}) })
EndIf

If Len(aDupl) == 0
	aDupl := {{Ctod(""),TransForm(MaFisRet(,"NF_BASEDUP"),PesqPict("SE1","E1_VALOR"))}}
	aVencto := {{dDataBase,MaFisRet(,"NF_BASEDUP")}}
EndIf
//зддддддддддддддддддддддддддддддддддддддддддддд©
//ЁAnalise da Rentabilidade - Valor Presente    Ё
//юддддддддддддддддддддддддддддддддддддддддддддды
aRentabil := a410RentPV( aCols ,nUsado ,@aRenTab ,@aVencto ,nPTES,nPProduto,nPLocal,nPQtdVen )

If cPaisLoc=="BRA" .And. AliasIndic("CDA")
	aAdd(aTitles,STR0114)	//"LanГamentos da ApuraГЦo de ICMS"
	nLancAp	:=	Len(aTitles)
EndIf

//зддддддддддддддддддддддддддддддддддддддддддддд©
//ЁMonta a tela de exibicao dos valores fiscais Ё
//юддддддддддддддддддддддддддддддддддддддддддддды
DEFINE MSDIALOG oDlg TITLE OemToAnsi(STR0043) FROM 09,00 TO 28,80 //"Planilha Financeira"
oFolder := TFolder():New(001,001,aTitles,{"HEADER"},oDlg,,,, .T., .F.,315,140)
//зддддддддддддддддддддддддддддддддддддддддддддд©
//ЁFolder 1                                     Ё
//юддддддддддддддддддддддддддддддддддддддддддддды
MaFisRodape(1,oFolder:aDialogs[1],,{005,001,310,60},Nil,.T.)
If cPaisLoc <> "PTG"
	@ 070,005 SAY RetTitle("F2_FRETE")		SIZE 40,10 PIXEL OF oFolder:aDialogs[1]
	@ 070,105 SAY RetTitle("F2_SEGURO")		SIZE 40,10 PIXEL OF oFolder:aDialogs[1]
	@ 070,205 SAY RetTitle("F2_DESCONT")	SIZE 40,10 PIXEL OF oFolder:aDialogs[1]
	@ 085,005 SAY RetTitle("F2_FRETAUT")	SIZE 40,10 PIXEL OF oFolder:aDialogs[1]
	@ 085,105 SAY RetTitle("F2_DESPESA")	SIZE 40,10 PIXEL OF oFolder:aDialogs[1]
	@ 085,205 SAY RetTitle("F2_VALFAT")		SIZE 40,10 PIXEL OF oFolder:aDialogs[1]
	@ 070,050 MSGET MaFisRet(,"NF_FRETE")		PICTURE PesqPict("SF2","F2_FRETE",16,2)		SIZE 50,07 PIXEL WHEN .F. OF oFolder:aDialogs[1]
	@ 070,150 MSGET MaFisRet(,"NF_SEGURO")  	PICTURE PesqPict("SF2","F2_SEGURO",16,2)	SIZE 50,07 PIXEL WHEN .F. OF oFolder:aDialogs[1]
	@ 070,250 MSGET MaFisRet(,"NF_DESCONTO")	PICTURE PesqPict("SF2","F2_DESCONTO",16,2)	SIZE 50,07 PIXEL WHEN .F. OF oFolder:aDialogs[1]
	@ 085,050 MSGET MaFisRet(,"NF_AUTONOMO")	PICTURE PesqPict("SF2","F2_FRETAUT",16,2)	SIZE 50,07 PIXEL WHEN .F. OF oFolder:aDialogs[1]
	@ 085,150 MSGET MaFisRet(,"NF_DESPESA")		PICTURE PesqPict("SF2","F2_DESPESA",16,2)	SIZE 50,07 PIXEL WHEN .F. OF oFolder:aDialogs[1]
	@ 085,250 MSGET MaFisRet(,"NF_BASEDUP")		PICTURE PesqPict("SF2","F2_VALFAT",16,2)	SIZE 50,07 PIXEL WHEN .F. OF oFolder:aDialogs[1]
	@ 105,005 TO 106,310 PIXEL OF oFolder:aDialogs[1]
	@ 110,005 SAY OemToAnsi(STR0048)   SIZE 40,10 PIXEL OF oFolder:aDialogs[1] //"Total da Nota"
	@ 110,050 MSGET MaFisRet(,"NF_TOTAL")      PICTURE Iif(cPaisLoc=="CHI",TM(0,16,NIL),PesqPict("SF2","F2_VALBRUT",16,2))                   	SIZE 50,07 PIXEL WHEN .F. OF oFolder:aDialogs[1]
	@ 110,270 BUTTON OemToAnsi(STR0049)			SIZE 040,11 FONT oFolder:aDialogs[1]:oFont ACTION oDlg:End() OF oFolder:aDialogs[1] PIXEL		//"Sair"
Else 
	@ 070,005 SAY RetTitle("F2_DESCONT")	SIZE 40,10 PIXEL OF oFolder:aDialogs[1]
	@ 070,105 SAY RetTitle("F2_FRETE")		SIZE 40,10 PIXEL OF oFolder:aDialogs[1]
	@ 070,205 SAY RetTitle("F2_SEGURO")		SIZE 40,10 PIXEL OF oFolder:aDialogs[1]
	@ 085,005 SAY RetTitle("F2_DESPESA")	SIZE 40,10 PIXEL OF oFolder:aDialogs[1]
	@ 085,105 SAY RetTitle("F2_DESNTRB")	SIZE 40,10 PIXEL OF oFolder:aDialogs[1]
	@ 085,205 SAY RetTitle("F2_TARA")		SIZE 40,10 PIXEL OF oFolder:aDialogs[1]
	@ 110,005 SAY RetTitle("F2_VALFAT")		SIZE 40,10 PIXEL OF oFolder:aDialogs[1]
	@ 070,050 MSGET MaFisRet(,"NF_DESCONTO")	PICTURE PesqPict("SF2","F2_DESCONTO",16,2)	SIZE 50,07 PIXEL WHEN .F. OF oFolder:aDialogs[1]
	@ 070,150 MSGET MaFisRet(,"NF_FRETE")		PICTURE PesqPict("SF2","F2_FRETE",16,2)		SIZE 50,07 PIXEL WHEN .F. OF oFolder:aDialogs[1]
	@ 070,250 MSGET MaFisRet(,"NF_SEGURO")  	PICTURE PesqPict("SF2","F2_SEGURO",16,2)	SIZE 50,07 PIXEL WHEN .F. OF oFolder:aDialogs[1]
	@ 085,050 MSGET MaFisRet(,"NF_DESPESA")		PICTURE PesqPict("SF2","F2_DESPESA",16,2)	SIZE 50,07 PIXEL WHEN .F. OF oFolder:aDialogs[1]
	@ 085,150 MSGET MaFisRet(,"NF_DESNTRB")		PICTURE PesqPict("SF2","F2_DESNTRB",16,2)	SIZE 50,07 PIXEL WHEN .F. OF oFolder:aDialogs[1]
	@ 085,250 MSGET MaFisRet(,"NF_TARA")		PICTURE PesqPict("SF2","F2_TARA",16,2)		SIZE 50,07 PIXEL WHEN .F. OF oFolder:aDialogs[1]
	@ 110,050 MSGET MaFisRet(,"NF_BASEDUP")		PICTURE PesqPict("SF2","F2_VALFAT",16,2)	SIZE 50,07 PIXEL WHEN .F. OF oFolder:aDialogs[1]
	@ 105,005 TO 106,310 PIXEL OF oFolder:aDialogs[1]
	@ 110,105 SAY OemToAnsi(STR0048)   SIZE 40,10 PIXEL OF oFolder:aDialogs[1] //"Total da Nota"
	@ 110,150 MSGET MaFisRet(,"NF_TOTAL")      PICTURE Iif(cPaisLoc=="CHI",TM(0,16,NIL),PesqPict("SF2","F2_VALBRUT",16,2))                   	SIZE 50,07 PIXEL WHEN .F. OF oFolder:aDialogs[1]
	@ 110,270 BUTTON OemToAnsi(STR0049)			SIZE 040,11 FONT oFolder:aDialogs[1]:oFont ACTION oDlg:End() OF oFolder:aDialogs[1] PIXEL		//"Sair"
Endif
//зддддддддддддддддддддддддддддддддддддддддддддд©
//ЁFolder 2                                     Ё
//юддддддддддддддддддддддддддддддддддддддддддддды                                                                                                      
If lDtEmi
	@ 005,001 LISTBOX oDupl FIELDS TITLE aFlHead[1],aFlHead[2] SIZE 310,095 	OF oFolder:aDialogs[2] PIXEL
Else	
	@ 005,001 LISTBOX oDupl FIELDS TITLE aFlHead[3],aFlHead[1],aFlHead[2] SIZE 310,095 	OF oFolder:aDialogs[2] PIXEL
Endif	
oDupl:SetArray(aDupl)
oDupl:bLine := {|| aDupl[oDupl:nAt] }
@ 105,005 TO 106,310 PIXEL OF oFolder:aDialogs[2]
If cPaisLoc == "BRA"
	@ 110,005 SAY RetTitle("F2_VALFAT")		SIZE 40,10 PIXEL OF oFolder:aDialogs[2]
Else
	@ 110,005 SAY OemToAnsi(STR0051)	    SIZE 40,10 PIXEL OF oFolder:aDialogs[2]
Endif	
@ 110,050 MSGET MaFisRet(,"NF_BASEDUP")		PICTURE PesqPict("SF2","F2_VALFAT",16,2)	SIZE 50,07 PIXEL WHEN .F. OF oFolder:aDialogs[2]

//
// Template GEM - Gestao de empreendimentos imobiliarios
// Manutencao dos itens da condicao de venda 
//
If ExistBlock("GMMA410CVND",,.T.)
	If ExistBlock("GMMA410Dupl")
		@ 110,170 BUTTON OemToAnsi("Cond. de Venda") SIZE 050,11 FONT oFolder:aDialogs[1]:oFont ;
		          ACTION ( ExecBlock("GMMA410CVND",.F.,.F.,{nOpc ,M->C5_NUM ,M->C5_CONDPAG ,dDataCnd ,MaFisRet(,"NF_BASEDUP")}) ;
		                  ,aVencto := ExecBlock("GMMA410Dupl",.F.,.F.,{M->C5_NUM ,M->C5_CONDPAG,dDataCnd,,MaFisRet(,"NF_BASEDUP"),aVencto}) ;
		                  ,( aDupl := {} ,aEval(aVencto ,{|aTitulo| aAdd( aDupl ,{transform(aTitulo[1],x3Picture("E1_VENCTO")) ,transform(aTitulo[2],x3Picture("E1_VALOR"))})}) ;
		                  ,aRentabil := a410RentPV( aCols ,nUsado ,@aRenTab ,@aVencto ,nPTES,nPProduto,nPLocal,nPQtdVen ) );
		                  ,(oDupl:SetArray(aDupl),	oDupl:bLine := {|| aDupl[oDupl:nAt] }) ;
		                  ,(oRentab:SetArray(aRentabil) ,oRentab:bLine := {|| aRentabil[oRenTab:nAt] }) ) ;
		          OF oFolder:aDialogs[2] PIXEL
	EndIf
Else
	If ExistTemplate("GMMA410CVND",,.T.) .AND. HasTemplate("LOT")
		If ExistTemplate("GMMA410Dupl")
			@ 110,170 BUTTON OemToAnsi("Cond. de Venda") SIZE 050,11 FONT oFolder:aDialogs[1]:oFont ;
			          ACTION ( ExecTemplate("GMMA410CVND",.F.,.F.,{nOpc ,M->C5_NUM ,M->C5_CONDPAG ,dDataCnd ,MaFisRet(,"NF_BASEDUP")}) ;
			                  ,aVencto := ExecTemplate("GMMA410Dupl",.F.,.F.,{M->C5_NUM ,M->C5_CONDPAG,dDataCnd,,MaFisRet(,"NF_BASEDUP"),aVencto}) ;
			                  ,( aDupl := {} ,aEval(aVencto ,{|aTitulo| aAdd( aDupl ,{transform(aTitulo[1],x3Picture("E1_VENCTO")) ,transform(aTitulo[2],x3Picture("E1_VALOR"))})}) ;
			                  ,aRentabil := a410RentPV( aCols ,nUsado ,@aRenTab ,@aVencto ,nPTES,nPProduto,nPLocal,nPQtdVen ) );
			                  ,(oDupl:SetArray(aDupl),	oDupl:bLine := {|| aDupl[oDupl:nAt] }) ;
			                  ,(oRentab:SetArray(aRentabil) ,oRentab:bLine := {|| aRentabil[oRenTab:nAt] }) ) ;
			          OF oFolder:aDialogs[2] PIXEL
		EndIf
	EndIf
Endif
@ 110,270 BUTTON OemToAnsi(STR0049)			SIZE 040,11 FONT oFolder:aDialogs[1]:oFont ACTION oDlg:End() OF oFolder:aDialogs[2] PIXEL	//"Sair"
//зддддддддддддддддддддддддддддддддддддддддддддд©
//ЁFolder 3                                     Ё
//юддддддддддддддддддддддддддддддддддддддддддддды
@ 005,001 LISTBOX oRentab FIELDS TITLE aRFHead[1],aRFHead[2],aRFHead[3],aRFHead[4],aRFHead[5],aRFHead[6] SIZE 310,095 	OF oFolder:aDialogs[3] PIXEL
@ 110,270 BUTTON OemToAnsi(STR0049)			SIZE 040,11 FONT oFolder:aDialogs[3]:oFont ACTION oDlg:End() OF oFolder:aDialogs[3] PIXEL		//"Sair"
If Empty(aRentabil)
	aRentabil   := {{"",0,0,0,0,0}}
EndIf
oRentab:SetArray(aRentabil)
oRentab:bLine := {|| aRentabil[oRentab:nAt] }

If cPaisLoc=="BRA" .And. AliasIndic("CDA")
	oLancApICMS := A410LAICMS(oFolder:aDialogs[nLancAp],{005,001,310,095},@aHeadCDA,@aColsCDA,.T.,.F.)
	@ 110,270 BUTTON OemToAnsi(STR0049)			SIZE 040,11 FONT oFolder:aDialogs[nLancAp]:oFont ACTION oDlg:End() OF oFolder:aDialogs[nLancAp] PIXEL		//"Sair"
EndIf

ACTIVATE MSDIALOG oDlg CENTERED
MaFisEnd()
MaFisRestore()

RestArea(aAreaSA1)
RestArea(aArea)
Return(.T.)


/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁMa410TrackЁ Autor Ё Sergio Silveira       Ё Data Ё18/12/2001Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Faz o tratamento da chamada do System Tracker              Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё .T.                                                        Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ Nenhum                                                     Ё╠╠
╠╠цддддддддддедддддддддддддддбдддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё   DATA   Ё Programador   ЁManutencao Efetuada                         Ё╠╠
╠╠цддддддддддедддддддддддддддедддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё          Ё               Ё                                            Ё╠╠
╠╠юддддддддддадддддддддддддддадддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Static Function A410Track()

Local aEnt     := {}
Local cPedido  := M->C5_NUM
Local nPosItem := GDFieldPos( "C6_ITEM" )
Local nLoop    := 0

//зддддддддддддддддддддддддддддддддддддддддддддд©
//ЁInicializa a funcao fiscal                   Ё
//юддддддддддддддддддддддддддддддддддддддддддддды
For nLoop := 1 To Len( aCols )
	AAdd( aEnt, { "SC6", cPedido + aCols[ nLoop, nPosItem ] } )
Next nLoop

MaTrkShow( aEnt )

Return( .T. )


/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддбдддддддбддддддддддддддддддддддддбддддддбдддддддддддд©╠╠
╠╠ЁFun┤┘o    ЁA410DevolЁ Autor Ё Henry Fila             Ё Data Ё 07-09-2002 Ё╠╠
╠╠цддддддддддедддддддддадддддддаддддддддддддддддддддддддаддддддадддддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Programa de Consulta de Historicos da Revisao.               Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpC1 = Alias do arquivo                                     Ё╠╠
╠╠Ё          Ё ExpN1 = Numero do registro                                   Ё╠╠
╠╠Ё          Ё ExpN2 = Numero da opcao selecionada                          Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё Generico                                                     Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Function A410Devol(cAlias,nReg,nOpcx)
 
Local oDlgEsp
Local oLbx
Local lFornece  := .F.
Local lForn     := .T.
Local aRotina	:= {{OemtoAnsi(STR0053),"A410ProcDv",0,4}}
Local nOpca     := 0
Local aHSF1     := {}
Local aSF1      := {}
Local aCpoSF1   := {}
Local dDataDe   := CToD('  /  /  ')
Local dDataAte  := CToD('  /  /  ')
Local nCnt      := 0
Local nPosDoc   := 0
Local nPosSerie := 0
Local cDocSF1   := ''
Local cIndex    := ''
Local cQuery    := ''
Local cAliasSF1 := ''

Private cFornece := CriaVar("F1_FORNECE",.F.)
Private cLoja    := CriaVar("F1_LOJA",.F.)

If Inclui
	//-- Valida filtro de retorno de doctos fiscais.
	If A410FRet(@lFornece,@dDataDe,@dDataAte,@lForn)
		If lFornece
			Aadd( aHSF1, ' ' )
			SX3->(DbSetOrder(1))
			SX3->(DbSeek("SF1"))
			While SX3->(!Eof()) .And. SX3->X3_ARQUIVO == "SF1" 
			   If  SX3->X3_BROWSE == "S" .And. SX3->X3_CONTEXT <> "V"
					Aadd( aHSF1, X3Titulo() )
					Aadd( aCpoSF1, SX3->X3_CAMPO )
					//-- Armazena a posicao do documento e serie
					If AllTrim(SX3->X3_CAMPO) == 'F1_DOC'
						nPosDoc := Len(aHSF1)
					ElseIf AllTrim(SX3->X3_CAMPO) == 'F1_SERIE'
						nPosSerie := Len(aHSF1)
					EndIf
				EndIf
				SX3->(DbSkip())	
			EndDo
			//-- Retorna as notas que atendem o filtro.
			aSF1 := A410RetNF(aCpoSF1,dDataDe,dDataAte,lForn,lFornece)
			If !Empty(aSF1)
				DEFINE MSDIALOG oDlgEsp TITLE OemToAnsi(STR0098) FROM 00,00 TO 300,600 PIXEL //-- Retorno de Doctos. de Entrada
					oLbx:= TWBrowse():New( 012, 000, 300, 140, NIL, ;
			                                 aHSF1, NIL, oDlgEsp, NIL, NIL, NIL,,,,,,,,,, "ARRAY", .T. )
					oLbx:SetArray( aSF1 )
					oLbx:bLDblClick  := { || { aSF1[oLbx:nAT,1] := !aSF1[oLbx:nAT,1] }}
					oLbx:bLine := &('{ || A410Line(oLbx:nAT,aSF1) }')
				ACTIVATE MSDIALOG oDlgEsp ON INIT EnchoiceBar(oDlgEsp,{|| nOpca := 1, oDlgEsp:End()},{||oDlgEsp:End()}) CENTERED
				//-- Processa Devolucao
				If nOpca == 1
					ASort( aSF1,,,{|x,y| x[1] > y[1] })
					For nCnt := 1 To Len(aSF1)
						If !aSF1[nCnt,1]
							Exit
						EndIf
						#IFDEF TOP
							cDocSF1 += "( SD1.D1_DOC = '" + aSF1[nCnt,nPosDoc] + "' AND SD1.D1_SERIE = '" + aSF1[nCnt,nPosSerie] + "' ) OR "
						#ELSE
							cDocSF1 += "( SD1->D1_DOC == '" + aSF1[nCnt,nPosDoc] + "' .And. SD1->D1_SERIE == '" + aSF1[nCnt,nPosSerie] + "' ) .Or. "
						#ENDIF
					Next nCnt
					If !Empty(cDocSF1)
						#IFDEF TOP
							cDocSF1 := SubStr(cDocSF1,1,Len(cDocSF1)-3) + " )"
						#ELSE
							cDocSF1 := SubStr(cDocSF1,1,Len(cDocSF1)-5) + " )"
						#ENDIF
					EndIf
					A410ProcDv(cAlias,nReg,nOpcx,lFornece,cFornece,cLoja,cDocSF1)
				EndIf
			Else
	      	Aviso(OemToAnsi(STR0014),OemToAnsi(STR0101),{OemToAnsi(STR0040)}, 2) //-- //"Atencao!"###"Nenhum documento encontrado, favor verificar os dados informados  ..."###"Ok"
	      EndIf
		Else		
			DbSelectArea("SF1")
			cIndex := CriaTrab(NIL,.F.)
			cQuery := " SF1->F1_FILIAL == '" + xFilial("SF1") + "' "
		  	cQuery += " .And. SF1->F1_FORNECE == '" + cFornece + "' "
		  	cQuery += " .And. SF1->F1_LOJA    == '" + cLoja    + "' "
		  	cQuery += " .And. DtoS(SF1->F1_EMISSAO) >= '" + DtoS(dDataDe)  + "'"
			cQuery += " .And. DtoS(SF1->F1_EMISSAO) <= '" + DtoS(dDataAte) + "' "
			If lForn
			  	cQuery += " .And. !(SF1->F1_TIPO $ 'DB') "
			Else
			  	cQuery += " .And. SF1->F1_TIPO $ 'DB'
			EndIf
			
			If Existblock("A410RNF")
            	cQuery := ExecBlock("A410RNF",.F.,.F.,{dDataDe,dDataAte,lForn,lFornece})
			EndIf
			
			IndRegua("SF1",cIndex,SF1->(IndexKey()),,cQuery)
			
			If SF1->(!Eof())
				MaWndBrowse(0,0,300,600,OemToAnsi(STR0098),"SF1",,aRotina,,,,.T.,,,,,,.F.) 
			Else
	      	Aviso(OemToAnsi(STR0014),OemToAnsi(STR0101),{OemToAnsi(STR0040)}, 2) //-- //"Atencao!"###"Nenhum documento encontrado, favor verificar os dados informados  ..."###"Ok"
	      EndIf
			RetIndex( "SF1" )
			FErase( cIndex+OrdBagExt() )
		EndIf		
	EndIf
EndIf

Inclui := !Inclui

Return .T.


/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    Ёa410ProcDvЁAutor  ЁHenry Fila             Ё Data Ё07.09.2001Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё                                                            Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁExpC1: Alias do cabecalho do pedido de venda                Ё╠╠
╠╠Ё          ЁExpN2: Recno do cabecalho do pedido de venda                Ё╠╠
╠╠Ё          ЁExpN3: Opcao do arotina                                     Ё╠╠
╠╠Ё          ЁExpL3: Se traz baseado em uma entrada (SF1)                 Ё╠╠
╠╠Ё          Ё       .T. - Se baseia na nota fiscal de entrada            Ё╠╠
╠╠Ё          Ё       .F. - Copia um pedido de venda Normal                Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁNenhum                                                      Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁDescri┤└o ЁEsta rotina tem como objetivo efetuar a interface com o usuaЁ╠╠
╠╠Ё          Ёrio e o pedido de vendas                                    Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁObservacaoЁ                                                            Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё Materiais/Distribuicao/Logistica                           Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function A410ProcDv(cAlias,nReg,nOpc,lFornece,cFornece,cLoja,cDocSF1)

Local aArea     := GetArea()
Local aAreaSX3  := SX3->(GetArea())
Local aAreaSF1  := SF1->(GetArea())
Local aAreaSD1  := SD1->(GetArea())
Local aAreaSB8  := SB8->(GetArea())
Local aPosObj   := {}
Local aObjects  := {}
Local aSize     := {}
Local aPosGet   := {}
Local aRegSC6   := {}
Local aRegSCV   := {}
Local aInfo     := {}
Local aHeadSC6  := {}
Local aValor    := {}

Local lLiber 	:= .F.
Local lTransf	:= .F.
Local lQuery    := .F.
Local lContinua := .T.
Local lPoder3   := .T. 
Local lM410PcDv := ExistBlock("M410PCDV")
Local nOpcA		:= 0
Local nUsado    := 0
Local nCntFor   := 0
Local nTotalPed := 0
Local nTotalDes := 0
Local nNumDec   := TamSX3("C6_VALOR")[2]
Local cItem		:= StrZero(0,TamSX3("C6_ITEM")[1])
Local nGetLin   := 0
Local nStack    := GetSX8Len()
Local nPosPrc   := 0
Local nPValDesc := 0
Local nPPrUnit  := 0
Local nPQuant   := 0
Local nSldQtd   := 0
Local nSldQtd2  := 0
Local nSldLiq   := 0
Local nSldBru   := 0
Local nX        := 0
Local nCntSD1   := 0

Local cAliasSD1 := "SD1"
Local cAliasSB1 := "SB1"
Local cCodTES   := ""
Local cCadastro := IIF(cCadastro == Nil,OemToAnsi("STR0007"),cCadastro) //"Atualiza┤└o de Pedidos de Venda"
Local cCampo    :=""
Local cTipoPed  :=""
Local cQuery   := ""
Local oDlg
Local oGetd
Local oSAY1
Local oSAY2
Local oSAY3
Local oSAY4
Local aRecnoSE1RA := {} // Array com os titulos selecionados pelo Adiantamento
Local aHeadAGG    := {}
Local aColsAGG    := {}

#IFNDEF TOP
	Local cIndex   := ""
	Local nIndBrw  := 0		
#ENDIF	

//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Variaveis utilizadas na LinhaOk                      Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
PRIVATE aCols      := {}
PRIVATE aHeader    := {}
PRIVATE aHeadFor   := {}
PRIVATE aColsFor   := {}
PRIVATE N          := 1
If FindFunction("MsMatGrade") .And. IsAtNewGrd()
	PRIVATE oGrade	  := MsMatGrade():New('oGrade',,"C6_QTDVEN",,"a410GValid()",{ {VK_F4,{|| A440Saldo(.T.,oGrade:aColsAux[oGrade:nPosLinO][aScan(oGrade:aHeadAux,{|x| AllTrim(x[2])=="C6_LOCAL"})] )}} }) 
Else
	PRIVATE aColsGrade := {}
	PRIVATE aHeadgrade := {}
EndIf
//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Monta a entrada de dados do arquivo                  Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
PRIVATE aTELA[0][0],aGETS[0]

PRIVATE oGetPV	:= Nil

Default lFornece := .F.
Default cFornece := SF1->F1_FORNECE
Default cLoja    := SF1->F1_LOJA
Default cDocSF1  := ''

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁCarrega perguntas do MATA440 e MATA410                                  Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
Pergunte("MTA440",.F.)
lLiber := MV_PAR02 == 1
lTransf:= MV_PAR01 == 1

Pergunte("MTA410",.F.)

SB8->(dbSetOrder(3))

If SoftLock("SF1")

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Montagem do aHeader                                  Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
	dbSelectArea("SX3")
	dbSetOrder(1)
	MsSeek("SC6")
	While ( !EOF() .And. (SX3->X3_ARQUIVO == "SC6") )
		If (	X3USO(SX3->X3_USADO) .And.;
				!( Trim(SX3->X3_CAMPO) == "C6_NUM" );
				.And. Trim(x3_campo) <> "C6_QTDEMP";
				.And. Trim(x3_campo) <> "C6_QTDENT";
				.And. cNivel >= SX3->X3_NIVEL )
			nUsado++
			Aadd(aHeader,{ TRIM(X3Titulo()),;
				SX3->X3_CAMPO,;
				SX3->X3_PICTURE,;
				SX3->X3_TAMANHO,;
				SX3->X3_DECIMAL,;
				SX3->X3_VALID,;
				SX3->X3_USADO,;
				SX3->X3_TIPO,;
				SX3->X3_ARQUIVO,;
				SX3->X3_CONTEXT } )
		EndIf
		dbSelectArea("SX3")
		dbSkip()
	EndDo
	If ( lContinua )
		//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Montagem dos itens da Nota Fiscal de Devolucao/Retorno          Ё
		//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		dbSelectArea("SD1")
		dbSetOrder(1)
		#IFDEF TOP
			lQuery    := .T.
			cAliasSD1 := "QRYSD1"
			cAliasSB1 := "QRYSD1"
			aStruSD1  := SD1->(dbStruct())
			cQuery    := "SELECT SD1.*,B1_DESC,B1_UM,B1_SEGUM "
			cQuery    += "FROM "+RetSqlName("SD1")+" SD1, "
			cQuery    += RetSqlName("SB1")+" SB1 "
			cQuery    += "WHERE SD1.D1_FILIAL='"+xFilial("SD1")+"' AND "
			If !lFornece
				cQuery    += "SD1.D1_DOC = '"+SF1->F1_DOC+"' AND "
				cQuery    += "SD1.D1_SERIE = '"+SF1->F1_SERIE+"' AND "
			Else
				If !Empty(cDocSF1)
					cQuery += " ( "
					cQuery += cDocSF1 + " AND "
				EndIf
			EndIf
			cQuery    += "SD1.D1_FORNECE = '"+cFornece+"' AND "
			cQuery    += "SD1.D1_LOJA = '"+cLoja+"' AND "
			cQuery    += "SD1.D_E_L_E_T_=' ' AND "

			cQuery    += "SB1.B1_FILIAL = '"+xFilial("SB1")+"' AND "
			cquery    += "SB1.B1_COD = SD1.D1_COD AND "
			cQuery    += "SB1.D_E_L_E_T_=' ' "

			cQuery    += "ORDER BY "+SqlOrder(SD1->(IndexKey()))

			cQuery    := ChangeQuery(cQuery)

			dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSD1,.T.,.T.)

			For nX := 1 To Len(aStruSD1)
				If aStruSD1[nX][2]<>"C"
					TcSetField(cAliasSD1,aStruSD1[nX][1],aStruSD1[nX][2],aStruSD1[nX][3],aStruSD1[nX][4])
				EndIf
			Next nX
		#ELSE
			If lFornece
				cIndex := CriaTrab(NIL,.F.)
				cQuery := " SD1->D1_FILIAL == '" + xFilial("SD1") + "' "
			  	cQuery += " .And. SD1->D1_FORNECE == '" + cFornece + "' "
			  	cQuery += " .And. SD1->D1_LOJA    == '" + cLoja    + "' "
				If !Empty(cDocSF1)
					cQuery += " .And. ( "
					cQuery += cDocSF1
				EndIf
				IndRegua("SD1",cIndex,SD1->(IndexKey()),,cQuery)
				nIndBrw := RetIndex("SD1")
				#IFNDEF TOP
					dbSetIndex(cIndex+OrdBagExt())
				#ENDIF
				dbSetOrder(nIndBrw+1)				
				SD1->(DbGotop())
			Else 
				MsSeek( xFilial("SD1")+SF1->F1_DOC+SF1->F1_SERIE+SF1->F1_FORNECE+SF1->F1_LOJA)
			EndIf
		#ENDIF

		While !Eof() .And. (cAliasSD1)->D1_FILIAL == xFilial("SD1") .And.;
			(cAliasSD1)->D1_FORNECE == cFornece .And.;
			(cAliasSD1)->D1_LOJA == cLoja .And.;
			If(!lFornece,(cAliasSD1)->D1_DOC == SF1->F1_DOC .And.;
							 (cAliasSD1)->D1_SERIE == SF1->F1_SERIE,.T.)

			//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Se existe quantidade a ser devolvida                            Ё
			//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			If (cAliasSD1)->D1_QUANT > (cAliasSD1)->D1_QTDEDEV

				cItem := Soma1(cItem)

				If !lQuery
					SB1->(dbSetOrder(1))
					SB1->(MsSeek(xFilial("SB1")+(cAliasSD1)->D1_COD))
				Endif

				SF1->(dbSetOrder(1))
				SF1->(MsSeek(xFilial("SF1")+(cAliasSD1)->D1_DOC+(cAliasSD1)->D1_SERIE+(cAliasSD1)->D1_FORNECE+(cAliasSD1)->D1_LOJA+(cAliasSD1)->D1_TIPO))

				//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Verifica se existe um tes de devolucao correspondente           Ё
				//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды

				dbSelectArea("SF4")
				DbSetOrder(1)
				If MsSeek(xFilial("SF4")+(cAliasSD1)->D1_TES)
					//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					//Ё Verifica o poder de terceiros                                   Ё
					//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
					If lPoder3 
						lPoder3 := ( SF4->F4_PODER3=="R" ) 
					EndIf

					If Empty(SF4->F4_TESDV) .Or. !(SF4->(MsSeek(xFilial("SF4")+SF4->F4_TESDV)))
						Help(" ",1,"DSNOTESDEV")
						lContinua := .F.
						Exit 
					Else
						cCodTES := SF4->F4_CODIGO
					EndIf
					
					If !(lPoder3 .Or. SF1->F1_TIPO=="N")
						Help(" ",1,"A410PODER3")
						lContinua := .F.
						Exit 						
					EndIf
				
				EndIf

				aValor := A410SNfOri((cAliasSD1)->D1_FORNECE,;
											(cAliasSD1)->D1_LOJA,;
											(cAliasSD1)->D1_DOC,;
											(cAliasSD1)->D1_SERIE,;
											If(lPoder3,"",(cAliasSD1)->D1_ITEM),;
											(cAliasSD1)->D1_COD,;
											If(lPoder3,(cAliasSD1)->D1_IDENTB6,),;
											If(lPoder3,(cAliasSD1)->D1_LOCAL,),;
											cAliasSD1)

				nSldQtd:= aValor[1]
				nSldQtd2:=ConvUm((cAliasSD1)->D1_COD,nSldQtd,0,2)
				nSldLiq:= aValor[2]
				nSldBru:= nSldLiq+A410Arred(nSldLiq*(cAliasSD1)->D1_VALDESC/((cAliasSD1)->D1_TOTAL-(cAliasSD1)->D1_VALDESC),"C6_VALOR")

				//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Verifica se existe saldo                                        Ё
				//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
				If nSldQtd <> 0

					nCntSD1++
					If nCntSD1 > 900  // No. maximo de Itens 
						Exit
					EndIf

					AADD(aCols,Array(Len(aHeader)+1))
					For nCntFor := 1 To Len(aHeader)
						cCampo := Alltrim(aHeader[nCntFor,2])

						If ( aHeader[nCntFor,10] # "V" .And. !cCampo$"C6_QTDLIB#C6_RESERVA" )

							Do Case

							Case Alltrim(aHeader[nCntFor][2]) == "C6_ITEM"
								aCols[Len(aCols)][nCntFor] := cItem
							Case Alltrim(aHeader[nCntFor][2]) == "C6_PRODUTO"
								aCols[Len(aCols)][nCntFor] := (cAliasSD1)->D1_COD								
								SB1->(dbSetOrder(1))
								SB1->(MsSeek(xFilial("SB1")+(cAliasSD1)->D1_COD))
				 				If ExistTrigger("C6_PRODUTO")
				   					RunTrigger(2,Len(aCols))
								EndIf
							Case Alltrim(aHeader[nCntFor][2]) == "C6_DESCRI"
								aCols[Len(aCols)][nCntFor] := (cAliasSB1)->B1_DESC
							Case Alltrim(aHeader[nCntFor][2]) == "C6_SEGUM"
								aCols[Len(aCols)][nCntFor] := (cAliasSB1)->B1_SEGUM
							Case Alltrim(aHeader[nCntFor][2]) == "C6_UM"
								aCols[Len(aCols)][nCntFor] := (cAliasSB1)->B1_UM
							Case Alltrim(aHeader[nCntFor][2]) == "C6_UNSVEN"
								aCols[Len(aCols)][nCntFor] := a410Arred(nSldQtd2,"C6_UNSVEN")
							Case Alltrim(aHeader[nCntFor][2]) == "C6_QTDVEN"
								aCols[Len(aCols)][nCntFor] := a410Arred(nSldQtd,"C6_QTDVEN")
							Case Alltrim(aHeader[nCntFor][2]) == "C6_PRCVEN"
								aCols[Len(aCols)][nCntFor] := a410Arred(nSldLiq/IIf(nSldQtd==0,1,nSldQtd),"C6_PRCVEN")
							Case Alltrim(aHeader[nCntFor][2]) == "C6_PRUNIT"
								aCols[Len(aCols)][nCntFor] := (cAliasSD1)->D1_VUNIT
							Case Alltrim(aHeader[nCntFor][2]) == "C6_VALOR"
								If nSldQtd <> (cAliasSD1)->D1_QUANT
									aCols[Len(aCols)][nCntFor] := a410Arred(nSldQtd*a410Arred(nSldLiq/IIf(nSldQtd==0,1,nSldQtd),"C6_PRCVEN"),"C6_VALOR")
								Else
									aCols[Len(aCols)][nCntFor] := nSldLiq
								EndIf
							Case Alltrim(aHeader[nCntFor][2]) == "C6_VALDESC"
								If (cAliasSD1)->D1_VALDESC>0
									aCols[Len(aCols)][nCntFor] := a410Arred(((cAliasSD1)->D1_VUNIT-a410Arred(nSldLiq/IIf(nSldQtd==0,1,nSldQtd),"C6_PRCVEN"))*a410Arred(nSldQtd,"C6_QTDVEN"),"C6_VALDESC")
								Else
									aCols[Len(aCols)][nCntFor] := 0
								EndIf
							Case Alltrim(aHeader[nCntFor][2]) == "C6_DESCONT"
								If (cAliasSD1)->D1_DESC>0
									aCols[Len(aCols)][nCntFor] :=(cAliasSD1)->D1_DESC
								Else
									aCols[Len(aCols)][nCntFor] := 0
								EndIf
							Case Alltrim(aHeader[nCntFor][2]) == "C6_TES"
								aCols[Len(aCols)][nCntFor] := cCodTES
								SF4->(dbSetOrder(1))
								SF4->(MsSeek(xFilial("SF4")+cCodTES))								
				 				If ExistTrigger("C6_TES    ")
				   					RunTrigger(2,Len(aCols))
								EndIf
							Case Alltrim(aHeader[nCntFor][2]) == "C6_CF"
								aCols[Len(aCols)][nCntFor] := SF4->F4_CF
							Case Alltrim(aHeader[nCntFor][2]) == "C6_NFORI"
								aCols[Len(aCols)][nCntFor] := (cAliasSD1)->D1_DOC
							Case Alltrim(aHeader[nCntFor][2]) == "C6_SERIORI"
								aCols[Len(aCols)][nCntFor] := (cAliasSD1)->D1_SERIE
							Case Alltrim(aHeader[nCntFor][2]) == "C6_ITEMORI" 
								aCols[Len(aCols)][nCntFor] := (cAliasSD1)->D1_ITEM
							Case Alltrim(aHeader[nCntFor][2]) == "C6_NUMLOTE"
								aCols[Len(aCols)][nCntFor] := IIF(SF4->F4_ESTOQUE == "S",(cAliasSD1)->D1_NUMLOTE ,"")
							Case Alltrim(aHeader[nCntFor][2]) == "C6_LOTECTL"
								aCols[Len(aCols)][nCntFor] := IIF(SF4->F4_ESTOQUE == "S",(cAliasSD1)->D1_LOTECTL ,"")
							Case Alltrim(aHeader[nCntFor][2]) == "C6_LOCAL"
								aCols[Len(aCols)][nCntFor] := (cAliasSD1)->D1_LOCAL
							Case Alltrim(aHeader[nCntFor][2]) == "C6_IDENTB6"
								aCols[Len(aCols)][nCntFor] := (cAliasSD1)->D1_IDENTB6
							Case Alltrim(aHeader[nCntFor][2]) == "C6_DTVALID"			
								If SF4->F4_ESTOQUE == "S"
									aCols[Len(aCols)][nCntFor] := (cAliasSD1)->D1_DTVALID
									If SB8->(MsSeek(xFilial("SB8")+(cAliasSD1)->D1_COD+(cAliasSD1)->D1_LOCAL+(cAliasSD1)->D1_LOTECTL+IIf(Rastro((cAliasSD1)->D1_COD,"S"),(cAliasSD1)->D1_NUMLOTE,"")))
										aCols[Len(aCols)][nCntFor] := SB8->B8_DTVALID
									Endif   
								Else
									aCols[Len(aCols)][nCntFor] := CTOD("  /  /  ")
								EndIf
							Case Alltrim(aHeader[nCntFor][2]) == "C6_CLASFIS" .AND. (cAliasSD1)->D1_TIPO <> "B"
								aCols[Len(aCols)][nCntFor] := (cAliasSD1)->D1_CLASFIS								
							OtherWise
								aCols[Len(aCols)][nCntFor] := CriaVar(cCampo)
							EndCase
						Else
							aCols[Len(aCols)][nCntFor] := CriaVar(cCampo)
						EndIf
					Next nCntFor

					aCols[Len(aCols)][Len(aHeader)+1] := .F.

					If lM410PCDV
						ExecBlock("M410PCDV",.F.,.F.,{cAliasSD1})
					Endif

				Endif

			Endif

			dbSelectArea(cAliasSD1)
			dbSkip()
		EndDo
		If ( lQuery )
			dbSelectArea(cAliasSD1)
			dbCloseArea()
			ChkFile("SC6",.F.)
			dbSelectArea("SC6")
		Else
			If lFornece
				RetIndex( "SD1" )
				FErase( cIndex+OrdBagExt() )
			EndIf
		EndIf

		If (lContinua)

			//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Inicializa as variaveis de busca do acols                                 Ё
			//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды

			nPosPrc   := aScan(aHeader,{|x| AllTrim(x[2])=="C6_PRCVEN"})
			nPValDesc := aScan(aHeader,{|x| AllTrim(x[2])=="C6_VALDESC"})
			nPPrUnit  := aScan(aHeader,{|x| AllTrim(x[2])=="C6_PRUNIT"})
			nPQuant   := aScan(aHeader,{|x| AllTrim(x[2])=="C6_QTDVEN"})


			//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Inici aliza desta forma para criar uma nova instancia de variaveis privateЁ
			//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Cria Variaveis de Memoria da Enchoice                                 Ё
			//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			DbSelectArea("SX3")
			DbSetOrder(1)
			DbSeek("SC5")
			While ( !Eof() .And. (SX3->X3_ARQUIVO == "SC5") )
				cCampo := SX3->X3_CAMPO

				If	( SX3->X3_CONTEXT <> "V" )
					Do Case

					Case Alltrim(cCampo) == "C5_TIPO"

						cTipoPed := "" 

						//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
						//Ё Verifica o tipo da nota para o retorno do pedido     Ё
						//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
						Do Case
						Case SF1->F1_TIPO == "N" .And. lPoder3
							cTipoPed := "B" 
						Case SF1->F1_TIPO == "B" .And. lPoder3
							cTipoPed := "N" 
						EndCase

						If Empty(cTipoPed)
							cTipoPed := "D" 
						Endif

						_SetOwnerPrvt(Trim(SX3->X3_CAMPO),cTipoPed )

					Case Alltrim(cCampo) == "C5_CLIENTE"
						_SetOwnerPrvt(Trim(SX3->X3_CAMPO),cFornece)
					Case Alltrim(cCampo) == "C5_LOJACLI"
						_SetOwnerPrvt(Trim(SX3->X3_CAMPO),cLoja)
					Case Alltrim(cCampo) == "C5_EMISSAO"
						_SetOwnerPrvt(Trim(SX3->X3_CAMPO),dDataBase)
					Case Alltrim(cCampo) == "C5_CONDPAG"
						_SetOwnerPrvt(Trim(SX3->X3_CAMPO),SF1->F1_COND)
					Case Alltrim(cCampo) == "C5_CLIENT"
						_SetOwnerPrvt(Trim(SX3->X3_CAMPO),cFornece)						
					Case Alltrim(cCampo) == "C5_LOJAENT"
						_SetOwnerPrvt(Trim(SX3->X3_CAMPO),cLoja)
					OtherWise
						_SetOwnerPrvt(Trim(SX3->X3_CAMPO),CriaVar(SX3->X3_CAMPO))
					EndCase
				Else
					_SetOwnerPrvt(Trim(SX3->X3_CAMPO),CriaVar(SX3->X3_CAMPO))
				Endif

				DbSelectArea("SX3")
				DbSkip()
			EndDo

			//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Busca o tipo do cliente/fornecedor                   Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
			If M->C5_TIPO$"DB"
				SA2->(dbSetOrder(1))
				If SA2->(MsSeek(xFilial("SA2")+M->C5_CLIENTE+M->C5_LOJACLI))
					_SetOwnerPrvt("C5_TIPOCLI",If(SA2->A2_TIPO=="J","R",SA2->A2_TIPO))
				EndIf
			Else
				SA1->(dbSetOrder(1))
				If SA1->(MsSeek(xFilial("SA1")+M->C5_CLIENTE+M->C5_LOJACLI))
					_SetOwnerPrvt("C5_TIPOCLI",SA1->A1_TIPO)
				Endif 
			EndIf

			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Marca o cliente utilizado para verificar posterior mudanca Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			a410ChgCli(M->C5_CLIENTE+M->C5_LOJACLI)
		Endif

	EndIf
Endif
//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Caso nao ache nenhum item , abandona rotina.         Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
If ( lContinua )
	If ( Len(aCols) == 0 )
		lContinua := .F.
	EndIf
EndIf

aRegSC6 := {}
aRegSCV := {}

If ( lContinua )
	//зддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁMonta o array com as formas de pagamento do SX5Ё
	//юддддддддддддддддддддддддддддддддддддддддддддддды
	Ma410MtFor(@aHeadFor,@aColsFor)
	A410ReCalc(.F.)	

	If ( Type("l410Auto") == "U" .OR. !l410Auto )
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Faz o calculo automatico de dimensoes de objetos     Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
		aSize := MsAdvSize()
		aObjects := {}
		AAdd( aObjects, { 100, 100, .t., .t. } )
		AAdd( aObjects, { 100, 100, .t., .t. } )
		AAdd( aObjects, { 100, 015, .t., .f. } )
		aInfo := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 3, 3 }
		aPosObj := MsObjSize( aInfo, aObjects )
		aPosGet := MsObjGetPos(aSize[3]-aSize[1],315,{{003,033,160,200,240,263}} )
		nGetLin := aPosObj[3,1]

		DEFINE MSDIALOG oDlg TITLE cCadastro From aSize[7],0 to aSize[6],aSize[5] of oMainWnd PIXEL
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Armazenar dados do Pedido anterior.                  Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
		IF M->C5_TIPO $ "DB"
			aTrocaF3 := {{"C5_CLIENTE","SA2"}}
		Else
			aTrocaF3 := {}
		EndIf
		oGetPV:=MSMGet():New( "SC5", nReg, 3, , , , , aPosObj[1],,3,,,"A415VldTOk",,,.T.)
		A410Limpa(.F.,M->C5_TIPO)
//		@ nGetLin,aPosGet[1,1]  SAY OemToAnsi(IIF(M->C5_TIPO$"DB",STR0008,STR0009)) SIZE 020,09 PIXEL	//"Fornec.:"###"Cliente: "
		@ nGetLin,aPosGet[1,2]  SAY oSAY1 VAR Space(40)						SIZE 120,09 PICTURE "@!"	OF oDlg PIXEL
		@ nGetLin,aPosGet[1,3]  SAY OemToAnsi(STR0011)						SIZE 020,09 OF oDlg PIXEL	//"Total :"
		@ nGetLin,aPosGet[1,4]  SAY oSAY2 VAR 0 PICTURE TM(0,16,Iif(cPaisloc=="CHI",NIL,nNumDec))	SIZE 050,09 OF oDlg PIXEL		
		@ nGetLin,aPosGet[1,5]  SAY OemToAnsi(STR0012)						SIZE 030,09 OF oDlg PIXEL 	//"Desc. :"
		@ nGetLin,aPosGet[1,6]  SAY oSAY3 VAR 0 PICTURE TM(0,16,Iif(cPaisloc=="CHI",NIL,nNumDec))		SIZE 050,09 OF oDlg PIXEL RIGHT
		@ nGetLin+10,aPosGet[1,5]  SAY OemToAnsi("=")							SIZE 020,09 OF oDlg PIXEL
		If cPaisLoc == "BRA"				
			@ nGetLin+10,aPosGet[1,6]  SAY oSAY4 VAR 0								SIZE 050,09 PICTURE TM(0,16,2) OF oDlg PIXEL RIGHT
		Else
			@ nGetLin+10,aPosGet[1,6]  SAY oSAY4 VAR 0								SIZE 050,09 PICTURE TM(0,16,Iif(cPaisloc=="CHI",NIL,nNumDec)) OF oDlg PIXEL RIGHT
		EndIf
		oDlg:Cargo	:= {|c1,n2,n3,n4| oSay1:SetText(c1),;
			oSay2:SetText(n2),;
			oSay3:SetText(n3),;
			oSay4:SetText(n4) }
		Set Key VK_F4 to A440Stok(NIL,"A410")
		oGetd:=MsGetDados():New(aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],3,"A410LinOk","A410TudOk","+C6_ITEM/C6_Local/C6_TES/C6_CF/C6_PEDCLI",.T.,,1,,ITENSSC6*IIF(MaGrade(),1,3.33),"A410Blq()")
		Private oGetDad:=oGetd
		A410Bonus(2)
		Ma410Rodap(oGetD,nTotalPed,nTotalDes)
		ACTIVATE MSDIALOG oDlg ON INIT Ma410Bar(oDlg,{||nOpcA:=1,if(A410VldTOk(nOpc).And.oGetd:TudoOk(),If(!obrigatorio(aGets,aTela),nOpcA := 0,oDlg:End()),nOpcA := 0)},{||oDlg:End()},nOpc,oGetD,nTotalPed,@aRecnoSE1RA,@aHeadAGG,@aColsAGG)
		SetKey(VK_F4,)
	EndIf
	If ( nOpcA == 1 )
		A410Bonus(1)
		If a410Trava()
			//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Inicializa a gravacao dos lancamentos do SIGAPCO          Ё
			//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			PcoIniLan("000100")
			If !A410Grava(lLiber,lTransf,2,aHeadFor,aColsFor,aRegSC6,aRegSCV,,,aRecnoSE1RA,aHeadAGG,aColsAGG)
				Help(" ",1,"A410NAOREG")
			EndIf
			If ( (ExistBlock("M410STTS") ) )
				ExecBlock("M410STTS",.f.,.f.)
			EndIf
			//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Finaliza a gravacao dos lancamentos do SIGAPCO            Ё
			//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			PcoFinLan("000100")
		EndIf
	Else
		While GetSX8Len() > nStack
			RollBackSX8()
		EndDo
		If ( (ExistBlock("M410ABN")) )
			ExecBlock("M410ABN",.f.,.f.)
		EndIf
	EndIf
Else
	While GetSX8Len() > nStack
		RollBackSX8()
	EndDo
EndIf

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁLimpa cliente anterior para proximo pedido                              Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
a410ChgCli("")

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁDestrava Todos os Registros                                             Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
MsUnLockAll()

RestArea(aAreaSX3)
RestArea(aAreaSF1)
RestArea(aAreaSD1)
RestArea(aAreaSB8)
RestArea(aArea)

Return( nOpcA )


/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁMa410FormaЁAutor  ЁHenry Fila             Ё Data Ё07.09.2001Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁMostra planilha de formacao de precos                       Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁNenhum                                                      Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁNenhum                                                      Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁDescri┤└o ЁEsta rotina tem como objetivo efetuar a interface com o usuaЁ╠╠
╠╠Ё          Ёrio e o pedido de vendas                                    Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁObservacaoЁ                                                            Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё Materiais/Distribuicao/Logistica                           Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function Ma410Forma()

Local aArea    := GetArea()
Local aAreaSB1 := SB1->(GetArea())
Local aHeaderBk:= aClone(aHeader)

Local nPosProd := aScan(aHeader,{|x| AllTrim(x[2])=="C6_PRODUTO"})
Local nPosQtde := aScan(aHeader,{|x| AllTrim(x[2])=="C6_QTDVEN"})

Private cArqMemo   := ""
Private lDirecao   := .T.
Private nQualCusto := 1
Private cProg      := "C010"

If VerSenha(107)
	If !Empty(aCols[n][nPosProd])
		If Ma410Plan()
			SB1->(dbSetOrder(1))
			If SB1->(MsSeek(xFilial("SB1")+aCols[n][nPosProd]) )
				If FindFunction("MTC010SX1")
					MTC010SX1()
				EndIf
				If Pergunte("MTC010",.T.)
					MC010Forma("SB1",SB1->(Recno()),98,aCols[n][nPosQtde],2)
					aHeader := aClone(aHeaderBk)
					Pergunte("MTA410",.F.)
				Endif
			Endif
		Endif
	Else
		Aviso(OemToAnsi(STR0054),OemtoAnsi(STR0055),{OemtoAnsi(STR0040)})
	Endif
Else
	Help(" ",1,"SEMPERM")
Endif

RestArea(aAreaSB1)
RestArea(aArea)

Return

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤└o    ЁMa410Plan Ё Autor Ё Henry Fila            Ё Data Ё          Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤└o Ё Le planilha de formacao gravadas no disco                  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё SIGAEST                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Function Ma410Plan()
Local aDiretorio := {}
Local nX         := 0
Local oPlan
Local oDlg
Local oBtnA
Local lRet:=.F.

aDiretorio := Directory("*.PDV")

For nX := 1 To Len(aDiretorio)
	aDiretorio[nX] := SubStr(aDiretorio[nX][1],1,AT(".",aDiretorio[nX][1])-1)
	If aDiretorio[nX] == "STANDARD"
		aDiretorio[nX] := Space(14)
	Else
		aDiretorio[nX] := "   "+aDiretorio[nX]+Space(11-Len(aDiretorio[nX]))
	EndIf
Next nX
If Len(aDiretorio)>0
	Asort(aDiretorio)
	If Empty(aDiretorio[1])
		aDiretorio[1] := "   STANDARD   "
	EndIf

	nX :=1

	DEFINE MSDIALOG oDlg FROM 15,6 TO 222,309 TITLE STR0069 PIXEL	//"Selecione Planilha"
	@ 11,12 LISTBOX oPlan FIELDS HEADER  ""  SIZE 131, 69 OF oDlg PIXEL;
		ON CHANGE (nX := oPlan:nAt) ON DBLCLICK (Eval(oBtnA:bAction))
	oPlan:SetArray(aDiretorio)
	oPlan:bLine := { || {aDiretorio[oPlan:nAT]} }
	DEFINE SBUTTON oBtnA FROM 83, 088 TYPE 1 ENABLE OF oDlg Action(lRet := .T.,oDlg:End())
	DEFINE SBUTTON FROM 83, 115 TYPE 2 ENABLE OF oDlg Action (lRet:= .F.,ODlg:End())
	ACTIVATE MSDIALOG oDlg CENTER

	cArqMemo := AllTrim(aDiretorio[nX])
EndIf
RETURN lRet

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁMa410GrXmlЁAutor  ЁEduardo Riera          Ё Data Ё08.02.2002Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁGravacao do Pedido de Venda no Formato XML.                 Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁNenhum                                                      Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁNenhum                                                      Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁDescri┤└o ЁEsta rotina tem como objetivo efetuar a gravacao do pedido  Ё╠╠
╠╠Ё          Ёde Venda no Formato XML para ser utilizada pelo Job de      Ё╠╠
╠╠Ё          ЁGravacao do Pedido de Venda                                 Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁObservacaoЁNenhuma                                                     Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё Materiais/Distribuicao/Logistica                           Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function Ma410GrXml()

Local aArea    := GetArea()
Local aXml     := {}
Local aFile    := {}
Local bCampo   := {|nCPO| Field(nCPO) }
Local cString  := ""
Local cName    := ""
Local lRetorno := .T.
Local nX       := 0
Local nY       := 0
Local nItem    := 0
Local nErro    := 0
Local nHandle  := 0
Local nPItem   := aScan(aHeader,{|x| AllTrim(x[2]) == "C6_ITEM"})
Local nUsado   := Len(aHeader)


If File(DIRMASC+"MSPV.XML")
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁVerifica se existe a mascara do Pedido de Venda                         Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	FT_FUse(DIRMASC+"MSPV.XML")
	FT_FGotop()
	While !FT_FEof()
		aadd(aXml,FT_FREADLN())
		FT_FSkip()
	EndDo
	FT_FUSE()
	aadd(aFile,aXml[1])
	aadd(aFile,aXml[2])
	aadd(aFile,StrTran(aXml[3],"><",">MS_MATA410<"))
	aadd(aFile,StrTran(aXml[4],"><",">6.09<"))
	aadd(aFile,StrTran(aXml[5],"><",">"+Dtos(Date())+" "+Time()+"<"))
	aadd(aFile,StrTran(aXml[6],"><",">"+"MSPV"+"<"))
	aadd(aFile,StrTran(aXml[7],"><",">"+cEmpAnt+"<"))
	aadd(aFile,StrTran(aXml[8],"><",">"+cFilAnt+"<"))
	aadd(aFile,aXml[09])
	aadd(aFile,aXml[10])
	aadd(aFile,StrTran(aXml[11],"><",">"+cEmpAnt+cFilAnt+M->C5_NUM+"<"))
	aadd(aFile,StrTran(aXml[12],"><",">"+"DIRECTUPDATE"+"<"))
	aadd(aFile,aXml[13])
	nItem := 1
	dbSelectArea("SC5")
	For nX := 1 To FCount()
		If !Empty(M->&(EVAL(bCampo,nX)))
			aadd(aFile,aXml[14])
			aadd(aFile,StrTran(aXml[15],"><",">"+SC5->(FieldName(nX))+"<"))
			Do Case
				Case ValType(M->&(EVAL(bCampo,nX)))=="D"
					aadd(aFile,StrTran(aXml[16],"><",">"+DTOC(M->&(EVAL(bCampo,nX)))+"<"))
				Case ValType(M->&(EVAL(bCampo,nX)))=="N"
					aadd(aFile,StrTran(aXml[16],"><",">"+Str(M->&(EVAL(bCampo,nX)),TamSx3(SC5->(FieldName(nX)))[1],TamSx3(SC5->(FieldName(nX)))[2])+"<"))
				OtherWise
					aadd(aFile,StrTran(aXml[16],"><",">"+M->&(EVAL(bCampo,nX))+"<"))
			EndCase
			aadd(aFile,aXml[17])
		EndIf
	Next nX
	aadd(aFile,aXml[18])
	aadd(aFile,aXml[19])
	For nX := 1 To Len(aCols)
		If !aCols[nX][nUsado+1]	
			aadd(aFile,aXml[20])
			aadd(aFile,StrTran(aXml[21],"><",">"+aCols[nX][nPItem]+"<"))
			nItem := 1
			For nY := 1 To Len(aHeader)
				If nY <= Len(aCols[nX]) .And. !Empty(aCols[nX][nY])
					aadd(aFile,aXml[22])
					aadd(aFile,StrTran(aXml[23],"><",">"+aHeader[nY][2]+"<"))
					Do Case
						Case ValType(aCols[nX][nY])=="D"
							aadd(aFile,StrTran(aXml[24],"><",">"+Dtoc(aCols[nX][nY])+"<"))
						Case ValType(aCols[nX][nY])=="N"
							If !Empty(TamSx3(aHeader[nY][2]))
								aadd(aFile,StrTran(aXml[24],"><",">"+Str(aCols[nX][nY],TamSx3(aHeader[nY][2])[1],TamSx3(aHeader[nY][2])[2])+"<"))
							EndIf
						OtherWise
							aadd(aFile,StrTran(aXml[24],"><",">"+aCols[nX][nY]+"<"))
						EndCase
						aadd(aFile,aXml[25])
				EndIf
			Next nY
			aadd(aFile,aXml[26])
		EndIf
	Next nX
	For nX := 27 To Len(aXml)
		aadd(aFile,aXml[nX])
	Next nX
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁEfetua a gravacao no diretorio de destino                               Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If !File(DIRXMLTMP+"*.*")
		nErro := MakeDir(DIRXMLTMP)
		If nErro <> 0
			ConOut(Repl("-",80))
			ConOut("")
			ConOut("MATA410XML - DIRECTORY ERROR: "+Str(nErro))
			ConOut("")
			ConOut(Repl("-",80))
			lRetorno := .F.
		EndIf
	EndIf
	If lRetorno
		cName   := AllTrim(DIRXMLTMP+CriaTrab(,.F.)+".XML")
		nHandle := FCreate(cName,0)
		nErro   := FError()
		If nErro <> 0
			ConOut(Repl("-",80))
			ConOut("")
			ConOut("MATA410XML - CREATE XML ERROR: "+Str(nErro))
			ConOut("")
			ConOut(Repl("-",80))
			lRetorno := .F.
		Else
			If lRetorno
				While ( __lSX8 )
					ConfirmSX8()
				EndDo
			EndIf
			cString := ""
			For nX := 1 To Len(aFile)
				cString += aFile[nX]+Chr(13)+Chr(10)
				If Mod(nX,100)==0
					FWrite(nHandle,cString)
					cString := ""
					nErro   := FError()
					If nErro <> 0
						FClose(nHandle)
						ConOut(Repl("-",80))
						ConOut("")
						ConOut("MATA410XML - WRITE XML ERROR: "+Str(nErro))
						ConOut("")
						ConOut(Repl("-",80))
						lRetorno := .F.
						nX := Len(aFile)+1
					EndIf
				EndIf
			Next nX
			FWrite(nHandle,cString)
			nErro   := FError()
			If nErro <> 0
				ConOut(Repl("-",80))
				ConOut("")
				ConOut("MATA410XML - WRITE XML ERROR: "+Str(nErro))
				ConOut("")
				ConOut(Repl("-",80))
				lRetorno := .F.
			EndIf
			FClose(nHandle)
			nErro   := FError()
			If nErro <> 0
				ConOut(Repl("-",80))
				ConOut("")
				ConOut("MATA410XML - CLOSE XML ERROR: "+Str(nErro))
				ConOut("")
				ConOut(Repl("-",80))
				lRetorno := .F.
			EndIf
		EndIf
	EndIf
Else
	lRetorno := .F.
EndIf
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁRestaura a integridade da rotina                                        Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
RestArea(aArea)
Return(lRetorno)


/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁMa410VldUsЁ Autor Ё Henry Fila            Ё Data Ё17/03/2003Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Tratamento da confirmacao / nao confirmacao da inclusao    Ё╠╠
╠╠Ё          Ё ou alteracao                                               Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё ExpL1 := Ma410VldUs( ExpN1 )                               Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpN1 -> Opcao : 1 -> Confirma / 0 -> Nao Confirma         Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё ExpL1 -> Validacao                                         Ё╠╠
╠╠цддддддддддедддддддддддддддбдддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё   DATA   Ё Programador   ЁManutencao Efetuada                         Ё╠╠
╠╠цддддддддддедддддддддддддддедддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё          Ё               Ё                                            Ё╠╠
╠╠юддддддддддадддддддддддддддадддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Function Ma410VldUs( nOpca )

Local lRet := .T.

//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Faz a chamada do ponto passando nOpca como parametro Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды

If ExistBlock( "MA410VLD" )
	lRet := ExecBlock( "MA410VLD", .F., .F., { nOpca } )
EndIf

Return( lRet )

/*/
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбддддддддддд©╠╠
╠╠ЁFun┤└o    ЁMyMata410 Ё Autor Ё Eduardo Riera         Ё Data Ё17.04.2003 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддаддддддддддд╢╠╠
╠╠Ё          ЁRotina de teste da rotina automatica do programa MATA410     Ё╠╠
╠╠Ё          Ё                                                             Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁNenhum                                                       Ё╠╠
╠╠Ё          Ё                                                             Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁNenhum                                                       Ё╠╠
╠╠Ё          Ё                                                             Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁDescri┤└o ЁEsta rotina tem como objetivo efetuar testes na rotina de    Ё╠╠
╠╠Ё          Ёpedido de venda                                              Ё╠╠
╠╠Ё          Ё                                                             Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё Materiais                                                   Ё╠╠
╠╠юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function MyMata410()

Local aCabec := {}
Local aItens := {}
Local aLinha := {}
Local nX     := 0
Local nY     := 0
Local cDoc   := ""
Local lOk    := .T.
PRIVATE lMsErroAuto := .F.
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//| Abertura do ambiente                                         |
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
ConOut(Repl("-",80))
ConOut(PadC("Teste de Inclusao de 10 pedidos de venda  com 30 itens cada",80))
PREPARE ENVIRONMENT EMPRESA "99" FILIAL "01" MODULO "FAT" TABLES "SC5","SC6","SA1","SA2","SB1","SB2","SF4"
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//| Verificacao do ambiente para teste                           |
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
dbSelectArea("SB1")
dbSetOrder(1)
If !SB1->(MsSeek(xFilial("SB1")+"PA001"))
	lOk := .F.
	ConOut("Cadastrar produto: PA001")
EndIf
dbSelectArea("SF4")
dbSetOrder(1)
If !SF4->(MsSeek(xFilial("SF4")+"501"))
	lOk := .F.
	ConOut("Cadastrar TES: 501")
EndIf
dbSelectArea("SE4")
dbSetOrder(1)
If !SE4->(MsSeek(xFilial("SE4")+"001"))
	lOk := .F.
	ConOut("Cadastrar condicao de pagamento: 001")
EndIf
If !SB1->(MsSeek(xFilial("SB1")+"PA002"))
	lOk := .F.
	ConOut("Cadastrar produto: PA002")
EndIf
dbSelectArea("SA1")
dbSetOrder(1)
If !SA1->(MsSeek(xFilial("SA1")+"CL000101"))
	lOk := .F.
	ConOut("Cadastrar cliente: CL000101")
EndIf
If lOk
	ConOut("Inicio: "+Time())
	For nY := 1 To 10
		cDoc := GetSxeNum("SC5","C5_NUM")
		RollBAckSx8()
		aCabec := {}
		aItens := {}
		aadd(aCabec,{"C5_NUM"   ,cDoc,Nil})
		aadd(aCabec,{"C5_TIPO" ,"N",Nil})
		aadd(aCabec,{"C5_CLIENTE",SA1->A1_COD,Nil})
		aadd(aCabec,{"C5_LOJACLI",SA1->A1_LOJA,Nil})
		aadd(aCabec,{"C5_LOJAENT",SA1->A1_LOJA,Nil})
		aadd(aCabec,{"C5_CONDPAG",SE4->E4_CODIGO,Nil})
		If cPaisLoc == "PTG"
			aadd(aCabec,{"C5_DECLEXP","TESTE",Nil})
		Endif
		For nX := 1 To 30
			aLinha := {}
			aadd(aLinha,{"C6_ITEM",StrZero(nX,2),Nil})
			aadd(aLinha,{"C6_PRODUTO",SB1->B1_COD,Nil})
			aadd(aLinha,{"C6_QTDVEN",1,Nil})
			aadd(aLinha,{"C6_PRCVEN",100,Nil})
			aadd(aLinha,{"C6_PRUNIT",100,Nil})
			aadd(aLinha,{"C6_VALOR",100,Nil})
			aadd(aLinha,{"C6_TES","501",Nil})
			aadd(aItens,aLinha)
		Next nX
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//| Teste de Inclusao                                            |
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		MATA410(aCabec,aItens,3)
		If !lMsErroAuto
			ConOut("Incluido com sucesso! "+cDoc)
		Else
			ConOut("Erro na inclusao!")
		EndIf
	Next nY
	ConOut("Fim  : "+Time())
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//| Teste de alteracao                                           |
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	aCabec := {}
	aItens := {}
	aadd(aCabec,{"C5_NUM",cDoc,Nil})
	aadd(aCabec,{"C5_TIPO","N",Nil})
	aadd(aCabec,{"C5_CLIENTE",SA1->A1_COD,Nil})
	aadd(aCabec,{"C5_LOJACLI",SA1->A1_LOJA,Nil})
	aadd(aCabec,{"C5_LOJAENT",SA1->A1_LOJA,Nil})
	aadd(aCabec,{"C5_CONDPAG",SE4->E4_CODIGO,Nil})
	If cPaisLoc == "PTG"
		aadd(aCabec,{"C5_DECLEXP","TESTE",Nil})
	Endif
	For nX := 1 To 30
		aLinha := {}
		aadd(aLinha,{"LINPOS","C6_ITEM",StrZero(nX,2)})
		aadd(aLinha,{"AUTDELETA","N",Nil})
		aadd(aLinha,{"C6_PRODUTO",SB1->B1_COD,Nil})
		aadd(aLinha,{"C6_QTDVEN",2,Nil})
		aadd(aLinha,{"C6_PRCVEN",100,Nil})
		aadd(aLinha,{"C6_PRUNIT",100,Nil})
		aadd(aLinha,{"C6_VALOR",200,Nil})
		aadd(aLinha,{"C6_TES","501",Nil})
		aadd(aItens,aLinha)
	Next nX	
	ConOut(PadC("Teste de alteracao",80))
	ConOut("Inicio: "+Time())
	MATA410(aCabec,aItens,4)
	ConOut("Fim  : "+Time())
	ConOut(Repl("-",80))	
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//| Teste de Exclusao                                            |
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	ConOut(PadC("Teste de exclusao",80))
	ConOut("Inicio: "+Time())
	MATA410(aCabec,aItens,5)
	If !lMsErroAuto
		ConOut("Exclusao com sucesso! "+cDoc)
	Else
		ConOut("Erro na exclusao!")
	EndIf
	ConOut("Fim  : "+Time())
	ConOut(Repl("-",80))
EndIf
RESET ENVIRONMENT
Return(.T.)

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁMa410VldQEKЁ Autor Ё Cleber Souza         Ё Data Ё19/09/2003Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Verifica se na devolucao a Nota Original ainda naum foi    Ё╠╠
╠╠Ё          Ё liberada do CQ.                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё ExpL1 := Ma410VldUs( ExpC1,ExpC2,ExpC3,ExpC4,ExpC5,ExpC6 ) Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё ExpL1 -> Validacao                                         Ё╠╠
╠╠цддддддддддедддддддддддддддбдддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё   DATA   Ё Programador   ЁManutencao Efetuada                         Ё╠╠
╠╠цддддддддддедддддддддддддддедддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё          Ё               Ё                                            Ё╠╠
╠╠юддддддддддадддддддддддддддадддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Function Ma410VldQEK( cForn,cLoja,cNfOri,cSerOri,cItemOri,cProdOri)

Local lRetorna   := .t.
Local aSaldoQEK  := {}
Local aArea      := GetArea()
Local lLibDev    := GetMV("MV_QLIBDEV",.T.,.F.)

//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё verifica se esta liberado pelo Quality.              Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды

//Pesquisa NF Original
SD1->(dbSetOrder(1))
SD1->(dbSeek(xFilial("SD1")+cNfOri+cSerOri+cForn+cLoja+cProdOri+cItemOri))
	
//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё verifica se tipo de Nota mais TES saum usados no QIE.Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
If !QIETipoNf(SD1->D1_TIPO,SD1->D1_TES)
	
	//Posiciona na Entrada do QEK
	dbSelectArea("QEK")
	dbSetOrder(10)
	If dbSeek(xFilial("QEK")+SD1->D1_FORNECE+SD1->D1_LOJA+SD1->D1_DOC+SD1->D1_SERIE+SD1->D1_ITEM+SD1->D1_TIPO+SD1->D1_NUMSEQ)
		If QEK->QEK_SITENT $ ("1,0")
			Help(" ",1,"A410SIQEK") //"Ainda nao foi digitado o laudo para esta entrada na Inspecao de Entrada."
			lRetorna := .f.
		Else
			If !lLibDev
				aSaldoQEK := A175CalcQt(SD1->D1_NUMcq, SD1->D1_COD, SD1->D1_LOCAL)
				If aSaldoQEK[6] > 0
					Help(" ",1,"A410SLQEK") //"Ainda existe saldo dessa entrada na Qualidade para ser liberada."
					lRetorna := .f.
				EndIF
			EndIF
		EndIF
	EndIF
EndIF

RestArea(aArea)

Return lRetorna


/*/
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбддддддддддд©╠╠
╠╠ЁFun┤└o    ЁMa410CustoЁ Autor Ё Eduardo Riera         Ё Data Ё23.02.2005 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддаддддддддддд╢╠╠
╠╠Ё          ЁRotina de calculo do CMV de um item do pedido de venda       Ё╠╠
╠╠Ё          Ё                                                             Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁExpN1: Item da funcao fiscal                                 Ё╠╠
╠╠Ё          ЁExpA2: Array de vencimentos na seguinte estrutura            Ё╠╠
╠╠Ё          Ё       [D] Data de vencto                                    Ё╠╠
╠╠Ё          Ё       [C] Valor da Parcela                                  Ё╠╠
╠╠Ё          ЁExpC3: Codigo da TES                                         Ё╠╠
╠╠Ё          ЁExpC4: Codigo do Produto                                     Ё╠╠
╠╠Ё          ЁExpC5: Local padrЦo                                          Ё╠╠
╠╠Ё          ЁExpN6: Quantidade vendida                                    Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁExpN1: CMV convertido para o valor presente                  Ё╠╠
╠╠Ё          Ё                                                             Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁDescri┤└o ЁEsta funcao calcula o CMV do pedido de venda considerando o  Ё╠╠
╠╠Ё          Ёvalor presente caso haja parcelamento de pagamentos.         Ё╠╠
╠╠Ё          Ё                                                             Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё Mata410/mata415                                             Ё╠╠
╠╠юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function Ma410Custo(nItem,aVencto,cTES,cProduto,cLocal,nQtdVen)

Local nX        := 0
Local nVlrPed   := MaFisRet(,"NF_BASEDUP")
Local nVlrParc  := 0
Local nVlrItem  := 0
Local nVlrPres  := 0
Local nTaxa     := SuperGetMv("MV_JUROS")
Local aRet      := {}
Local aDupl     := {}
Local aCusto    := {}
Local nTotal	:= 0

//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Posiciona registros                                  Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
dbSelectArea("SF4")
dbSetOrder(1)
MsSeek(xFilial()+cTES)
//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Proporcionaliza o valor do item com o valor do pedidoЁ
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
For nX := 1 to Len(aVencto)
	nVlrParc := aVencto[nX,2]
	nVlrItem := MaFisRet(nItem,"IT_BASEDUP")
	nVlrItem := nVlrParc*nVlrItem/nVlrPed
	
	nVlrPres := MaValPres(nVlrItem,aVencto[nX][1],nTaxa)
	If nX == Len(aVencto) .And. ((nTotal+nVlrPres) <> nVlrPed .And. !(nTaxa > 0))
		nVlrPres += (nVlrPed - (nTotal+nVlrPres) )
	EndIf
	nTotal += nVlrPres
	aadd(aRet,{'MT410  ','   ',' ',aVencto[nX][1],nVlrPres})
Next nX
For nX := 1 to Len(aRet)
	aAdd(aDupl,aRet[nX][2]+"Ё"+aRet[nX][1]+"Ё "+aRet[nX][3]+" Ё"+DTOC(aRet[nX][4])+"Ё "+Transform(aRet[nX][5],PesqPict("SE2","E2_VALOR",14,1)))
Next nX

dbSelectArea("SF4")
dbSetOrder(1)
MsSeek(xFilial("SF4")+cTes)
If  cPaisLoc <> "BRA"
	aadd(aCusto,{nTotal,;
		0,;
		0,;
		"N",;
		"N",;
		"0",;
		"0",;
		cProduto,;
		cLocal,;
		nQtdVen,;
		0})
Else
	aadd(aCusto,{nTotal-IIf(SF4->F4_IPI=="R",0,MaFisRet(nItem,"IT_VALIPI"))+MaFisRet(nItem,"IT_VALCMP"),;
		MaFisRet(nItem,"IT_VALIPI"),;
		MaFisRet(nItem,"IT_VALICM"),;
		SF4->F4_CREDIPI,;
		SF4->F4_CREDICM,;
		MaFisRet(nItem,"IT_NFORI"),;
		MaFisRet(nItem,"IT_SERORI"),;
		cProduto,;
		cLocal,;
		nQtdVen,;
		If(SF4->F4_IPI=="R",MaFisRet(nItem,"IT_VALIPI"),0) })
EndIf
Return(RetCusEnt(aDupl,aCusto,'N')[1][1])

/*/
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбддддддддддд©╠╠
╠╠ЁFun┤└o    ЁMA410BOM  Ё Autor Ё Eduardo Riera         Ё Data Ё06.12.2001 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддаддддддддддд╢╠╠
╠╠Ё          ЁMa410Impos()                                                 Ё╠╠
╠╠Ё          ЁFuncao de calculo dos impostos contidos no pedido de venda   Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁNenhum                                                       Ё╠╠
╠╠Ё          Ё                                                             Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁNenhum                                                       Ё╠╠
╠╠Ё          Ё                                                             Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁDescri┤└o ЁEsta funcao efetua os calculos de impostos (ICMS,IPI,ISS,etc)Ё╠╠
╠╠Ё          Ёcom base nas funcoes fiscais, a fim de possibilitar ao usua- Ё╠╠
╠╠Ё          Ёrio o valor de desembolso financeiro.                        Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё Generico                                                    Ё╠╠
╠╠юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Static Function Ma410Bom(aHeader,aCols,nX)

Local aArea     := GetArea()
Local aBOM      := {}
Local aHeadBom  := {"",RetTitle("C6_PRODUTO"),RetTitle("C6_QTDVEN"),RetTitle("C6_DESCRI")}
Local nPProduto := aScan(aHeader,{|x| AllTrim(x[2]) == "C6_PRODUTO"})
Local nPTES     := aScan(aHeader,{|x| AllTrim(x[2]) == "C6_TES"})
Local nPQtdVen  := aScan(aHeader,{|x| AllTrim(x[2]) == "C6_QTDVEN"})
Local nPPrcVen  := aScan(aHeader,{|x| AllTrim(x[2]) == "C6_PRCVEN"})
Local nPItem    := aScan(aHeader,{|x| AllTrim(x[2]) == "C6_ITEM"})
Local nPTotal   := aScan(aHeader,{|x| AllTrim(x[2]) == "C6_VALOR"})
Local nY        := 0
Local nZ        := 0
Local cItem     := ""
Local lMA410BOM := ExistBlock("MA410BOM")
PRIVATE N := nX
//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Posiciona registros                                  Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
dbSelectArea("SB1")
dbSetOrder(1)
MsSeek(xFilial("SB1")+aCols[nX][nPProduto])
//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Verifica os produtos do primeiro nМvel da estrutura  Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
dbSelectArea("SG1")
dbSetOrder(1)
MsSeek(xFilial("SG1")+aCols[nX][nPProduto])
While !Eof() .And. xFilial("SG1") == SG1->G1_FILIAL .And.;
	aCols[nX][nPProduto] == SG1->G1_COD

	dbSelectArea("SB1")
	dbSetOrder(1)
	MsSeek(xFilial("SB1")+aCols[nX][nPProduto])
	If SB1->B1_FANTASM<>"S"
		aadd(aBOM,{SG1->G1_COMP,ExplEstr(aCols[nX][nPQtdVen],dDataBase,"",SB1->B1_REVATU),SB1->B1_DESC})
	EndIf
	dbSelectArea("SG1")
	dbSkip()
EndDo

If lMA410BOM
	ExecBlock("MA410BOM",.F.,.F.,{aBOM})
Else
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Adiciona os produtos no aCols                        Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
	For nX := 1 To Len(aBOM)
		cItem := aCols[Len(aCols)][nPItem]
		aadd(aCOLS,Array(Len(aHeader)+1))
		For nY	:= 1 To Len(aHeader)
			If ( AllTrim(aHeader[nY][2]) == "C6_ITEM" )
				aCols[Len(aCols)][nY] := Soma1(cItem)
			Else
				If (aHeader[nY,2] <> "C6_REC_WT") .And. (aHeader[nY,2] <> "C6_ALI_WT")				
					aCols[Len(aCols)][nY] := CriaVar(aHeader[nY][2])
				EndIf	
			EndIf
		Next nY
		N := Len(aCols)
		aCOLS[N][Len(aHeader)+1] := .F.
		A410Produto(aBom[nX][1],.F.)
		aCols[N][nPProduto]      := aBom[nX][1]
		A410MultT("M->C6_PRODUTO",aBom[nX][1])
		If ExistTrigger("C6_PRODUTO")
			RunTrigger(2,N,Nil,,"C6_PRODUTO")
		Endif

		A410SegUm(.T.)
		A410MultT("M->C6_QTDVEN",aBom[nX][2])
		If ExistTrigger("C6_QTDVEN ")
			RunTrigger(2,N,Nil,,"C6_QTDVEN ")
		Endif
		If Empty(aCols[N][nPTotal]) .Or. Empty(aCols[N][nPTES])
			aCOLS[N][Len(aHeader)+1] := .T.
		EndIf
	Next nX
Endif	

RestArea(aArea)
Return(.T.)

/*/
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбддддддддддд©╠╠
╠╠ЁFuncao    ЁMa410NFVP3Ё Autor Ё Eduardo Riera         Ё Data Ё           Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддаддддддддддд╢╠╠
╠╠Ё          Ё                                                             Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁNenhum                                                       Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁNenhum                                                       Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁDescricao Ё                                                             Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё Generico                                                    Ё╠╠
╠╠юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function Ma410NFVP3()

Local aArea := GetArea("SC5")
Local lQuery:= .F.
Local cAliasSB6 := "SB6"
Local nX    := 0

dbSelectArea("SC6")
dbSetOrder(1)
MsSeek(xFilial("SC6")+SC5->C5_NUM)

While !Eof() .And. SC6->C6_FILIAL == xFilial("SC6") .And. SC6->C6_NUM == SC5->C5_NUM
	//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Verifica os produto que possuem poder de terceiro neste cliente Ё
	//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	dbSelectArea("SB6")
	dbSetOrder(2)
	#IFDEF TOP
	If TcSrvType()<>"AS/400"
		lQuery    := .T.
		cAliasSB6 := "F4PODER3_SQL"
		cQuery := "SELECT DISTINCT(SD1.R_E_C_N_O_) SD1RECNO,D1_TOTAL,D1_VALDESC,0 SD2RECNO,0 D2_TOTAL,0 D2_DESCON,D1_LOTECTL,D1_NUMLOTE,"
		cQuery += "B6_FILIAL,B6_PRODUTO,B6_CLIFOR,B6_LOJA,B6_PODER3,B6_IDENTB6,B6_TPCF,B6_QUANT,B6_QULIB "
		cQuery += "FROM "+RetSqlName("SB6")+" SB6 ,"
		cQuery += RetSqlName("SD2")+" SD2 "
		cQuery += "WHERE SB6.B6_FILIAL='"+xFilial("SB6")+"' AND "
		cQuery += "SB6.B6_PRODUTO='"+SC6->C6_PRODUTO+"' AND "
		cQuery += "SB6.B6_CLIFOR='"+SC5->C5_CLIENTE+"' AND "
		cQuery += "SB6.B6_LOJA='"+SC5->C5_LOJACLI+"' AND "
		cQuery += "SB6.B6_PODER3='R' AND "
		cQuery += "SB6.B6_TPCF = 'C' AND "
		cQuery += "SB6.D_E_L_E_T_=' ' AND "
		cQuery += "SB6.B6_TIPO='E' AND "
		cQuery += "SD2.D2_FILIAL='"+xFilial("SD2")+"' AND "
		cQuery += "SD2.D2_NUMSEQ=SB6.B6_IDENT AND "
		cQuery += "SD2.D_E_L_E_T_=' ' "
		cQuery += "UNION ALL "
		cQuery += "SELECT DISTINCT(0) SD1RECNO,0 D1_TOTAL,0 D1_VALDESC,SD2.R_E_C_N_O_ SD2RECNO,D2_TOTAL,D2_DESCON, '' D1_LOTECTL, '' D1_NUMLOTE,"
		cQuery += "B6_FILIAL,B6_PRODUTO,B6_CLIFOR,B6_LOJA,B6_PODER3,B6_IDENTB6,B6_TPCF,B6_QUANT,B6_QULIB "
		cQuery += "FROM "+RetSqlName("SB6")+" SB6 ,"
		cQuery += RetSqlName("SD1")+" SD1 "
		cQuery += "WHERE SB6.B6_FILIAL='"+xFilial("SB6")+"' AND "
		cQuery += "SB6.B6_PRODUTO='"+SC6->C6_PRODUTO+"' AND "
		cQuery += "SB6.B6_CLIFOR='"+SC5->C5_CLIENTE+"' AND "
		cQuery += "SB6.B6_LOJA='"+SC5->C5_LOJACLI+"' AND "
		cQuery += "SB6.B6_PODER3='D' AND "
		cQuery += "SB6.B6_TPCF = 'C' AND "
		cQuery += "SB6.D_E_L_E_T_=' ' AND "
		cQuery += "SB6.B6_TIPO='E' AND "
		cQuery += "SD1.D1_FILIAL='"+xFilial("SD1")+"' AND "
		cQuery += "SD1.D1_DOC=SB6.B6_DOC AND "
		cQuery += "SD1.D1_SERIE=SB6.B6_SERIE AND "
		cQuery += "SD1.D1_FORNECE=SB6.B6_CLIFOR AND "
		cQuery += "SD1.D1_LOJA=SB6.B6_LOJA AND "
		cQuery += "SD1.D1_LOCAL=SB6.B6_LOCAL AND "
		cQuery += "SD1.D1_COD=SB6.B6_PRODUTO AND "
		cQuery += "SD1.D1_IDENTB6=SB6.B6_IDENT AND "
		cQuery += "SD1.D1_QUANT=SB6.B6_QUANT AND "
		cQuery += "SD1.D_E_L_E_T_=' ' "

		cQuery := ChangeQuery(cQuery)

		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSB6,.T.,.T.)

		TcSetField(cAliasSD1,"D1_TOTAL","N",TamSx3("D1_TOTAL")[1], TamSx3("D1_TOTAL")[2] )
		TcSetField(cAliasSD1,"D1_VALDESC","N",TamSx3("D1_VALDESC")[1], TamSx3("D1_TOTAL")[2] )
		TcSetField(cAliasSD1,"D2_TOTAL","N",TamSx3("D2_TOTAL")[1], TamSx3("D2_TOTAL")[2] )
		TcSetField(cAliasSD1,"D2_DESCON","N",TamSx3("D2_DESCON")[1], TamSx3("D2_DESCON")[2] )
		TcSetField(cAliasSD1,"SD1RECNO","N",12, 0 )
		TcSetField(cAliasSD1,"SD2RECNO","N",12, 0 )
	Else
	#ENDIF
		If IsTriangular()
			MsSeek(xFilial("SB6")+cProduto)
		Else
			MsSeek(xFilial("SB6")+cProduto+cCliFor+cLoja,.F.)
		EndIf
	#IFDEF TOP
	EndIf
	#ENDIF
	While !Eof() .And. (cAliasSB6)->B6_FILIAL = xFilial("SB6") .And.;
		(cAliasSB6)->B6_PRODUTO == cProduto .And.;
		IIF(IsTriangular(),.T.,(cAliasSB6)->B6_CLIFOR == cCliFor .And.;
		(cAliasSB6)->B6_LOJA == cLoja )

		If lMtProcP3
			lProcessa := ExecBlock("MTPROCP3",.F.,.F.,{cAliasSB6,lQuery})
		Endif

		If lProcessa
			If ((cES == "E" .And. (cAliasSB6)->B6_TIPO == "E") .Or. (cES == "S" .And. (cAliasSB6)->B6_TIPO == "D") ) .And.;
				(cAliasSB6)->B6_TPCF==cTpCliFor
				If !lQuery
					//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					//Ё Verificar qual eh a tabela de origem do poder de terceiros          Ё
					//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
					If ((cAliasSB6)->B6_TIPO=="D" .And. (cAliasSB6)->B6_PODER3 == "R" ) .Or. ((cAliasSB6)->B6_TIPO=="E" .And. (cAliasSB6)->B6_PODER3 == "D")
						If (cAliasSB6)->B6_PODER3 == "R"
							dbSelectArea("SD1")
							dbSetOrder(4)
							MsSeek(xFilial("SD1")+(cAliasSB6)->B6_IDENT)
						Else
							dbSelectArea("SD1")
							dbSetOrder(1)
							MsSeek(xFilial("SD1")+(cAliasSB6)->B6_DOC+(cAliasSB6)->B6_SERIE+(cAliasSB6)->B6_CLIFOR+(cAliasSB6)->B6_LOJA+(cAliasSB6)->B6_PRODUTO)
							While !Eof() .And. xFilial("SD1") == SD1->D1_FILIAL .And.;
								(cAliasSB6)->B6_DOC == SD1->D1_DOC .And.;
								(cAliasSB6)->B6_SERIE == SD1->D1_SERIE .And.;
								(cAliasSB6)->B6_CLIFOR == SD1->D1_FORNECE .And.;
								(cAliasSB6)->B6_LOJA == SD1->D1_LOJA .And.;
								(cAliasSB6)->B6_PRODUTO == SD1->D1_COD
								If (cAliasSB6)->B6_IDENT==SD1->D1_IDENTB6 .And. (cAliasSB6)->B6_QUANT=SD1->D1_QUANT
									Exit
								EndIf
								dbSelectArea("SD1")
								dbSkip()
							EndDo
						EndIf
					Else
						If (cAliasSB6)->B6_PODER3=="R"
							dbSelectArea("SD2")
							dbSetOrder(4)
							MsSeek(xFilial("SD2")+(cAliasSB6)->B6_IDENT)
						Else
							dbSelectArea("SD2")
							dbSetOrder(3)
							MsSeek(xFilial("SD2")+(cAliasSB6)->B6_DOC+(cAliasSB6)->B6_SERIE+(cAliasSB6)->B6_CLIFOR+(cAliasSB6)->B6_LOJA+(cAliasSB6)->B6_PRODUTO)
							While !Eof() .And. xFilial("SD2") == SD2->D2_FILIAL .And.;
								(cAliasSB6)->B6_DOC == SD2->D2_DOC .And.;
								(cAliasSB6)->B6_SERIE == SD2->D2_SERIE .And.;
								(cAliasSB6)->B6_CLIFOR == SD2->D2_CLIENTE .And.;
								(cAliasSB6)->B6_LOJA == SD2->D2_LOJA .And.;
								(cAliasSB6)->B6_PRODUTO == SD2->D2_COD
								If (cAliasSB6)->B6_IDENT==SD2->D2_IDENTB6 .And. (cAliasSB6)->B6_QUANT=SD2->D2_QUANT
									Exit
								EndIf
								dbSelectArea("SD2")
								dbSkip()
							EndDo
						EndIf
					EndIf
				EndIf
				//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Calculo do saldo em valor e quantidade para devolucao de terceiros  Ё
				//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
				nSldQtd := 0
				nSldBru := 0
				nSldLiq := 0
				If (cAliasSB6)->B6_PODER3 == "R" .And. (SB6->(FieldPos("B6_IDENTB6"))==0 .Or. Empty((cAliasSB6)->B6_IDENTB6))
					//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					//Ё Na primeira remessa deve-se tirar os valores contidos na interface  Ё
					//Ё para evitar baixa de saldo maior que o disponivel                   Ё
					//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
					If cES == "E"
						For nX := 1 To Len(aCols)
							If nX <> N .And. !aCols[nX][Len(aHeader)+1] .And. aCols[nX][nPIdentB6]==(cAliasSB6)->B6_IDENT
								nSldQtd -= aCols[nX][nPQuant]
								nSldBru -= aCols[nX][nPValor]
							EndIf
						Next nX
					Else
						For nX := 1 To Len(aCols)
							If nX <> N .And. !aCols[nX][Len(aHeader)+1] .And. aCols[nX][nPIdentB6]==(cAliasSB6)->B6_IDENT
								nSldQtd -= aCols[nX][nPQuant]
								nSldLiq -= aCols[nX][nPValor]
								
								//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
								//Ё Desconsidera a quantidade ja faturada                               Ё
								//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
								If !Empty( cNumPV )
									SC6->( dbSetOrder( 1 ) )
									If SC6->( MsSeek( xFilial( "SC6" ) + cNumPv + aCols[nX, nPItem ] ) )
										nSldQtd += SC6->C6_QTDENT
										nSldLiq += aCols[nX,nPUnit] * SC6->C6_QTDENT
										nSldLiq := A410Arred( nSldLiq, "C6_VALOR" )
									EndIf
								EndIf
								
							EndIf
						Next nX
					EndIf
					//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					//Ё Calculo do saldo do poder de terceiros                              Ё
					//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
					nSldQtd  += (cAliasSB6)->B6_QUANT-(cAliasSB6)->B6_QULIB
				Else
					//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					//Ё Calculo do saldo do poder de terceiros                              Ё
					//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
					nSldQtd  -= (cAliasSB6)->B6_QUANT-(cAliasSB6)->B6_QULIB
				EndIf
				//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Verificar qual eh a tabela de origem do poder de terceiros e calculaЁ
				//Ё o valor total do saldo de poder de/em terceiros                     Ё
				//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
				If ((cAliasSB6)->B6_TIPO=="D" .And. (cAliasSB6)->B6_PODER3 == "R" ) .Or. ((cAliasSB6)->B6_TIPO=="E" .And. (cAliasSB6)->B6_PODER3 == "D")
					If (cAliasSB6)->B6_PODER3 == "R"
						nSldLiq += (cAliasSD1)->D1_TOTAL-(cAliasSD1)->D1_VALDESC
						nSldBru += nSldLiq+A410Arred(nSldLiq*(cAliasSD1)->D1_VALDESC/((cAliasSD1)->D1_TOTAL-(cAliasSD1)->D1_VALDESC),"C6_VALOR")
					Else
						nSldLiq -= (cAliasSD1)->D1_TOTAL-(cAliasSD1)->D1_VALDESC
						nSldBru -= Abs(nSldLiq)+A410Arred(Abs(nSldLiq)*(cAliasSD1)->D1_VALDESC/((cAliasSD1)->D1_TOTAL-(cAliasSD1)->D1_VALDESC),"C6_VALOR")
					EndIf
				Else
					If (cAliasSB6)->B6_PODER3 == "R"
						nSldBru += (cAliasSD2)->D2_TOTAL+(cAliasSD2)->D2_DESCON
						nSldLiq += nSldBru-A410Arred(nSldBru*(cAliasSD2)->D2_DESCON/((cAliasSD2)->D2_TOTAL+(cAliasSD2)->D2_DESCON),"C6_VALOR")
					Else
						nSldBru -= (cAliasSD2)->D2_TOTAL+(cAliasSD2)->D2_DESCON
						nSldLiq -= Abs(nSldBru)-A410Arred(Abs(nSldBru)*(cAliasSD2)->D2_DESCON/((cAliasSD2)->D2_TOTAL+(cAliasSD2)->D2_DESCON),"C6_VALOR")
					EndIf
				EndIf
				//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Atualiza o arquivo temporario com os dados do poder de terceiro     Ё
				//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
				dbSelectArea(cAliasTRB)
				dbSetOrder(3)
				If nSldQtd <> 0 .Or. nSldLiq <> 0
					If !Empty((cAliasSB6)->B6_IDENTB6)
						(cAliasTRB)->(MsSeek((cAliasSB6)->B6_IDENTB6))
					Else
						(cAliasTRB)->(MsSeek((cAliasSB6)->B6_IDENT))
					EndIf
					If (cAliasTRB)->(!Found())
						RecLock(cAliasTRB,.T.)
						For nX := 1 To Len(aStruTRB)
							If !AllTrim(aStruTRB[nX][1])$"B6_SALDO#B6_TOTALL#B6_TOTALB#B6_QULIB"
								If (cAliasSB6)->(FieldPos(aStruTRB[nX][1]))<>0 .And. (cAliasTrb)->(FieldPos(aStruTRB[nX][1]))<>0
									(cAliasTRB)->(FieldPut(nX,(cAliasSB6)->(FieldGet(FieldPos(aStruTRB[nX][1])))))
								EndIf
							EndIf
						Next nX
					Else
						RecLock(cAliasTRB)
					EndIf
					//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					//Ё Verifica o documento original para obter alguns dados posteriores   Ё
					//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
					If (cAliasSB6)->B6_PODER3 == "R" .And. (SB6->(FieldPos("B6_IDENTB6"))==0 .Or. Empty((cAliasSB6)->B6_IDENTB6))
						For nX := 1 To Len(aStruTRB)
							If !AllTrim(aStruTRB[nX][1])$"B6_SALDO#B6_TOTALL#B6_TOTALB#B6_QULIB"
								If (cAliasSB6)->(FieldPos(aStruTRB[nX][1]))<>0 .And. (cAliasTrb)->(FieldPos(aStruTRB[nX][1]))<>0
									(cAliasTRB)->(FieldPut(nX,(cAliasSB6)->(FieldGet(FieldPos(aStruTRB[nX][1])))))
								EndIf
							EndIf
						Next nX
						If (cAliasSB6)->B6_TIPO=="D"
							(cAliasTRB)->SD1RECNO := IIf(lQuery,(cAliasSD1)->SD1RECNO,SD1->(RecNo()))
						Else
							(cAliasTRB)->SD2RECNO := IIf(lQuery,(cAliasSD2)->SD2RECNO,SD2->(RecNo()))
						EndIf
					EndIf
					(cAliasTRB)->B6_SALDO += a410Arred(nSldQtd,"C6_QTDVEN")
					(cAliasTRB)->B6_QULIB += a410Arred((cAliasSB6)->B6_QULIB,"C6_QTDVEN")
					(cAliasTRB)->B6_TOTALL+= nSldLiq
					(cAliasTRB)->B6_TOTALB+= nSldBru
					//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					//Ё Calcula o valor unitario do poder de terceiros                      Ё
					//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
					(cAliasTRB)->B6_PRCVEN:= a410Arred((cAliasTRB)->B6_TOTALL/((cAliasTRB)->B6_SALDO+(cAliasTRB)->B6_QULIB),"D2_PRCVEN")
					(cAliasTRB)->B6_PRUNIT:= a410Arred((cAliasTRB)->B6_TOTALB/((cAliasTRB)->B6_SALDO+(cAliasTRB)->B6_QULIB),"D2_PRCVEN")
					If cES == "E"
						(cAliasTRB)->D2_LOTECTL:= (cAliasSD2)->D2_LOTECTL
						(cAliasTRB)->D2_NUMLOTE:= (cAliasSD2)->D2_NUMLOTE
					Else
						(cAliasTRB)->D1_LOTECTL:= (cAliasSD1)->D1_LOTECTL
						(cAliasTRB)->D1_NUMLOTE:= (cAliasSD1)->D1_NUMLOTE
					EndIf
					MsUnLock()
				EndIf
			EndIf
		EndIf
		dbSelectArea(cAliasSB6)
		dbSkip()
	EndDo
EndDo
If lQuery
	dbSelectArea(cAliasSB6)
	dbCloseArea()
	dbSelectArea("SB6")
EndIf
//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Retira os documentos totalmente devolvidos                          Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
dbSelectArea(cAliasTRB)
dbClearIndex()
dbGotop()
While !Eof()
	If (cAliasTRB)->B6_SALDO<=0
		dbDelete()
	EndIf
	dbSkip()
EndDo
Pack
aNomInd := {}
For nX := 1 To Len(aChave)
	aadd(aNomInd,SubStr(cNomeTrb,1,7)+chr(64+nX))
	IndRegua(cAliasTRB,aNomInd[nX],aChave[nX])
Next nX
dbClearIndex()
For nX := 1 To Len(aNomInd)
	dbSetIndex(aNomInd[nX])
Next nX
dbSetOrder(1)
dbGotop()
PRIVATE aHeader := aHeadTRB
xPesq := aPesq[(cAliasTRB)->(IndexOrd())][1]
//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Posiciona registros                                                 Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If cTpCliFor == "C"
	dbSelectArea("SA1")
	dbSetOrder(1)
	MsSeek(xFilial("SA1")+cCliFor+cLoja)
Else
	dbSelectArea("SA2")
	dbSetOrder(1)
	MsSeek(xFilial("SA2")+cCliFor+cLoja)
EndIf
dbSelectArea("SB1")
dbSetOrder(1)
MsSeek(xFilial("SB1")+cProduto)
//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Calcula as coordenadas da interface                                 Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
aSize[1] /= 1.5
aSize[2] /= 1.5
aSize[3] /= 1.5
aSize[4] /= 1.3
aSize[5] /= 1.5
aSize[6] /= 1.3
aSize[7] /= 1.5

AAdd( aObjects, { 100, 020,.T.,.F.,.T.} )
AAdd( aObjects, { 100, 060,.T.,.T.} )
AAdd( aObjects, { 100, 020,.T.,.F.} )
aInfo   := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 3, 3 }
aPosObj := MsObjSize( aInfo, aObjects,.T.)

//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Interface com o usuario                                             Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If !(cAliasTRB)->(Eof())
	DEFINE MSDIALOG oDlg TITLE OemToAnsi(STR0075) FROM aSize[7],000 TO aSize[6],aSize[5] OF oMainWnd PIXEL //"Documentos de Origem"
	@ aPosObj[1,1],aPosObj[1,2] MSPANEL oPanel PROMPT "" SIZE aPosObj[1,3],aPosObj[1,4] OF oDlg CENTERED LOWERED
	If !IsTriangular()
		If cTpCliFor == "C"
			cTexto1 := AllTrim(RetTitle("F2_CLIENTE"))+"/"+AllTrim(RetTitle("F2_LOJA"))+": "+SA1->A1_COD+"/"+SA1->A1_LOJA+"  -  "+RetTitle("A1_NOME")+": "+SA1->A1_NOME
		Else
			cTexto1 := AllTrim(RetTitle("F1_FORNECE"))+"/"+AllTrim(RetTitle("F1_LOJA"))+": "+SA2->A2_COD+"/"+SA2->A2_LOJA+"  -  "+RetTitle("A2_NOME")+": "+SA2->A2_NOME
		EndIf
	Else
		cTexto1 := STR0076 //"Operacao Triangular"
	EndIf
	@ 002,005 SAY cTexto1 SIZE aPosObj[1,3],008 OF oPanel PIXEL
	cTexto2 := AllTrim(RetTitle("B1_COD"))+": "+SB1->B1_COD+"/"+SB1->B1_DESC
	@ 012,005 SAY cTexto2 SIZE aPosObj[1,3],008 OF oPanel PIXEL
	oGetDb := MsGetDB():New(aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],1,"Allwaystrue","allwaystrue","",.F., , ,.F., ,cAliasTRB)

	DEFINE SBUTTON FROM aPosObj[3,1]+000,aPosObj[3,4]-030 TYPE 1 ACTION (nOpcA := 1,oDlg:End()) ENABLE OF oDlg
	DEFINE SBUTTON FROM aPosObj[3,1]+012,aPosObj[3,4]-030 TYPE 2 ACTION (nOpcA := 0,oDlg:End()) ENABLE OF oDlg

	@ aPosObj[3,1]+00,aPosObj[3,2]+00 SAY OemToAnsi(STR0077) PIXEL //"Pesquisar por:"
	@ aPosObj[3,1]+12,aPosObj[3,2]+00 SAY OemToAnsi(STR0078) PIXEL //"Localizar"
	@ aPosObj[3,1]+00,aPosObj[3,2]+40 MSCOMBOBOX oCombo VAR cCombo ITEMS aOrdem SIZE 100,044 OF oDlg PIXEL ;
	VALID ((cAliasTRB)->(dbSetOrder(oCombo:nAt)),(cAliasTRB)->(dbGotop()),xPesq := aPesq[(cAliasTRB)->(IndexOrd())][1],.T.)
	@ aPosObj[3,1]+12,aPosObj[3,2]+40 MSGET oGet VAR xPesq Of oDlg PICTURE aPesq[(cAliasTRB)->(IndexOrd())][2] PIXEL ;
	VALID ((cAliasTRB)->(MsSeek(Iif(ValType(xPesq)=="C",AllTrim(xPesq),xPesq),.T.)),.T.).And.IIf(oGetDb:oBrowse:Refresh()==Nil,.T.,.T.)
	ACTIVATE MSDIALOG oDlg CENTERED
Else
	Help(" ",1,"F4NAONOTA")
	lRetorno := .F.
EndIf
If nOpcA == 1
	lRetorno := .T.
	aHeader   := aClone(aSavHead)
	If cES == "S"
		//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Verifica os campos a serem atualizados                              Ё
		//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		nPNfOri   := aScan(aHeader,{|x| AllTrim(x[2])=="C6_NFORI"})
		nPSerOri  := aScan(aHeader,{|x| AllTrim(x[2])=="C6_SERIORI"})
		nPItemOri := aScan(aHeader,{|x| AllTrim(x[2])=="C6_ITEMORI"})
		nPLocal   := aScan(aHeader,{|x| AllTrim(x[2])=="C6_LOCAL"})
		nPPrUnit  := aScan(aHeader,{|x| AllTrim(x[2])=="C6_PRUNIT"})
		nPPrcVen  := aScan(aHeader,{|x| AllTrim(x[2])=="C6_PRCVEN"})
		nPQuant   := aScan(aHeader,{|x| AllTrim(x[2])=="C6_QTDVEN"})
		nPQuant2UM:= aScan(aHeader,{|x| AllTrim(x[2])=="C6_UNSVEN"})
		nPLoteCtl := aScan(aHeader,{|x| AllTrim(x[2])=="C6_LOTECTL"})
		nPNumLote := aScan(aHeader,{|x| AllTrim(x[2])=="C6_NUMLOTE"})
		nPDtValid := aScan(aHeader,{|x| AllTrim(x[2])=="C6_DTVALID"})
		nPPotenc  := aScan(aHeader,{|x| AllTrim(x[2])=="C6_POTENCI"})
		nPValor   := aScan(aHeader,{|x| AllTrim(x[2])=="C6_VALOR"})
		nPValDesc := aScan(aHeader,{|x| AllTrim(x[2])=="C6_VALDESC"})
		nPIdentB6 := aScan(aHeader,{|x| AllTrim(x[2])=="C6_IDENTB6"})
		//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Posiciona registros                                                 Ё
		//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		SD1->(MsGoto((cAliasTRB)->SD1RECNO))
		SF4->(dbSetOrder(1))
		SF4->(MsSeek(xFilial("SF4")+aCols[n][nPTES]))
		//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Preenche acols                                                      Ё
		//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If nPIdentB6 <> 0
			aCols[N][nPIdentB6] := (cAliasTRB)->B6_IDENT
		EndIf
		If nPNfOri <> 0
			aCols[N][nPNfOri] := SD1->D1_DOC
		EndIf
		If nPSerOri <> 0
			aCols[N][nPSerOri] := SD1->D1_SERIE
		EndIf
		If nPItemOri <> 0
			aCols[N][nPItemOri] := SD1->D1_ITEM
		EndIf
		If nPPrUnit <> 0
			If (cAliasTRB)->B6_PRUNIT == (cAliasTRB)->B6_PRCVEN .And. Abs((cAliasTRB)->B6_PRCVEN-SD1->D1_VUNIT)<=.01
				aCols[N][nPPrUnit] := 0
			Else
				aCols[N][nPPrUnit] := A410Arred((cAliasTRB)->B6_PRUNIT,"C6_PRUNIT")
			EndIf
		EndIf
		If nPPrcVen <> 0
			If (cAliasTRB)->B6_PRUNIT == (cAliasTRB)->B6_PRCVEN .And. Abs((cAliasTRB)->B6_PRCVEN-SD1->D1_VUNIT)<=.01
				aCols[N][nPPrcVen] := A410Arred(SD1->D1_VUNIT,"C6_PRCVEN")
			Else
				aCols[N][nPPrcVen] := A410Arred((cAliasTRB)->B6_PRCVEN,"C6_PRCVEN")
			EndIf
		EndIf
		If nPQuant <> 0 .And. (aCols[N][nPQuant] > (cAliasTRB)->B6_SALDO .Or. aCols[N][nPQuant] == 0 )
			aCols[N][nPQuant] := Min((cAliasTRB)->B6_SALDO,A410SNfOri(cCliFor,cLoja,SD1->D1_DOC,SD1->D1_SERIE,"",SD1->D1_COD,(cAliasTRB)->B6_IDENT,aCols[n][nPosLocal])[1])
			If nPQuant2UM <> 0
				aCols[N][nPQuant2UM] := ConvUm(cProduto,aCols[N][nPQuant],0,2)
			EndIf
		EndIf
		If Rastro(cProduto) .And. SF4->F4_ESTOQUE=="S"
			If nPLoteCtl <> 0
				aCols[N][nPLoteCtl] := SD1->D1_LOTECTL
			EndIf
			If nPNumLote <> 0
				aCols[N][nPNumLote] := SD1->D1_NUMLOTE
			EndIf
			If nPDtValid <> 0 .Or. nPPotenc <> 0
				dbSelectArea("SB8")
				dbSetOrder(3)
				If MsSeek(xFilial("SB8")+cProduto+aCols[N][nPLocal]+aCols[n][nPLoteCtl]+IIf(Rastro(cProduto,"S"),aCols[N][nPNumLote],""))
					If nPDtValid <> 0
						aCols[n][nPDtValid] := SB8->B8_DTVALID
					EndIf
					If nPPotenc <> 0
						aCols[n][nPPotenc] := SB8->B8_POTENCI
					EndIf
				EndIf
			EndIf
		EndIf
		A410MultT("C6_QTDVEN",aCols[N,nPQuant])
		A410MultT("C6_PRCVEN",aCols[N,nPPrcVen])
		If nPValDesc <> 0 .And. nPPrUnit > 0
			If aCols[n][nPPrUnit]<>0
				aCols[n][nPValDesc] := a410Arred((aCols[n][nPPrUnit]-aCols[n][nPPrcVen])*IIf(aCols[n][nPQuant]==0,1,aCols[n][nPQuant]),"C6_VALDESC")
				A410MultT("C6_VALDESC",aCols[n][nPValDesc])
			EndIf
		EndIf
		If nPLocal <> 0
			aCols[N][nPLocal] := SD1->D1_LOCAL
			// Pesquisa os armazens dos movimentos do controle de qualidade
			If SD1->D1_LOCAL == cLocalCQ
				// Monta array com os armazens tratados na movimentacao do CQ
				cSeekSD7   := xFilial('SD7')+SD1->D1_NUMCQ+SD1->D1_COD+SD1->D1_LOCAL
				SD7->(dbSetOrder(1))
				SD7->(dbSeek(cSeekSD7))
				Do While !SD7->(Eof()) .And. cSeekSD7 == SD7->D7_FILIAL+SD7->D7_NUMERO+SD7->D7_PRODUTO+SD7->D7_LOCAL
					If SD7->D7_TIPO >= 1 .And. SD7->D7_TIPO <= 2 .And. SD7->D7_ESTORNO # 'S'
						If aScan(aArmazensCQ,SD7->D7_LOCDEST) == 0
							AADD(aArmazensCQ,SD7->D7_LOCDEST)
						EndIf
					EndIf
					SD7->(dbSkip())
				EndDo
				// Monta texto para apresentacao no combobox
				If Len(aArmazensCQ) > 1
					nOpca:=0
					For nx:=1 to Len(aArmazensCQ)
						AADD(aTextoCQ,OemToAnsi(STR0046)+" "+aArmazensCQ[nx])
					Next nx
					DEFINE MSDIALOG oDlg TITLE OemToAnsi(STR0084) From 130,70 To 270,360 OF oMainWnd PIXEL
					@ 05,13 SAY OemToAnsi(STR0085) OF oDlg PIXEL SIZE 110,9
					@ 17,13 TO 42,122 LABEL "" OF oDlg  PIXEL
					@ 23,17 MSCOMBOBOX oCombo VAR cCombo ITEMS aTextoCQ SIZE 100,044 OF oDlg PIXEL ON CHANGE (cLocalCQ:=aArmazensCQ[oCombo:nAt])
					DEFINE SBUTTON FROM 50,072 TYPE 1 Action (nOpca:=1,oDlg:End()) ENABLE OF oDlg PIXEL
					DEFINE SBUTTON FROM 50,099 TYPE 2 ACTION oDlg:End() ENABLE OF oDlg PIXEL
					ACTIVATE MSDIALOG oDlg
					// Utiliza armazem relacionado ao movimento do CQ
					If nOpca == 1
						aCols[N][nPLocal] := cLocalCQ
					EndIf
				ElseIf Len(aArmazensCQ) > 0
					aCols[N][nPLocal] := aArmazensCQ[1]
				EndIf
			EndIf
		EndIf
	Else
		//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Verifica os campos a serem atualizados                              Ё
		//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		nPNfOri   := aScan(aHeader,{|x| AllTrim(x[2])=="D1_NFORI"})
		nPSerOri  := aScan(aHeader,{|x| AllTrim(x[2])=="D1_SERIORI"})
		nPItemOri := aScan(aHeader,{|x| AllTrim(x[2])=="D1_ITEMORI"})
		nPLocal   := aScan(aHeader,{|x| AllTrim(x[2])=="D1_LOCAL"})
		nPPrcVen  := aScan(aHeader,{|x| AllTrim(x[2])=="D1_VUNIT"})
		nPQuant   := aScan(aHeader,{|x| AllTrim(x[2])=="D1_QUANT"})
		nPQuant2UM:= aScan(aHeader,{|x| AllTrim(x[2])=="D1_QTSEGUM"})
		nPLoteCtl := aScan(aHeader,{|x| AllTrim(x[2])=="D1_LOTECTL"})
		nPNumLote := aScan(aHeader,{|x| AllTrim(x[2])=="D1_NUMLOTE"})
		nPDtValid := aScan(aHeader,{|x| AllTrim(x[2])=="D1_DTVALID"})
		nPPotenc  := aScan(aHeader,{|x| AllTrim(x[2])=="D1_POTENCI"})
		nPValor   := aScan(aHeader,{|x| AllTrim(x[2])=="D1_TOTAL"})
		nPValDesc := aScan(aHeader,{|x| AllTrim(x[2])=="D1_VALDESC"})
		nPDesc    := aScan(aHeader,{|x| AllTrim(x[2])=="D1_DESC"})
		nPIdentB6 := aScan(aHeader,{|x| AllTrim(x[2])=="D1_IDENTB6"})
		//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Posiciona registros                                                 Ё
		//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		SD2->(MsGoto((cAliasTRB)->SD2RECNO))
		SF4->(dbSetOrder(1))
		SF4->(MsSeek(xFilial("SF4")+aCols[n][nPTES]))
		nRegistro := (cAliasTRB)->SD2RECNO
		//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Preenche acols                                                      Ё
		//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If nPIdentB6 <> 0
			//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Libera a trava obtida                                               Ё
			//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			If FindFunction( "LEAVE1CODE" )
				cAntB6Ident := aCols[ n, nPIdentB6 ]
				If !Empty( cAntB6Ident ) .And. cAntB6Ident <> (cAliasTRB)->B6_IDENT
					Leave1Code( "SD1_D1_IDENTB6" + cAntB6Ident )
				EndIf
			EndIf

			aCols[N][nPIdentB6] := (cAliasTRB)->B6_IDENT
		EndIf
		If nPNfOri <> 0
			aCols[N][nPNfOri] := SD2->D2_DOC
		EndIf
		If nPSerOri <> 0
			aCols[N][nPSerOri] := SD2->D2_SERIE
		EndIf
		If nPItemOri <> 0
			aCols[N][nPItemOri] := SD2->D2_ITEM
		EndIf
		If nPLocal <> 0
			aCols[N][nPLocal] := SD2->D2_LOCAL
		EndIf
		If nPPrcVen <> 0
			If (cAliasTRB)->B6_PRUNIT == (cAliasTRB)->B6_PRCVEN .And. Abs((cAliasTRB)->B6_PRCVEN-SD2->D2_PRCVEN)<=.01
				aCols[N][nPPrcVen] := A410Arred(SD2->D2_PRCVEN,"D1_VUNIT")
			Else
				aCols[N][nPPrcVen] := A410Arred((cAliasTRB)->B6_PRUNIT,"D1_VUNIT")
			EndIf
		EndIf
		If nPQuant <> 0 .And. ( aCols[N][nPQuant] > (cAliasTRB)->B6_SALDO .Or. aCols[N][nPQuant]==0 )
			aCols[N][nPQuant] := (cAliasTRB)->B6_SALDO
			If nPQuant2UM <> 0
				aCols[N][nPQuant2UM] := ConvUm(cProduto,aCols[N][nPQuant],0,2)
			EndIf
		EndIf
		If Rastro(cProduto) .And. SF4->F4_ESTOQUE=="S"
			If nPLoteCtl <> 0
				aCols[N][nPLoteCtl] := SD2->D2_LOTECTL
			EndIf
			If nPNumLote <> 0
				aCols[N][nPNumLote] := SD2->D2_NUMLOTE
			EndIf
			If nPDtValid <> 0 .Or. nPPotenc <> 0
				dbSelectArea("SB8")
				dbSetOrder(3)
				If MsSeek(xFilial("SB8")+cProduto+aCols[N][nPLocal]+aCols[n][nPLoteCtl]+IIf(Rastro(cProduto,"S"),aCols[N][nPNumLote],""))
					If nPDtValid <> 0
						aCols[n][nPDtValid] := SB8->B8_DTVALID
					EndIf
					If nPPotenc <> 0
						aCols[n][nPPotenc] := SB8->B8_POTENCI
					EndIf
				EndIf
			EndIf
		EndIf
		If nPValDesc <> 0 .And. nPQuant <> 0 .And. nPDesc <> 0
			aCols[n][nPValDesc] := a410Arred(((cAliasTRB)->B6_PRUNIT-(cAliasTRB)->B6_PRCVEN)*IIf(aCols[n][nPQuant]==0,1,aCols[n][nPQuant]),"D1_VALDESC")
		EndIf
		aCols[n][nPValor] := a410Arred(IIf(aCols[n][nPQuant]==0,1,aCols[n][nPQuant])*aCols[n][nPPrcVen],"D1_TOTAL")
	EndIf
	If !Empty(cReadVar)
		
		Do Case
			Case cReadVar $ "M->C6_QTDVEN"
				&(cReadVar) := aCols[n][nPQuant]
			Case cReadVar $ "M->C6_UNSVEN"
				&(cReadVar) := aCols[n][nPQuant2UM]
			OtherWise
				&(cReadVar) := aCols[n][nPQuant]
		EndCase
	EndIf
EndIf
//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Restaura a integridade da rotina                                    Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
dbSelectArea(cAliasTRB)
dbCloseArea()
RestArea(aArea)
SetFocus(nHandle)
Return(lRetorno)

/*/
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбддддддддддд©╠╠
╠╠ЁFun┤└o    ЁMA410FluxoЁ Autor Ё Eduardo Riera         Ё Data Ё12.07.2005 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддаддддддддддд╢╠╠
╠╠Ё          ЁFuncao de calculo do fluxo de caixa para o pedido de venda   Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁExpC1: Numero do Pedido                                      Ё╠╠
╠╠Ё          ЁExpL2: Indica se deve tratar a condicao de pagamento    (OPC)Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁExpA1:[1] Data de vencimento                                 Ё╠╠
╠╠Ё          Ё      [2] Valor para o fluxo                                 Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁDescri┤└o ЁEsta funcao efetua o calculo do valor do pedido de venda paraЁ╠╠
╠╠Ё          Ёo fluxo de caixa.                                            Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё Generico                                                    Ё╠╠
╠╠юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function Ma410Fluxo(cNumPv,lSoma)

Local aArea		:= GetArea()
Local aAreaSA1	:= SA1->(GetArea())
Local aAreaSD1  := SD1->(GetArea())
Local aImpostos := {}
Local aFluxo    := {}
Local aFluxoTmp := {}
Local aFisGet	:= {}
Local aFisGetSC5:= {}
Local aEntr     := {}
Local nX        := 0
Local nY        := 0
Local nZ        := 0
Local nAcerto   := 0
Local nPrcLista := 0
Local nPSuframa := 0
Local nValMerc  := 0
Local nDesconto := 0
Local nAcresFin := 0
Local nQtdPeso  := 0
Local nRecOri   := 0
Local nPosEntr  := 0
Local nItem     := 0
Local cAliasSC6 := "SC6"
Local lQuery    := .F.
Local nTotDesc  := 0
Local lM410Ipi	:= ExistBlock("M410IPI")
Local dData		:= dDataBase    
Local lNaoGravaAID:= .F.  
Local aTransp	:= {"",""}
#IFDEF TOP
	Local cQuery   := ""
	Local aStruSC6 := {}
#ENDIF
Local nAcresUnit:= 0	// Valor do acrescimo financeiro do valor unitario
Local nAcresTot := 0	// Somatoria dos Valores dos acrescimos financeiros dos itens
Local nlValor	:= 0
Local cImpRet 	 := ""
Local nValRetImp := 0

DEFAULT lSoma   := .F.

//зддддддддддддддддддддддддддддддддддддддддддддд©
//ЁVerifica se foi calculado                    Ё
//юддддддддддддддддддддддддддддддддддддддддддддды
If AliasInDic("AID") .and. ( ProcName(2) <> "FC021PROC" )
	dbSelectArea("AID")
	dbSetOrder(1)
	If MsSeek(xFilial("AID")+cNumPv)
		While !Eof() .And. xFilial("AID") == AID->AID_FILIAL .And. cNumPv == AID->AID_NUMPV
			aadd(aFluxo,{AID->AID_DATA,AID->AID_VALOR})
			dbSelectArea("AID")
			dbSkip()
	    EndDo
	EndIf
EndIf            

If !Empty(aFluxo)
	#IFDEF TOP
			lQuery    := .T.
			cAliasSC6 := "Ma410Fl"
			aStruSC6  := SC6->(dbStruct())
			cQuery    := "SELECT * "
			cQuery    += "FROM "+RetSqlName("SC6")+" SC6 "
			cQuery    += "WHERE SC6.C6_FILIAL='"+xFilial("SC6")+"' AND "
			cQuery    += "SC6.C6_NUM='"+cNumPV+"' AND "
			cQuery    += "SC6.C6_BLQ NOT IN('R ','S ') AND "
		  	cQuery 	  += " SC6.C6_ENTREG < '" + DtoS(dDataBase) +"' AND "
			cQuery    += "SC6.D_E_L_E_T_=' ' "

			cQuery := ChangeQuery(cQuery)
			dbUseArea(.T.,"TOPCONN",cAliasSC6,TcGenQry(,,cQuery))

			For nX := 1 To Len(aStruSC6)
				If aStruSC6[nX][2] <> "C" .And. aStruSC6[nX][2] <> "M"
					TcSetField(cAliasSC6,aStruSC6[nX][1],aStruSC6[nX][2],aStruSC6[nX][3],aStruSC6[nX][4])
				EndIf
			Next nX
	#ELSE
			MsSeek(xFilial("SC6")+cNumPV)
	#ENDIF

	While !Eof() .And. xFilial("SC6") == (cAliasSC6)->C6_FILIAL .And. cNumPV == (cAliasSC6)->C6_NUM .And. !lNaoGravaAID
		//Se tiver C6 atrasado, zera o aFluxo e seta
		If (cAliasSC6)->C6_ENTREG < dDataBase
			lNaoGravaAID	:=	.T.
       		aFluxo	:=	{}
   		EndiF
		(cAliasSC6)->( DbSkip() )
	EndDO
	
	If lQuery
		dbSelectArea(cAliasSC6)
		dbCloseArea()
		dbSelectArea("SC6")
		lQuery:=.F.
	EndIf
Endif
//зддддддддддддддддддддддддддддддддддддддддддддд©
//ЁPosiciona o Pedido de Venda                  Ё
//юддддддддддддддддддддддддддддддддддддддддддддды
SA4->(dbSetOrder(1))
dbSelectArea("SC5")
dbSetOrder(1)  
If Empty(aFluxo) .And. MsSeek(xFilial("SC5")+cNumPV)  .And. !SC5->C5_TIPO$"DB"
	//If SoftLock("SC5")
		//зддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁBusca as referencias fiscais                 Ё
		//юддддддддддддддддддддддддддддддддддддддддддддды
		aFisGet	:= MaFisRelImp("MATA461",{"SC6"})
		aSort(aFisGet,,,{|x,y| x[3]<y[3]})
		//зддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁBusca referencias no SC5                     Ё
		//юддддддддддддддддддддддддддддддддддддддддддддды
		aFisGetSC5	:= MaFisRelImp("MATA461",{"SC5"})
		aSort(aFisGetSC5,,,{|x,y| x[3]<y[3]})

		//здддддддддддддддддддддддддд©
		//ЁPosiciona a trasnportadoraЁ
		//юдддддддддддддддддддддддддды
		If SA4->(dbSeek(xFilial("SA4")+SC5->C5_TRANSP))
			aTransp[01] := SA4->A4_EST
			aTransp[02] := Iif(SA4->(FieldPos("A4_TPTRANS")) > 0,SA4->A4_TPTRANS,"")
		Endif
		
		//зддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁInicializa a funcao fiscal                   Ё
		//юддддддддддддддддддддддддддддддддддддддддддддды
		MaFisSave()
		MaFisEnd()
		MaFisIni(Iif(Empty(SC5->C5_CLIENT),SC5->C5_CLIENTE,SC5->C5_CLIENT),;
			SC5->C5_LOJAENT,;
			IIf(SC5->C5_TIPO$'DB',"F","C"),;
			SC5->C5_TIPO,;
			SC5->C5_TIPOCLI,;
			Nil,;
			Nil,;
			Nil,;
			Nil,;
			"MATA461",; 
			Nil,;
			Nil,;
			Nil,;
			Nil,;
			Nil,;
			Nil,;
			Nil,;
			aTransp)

		//зддддддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁRealiza alteracoes de referencias do SC5         Ё
		//юддддддддддддддддддддддддддддддддддддддддддддддддды
		If Len(aFisGetSC5) > 0
			dbSelectArea("SC5")
			For nX := 1 to Len(aFisGetSC5)
				If !Empty(&(aFisGetSC5[nX][2]))
					MaFisAlt(aFisGetSC5[nX][1],&(aFisGetSC5[nX][2]),nItem,.T.)
				EndIf
			Next nX
		EndIf

		If cPaisLoc == 'ARG'
			SA1->(DbSetOrder(1))
			SA1->(MsSeek(xFilial()+IIf(!Empty(SC5->C5_CLIENT),SC5->C5_CLIENT,SC5->C5_CLIENTE)+SC5->C5_LOJAENT))
			MaFisAlt('NF_SERIENF',LocXTipSer('SA1',MVNOTAFIS))
		EndIf
		//зддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁAgrega os itens para a funcao fiscal         Ё
		//юддддддддддддддддддддддддддддддддддддддддддддды
		dbSelectArea("SC6")
		dbSetOrder(1)
		#IFDEF TOP
			lQuery    := .T.
			cAliasSC6 := "Ma410Fluxo"
			aStruSC6  := SC6->(dbStruct())
			cQuery    := "SELECT * "
			cQuery    += "FROM "+RetSqlName("SC6")+" SC6 "
			cQuery    += "WHERE SC6.C6_FILIAL='"+xFilial("SC6")+"' AND "
			cQuery    += "SC6.C6_NUM='"+cNumPV+"' AND "
			cQuery    += "SC6.C6_BLQ NOT IN('R ','S ') AND "
			cQuery    += "SC6.D_E_L_E_T_=' ' "

			cQuery := ChangeQuery(cQuery)
			dbUseArea(.T.,"TOPCONN",cAliasSC6,TcGenQry(,,cQuery))

			For nX := 1 To Len(aStruSC6)
				If aStruSC6[nX][2] <> "C" .And. aStruSC6[nX][2] <> "M"
					TcSetField(cAliasSC6,aStruSC6[nX][1],aStruSC6[nX][2],aStruSC6[nX][3],aStruSC6[nX][4])
				EndIf
			Next nX
		#ELSE
			MsSeek(xFilial("SC6")+cNumPV)
		#ENDIF
		While !Eof() .And. xFilial("SC6") == (cAliasSC6)->C6_FILIAL .And. cNumPV == (cAliasSC6)->C6_NUM
			If !Substr((cAliasSc6)->C6_BLQ,1,1) $"RS" .And. Empty((cAliasSc6)->C6_BLOQUEI)
				nItem++
				If !Empty((cAliasSC6)->C6_NFORI) .And. !Empty((cAliasSC6)->C6_ITEMORI)
					SD1->(dbSetOrder(1))
					If SD1->(MSSeek(xFilial("SD1")+(cAliasSC6)->C6_NFORI+(cAliasSC6)->C6_SERIORI+SC5->C5_CLIENTE+SC5->C5_LOJACLI+(cAliasSC6)->C6_PRODUTO+(cAliasSC6)->C6_ITEMORI))
						nRecOri := SD1->(Recno())
					Endif
				Endif

				//зддддддддддддддддддддддддддддддддддддддддддддд©
				//ЁCalcula o preco de lista                     Ё
				//юддддддддддддддддддддддддддддддддддддддддддддды
				nValMerc  := IIf((cAliasSC6)->C6_QTDVEN==0,(cAliasSC6)->C6_VALOR,((cAliasSC6)->C6_QTDVEN-(cAliasSC6)->C6_QTDENT)*(cAliasSC6)->C6_PRCVEN)
				nPrcLista := (cAliasSC6)->C6_PRUNIT
				If ( nPrcLista == 0 )
					nPrcLista := NoRound(nValMerc/IIf((cAliasSC6)->C6_QTDVEN==0,(cAliasSC6)->C6_VALOR,((cAliasSC6)->C6_QTDVEN-(cAliasSC6)->C6_QTDENT)),TamSX3("C6_PRCVEN")[2])
				EndIf
				
				nAcresUnit:= A410Arred((cAliasSC6)->C6_PRCVEN*SC5->C5_ACRSFIN/100,"D2_PRCVEN")
				nAcresFin := A410Arred(((cAliasSC6)->C6_QTDVEN-(cAliasSC6)->C6_QTDENT)*nAcresUnit,"D2_TOTAL")
				nAcresTot += nAcresFin
				nValMerc  += nAcresFin
				nDesconto := a410Arred(nPrcLista*((cAliasSC6)->C6_QTDVEN-(cAliasSC6)->C6_QTDENT),"D2_DESCON")-nValMerc
				nDesconto := IIf(nDesconto==0,(cAliasSC6)->C6_VALDESC,nDesconto)				
				nDesconto := Max(0,nDesconto)
				nPrcLista += nAcresUnit
					
				If cPaisLoc=="BRA"
					nValMerc  += nDesconto
				EndIf
				//зддддддддддддддддддддддддддддддддддддддддддддд©
				//ЁAgrega os itens para a funcao fiscal         Ё
				//юддддддддддддддддддддддддддддддддддддддддддддды
				MaFisAdd((cAliasSC6)->C6_PRODUTO,;   	// 1-Codigo do Produto ( Obrigatorio )
							(cAliasSC6)->C6_TES,;	   	// 2-Codigo do TES ( Opcional )
							(cAliasSC6)->C6_QTDENT,;  	// 3-Quantidade ( Obrigatorio )
							(cAliasSC6)->C6_PRUNIT,;	// 4-Preco Unitario ( Obrigatorio )
							nDesconto,; 				// 5-Valor do Desconto ( Opcional )
							"",;	   					// 6-Numero da NF Original ( Devolucao/Benef )
							"",;						// 7-Serie da NF Original ( Devolucao/Benef )
							nRecOri,;					// 8-RecNo da NF Original no arq SD1/SD2
							0,;							// 9-Valor do Frete do Item ( Opcional )
							0,;							// 10-Valor da Despesa do item ( Opcional )
							0,;							// 11-Valor do Seguro do item ( Opcional )
							0,;							// 12-Valor do Frete Autonomo ( Opcional )
							nValMerc,;					// 13-Valor da Mercadoria ( Obrigatorio )
							0)							// 14-Valor da Embalagem ( Opiconal )

				SB1->(dbSetOrder(1))
				If SB1->(MsSeek(xFilial("SB1")+(cAliasSC6)->C6_PRODUTO))
					nQtdPeso := ((cAliasSC6)->C6_QTDVEN-(cAliasSC6)->C6_QTDENT)*SB1->B1_PESO
				EndIf
				//зддддддддддддддддддддддддддддддддддддддддддддд©
				//ЁCalculo do ISS                               Ё
				//юддддддддддддддддддддддддддддддддддддддддддддды
				SF4->(dbSetOrder(1))
				SF4->(MsSeek(xFilial("SF4")+(cAliasSC6)->C6_TES))
				If ( SC5->C5_INCISS == "N" .And. SC5->C5_TIPO == "N")
					If ( SF4->F4_ISS=="S" )
						nPrcLista := a410Arred(nPrcLista/(1-(MaAliqISS(nItem)/100)),"D2_PRCVEN")
						nValMerc  := a410Arred(nValMerc/(1-(MaAliqISS(nItem)/100)),"D2_PRCVEN")
					EndIf
				EndIf
				//зддддддддддддддддддддддддддддддддддддддддддддд©
				//ЁAltera peso para calcular frete              Ё
				//юддддддддддддддддддддддддддддддддддддддддддддды
				MaFisAlt("IT_PESO",nQtdPeso,nItem)
				MaFisAlt("IT_PRCUNI",nPrcLista,nItem)
				MaFisAlt("IT_VALMERC",nValMerc,nItem)
				//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//ЁForca os valores de impostos que foram informados no SC6.              Ё
				//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
				dbSelectArea("SC6")
				For nX := 1 to Len(aFisGet)
					If !Empty(&(aFisGet[nX][2]))
						MaFisAlt(aFisGet[nX][1],&(aFisGet[nX][2]),nItem,.T.)
					EndIf
				Next nX
				If nItem == 1
					//зддддддддддддддддддддддддддддддддддддддддддддддддд©
					//ЁRealiza alteracoes de referencias do SC5 Suframa Ё
					//юддддддддддддддддддддддддддддддддддддддддддддддддды
					nPSuframa:=aScan(aFisGetSC5,{|x| x[1] == "NF_SUFRAMA"})
					If !Empty(nPSuframa)
						dbSelectArea("SC5")
						If !Empty(&(aFisGetSC5[nPSuframa][2]))
							MaFisAlt(aFisGetSC5[nPSuframa][1],Iif(&(aFisGetSC5[nPSuframa][2]) == "1",.T.,.F.),nItem,.T.)
						EndIf
					Endif
				EndIf
				//зддддддддддддддддддддддддддддддддддддддддддддд©
				//ЁVerifica a data de entrega para as duplicatasЁ
				//юддддддддддддддддддддддддддддддддддддддддддддды
				dData   := Iif( (cAliasSc6)->C6_ENTREG < dDataBase, dDataBase, (DataValida((cAliasSc6)->C6_ENTREG)))
				aadd(aFluxoTmp,{dData,nItem})
				If SF4->F4_DUPLIC=="S"
					nTotDesc += MaFisRet(nItem,"IT_DESCONTO")
				Else
					If GetNewPar("MV_TPDPIND","1")=="1"
						nTotDesc += MaFisRet(nItem,"IT_DESCONTO")
					EndIf
				EndIf
			EndIf
			dbSelectArea(cAliasSC6)
			dbSkip()
		EndDo
		If lQuery
			dbSelectArea(cAliasSC6)
			dbCloseArea()
			dbSelectArea("SC6")
		EndIf
		//зддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁIndica os valores do cabecalho               Ё
		//юддддддддддддддддддддддддддддддддддддддддддддды
		MaFisAlt("NF_FRETE",SC5->C5_FRETE)
		MaFisAlt("NF_VLR_FRT",SC5->C5_VLR_FRT)
		MaFisAlt("NF_SEGURO",SC5->C5_SEGURO)
		MaFisAlt("NF_AUTONOMO",SC5->C5_FRETAUT)
		MaFisAlt("NF_DESPESA",SC5->C5_DESPESA)  
		If cPaisLoc == "PTG"                    
			MaFisAlt("NF_DESNTRB",SC5->C5_DESNTRB)  
			MaFisAlt("NF_TARA",SC5->C5_TARA)  
		Endif
		If SC5->C5_DESCONT > 0
			MaFisAlt("NF_DESCONTO",Min(MaFisRet(,"NF_VALMERC")-0.01,nTotDesc+SC5->C5_DESCONT),/*nItem*/,/*lNoCabec*/,/*nItemNao*/,GetNewPar("MV_TPDPIND","1")=="2" )
		EndIf
		If SC5->C5_PDESCAB > 0
			MaFisAlt("NF_DESCONTO",A410Arred(MaFisRet(,"NF_VALMERC")*SC5->C5_PDESCAB/100,"C6_VALOR")+MaFisRet(,"NF_DESCONTO"))
		EndIf
		MaFisWrite(1)
		//зддддддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁObtem os valores da funcao fiscal                Ё
		//юддддддддддддддддддддддддддддддддддддддддддддддддды
		For nX := 1 To Len(aFluxoTmp)
			nPosEntr := Ascan(aEntr,{|x| x[1]==aFluxoTmp[nX][1]})
			If nPosEntr == 0
				Aadd(aEntr,{aFluxoTmp[nX][1],MaFisRet(aFluxoTmp[nX][2],"IT_BASEDUP"),MaFisRet(aFluxoTmp[nX][2],"IT_VALIPI"),MaFisRet(aFluxoTmp[nX][2],"IT_VALSOL")})
			Else
				aEntr[nPosEntr][2]+= MaFisRet(aFluxoTmp[nX][2],"IT_BASEDUP")
				aEntr[nPosEntr][3]+= MaFisRet(aFluxoTmp[nX][2],"IT_VALIPI")
				aEntr[nPosEntr][4]+= MaFisRet(aFluxoTmp[nX][2],"IT_VALSOL")
			EndIf
			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//ЁPonto de Entrada M410IPI para alterar os valores do IPI referente a palnilha financeira           Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			If lM410Ipi 
				VALORIPI    := MaFisRet(nItem,"IT_VALIPI")
				BASEIPI     := MaFisRet(nItem,"IT_BASEIPI")
				QUANTIDADE  := MaFisRet(nItem,"IT_QUANT")
				ALIQIPI     := MaFisRet(nItem,"IT_ALIQIPI")
				BASEIPIFRETE:= MaFisRet(nItem,"IT_FRETE")
				MaFisAlt("IT_VALIPI",ExecBlock("M410IPI",.F.,.F.,),nItem,.T.)
				MaFisLoad("IT_BASEIPI",BASEIPI ,nItem)
				MaFisLoad("IT_ALIQIPI",ALIQIPI ,nItem)
				MaFisLoad("IT_FRETE"  ,BASEIPIFRETE,nItem,"11")
				MaFisEndLoad(nItem,1)
			EndIf
		Next nX
		//зддддддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁCalcula os venctos conforme a condicao de pagto  Ё
		//юддддддддддддддддддддддддддддддддддддддддддддддддды
		dbSelectarea("SE4")
		dbSetOrder(1)
		If MsSeek(xFilial("SE4")+SC5->C5_CONDPAG)
			For nY := 1 to Len(aEntr)
				nAcerto  := 0
				
				If SFB->FB_JNS == 'J' .And. cPaisLoc == 'COL'
					dbSelectArea("SFC")
					dbSetOrder(2)
					If dbSeek(xFilial("SFC") + SF4->F4_CODIGO + "RV0" )
						nValRetImp 	:= MaFisRet(,"NF_VALIV2")
						Do Case
							Case FC_INCDUPL == '1'
								nlValor := aEntr[nY][2] - nValRetImp
							Case FC_INCDUPL == '2'
								nlValor :=aEntr[nY][2] + nValRetImp
							Otherwise
								nlValor :=aEntr[nY][2]
						EndCase
					Elseif dbSeek(xFilial("SFC") + SF4->F4_CODIGO + "RF0" )
						nValRetImp 	:= MaFisRet(,"NF_VALIV4")
						Do Case
							Case FC_INCDUPL == '1'
								nlValor := aEntr[nY][2] - nValRetImp
							Case FC_INCDUPL == '2'
								nlValor :=aEntr[nY][2] + nValRetImp
							Otherwise
								nlValor :=aEntr[nY][2]
						EndCase
					Elseif dbSeek(xFilial("SFC") + SF4->F4_CODIGO + "RC0" )
						nValRetImp 	:= MaFisRet(,"NF_VALIV7")
						Do Case
							Case FC_INCDUPL == '1'
								nlValor := aEntr[nY][2] - nValRetImp
							Case FC_INCDUPL == '2'
								nlValor :=aEntr[nY][2] + nValRetImp
							Otherwise
								nlValor :=aEntr[nY][2]
						EndCase
					Endif
				ElseIf cPaisLoc=="EQU" 
					nlValor := aEntr[nY][2]
					SA1->(DbSetOrder(1))
					SA1->(MsSeek(xFilial()+IIf(!Empty(SC5->C5_CLIENT),SC5->C5_CLIENT,SC5->C5_CLIENTE)+SC5->C5_LOJAENT))
					cNatureza:=SA1->A1_NATUREZ
					lPParc:=Posicione("SED",1,xFilial("SED")+cNatureza,"ED_RATRET")=="1"	
					If lPParc
						DbSelectArea("SFC")
						SFC->(dbSetOrder(2))
						If DbSeek(xFilial("SFC") + SF4->F4_CODIGO + "RIR") //RetenГЦo IVA
							cImpRet		:= SFC->FC_IMPOSTO
							DbSelectArea("SFB")
							SFB->(dbSetOrder(1))
							If SFB->(DbSeek(xFilial("SFB")+AvKey(cImpRet,"FB_CODIGO")))
								nValRetImp 	:= MaFisRet(,"NF_VALIV"+SFB->FB_CPOLVRO)
						    Endif       
						    DbSelectArea("SFC")
							If SFC->FC_INCDUPL == '1'
								nlValor	:=aEntr[nY][2] - nValRetImp				
							ElseIf SFC->FC_INCDUPL == '2'
								nlValor :=aEntr[nY][2] + nValRetImp
							EndIf   
						EndIf	
				    Endif
				Else
					nlValor := aEntr[nY][2]
				EndIf
				
				aFluxoTmp := Condicao(nlValor,SC5->C5_CONDPAG,aEntr[nY][3],aEntr[nY][1],aEntr[nY][4],,,nAcresTot)
				If !Empty(aFluxoTmp)             
					If cPaisLoc=="EQU"
						For nX := 1 To Len(aFluxoTmp)
							If nX==1                            
								If SFC->FC_INCDUPL == '1'
									aFluxoTmp[nX][2]+= nValRetImp
								ElseIf SFC->FC_INCDUPL == '2'
									aFluxoTmp[nX][2]-= nValRetImp
								Endif										
							Endif	
						Next nX
					Else
						For nX := 1 To Len(aFluxoTmp)
							nAcerto += aFluxoTmp[nX][2]
						Next nX	
						aFluxoTmp[Len(aFluxoTmp)][2] += aEntr[nY][2] - nAcerto	
					Endif
					For nX := 1 To Len(aFluxoTmp)
						nZ := aScan(aFluxo,{|x| x[1] == aFluxoTmp[nX][1]})
						If nZ == 0
							aadd(aFluxo,{aFluxoTmp[nX][1],0})
							nZ := Len(aFluxo)
						EndIf
						aFluxo[nZ][2] += aFluxoTmp[nX][2]
					Next nX
				EndIf
			Next nY
		EndIf
		If Len(aFluxo) == 0
			aDupl := {{dDataBase,MaFisRet(,"NF_BASEDUP"),PesqPict("SE1","E1_VALOR")}}
		Endif
		MaFisEnd()
		MaFisRestore()
		//зддддддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁAtualiza a tabela de fluxo de caixa do PV        Ё
		//юддддддддддддддддддддддддддддддддддддддддддддддддды
		If AliasInDic("AID")
			Begin Transaction
				dbSelectArea("AID")
				#IFDEF TOP
					cQuery := "DELETE FROM "+RetSqlName("AID")+" WHERE AID_FILIAL='"+xFilial("AID")+"' AND AID_NUMPV='"+cNumPV+"' "
					TcSqlExec(cQuery)
				#ELSE
					dbSelectArea("AID")
					dbSetOrder(1)
					If MsSeek(xFilial("AID")+cNumPv)
						While !Eof() .And. xFilial("AID") == AID->AID_FILIAL .And. cNumPv == AID->AID_NUMPV
							RecLock("AID")
							dbDelete()
							dbSelectArea("AID")
							dbSkip()
					    EndDo
					EndIf
				#ENDIF
				If !lNaoGravaAID
					For nX := 1 To Len(aFluxo)
						If aFluxo[nX,2] <> 0
							RecLock("AID",.T.)
							AID->AID_FILIAL := xFilial("AID")
							AID->AID_NUMPV  := cNumPV
							AID->AID_DATA   := aFluxo[nX,1]
							AID->AID_VALOR  := aFluxo[nX,2]
							MsUnLock()
						EndIf
					Next nX
				Endif
			End Transaction
		EndIf
		SC5->(MsUnLockAll())
//	EndIf
EndIf
If lSoma
	aFluxoTmp := aClone(aFluxo)
	aFluxo := {{dDataBase,0}}
	For nX := 1 To Len(aFluxoTMP)
		aFluxo[1][2] += aFluxoTMP[nX][2]
	Next nX
EndIf
RestArea(aAreaSA1)
RestArea(aArea)
Return(aFluxo)

/*эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤└o    Ё A410Line  Ё Autor Ё Patricia A. Salomao  Ё Data Ё 26/07/05 Ё╠╠
╠╠цддддддддддедддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤└o Ё Atualizacao da bLine do documento.                         Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё A410Line(ExpN1)                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpN1 - Posicao da linha no listbox                        Ё╠╠
╠╠Ё          Ё ExpA1 - Array com as notas de entrada                      Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё MATA410                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/
Function A410Line(nAT,aSF1)

Static oNoMarked := LoadBitmap( GetResources(),'LBNO'			)
Static oMarked	  := LoadBitmap( GetResources(),'LBOK'			)
Local abLine     := {}
Local nCnt       := 0

For nCnt := 1 To Len(aSF1[nAT])
	If nCnt == 1
		Aadd( abLine, Iif(aSF1[ nAT, nCnt ] , oMarked, oNoMarked ) )
	Else
		Aadd( abLine, aSF1[ nAT, nCnt ] )
	EndIf
Next nCnt

Return abLine

/*эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤└o    Ё A410RetNF Ё Autor Ё Patricia A. Salomao  Ё Data Ё 26/07/05 Ё╠╠
╠╠цддддддддддедддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤└o Ё Retorna as notas                                           Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё A410RetNF(ExpA1)                                           Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpA1 - Campos que deverao ser apresentados                Ё╠╠
╠╠Ё          Ё ExpD1 - Data Inicio                                        Ё╠╠
╠╠Ё          Ё ExpD2 - Data Final                                         Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё MATA410                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/
Static Function A410RetNF(aCpoSF1,dDataDe,dDataAte,lForn, lFornece)
Local aSF1      := {}
Local aAux      := {}
Local aAreaSF1  := SF1->(GetArea())
Local nCnt      := 0
Local cAliasSF1 := 'SF1'
Local cQuery    := ''
Local cIndex    := ''
Local nIndexSF1 := 0

#IFDEF TOP
	cAliasSF1 := GetNextAlias()
	cQuery := " SELECT * "
  	cQuery += "   FROM " + RetSqlName("SF1")
  	cQuery += "   WHERE F1_FILIAL  = '" + xFilial("SF1") + "' "
  	cQuery += "     AND F1_FORNECE = '" + cFornece + "' "
  	cQuery += "     AND F1_LOJA    = '" + cLoja    + "' "
  	cQuery += "     AND F1_DTDIGIT BETWEEN '" + DtoS(dDataDe) + "' AND '" + DtoS(dDataAte) + "' "
  	cQuery += "     AND F1_STATUS  <> '" + Space(Len(SF1->F1_STATUS)) + "' "
	If lForn
	  	cQuery += " AND F1_TIPO NOT IN ('D','B')
	Else
	  	cQuery += " AND F1_TIPO IN ('D','B')
	EndIf
  	cQuery += "     AND D_E_L_E_T_ = ' ' "
  	
  	If Existblock("A410RNF")
            cQuery := ExecBlock("A410RNF",.F.,.F.,{dDataDe,dDataAte,lForn,lFornece})
	EndIf
	
	cQuery := ChangeQuery( cQuery )
	dbUseArea( .T., "TOPCONN", TcGenQry( , , cQuery ), cAliasSF1, .F., .T. )
#ELSE
	DbSelectArea("SF1")
	cIndex := CriaTrab(NIL,.F.)
	cQuery := " SF1->F1_FILIAL == '" + xFilial("SF1") + "' "
  	cQuery += " .And. SF1->F1_FORNECE == '" + cFornece + "' "
  	cQuery += " .And. SF1->F1_LOJA    == '" + cLoja    + "' "
  	cQuery += " .And. DtoS(SF1->F1_DTDIGIT) >= '" + DtoS(dDataDe)  + "'"
	cQuery += " .And. DtoS(SF1->F1_DTDIGIT) <= '" + DtoS(dDataAte) + "' "
  	cQuery += " .And. SF1->F1_STATUS  <> '" + Space(Len(SF1->F1_STATUS)) + "' "
	If lForn
	  	cQuery += " .And. !(SF1->F1_TIPO $ 'DB') "
	Else
	  	cQuery += " .And. SF1->F1_TIPO $ 'DB'
	EndIf
	
  	If Existblock("A410RNF")
            cQuery := ExecBlock("A410RNF",.F.,.F.,{dDataDe,dDataAte,lForn,lFornece})
	EndIf
	
	IndRegua("SF1",cIndex,"F1_DOC+F1_SERIE",,cQuery)
	SF1->(DbGotop())
#ENDIF

While (cAliasSF1)->(!Eof())
	aAux := {}
	Aadd( aAux, .F. )
	For nCnt := 1 To Len(aCpoSF1)
		Aadd( aAux, &(aCpoSF1[nCnt]) )
	Next nCnt
	aAdd( aSF1, aClone(aAux) )
	(cAliasSF1)->(DbSkip())
EndDo

#IFDEF TOP
	(cAliasSF1)->(DbCloseArea())
#ELSE
	RetIndex( "SF1" )
	FErase( cIndex+OrdBagExt() )
#ENDIF

RestArea(aAreaSF1)

Return aSF1

/*эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤└o    Ё A410FRet Ё Autor Ё Patricia A. Salomao   Ё Data Ё 26/07/05 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤└o Ё Filtro para retornar de doctos fiscais.                    Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё A410FRet()                                                 Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametro Ё ExpL1 - Fornecedor ?                                       Ё╠╠
╠╠Ё          Ё ExpD1 - Data Inicio                                        Ё╠╠
╠╠Ё          Ё ExpD2 - Data Fim                                           Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё MATA410                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/
Static Function A410FRet(lFornece,dDataDe,dDataAte,lForn)
Local cTDataDe  := OemToAnsi(STR0096) //-- Dt. Entrada De
Local cTDataAte := OemToAnsi(STR0097) //-- Dt. Entrada Ate
Local cTFornece := RetTitle("F1_FORNECE")
Local cTLoja    := RetTitle("F1_LOJA")
Local nOpcao    := 0
Local lCliente  := .F.
Local lDocto    := .F.
Local oDlgEsp
Local oCliente
Local oForn
Local oDocto 
Local oFornece
Local oPanelCli
Local oPanelFor

DEFINE MSDIALOG oDlgEsp FROM 00,00 TO 190,490 PIXEL TITLE OemToAnsi(STR0098) //-- Retorno de Doctos. de Entrada

	//-- Fornecedor'
	@ 02,10 CHECKBOX oForn VAR lForn PROMPT OemToAnsi(STR0099) SIZE 50,010 ;
		ON CLICK( lCliente := .F., oCliente:Refresh(), A410CliFor(lForn,@lFornece,@lDocto,oDocto,oFornece,oDlgEsp,oPanelCli,oPanelFor)) OF oDlgEsp PIXEL  //-- Fornecedor

	//-- 'Cliente'
	@ 02,120 CHECKBOX oCliente VAR lCliente PROMPT OemToAnsi(STR0105) SIZE 50,010 ;
		ON CLICK( lForn := .F., oForn:Refresh(), A410CliFor(lForn,@lFornece,@lDocto,oDocto,oFornece,oDlgEsp,oPanelCli,oPanelFor)) OF oDlgEsp PIXEL //-- Cliente


	@ 018,000 MSPANEL oPanelCli OF oDlgEsp SIZE 245,020               

	@ 018,000 MSPANEL oPanelFor OF oDlgEsp SIZE 245,020

	cTFornece := RetTitle("F1_FORNECE")
    cTLoja    := RetTitle("F2_LOJA")
	@ 001,05 SAY cTFornece PIXEL SIZE 47 ,9 OF oPanelFor
	@ 001,40 MSGET cFornece F3 'FOR' SIZE 65, 10 OF oPanelFor PIXEL

	@ 001,120 SAY cTLoja PIXEL OF oPanelFor
	@ 001,160 MSGET cLoja SIZE 20, 10 OF oPanelFor PIXEL ;
			VALID Vazio() .Or. ExistCpo('SA2',cFornece+AllTrim(cLoja),1)

    cTLoja    := RetTitle("F2_LOJA")
	@ 001,05 SAY RetTitle("F2_CLIENTE") PIXEL SIZE 50 ,10 OF oPanelCli
	@ 001,40 MSGET cFornece F3 'SA1' SIZE 65, 10 OF oPanelCli PIXEL

	@ 001,120 SAY cTLoja PIXEL OF oPanelCli
	@ 001,160 MSGET cLoja SIZE 20, 10 OF oPanelCli PIXEL ;
			VALID Vazio() .Or. ExistCpo('SA1',cFornece+AllTrim(cLoja),1)
	oPanelCli:Hide()


	@ 39,05 SAY cTDataDe PIXEL //-- Data De
	@ 38,40 MSGET dDataDe PICTURE "@D" SIZE 45, 10 OF oDlgEsp PIXEL

	@ 39,120 SAY cTDataAte PIXEL //-- Data Ate
	@ 38,160 MSGET dDataAte PICTURE "@D" SIZE 45, 10 OF oDlgEsp PIXEL

	@ 60,003 TO 085,195 LABEL OemToAnsi(STR0100) OF oDlgEsp PIXEL  //-- Tipo de Selecao

	//-- 'Fornecedor'
	@ 70,10 CHECKBOX oFornece VAR lFornece PROMPT PadR(IIf(lForn,OemToAnsi(STR0099),RetTitle("F2_CLIENTE")),20) SIZE 50,010 ;
		ON CLICK( lDocto := .F., oDocto:Refresh() ) OF oDlgEsp PIXEL  //-- Fornecedor

	//-- 'Documento'
	@ 70,120 CHECKBOX oDocto VAR lDocto PROMPT OemToAnsi(STR0102) SIZE 50,010 ;
		ON CLICK( lFornece := .F., oFornece:Refresh() ) OF oDlgEsp PIXEL //-- Documento

	DEFINE SBUTTON FROM 05,215 TYPE 1 OF oDlgEsp ENABLE ;
		ACTION If(!Empty(cFornece) .And. !Empty(cLoja) .And. ;
			 		 !Empty(dDataDe) .And. !Empty(dDataAte) .And. ;
			 		 (lFornece .Or. lDocto)  .And. IIF(lForn,ExistCpo('SA2',cFornece+AllTrim(cLoja),1),ExistCpo('SA1',cFornece+AllTrim(cLoja),1)),(nOpcao := 1,oDlgEsp:End()),.F.)

DEFINE SBUTTON FROM 20,215 TYPE 2 OF oDlgEsp ENABLE ACTION (nOpcao := 0,oDlgEsp:End())

ACTIVATE MSDIALOG oDlgEsp CENTERED

Return ( nOpcao == 1 )

/*/
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбддддддддддд©╠╠
╠╠ЁFun┤└o    ЁA410rentPVЁ Autor Ё Eduardo Riera         Ё Data Ё16.11.2005 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддаддддддддддд╢╠╠
╠╠Ё          ЁFuncao de calculo da rentabilidade do pedido de venda        Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ nOpc                                                        Ё╠╠
╠╠Ё          Ё                                                             Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁNenhum                                                       Ё╠╠
╠╠Ё          Ё                                                             Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁDescri┤└o ЁEsta funcao efetua o calculo da rentabilidade de um pedido deЁ╠╠
╠╠Ё          Ёvenda.                                                       Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё Generico                                                    Ё╠╠
╠╠юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Static Function a410RentPV( aCols ,nUsado ,aRenTab ,aVencto ,nPTES,nPProduto,nPLocal,nPQtdVen )
Local nItem    := 0
Local nX       := 0
Local nY       := 0
Local nPos     := 0

If len(aRenTab) > 0 .AND. (aRentab[Len(aRentab)][1] == "")
	aSize( aRentab ,Len(aRentab)-1)
	For nX := 1 To Len(aRentab)
		aRentab[nX][2] := val(StrTran(StrTran(aRentab[nX][2],".",""),",","."))
		aRentab[nX][3] := val(StrTran(StrTran(aRentab[nX][3],".",""),",","."))
		aRentab[nX][4] := 0
		aRentab[nX][5] := 0
		aRentab[nX][6] := 0
	Next nX
EndIf

For nX := 1 To Len(aCols)
	If Len(aCols[nX])==nUsado .Or. !aCols[nX][nUsado+1]
		nItem++
		nY := aScan(aRentab,{|x| x[1] == aCols[nX][nPProduto]})
		If nY <> 0
			aRentab[nY][4] += Max(Ma410Custo(nItem,aVencto,aCols[nX][nPTES],aCols[nX][nPProduto],aCols[nX][nPLocal],aCols[nX][nPQtdVen]),0)
			aRentab[nY][5] := Max(aRentab[nY][4]-aRentab[nY][3],0)
			aRentab[nY][6] := aRentab[nY][5]/aRentab[nY][4]*100
		EndIf
	EndIf
Next nX
aadd(aRentab,{"",0,0,0,0,0})
For nX := 1 To Len(aRentab)
	If nX <> Len(aRentab)
		aRentab[Len(aRentab)][2] += aRentab[nX][2]
		aRentab[Len(aRentab)][3] += aRentab[nX][3]
		aRentab[Len(aRentab)][4] += aRentab[nX][4]
		aRentab[Len(aRentab)][5] += aRentab[nX][5]
		aRentab[Len(aRentab)][6] := aRentab[Len(aRentab)][5]/aRentab[Len(aRentab)][4]*100
	EndIf
	aRentab[nX][2] := TransForm(aRentab[nX][2],"@e 999,999,999.999999")
	aRentab[nX][3] := TransForm(aRentab[nX][3],"@e 999,999,999.999999")
	aRentab[nX][4] := TransForm(aRentab[nX][4],"@e 999,999,999.999999")
	aRentab[nX][5] := TransForm(aRentab[nX][5],"@e 999,999,999.999999")
	aRentab[nX][6] := TransForm(aRentab[nX][6],"@e 999,999,999.999999")
Next nX
If Existblock("MA410RPV")
	aRentab := ExecBlock("MA410RPV",.F.,.F.,aRentab)
EndIf
Return( aRentab )

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤└o    ЁA410CliForЁ Autor Ё Patricia A. Salomao   Ё Data Ё 26/07/05 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤└o ЁDesenha Tela conforme selecao: Fornecedor ou Cliente        Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   ЁA410CliFor()                                                Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametro Ё ExpL1 - Fornecedor ? (1o.CheckBox)                         Ё╠╠
╠╠Ё          Ё ExpL2 - Fornecedor ? (2o.CheckBox)                         Ё╠╠
╠╠Ё          Ё ExpL3 - Documento  ? (2o.CheckBox)                         Ё╠╠
╠╠Ё          Ё ExpO1 - Objeto Documento                                   Ё╠╠
╠╠Ё          Ё ExpO2 - Objeto Fornecedor                                  Ё╠╠
╠╠Ё          Ё ExpO3 - Objeto Dialog                                      Ё╠╠
╠╠Ё          Ё ExpO4 - Objeto Panel Cliente                               Ё╠╠
╠╠Ё          Ё ExpO5 - Objeto Panel Fornecedor                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё MATA410                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Function A410CliFor(lForn,lFornece,lDocto,oDocto,oFornece,oDlgEsp,oPanelCli,oPanelFor)

//-- Se Clicou em Fornecedor
If lForn
	oPanelCli:Hide()
	oPanelFor:Show()
Else //-- Se Clicou em Cliente
	oPanelFor:Hide()
	oPanelCli:Show()
EndIf

oFornece:cCaption := IIf(lForn,OemToAnsi(STR0099),RetTitle("F2_CLIENTE"))
oFornece:cTitle   := IIf(lForn,OemToAnsi(STR0099),RetTitle("F2_CLIENTE"))
If lFornece
	lDocto := .F.
	oDocto:Refresh()
ElseIf lDocto
	 lFornece := .F.
	 oFornece:Refresh() 
EndIf

oDlgEsp:SetFocus()
oDlgEsp:Refresh()

Return .T.


/*/
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбддддддддддд©╠╠
╠╠ЁFun┤└o    Ёa410FretePЁ Autor Ё Kleber Dias Gomes     Ё Data Ё25/07/2006 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддаддддддддддд╢╠╠
╠╠Ё          ЁFuncao de Calculo da Frete Pauta.                            Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁNenhum                                                       Ё╠╠
╠╠Ё          Ё                                                             Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁNenhum                                                       Ё╠╠
╠╠Ё          Ё                                                             Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁDescri┤└o ЁEsta funcao efetua o calculo do frete pauta.                 Ё╠╠
╠╠Ё          Ё                                                             Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       ЁSIGAFAT                                                      Ё╠╠
╠╠юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function a410FreteP()

Local aArea    := GetArea()
Local cAliasDV9:= ""
Local lRetorno := .T.
Local nScan    := 0
#IFDEF TOP
Local aStruDV9 := {}
Local cQuery   := ""
Local lQuery   := .F.
Local nY       := 0
#ENDIF

If ValType(aFreteP) == "A"
	If M->C5_KM > 0
		If Empty(aFreteP)
			dbSelectArea("DV9")
			dbSetOrder(1)
			dbGoTop()
			#IFDEF TOP
				lQuery  := .T.
				aStruDV9:= DV9->(dbStruct())
				cAliasDV9:= GetNextAlias()
				cQuery := "SELECT * "
				cQuery += "FROM "+RetSqlName("DV9")+" DV9 "
				cQuery += "WHERE DV9.DV9_FILIAL='"+xFilial("DV9")+"' AND "
				cQuery += "DV9.D_E_L_E_T_=' ' "
				cQuery += "ORDER BY "+SqlOrder(DV9->(IndexKey()))
				
				cQuery := ChangeQuery(cQuery)
				
				dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasDV9,.T.,.T.)
				
				For nY := 1 To Len(aStruDV9)
					If aStruDV9[nY][2] <> "C"
						TcSetField(cAliasDV9,aStruDV9[nY][1],aStruDV9[nY][2],aStruDV9[nY][3],aStruDV9[nY][4])
					EndIf
				Next nY
			#ELSE
				lQuery   := .F.
				cAliasDV9:= "DV9"
			#ENDIF
			While !Eof() .And. (cAliasDV9)->DV9_FILIAL == xFilial("DV9")
				aAdd(aFreteP,{(cAliasDV9)->DV9_KM,(cAliasDV9)->(Recno())})
				dbSkip()
			EndDo
			aSort(aFreteP,,,{|x,y| x[1] < y[1] })

			If lQuery
				dbSelectArea(cAliasDV9)
	            dbCloseArea()
	            dbSelectArea("DV9")
	   		EndIf
		EndIf
		nScan := Ascan(aFreteP,{|x| x[1] >= M->C5_KM})
		If nScan > 0
			dbSelectArea("DV9")
			dbGoTo(aFreteP[nScan][2])
			Do Case
				Case DV9->DV9_TIPVAL == "1"
					M->C5_VLR_FRT := NoRound((M->C5_PBRUTO/1000)*DV9->DV9_VALOR,TamSX3("C5_VLR_FRT")[2])
				Case DV9->DV9_TIPVAL == "2"
					M->C5_VLR_FRT := NoRound((M->C5_PBRUTO*DV9->DV9_VALOR),TamSX3("C5_VLR_FRT")[2])
				Case DV9->DV9_TIPVAL == "3"
					M->C5_VLR_FRT := NoRound(DV9->DV9_VALOR,TamSX3("C5_VLR_FRT")[2])
			EndCase
		Else
			M->C5_VLR_FRT := 0
		EndIf
	Else
		M->C5_VLR_FRT := 0
	EndIf
EndIf

RestArea(aArea)

Return lRetorno
                                                               


//------------------------- FUNCOES ANTIGAS PARA MANTER LEGADO DA GRADE DE PRODUTOS ANTIGA ------------------- 


//----------------------------------------------------------------------
// FUNCAO UTILIZADA SOMENTE PARA MANTER LEGADO DA UTILIZACAO DE GRADE
// DE PRODUTOS SEM OBJETO ATE LANCAMENTO DO R4.   
//
// ATENCAO: Foi incluido um "_" no inicio do nome da funcao
//----------------------------------------------------------------------
Function _a410GValid(nLinAcols,lQtdVen)

Local lRetorno	:=.T.
Local nColuna	:= aScan(aHeader,{|x| AllTrim(x[2]) == AllTrim(Substr(Readvar(),4))})
Local cProdGrd	:= ""
Local cMascara	:= SuperGetMv("MV_MASCGRD")
Local nTamRef	:= Val(Substr(cMascara,1,2))


Local nQuant	:= &(ReadVar())

cProdGrd := Substr(SB4->B4_COD,1,nTamRef)+aCols[n][1]+aHeader[nColuna][1]

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁVerifica se o Produto Existe                                            Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
dbSelectArea("SB1")
dbSetOrder(1)
If ( !MsSeek(xFilial("SB1")+cProdGrd,.F.) )
	HELP(" ",1,"NAOEXISTE")
	lRetorno := .F.
EndIf
If lRetorno .And. lQtdVen
	lRetorno := RegistroOk("SB1")
EndIf
If ( lRetorno )
	If ( ExistBlock("A410GVLD") )
		ExecBlock("A410GVLD",.F.,.F.,{nLinAcols,n,nColuna})
	EndIf
EndIf
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁVerifica a quantidade Liberada                                          Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If ( !lQtdVen )
	If ( SuperGetMv("MV_LIBACIM") )
		If  ( nQuant > aColsGrade[nLinAcols][n][nColuna][1] )
			Help(" ",1,"A410LIB")
			lRetorno := .F.
		EndIf
		If ( lRetorno .And. nQuant > (aColsGrade[nLinAcols][n][nColuna][1] - aColsGrade[nLinAcols][n][nColuna][3]) )
			HELP(" ",1,"A440QTDL")
			lRetorno := .F.
		EndIf
	EndIf
EndIf
Return(lRetorno)
                                      

//----------------------------------------------------------------------
// FUNCAO UTILIZADA SOMENTE PARA MANTER LEGADO DA UTILIZACAO DE GRADE
// DE PRODUTOS SEM OBJETO ATE LANCAMENTO DO R4.   
//
// ATENCAO: Foi incluido um "_" no inicio do nome da funcao
//----------------------------------------------------------------------
Function _A410QtdGra()

Local nPProduto:= aScan(aHeader,{|x| AllTrim(x[2])=="C6_PRODUTO"})
Local nPQtdVen := aScan(aHeader,{|x| AllTrim(x[2])=="C6_QTDVEN"})
Local nPQtdLib := aScan(aHeader,{|x| AllTrim(x[2])=="C6_QTDLIB"})
Local nPLocal  := aScan(aHeader,{|x| AllTrim(x[2])=="C6_LOCAL"})
Local nPQtdVen2 := aScan(aHeader,{|x| AllTrim(x[2])=="C6_UNSVEN"})
Local nQtdInf	:= &(ReadVar())
Local nOpcA 	:= 0
Local nCntFor	:= 0
Local nColuna 	:= 0
Local nLinha	:= 0
Local nColQtd  := 0
Local nColQtd2 := 0
Local cProdRef	:= aCols[n][nPProduto]
Local lQtdVen	:= If("C6_QTDVEN"$ReadVar(),.T.,.F.)
Local lQtdVen2  := If("C6_UNSVEN"$ReadVar(),.T.,.F.)
Local lGrade	:= MaGrade()
Local lPergunte := INCLUI .Or. ALTERA
Local lShowGrd  := .T.

Local aHeadAux	:= {}
Local aColsAux	:= {}
Local aAltGr	:= {}
Local aBackRot	:= aClone(aRotina)
Local bSetKey
Local cLocal	:= aCols[n][nPLocal]
Local   oDlg,oGet
nColQtd := If(lQtdVen,1,If(lQtdVen2,5,2))
nColQtd2:= If(nColQtd==1,5,If(nColQtd==2,0,1))
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Muda o valor do aRotina para nao incluir linha na GetDados   Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
PRIVATE aRotina := {{"","",0,1},{"","",0,1},{"","",0,6}}
//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Verifica se a grade esta ativa                       Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
If ( lGrade )
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Arrays auxiliares para armazenar a getdados principalЁ
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
	aHeadAux:=Aclone(aHeader)
	aColsAux:=Aclone(aCols)
	nAux    :=n
	If ( MatGrdPrRf(@cProdRef) )
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁPosiciona  no SB4 para validacao da funcao a410GValid                   Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		dbSelectArea("SB4")
		dbSetOrder(1)
		MsSeek(xFilial("SB4")+cProdRef)
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁMonta aHeader e aCols                                                   Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		aHeader		:= Aclone(aHeadGrade[n])
		aHeader[1] 	:=  {" ","R","@!",4,0,"","","C","","V"}
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁMontagem do Array dos campos  que podem ser alterados                   Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		For nCntFor := 2 To Len(aHeader)
			aadd(aAltGr,aHeader[nCntFor][2])
			aHeader[nCntFor][6] :=  "A410GValid("+Str(nAux)+","+If(lQtdVen.Or.lQtdVen2,".T.",".F.")+")"
		Next nCntFor
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁMontagem do ACols                                                       Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		aCols		:= {}
		For nLinha :=  1 To Len(aColsGrade[nAux])
			aadd(aCols,Array(Len(aHeader)+1))
			aCols[nLinha][Len(aHeader)+1] := .F.
			For nColuna := 1 To Len(aHeader)
				If  ( nColuna ==  1 )
					aCols[nLinha][nColuna] := aColsGrade[nAux][nLinha][nColuna]
				Else
					aCols[nLinha][nColuna] := aColsGrade[nAux][nLinha][nColuna][nColQtd]
				EndIf
			Next nColuna
		Next nLinha
		N := 1
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Ponto de entrada antes da exibicao da janela de grade                  Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If ExistBlock( "A410GRDW" )
			ExecBlock( "A410GRDW", .F., .F. , { aHeadAux,aColsAux,nAux })
			lShowGrd := (SuperGetMv("MV_SHOWGRD",.F.,"1") == "1")
		EndIf

		If lShowGrd
			DEFINE MSDIALOG oDlg TITLE cCadastro FROM 10,10 TO 25,68   OF oMainWnd
			bSetKey := SetKey(VK_F4,{|| A440Saldo(.T.,cLocal)})
			oGet:=MSGetDados():New(20,8,107,222,3,"AllwaysTrue","AllwaysTrue",,.T.,aAltGr,1)
			ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{||nOpcA := If(lPergunte,1,0),oDlg:End()},{||oDlg:End()})
		Endif

		SetKey(VK_F4,bSetKey)
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁEfetua a Atualizacao no aColsGrade  Corrente                            Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If ( nOpcA == 1 )
			For nLinha :=  1 To Len(aColsGrade[nAux])
				For nColuna := 2 To Len(aHeader)
					aColsGrade[nAux][nLinha][nColuna][nColQtd ] := aCols[nLinha][nColuna]
					If aCols[nLinha][nColuna] <> 0
						Do Case
						Case ( nColQtd2 == 1 )
							aColsGrade[nAux][nLinha][nColuna][nColQtd2] := ConvUm(AllTrim(cProdRef),0,aCols[nLinha][nColuna],1)
						Case ( nColQtd2 == 5 )
							aColsGrade[nAux][nLinha][nColuna][nColQtd2] := ConvUm(AllTrim(cProdRef),aCols[nLinha][nColuna],0,2)
						EndCase
					EndIf
				Next nColuna
			Next nLinha
		EndIf
		aHeader  :=aClone(aHeadAux)
		aCols    :=aClone(aColsAux)
		n        :=nAux
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё  Soma as quantidades digitadas na getdados da grade  Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
		nQtdInf := MatGrdSoma(nAux,nQtdInf,nColQtd)
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁAtualiza a quantidade do acols original                                 Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If ( lQtdVen )
			A410MultT("C6_QTDVEN",nQtdInf,.F.)
			aCols[n][nPQtdVen]  := nQtdInf
			M->C6_QTDVEN		  := nQtdInf
			If ( nPQtdVen2 > 0 )
				nQtdInf := 0
				nQtdInf := MatGrdSoma(nAux,nQtdInf,5)
				A410MultT("C6_UNSVEN",nQtdInf)
				aCols[n][nPQtdVen2] := nQtdInf
				M->C6_UNSVEN        := nQtdInf
			EndIf
		Else
			If ( lQtdVen2 )
				A410MultT("C6_UNSVEN",nQtdInf)
				aCols[n][nPQtdVen2] := nQtdInf
				M->C6_UNSVEN        := nQtdInf
				nQtdInf := 0
				nQtdInf := MatGrdSoma(nAux,nQtdInf,1)
				A410MultT("C6_QTDVEN",nQtdInf,.F.)
				aCols[n][nPQtdVen]  := nQtdInf
				M->C6_QTDVEN		  := nQtdInf
			Else
				aCols[n][nPQtdLib]  := nQtdInf
				M->C6_QTDLIB		  := nQtdInf
			EndIf
		EndIf
	EndIf
EndIf
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Restaura aRotina Original                                    Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
aRotina:=aClone(aBackRot)
Return( .T. )
                                                                             

//----------------------------------------------------------------------
// FUNCAO UTILIZADA SOMENTE PARA MANTER LEGADO DA UTILIZACAO DE GRADE
// DE PRODUTOS SEM OBJETO ATE LANCAMENTO DO R4.   
//
// ATENCAO: Foi incluido um "_" no inicio do nome da funcao
//----------------------------------------------------------------------
Function _Ma410GraGr()

Local aColsOrig:= aClone(aCols)  //aCols Original
Local nMaxFor  := Len(aColsOrig)
Local nCntFor  := 0     // Contador
Local nPProduto:= aScan(aHeader,{|x| AllTrim(x[2])=="C6_PRODUTO"})
Local nPItem   := aScan(aHeader,{|x| AllTrim(x[2])=="C6_ITEM"})
Local nPItGrade:= aScan(aHeader,{|x| AllTrim(x[2])=="C6_ITEMGRD"})
Local nPPrcVen := aScan(aHeader,{|x| AllTrim(x[2])=="C6_PRCVEN"})
Local nPQtdVen := aScan(aHeader,{|x| AllTrim(x[2])=="C6_QTDVEN"})
Local nPQtdVen2:= aScan(aHeader,{|x| AllTrim(x[2])=="C6_UNSVEN"})
Local nPQtdLib := aScan(aHeader,{|x| AllTrim(x[2])=="C6_QTDLIB"})
Local nPValor  := aScan(aHeader,{|x| AllTrim(x[2])=="C6_VALOR"})
Local nPGrade  := aScan(aHeader,{|x| AllTrim(x[2])=="C6_GRADE"})
Local nPValDesc := aScan(aHeader,{|x| AllTrim(x[2])=="C6_VALDESC"})
Local nLinha   := 0     // Contador de Linhas
Local nColuna  := 0     // Contador de Colunas
Local nAcols   := 0     // Numero de Elementos do Acols
Local cProdRef := ""    // Codigo do Produto Grade
Local cItem    := "00"  // Controle de Itens do Pedido de Venda

Local lTestaDel:= If(Len(aColsOrig[1])==Len(aHeader),.F.,.T.)

If MaGrade() .And. Type("aHeadGrade")<>"U" .And. Len(aColsGrade)>0
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁInicializa o Acols para posterior atualizacao                           Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	aCols := {}
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁVarre o acols original para atualizar a variavel aCols                  Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	For nCntFor := 1 To nMaxFor
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁAtualiza o Controle de Itens do Pedido de Venda e da Grade              Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		cItem    := aColsOrig[nCntFor][nPitem] //Soma1(cItem,Len(SC6->C6_ITEM))
		cItemGrd := StrZero(0,TamSX3("C6_ITEMGRD")[1])
		cProdRef := aColsOrig[nCntFor][nPProduto]
		If ( !Empty(cProdRef) )
			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//ЁVerifica se foi digitado uma referencia                                 Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			If ( Len(aHeadGrade)>0 .And. aHeadGrade[nCntFor][1] == "R" )
				For nLinha := 1 To Len(aColsGrade[nCntFor])
					For nColuna := 2 To Len(aHeadGrade[nCntFor])
						//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
						//ЁVerifica se a valor a ser gravado                                       Ё
						//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
						If ( aColsGrade[nCntFor][nLinha][nColuna][1] <> 0 .And.;
								If(lTestaDel,!aColsOrig[nCntFor][Len(aHeader)+1],.T.) )
							cItemGrd := Soma1(cItemGrd,Len(SC6->C6_ITEMGRD))
							cProdRef := aColsOrig[nCntFor][nPProduto]
							MatGrdPrRf(@cProdRef)
							cProdRef += aColsGrade[nCntFor][nLinha][1]+;
								aHeadGrade[nCntFor][nColuna][1]
							aadd(aCols,aClone(aColsOrig[nCntFor]))
							nAcols := Len(aCols)
							aCols[nAcols][nPProduto ]  := PadR(cProdRef,Len(SB1->B1_COD))
							aCols[nAcols][nPItem    ]  := cItem
							If ( nPItGrade <> 0 )
								aCols[nAcols][nPItGrade ]  := cItemGrd
							EndIf
							If ( nPQtdVen <> 0 )
								aCols[nAcols][nPQtdVen  ]  := aColsGrade[nCntFor][nLinha][nColuna][1]
							EndIf
							If ( nPQtdVen2 <> 0 )
								aCols[nAcols][nPQtdVen2 ]  := aColsGrade[nCntFor][nLinha][nColuna][5]
							EndIf
							If ( nPQtdLib <> 0 )
								aCols[nAcols][nPQtdLib  ]  := aColsGrade[nCntFor][nLinha][nColuna][2]
							EndIf
							If ( nPValor <> 0 )
								aCols[nAcols][nPValor   ]  := a410Arred(aColsGrade[nCntFor][nLinha][nColuna][1]*aColsOrig[nCntFor][nPPrcVen],"C6_VALOR")
							EndIf
							If  ( nPGrade <>  0 )
								aCols[nAcols][nPGrade   ]  := "S"
							Endif
							If  ( nPValDesc !=  0 )
								aCols[nAcols][nPValDesc]	:= (aColsOrig[nCntFor][nPValDesc]/aColsOrig[nCntFor][nPQtdVen])*aCols[nAcols][nPQtdVen	]
							EndIf
						Else
							//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
							//ЁVerifica se o item ja foi gravado para deleta-lo                        Ё
							//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
							If ( !Empty(aColsGrade[nCntFor][nLinha][nColuna][4]) )
								cProdRef := aColsOrig[nCntFor][nPProduto]
								cItemGrd := Soma1(cItemGrd,Len(SC6->C6_ITEMGRD))
								MatGrdPrRf(@cProdRef)
								cProdRef += aColsGrade[nCntFor][nLinha][1]+;
									aHeadGrade[nCntFor][nColuna][1]
								aadd(aCols,aClone(aColsOrig[nCntFor]))
								nAcols := Len(aCols)
								aCols[nAcols][nPProduto ]  := PadR(cProdRef,Len(SB1->B1_COD))
								aCols[nAcols][nPItem    ]  := cItem
								If ( nPItGrade <> 0 )
									aCols[nAcols][nPItGrade ]  := cItemGrd
								EndIf
								If ( nPQtdVen <> 0 )
									aCols[nAcols][nPQtdVen  ]  := aColsGrade[nCntFor][nLinha][nColuna][1]
								EndIf
								If ( nPQtdLib <> 0 )
									aCols[nAcols][nPQtdLib  ]  := aColsGrade[nCntFor][nLinha][nColuna][2]
								EndIf
								If ( nPValor <> 0 )
									aCols[nAcols][nPValor   ]  := a410Arred(aColsGrade[nCntFor][nLinha][nColuna][1]*aColsOrig[nCntFor][nPPrcVen],"C6_VALOR")
								EndIf
								If  ( nPGrade <>  0 )
									aCols[nAcols][nPGrade   ]  := "S"
								EndIf
								If  ( nPValDesc !=  0 )
									aCols[nAcols][nPValDesc]	:= (aColsOrig[nCntFor][nPValDesc]/aColsOrig[nCntFor][nPQtdVen])*aCols[nAcols][nPQtdVen	]
								EndIf
								aCols[nAcols][Len(aHeader)+1] := .T.
							EndIf
						EndIf
					Next nColuna
				Next nLinha
			Else
				aadd(aCols,aClone(aColsOrig[nCntFor]))
				nAcols := Len(aCols)
				aCols[nAcols][nPItem    ]  := cItem
			EndIf
		EndIf
	Next nCntFor
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁOrdena o aCols                                                          Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	aCols       := aSort(aCols,,,{|x,y| x[nPItem]+x[nPItGrade] < y[nPItem]+y[nPItGrade] })
	aColsGrade  := {}
	aHeadGrade  := {}
EndIf
Return(Nil)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁPrograma  ЁMenuDef   Ё Autor Ё Marco Bianchi         Ё Data Ё01/09/2006Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Utilizacao de menu Funcional                               Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁArray com opcoes da rotina.                                 Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁParametros do array a Rotina:                               Ё╠╠
╠╠Ё          Ё1. Nome a aparecer no cabecalho                             Ё╠╠
╠╠Ё          Ё2. Nome da Rotina associada                                 Ё╠╠
╠╠Ё          Ё3. Reservado                                                Ё╠╠
╠╠Ё          Ё4. Tipo de Transa┤└o a ser efetuada:                        Ё╠╠
╠╠Ё          Ё	  1 - Pesquisa e Posiciona em um Banco de Dados           Ё╠╠
╠╠Ё          Ё    2 - Simplesmente Mostra os Campos                       Ё╠╠
╠╠Ё          Ё    3 - Inclui registros no Bancos de Dados                 Ё╠╠
╠╠Ё          Ё    4 - Altera o registro corrente                          Ё╠╠
╠╠Ё          Ё    5 - Remove o registro corrente do Banco de Dados        Ё╠╠
╠╠Ё          Ё5. Nivel de acesso                                          Ё╠╠
╠╠Ё          Ё6. Habilita Menu Funcional                                  Ё╠╠
╠╠цддддддддддедддддддддддддддбдддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё   DATA   Ё Programador   ЁManutencao efetuada                         Ё╠╠
╠╠цддддддддддедддддддддддддддедддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё          Ё               Ё                                            Ё╠╠
╠╠юддддддддддадддддддддддддддадддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/

Static Function MenuDef()
     
Local aRotina2  := {{STR0003,"A410Barra",0,3},;							// "Incluir"
					{STR0004,"A410Barra",0,4}}								// "Alterar"
Local aRotina3  := {{ OemToAnsi(STR0005),"A410Deleta"	,0,5,21,NIL},;
					{ OemToAnsi(STR0109),"Ma410Resid",0,2,0,NIL}}			//"Residuo"

Private aRotina := {	{	OemToAnsi(STR0001),"AxPesqui"		,0,1,0 ,.F.},;		//"Pesquisar"
							{ OemToAnsi(STR0002),"A410Visual"	,0,2,0 ,NIL},;		//"Visual"
							{ OemToAnsi(STR0003),"A410Inclui"	,0,3,0 ,NIL},;		//"Incluir"
							{ OemToAnsi(STR0004),"A410Altera"	,0,4,20,NIL},;		//"Alterar"
							{ OemToAnsi(STR0005),IIf((Type("l410Auto") <> "U" .And. l410Auto),"A410Deleta",aRotina3),0,5,0,NIL},; // Excluir
							{ OemToAnsi(STR0006),aRotina2 		,0,3,0 ,NIL},;		//"Cod.barra"
							{ OemToAnsi(STR0042),"A410PCopia"	,0,6,0 ,NIL},;		//"Copia"
							{ OemToAnsi(STR0052),"A410Devol"	,0,3,0 ,NIL},;		//"Dev. Compras"
							{ OemToAnsi(STR0095),"Ma410PvNfs"	,0,2,0 ,NIL},;		//"Prep.Doc.SaМda"
							{ OemToAnsi(STR0032),"A410Legend"	,0,3,0 ,.F.},;		//"Legenda"
							{ STR0103           ,"MsDocument"	,0,4,0 ,NIL} }		//"Conhecimento"
					
If ExistBlock("MA410MNU")
	ExecBlock("MA410MNU",.F.,.F.)
EndIf

Return(aRotina)

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁMa410Opc  Ё Autor Ё Kleber Dias Gomes     Ё Data Ё09/01/2007Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Funcao de retorno do codigo do opcional.                   Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpL1 = Se repete o mesmo grupo de opcional                Ё╠╠
╠╠Ё          Ё ExpN1 = Posicao do campo opcional do produto               Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё Nenhum                                                     Ё╠╠
╠╠цддддддддддедддддддддддддддбдддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё   DATA   Ё Programador   ЁManutencao Efetuada                         Ё╠╠
╠╠цддддддддддедддддддддддддддедддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё          Ё               Ё                                            Ё╠╠
╠╠юддддддддддадддддддддддддддадддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Function Ma410Opc(lOpcPadrao,nPOpcional)

Local cOpcional:= ""
Local aROpc    := {}
Local nI       := 0

Default lOpcPadrao:= GetNewPar("MV_REPGOPC","N") == "N"
Default nPOpcional:= If(lOpcPadrao,aScan(aHeader,{|x| AllTrim(x[2])=="C6_OPC"}),aScan(aHeader,{|x| AllTrim(x[2])=="C6_MOPC"}))

If lOpcPadrao
	cOpcional := aCols[n][nPOpcional]
Else
	aROpc:=STR2Array(aCols[n][nPOpcional],.F.)
	If !Empty(aRopc)
		For nI := 1 To Len(aROpc)
			cOpcional += aROpc[nI,2]
		Next
	Endif
Endif

Return cOpcional
/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁMa410ResidЁ Autor Ё Eduardo Riera         Ё Data Ё18/01/2007Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁRotina e eliminacao de residuo                              Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁExpC1: Alias do cabecalho do pedido de venda                Ё╠╠
╠╠Ё          ЁExpN2: Recno do cabecalho do pedido de venda                Ё╠╠
╠╠Ё          ЁExpN3: Opcao do arotina                                     Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё Nenhum                                                     Ё╠╠
╠╠цддддддддддедддддддддддддддбдддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё   DATA   Ё Programador   ЁManutencao Efetuada                         Ё╠╠
╠╠цддддддддддедддддддддддддддедддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё          Ё               Ё                                            Ё╠╠
╠╠юддддддддддадддддддддддддддадддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Function Ma410Resid(cAlias,nReg,nOpc)

Local aArea    := GetArea()
Local lValido  := .F.
Local lContinua:= .T.
Local lMt410Ace := Existblock("MT410ACE")

If SoftLock(cAlias)

	//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Ponto de entrada para validar acesso do usuario na funcao Ё
	//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If lMt410Ace
		lContinua := Execblock("MT410ACE",.F.,.F.,{nOpc})
	Endif		
	
	If a410Visual(cAlias,nReg,nOpc)==1
		If ExistBlock("M410VRES")
			lContinua := ExecBlock("M410VRES",.F.,.F.)
		EndIf
		If lContinua
			Begin Transaction
				//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Eliminacao de residuo                                Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
				dbSelectArea("SC6")
				dbSetOrder(1)
				MsSeek(xFilial("SC6")+SC5->C5_NUM)
				
				While ( !Eof() .And. SC6->C6_FILIAL == xFilial("SC6") .And. SC6->C6_NUM 	== SC5->C5_NUM )
					lValido  := .T.
					If lValido .And. !Empty(SC5->C5_PEDEXP) .And. SuperGetMv("MV_EECFAT") // Integracao SIGAEEC
						If FindFunction("EECZERASALDO")
							lValido := EECZeraSaldo(,SC5->C5_PEDEXP,,.T.,SC5->C5_NUM)
						Else
							lValido := EECCancelPed(,SC5->C5_PEDEXP,,.T.,SC5->C5_NUM)
						EndIf
					EndIf
			    	If lValido .And. (SC6->C6_QTDVEN - SC6->C6_QTDENT) > 0
		    		    MaResDoFat(,.T.,.F.)
		    		EndIf	
					dbSelectArea("SC6")
					dbSkip()
				EndDo
				SC6->(MaLiberOk({SC5->C5_NUM},.T.))
			End Transaction
		EndIf
	EndIf
EndIf
RestArea(aArea)
Return


/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤└o    ЁMta410Vis Ё Autor Ё Marco Bianchi         Ё Data Ё 01/12/06 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤└o ЁFuncao executada a partir da FillGetdados para validar cada Ё╠╠
╠╠Ё          Ёregistro da tabela. Se retornar .T. FILLGETDADOS considera  Ё╠╠
╠╠Ё          Ёo registro, se .F. despreza o registro.                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   ЁMColsVis()                                                  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametro Ё                                                            Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё MATA410                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/


Static Function Mta410Vis(cArqQry,nTotPed,nTotDes,lGrade)

Local lRet      := .T.
Local nTamaCols := Len(aCols)
Local nPosItem  := GDFieldPos("C6_ITEM")
Local nPosQtd   := GDFieldPos("C6_QTDVEN")
Local nPosQtd2  := GDFieldPos("C6_UNSVEN")
Local nPosVlr   := GDFieldPos("C6_VALOR")
Local nPosSld   := GDFieldPos("C6_SLDALIB")
Local nPosDesc  := GDFieldPos("C6_VALDESC")
Local lCriaCols := .F.		// Nao permitir que a funcao A410Grade crie o aCols
      
//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Verifica se este item foi digitada atraves de uma    Ё
//Ё grade, se for junta todos os itens da grade em uma   Ё
//Ё referencia , abrindo os itens so quando teclar enter Ё
//Ё na quantidade                                        Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
If !("MATA410" $ SuperGetMV("MV_GRDMULT",.F.,"")) .And. ( (cArqQry)->C6_GRADE == "S" .And. lGrade )
	a410Grade(.F.,,cArqQry,.T.,lCriaCols)   
	If ( nTamAcols==0 .Or. aCols[nTamAcols][nPosItem] <> (cArqQry)->C6_ITEM )
		lRet := .T.	
	Else
		lRet := .F.	
		aCols[nTamAcols][nPosQtd]  += (cArqQry)->C6_QTDVEN
		aCols[nTamAcols][nPosQtd2] += (cArqQry)->C6_UNSVEN
		If ( nPosDesc > 0 )
			aCols[nTamAcols][nPosDesc] += (cArqQry)->C6_VALDESC
		Endif
		If ( nPosSld > 0 )
			aCols[nTamAcols][nPosSld] += Ma440SaLib()
		EndIf
		aCols[nTamAcols][nPosVlr] += (cArqQry)->C6_VALOR
	EndIf
EndIf
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁEfetua a Somatoria do Rodape                                            Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
nTotPed	+= (cArqQry)->C6_VALOR
If ( (cArqQry)->C6_PRUNIT = 0 )
	nTotDes	+= (cArqQry)->C6_VALDESC
Else
	nTotDes += A410Arred(((cArqQry)->C6_PRUNIT*(cArqQry)->C6_QTDVEN),"C6_VALOR")-A410Arred(((cArqQry)->C6_PRCVEN*(cArqQry)->C6_QTDVEN),"C6_VALOR")
EndIf


Return(lRet)


/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤└o    ЁMta410Alt Ё Autor Ё Marco Bianchi         Ё Data Ё 29/01/07 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤└o ЁFuncao executada a partir da FillGetdados para validar cada Ё╠╠
╠╠Ё          Ёregistro da tabela. Se retornar .T. FILLGETDADOS considera  Ё╠╠
╠╠Ё          Ёo registro, se .F. despreza o registro.                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   ЁMColsAlt()                                                  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametro Ё                                                            Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё MATA410                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Static Function Mta410Alt(cArqQry,nTotalPed,nTotalDes,lGrade,lBloqueio,lNaoFatur,lContrat,aRegSC6)

Local lRet      := .T.           
Local lCriaCols := .F.		// Nao permitir que a funcao A410Grade crie o aCols
Local nTamaCols := Len(aCols)
Local nPosItem  := GDFieldPos("C6_ITEM")
Local nPosQtd   := GDFieldPos("C6_QTDVEN")
Local nPosQtd2  := GDFieldPos("C6_UNSVEN")
Local nPosVlr   := GDFieldPos("C6_VALOR")
Local nPosSld   := GDFieldPos("C6_SLDALIB")
Local nPosDesc  := GDFieldPos("C6_VALDESC")
           
If !(("R"$Alltrim((cArqQry)->C6_BLQ)).And.(SuperGetMv("MV_RSDOFAT")=="N"))
	lBloqueio := .F.
EndIf
If !"R"$Alltrim((cArqQry)->C6_BLQ) .Or. SuperGetMv("MV_RSDOFAT")=="S"
	If SC5->C5_TIPO$"CIP"
		If Empty((cArqQry)->C6_NOTA)
			lNaoFatur := .T.
		EndIf
	Else
	    dbSelectArea("SF4")
		dbSetOrder(1)
		dbSeek(xFilial("SF4")+(cArqQry)->C6_TES)
		If ( (cArqQry)->C6_QTDENT < (cArqQry)->C6_QTDVEN .AND. SF4->F4_QTDZERO <> "1" ) .OR. ;
	   		((cArqQry)->C6_QTDENT == (cArqQry)->C6_QTDVEN .AND. SF4->F4_QTDZERO == "1" .AND. Empty((cArqQry)->C6_NOTA))
			lNaoFatur := .T.
		EndIf
	EndIf
EndIf
If !Empty((cArqQry)->C6_CONTRAT) .And. !lContrat
	dbSelectArea("ADB")
	dbSetOrder(1)
	If MsSeek(xFilial("ADB")+(cArqQry)->C6_CONTRAT+SC6->C6_ITEMCON)
		If ADB->ADB_QTDEMP > 0 .And. ADB->ADB_PEDCOB == (cArqQry)->C6_NUM
			lContrat := .T.
		EndIf
	EndIf
	dbSelectArea(cArqQry)
EndIf

//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Verifica se este item foi digitada atraves de uma    Ё
//Ё grade, se for junta todos os itens da grade em uma   Ё
//Ё referencia , abrindo os itens so quando teclar enter Ё
//Ё na quantidade                                        Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
If !("MATA410" $ SuperGetMV("MV_GRDMULT",.F.,"")) .And. ( (cArqQry)->C6_GRADE == "S" .And. lGrade )
	a410Grade(.T.,,cArqQry,.T.,lCriaCols)
	If ( nTamAcols==0 .Or. aCols[nTamAcols][nPosItem] <> (cArqQry)->C6_ITEM )
		lRet := .T.	
	Else
		lRet := .F.	
		aCols[nTamAcols][nPosQtd]  += (cArqQry)->C6_QTDVEN
		aCols[nTamAcols][nPosQtd2] += (cArqQry)->C6_UNSVEN
		If ( nPosDesc > 0 )
			aCols[nTamAcols][nPosDesc] += (cArqQry)->C6_VALDESC
		Endif
		If ( nPosSld > 0 )
			aCols[nTamAcols][nPosSld] += Ma440SaLib()
		EndIf
		aCols[nTamAcols][nPosVlr] += (cArqQry)->C6_VALOR
	EndIf
EndIf


nTotalPed += (cArqQry)->C6_VALOR
If ( (cArqQry)->C6_PRUNIT = 0 )
	nTotalDes += (cArqQry)->C6_VALDESC
Else
	nTotalDes += A410Arred(((cArqQry)->C6_PRUNIT*(cArqQry)->C6_QTDVEN),"C6_VALOR")-A410Arred(((cArqQry)->C6_PRCVEN*(cArqQry)->C6_QTDVEN),"C6_VALOR")
EndIf

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁGuarda os registros do SC6 para posterior gravacao                      Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
aadd(aRegSC6,If((cArqQry)->(FieldPos("SC6RECNO")) > 0,(cArqQry)->SC6RECNO,(cArqQry)->(RecNo())))


Return lRet

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤└o    ЁMta410Del Ё Autor Ё Marco Bianchi         Ё Data Ё 30/01/07 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤└o ЁFuncao executada a partir da FillGetdados para validar cada Ё╠╠
╠╠Ё          Ёregistro da tabela. Se retornar .T. FILLGETDADOS considera  Ё╠╠
╠╠Ё          Ёo registro, se .F. despreza o registro.                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   ЁMColsDel()                                                  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametro Ё                                                            Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё MATA410                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Static Function Mta410Del(cArqQry,nTotalPed,nTotalDes,lGrade,aRegSC6,lPedTLMK,lLiber,lFaturado,lContrat)

Local lRet      := .T.
Local lCriaCols := .F.		// Nao permitir que a funcao A410Grade crie o aCols
Local nTamaCols :=Len(aCols)
Local nPosItem  := GDFieldPos("C6_ITEM")
Local nPosQtd   := GDFieldPos("C6_QTDVEN")
Local nPosQtd2  := GDFieldPos("C6_UNSVEN")
Local nPosVlr   := GDFieldPos("C6_VALOR")
Local nPosSld   := GDFieldPos("C6_SLDALIB")
Local nPosDesc  := GDFieldPos("C6_VALDESC")

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Verifica se algum item foi criado no TLMK                              Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If Left( ( cArqQry )->C6_PEDCLI, 3 ) == "TMK"
	lPedTLMK := .T.
EndIf

If ( (cArqQry)->C6_QTDEMP > 0 )
	lLiber := .T.
EndIf

If ( (cArqQry)->C6_QTDENT > 0 ) .Or. ( SC5->C5_TIPO $ "CIP" .And. !Empty((cArqQry)->C6_NOTA) )
	lFaturado  :=  .T.
EndIf
If !Empty((cArqQry)->C6_CONTRAT) .And. !lContrat
	dbSelectArea("ADB")
	dbSetOrder(1)
	If MsSeek(xFilial("ADB")+(cArqQry)->C6_CONTRAT+SC6->C6_ITEMCON)
		If ADB->ADB_QTDEMP > 0 .And. ADB->ADB_PEDCOB == (cArqQry)->C6_NUM
			lContrat := .T.
		EndIf
	EndIf
	dbSelectArea(cArqQry)
EndIf
//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Verifica se este item gerou OP/SC, caso tenha gerado Ё
//Ё inclui no array aOPs para perguntar se exclui ou nao Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
If (cArqQry)->C6_OP $ "01/03"
	AADD(aOPs,{(cArqQry)->C6_ITEM,Alltrim((cArqQry)->C6_PRODUTO),(cArqQry)->C6_NUMOP,(cArqQry)->C6_ITEMOP, '',''})
	If SC6->(FieldPos("C6_NUMSC")) > 0 .And. SC6->(FieldPos("C6_ITEMSC")) > 0 .And.  !Empty((cArqQry)->C6_NUMSC)
		aOPs[Len(aOPs)][5] := (cArqQry)->C6_NUMSC
		aOPs[Len(aOPs)][6] := (cArqQry)->C6_ITEMSC
	EndIf
EndIf
//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Verifica se este item foi digitada atraves de uma    Ё
//Ё grade, se for junta todos os itens da grade em uma   Ё
//Ё referencia , abrindo os itens so quando teclar enter Ё
//Ё na quantidade                                        Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
If !("MATA410" $ SuperGetMV("MV_GRDMULT",.F.,"")) .And. ( (cArqQry)->C6_GRADE == "S" .And. lGrade )
	a410Grade(.T.,,cArqQry,.T.,lCriaCols)
	If ( nTamAcols==0 .Or. aCols[nTamAcols][nPosItem] <> (cArqQry)->C6_ITEM )
		lRet := .T.	
	Else
		lRet := .F.	
		aCols[nTamAcols][nPosQtd]  += (cArqQry)->C6_QTDVEN
		aCols[nTamAcols][nPosQtd2] += (cArqQry)->C6_UNSVEN
		If ( nPosDesc > 0 )
			aCols[nTamAcols][nPosDesc] += (cArqQry)->C6_VALDESC
		Endif
		If ( nPosSld > 0 )
			aCols[nTamAcols][nPosSld] += Ma440SaLib()
		EndIf
		aCols[nTamAcols][nPosVlr] += (cArqQry)->C6_VALOR
	EndIf
EndIf

nTotalPed += (cArqQry)->C6_VALOR
If ( (cArqQry)->C6_PRUNIT = 0 )
	nTotalDes += (cArqQry)->C6_VALDESC
Else
	nTotalDes += A410Arred(((cArqQry)->C6_PRUNIT*(cArqQry)->C6_QTDVEN),"C6_VALOR")-A410Arred(((cArqQry)->C6_PRCVEN*(cArqQry)->C6_QTDVEN),"C6_VALOR")
EndIf

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁGuarda os registros do SC6 para posterior gravacao                      Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
aadd(aRegSC6,If((cArqQry)->(FieldPos("SC6RECNO")) > 0,(cArqQry)->SC6RECNO,(cArqQry)->(RecNo())))

Return lRet


/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤└o    ЁMta410Cop Ё Autor Ё Marco Bianchi         Ё Data Ё 30/01/07 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤└o ЁFuncao executada a partir da FillGetdados para validar cada Ё╠╠
╠╠Ё          Ёregistro da tabela. Se retornar .T. FILLGETDADOS considera  Ё╠╠
╠╠Ё          Ёo registro, se .F. despreza o registro.                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   ЁMColsCop()                                                  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametro Ё                                                            Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё MATA410                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Static Function Mta410Cop(cArqQry,nTotalPed,nTotalDes,lGrade)

Local lRet      := .T.
Local lCriaCols := .F.		// Nao permitir que a funcao A410Grade crie o aCols
Local nTamaCols :=Len(aCols)
Local nPosItem  := GDFieldPos("C6_ITEM")
Local nPosQtd   := GDFieldPos("C6_QTDVEN")
Local nPosQtd2  := GDFieldPos("C6_UNSVEN")
Local nPosVlr   := GDFieldPos("C6_VALOR")
Local nPosSld   := GDFieldPos("C6_SLDALIB")
Local nPosDesc  := GDFieldPos("C6_VALDESC")
                         
//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Verifica se este item foi digitada atraves de uma    Ё
//Ё grade, se for junta todos os itens da grade em uma   Ё
//Ё referencia , abrindo os itens so quando teclar enter Ё
//Ё na quantidade                                        Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
If !("MATA410" $ SuperGetMV("MV_GRDMULT",.F.,"")) .And. ( (cArqQry)->C6_GRADE == "S" .And. lGrade )
	a410Grade(.T.,,cArqQry,.F.,lCriaCols)
	If ( nTamAcols==0 .Or. aCols[nTamAcols][nPosItem] <> (cArqQry)->C6_ITEM )
		lRet := .T.	
	Else
		lRet := .F.	
		aCols[nTamAcols][nPosQtd]  += (cArqQry)->C6_QTDVEN
		aCols[nTamAcols][nPosQtd2] += (cArqQry)->C6_UNSVEN
		If ( nPosDesc > 0 )
			aCols[nTamAcols][nPosDesc] += (cArqQry)->C6_VALDESC
		Endif
		If ( nPosSld > 0 )
			aCols[nTamAcols][nPosSld] += Ma440SaLib()
		EndIf
		aCols[nTamAcols][nPosVlr] += (cArqQry)->C6_VALOR
	EndIf
EndIf
	
nTotalPed += (cArqQry)->C6_VALOR
If ( (cArqQry)->C6_PRUNIT = 0 )
	nTotalDes += (cArqQry)->C6_VALDESC
Else
	nTotalDes += A410Arred(((cArqQry)->C6_PRUNIT*(cArqQry)->C6_QTDVEN),"C6_VALOR")-A410Arred(((cArqQry)->C6_PRCVEN*(cArqQry)->C6_QTDVEN),"C6_VALOR")
EndIf


Return lRet


/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤└o    ЁAfterCols Ё Autor Ё Marco Bianchi         Ё Data Ё 24/01/07 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤└o ЁFuncao executada apos inclusao de nova linha no aCols pela  Ё╠╠
╠╠Ё          ЁFillgetDados.                                               Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   ЁAfterCols()                                                 Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametro Ё                                                            Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё MATA410                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Static Function AfterCols(cArqQry,cTipoDat,dCopia,dOrig,lCopia)
           
Local nPosProd  := GDFieldPos("C6_PRODUTO")
Local nPosGrade := GDFieldPos("C6_GRADE")
Local nPIdentB6 := GDFieldPos("C6_IDENTB6")
Local nPEntreg  := GDFieldPos("C6_ENTREG")
Local nPPedCli  := GDFieldPos("C6_PEDCLI")
Local nQtdLib   := GDFieldPos("C6_QTDLIB")
Local nAux      := 0
Local aLiberado := {}
Local cCampo    := ""

DEFAULT lCopia  := .F.
                                 
If !("MATA410" $ SuperGetMV("MV_GRDMULT",.F.,""))                                 
	If nPosGrade > 0 .And. aCols[Len(aCols)][nPosGrade] == "S"
		cProdRef := (cArqQry)->C6_PRODUTO
		MatGrdPrRf(@cProdRef,.T.)
		aCols[Len(aCols)][nPosProd] := cProdRef
	Else
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Mesmo nao sendo um item digitado atraves de grade e' necessa-Ё
		//Ё rio criar o Array referente a este item para controle da     Ё
		//Ё grade                                                        Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If FindFunction("MsMatGrade") .And. IsAtNewGrd()
			oGrade:MontaGrade(Len(aCols))
		Else
			MatGrdMont(Len(aCols))
		EndIf 
	EndIf	
EndIf

If Altera
	If ( SC5->C5_TIPO <> "D" )
		nAux := aScan(aLiberado,{|x| x[2] == aCols[Len(aCols)][nPIdentB6]})
		If ( nAux == 0 )
			aadd(aLiberado,{ (cArqQry)->C6_ITEM , aCols[Len(aCols)][nPIdentB6] , (cArqQry)->C6_QTDEMP, (cArqQry)->C6_QTDENT })
		Else
			aLiberado[nAux][3] += (cArqQry)->C6_QTDEMP
			aLiberado[nAux][4] += (cArqQry)->C6_QTDENT
		EndIf
	Else
		nAux := aScan(aLiberado,{|x| x[1] == (cArqQry)->C6_SERIORI .And.;
		x[2] == (cArqQry)->C6_NFORI   .And.;
		x[3] == (cArqQry)->C6_ITEMORI })
		If ( nAux == 0 )
			aadd(aLiberado,{ (cArqQry)->C6_SERIORI , (cArqQry)->C6_NFORI , (cArqQry)->C6_ITEMORI , (cArqQry)->C6_QTDEMP })
		Else
			aLiberado[nAux][4] += (cArqQry)->C6_QTDEMP
		EndIf
	EndIf
	// Necessario para disparar inicializador padrao
	aCols[Len(aCols)][nQtdLib] := CriaVar("C6_QTDLIB")
EndIf

If lCopia
	cCampo := Alltrim(aHeader[nPEntreg,2])           
	Do Case
		Case cTipoDat == "1"
			aCols[Len(aCols)][nPEntreg] := FieldGet(FieldPos(cCampo))
		Case cTipoDat == "2"
			aCols[Len(aCols)][nPEntreg] := If(FieldGet(FieldPos(cCampo)) < dCopia,dCopia,FieldGet(FieldPos(cCampo)) )
		Case cTipoDat == "3"
			aCols[Len(aCols)][nPEntreg] := dCopia + (FieldGet(FieldPos(cCampo)) - dOrig )
	EndCase

	If SubStr(aCols[Len(aCols)][nPPedCli],1,3)=="TMK"
		cCampo := Alltrim(aHeader[nPPedCli,2])           
		aCols[Len(aCols)][nPPedCli] := CriaVar(cCampo)
	EndIf	

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Estes campos nao podem ser copiados                  Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
	GDFieldPut("C6_QTDLIB"  ,CriaVar("C6_QTDLIB"  ),Len(aCols))
	GDFieldPut("C6_RESERVA" ,CriaVar("C6_RESERVA" ),Len(aCols))
	GDFieldPut("C6_CONTRAT" ,CriaVar("C6_CONTRAT" ),Len(aCols))
	GDFieldPut("C6_ITEMCON" ,CriaVar("C6_ITEMCON" ),Len(aCols))
	GDFieldPut("C6_PROJPMS" ,CriaVar("C6_PROJPMS" ),Len(aCols))
	GDFieldPut("C6_EDTPMS"  ,CriaVar("C6_EDTPMS"  ),Len(aCols))
	GDFieldPut("C6_TASKPMS" ,CriaVar("C6_TASKPMS" ),Len(aCols))
	GDFieldPut("C6_LICITA"  ,CriaVar("C6_LICITA"  ),Len(aCols))
	GDFieldPut("C6_PROJET"  ,CriaVar("C6_PROJET"  ),Len(aCols))
	GDFieldPut("C6_ITPROJ"  ,CriaVar("C6_ITPROJ"  ),Len(aCols))
	GDFieldPut("C6_CONTRT"  ,CriaVar("C6_CONTRT"  ),Len(aCols))
	GDFieldPut("C6_TPCONTR" ,CriaVar("C6_TPCONTR" ),Len(aCols))
	GDFieldPut("C6_ITCONTR" ,CriaVar("C6_ITCONTR" ),Len(aCols))
	GDFieldPut("C6_NUMOS"   ,CriaVar("C6_NUMOS"   ),Len(aCols))
	GDFieldPut("C6_NUMOSFAT",CriaVar("C6_NUMOSFAT"),Len(aCols))
	GDFieldPut("C6_OP"      ,CriaVar("C6_OP"      ),Len(aCols))
	GDFieldPut("C6_NUMOP"   ,CriaVar("C6_NUMOP"   ),Len(aCols))
	GDFieldPut("C6_ITEMOP"  ,CriaVar("C6_ITEMOP"  ),Len(aCols))
	GDFieldPut("C6_NUMSC"   ,CriaVar("C6_NUMSC"   ),Len(aCols))
	GDFieldPut("C6_ITEMSC"  ,CriaVar("C6_ITEMSC"  ),Len(aCols))
	GDFieldPut("C6_NUMORC"  ,CriaVar("C6_NUMORC"  ),Len(aCols))
	GDFieldPut("C6_BLQ"     ,CriaVar("C6_BLQ"     ),Len(aCols))
	GDFieldPut("C6_NOTA"    ,CriaVar("C6_NOTA"    ),Len(aCols))
	GDFieldPut("C6_SERIE"   ,CriaVar("C6_SERIE"   ),Len(aCols))
	
EndIf

Return(.T.) 

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠иммммммммммяммммммммммкмммммммяммммммммммммммммммммкммммммяммммммммммммм╩╠╠
╠╠╨Programa  ЁAjustaSX1 ╨Autor  ЁMarcelo Alexandre   ╨ Data Ё 08/05/2007  ╨╠╠
╠╠лммммммммммьмммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Uso       Ё MATA410		                                              ╨╠╠
╠╠хммммммммммомммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╪╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/

Static Function AjustaSX1()

Local aHelpPor	:= {}
Local aHelpEng	:= {}
Local aHelpSpa	:= {}

aHelpPor := {}
aHelpSpa := {}
aHelpEng := {}

AADD(aHelpPor,"Informe Saldo para considerar o saldo 	")
AADD(aHelpPor,"restante do pedido de vendas o que foi	") 
AADD(aHelpPor,"faturado e eliminado resМduo ou Total.	")

AADD(aHelpSpa,"Informe Saldo para considerar el saldo 	")
AADD(aHelpSpa,"restante del pedido de ventas, el que se ")
AADD(aHelpSpa,"facturС y eliminС por residuo ou total.	")

AADD(aHelpEng,"Inform the balance to consider the remai-")
AADD(aHelpEng,"ning balance of the request of sales that") 
AADD(aHelpEng,"was invoiced and removed residue or Total")

PutSx1("MTA410","04","Planilha Financeira?","Planilla financiera?","Financial Worksheet?","mv_ch4","N",1,0,2,"C","","","","","mv_par04","Saldo","Saldo","Balance","","Total","Total","Total","","","","","","","","","")
PutSX1Help("P.MTA41004.",aHelpPor,aHelpEng,aHelpSpa)
                       
PutSx1("MTA410","05","Contabiliza On-Line?","©Contabiliza en linea?","Record Online?"           ,"mv_ch5","N",1,0,2,"C","","","","","mv_par05","Sim","Si","Yes"  ,"","Nao","No","No","","","","","","","","","")
PutSx1("MTA410","06","Aglutina Lanc. Contabil?","©Agrupa Asto. Contable?","Group Accnt.Entry?"  ,"mv_ch6","N",1,0,2,"C","","","","","mv_par06","Sim","Si","Yes"  ,"","Nao","No","No","","","","","","","","","")
PutSx1("MTA410","07","Mostra lancamento?","©Muestra asiento?","Display Entry ?"                 ,"mv_ch7","N",1,0,2,"C","","","","","mv_par07","Sim","Si","Yes"  ,"","Nao","No","No","","","","","","","","","")
   
aHelpPor := {}
aHelpSpa := {}
aHelpEng := {}

AADD(aHelpPor,"Pedido de Venda gerado pelo mСdulo de")
AADD(aHelpPor,"gestЦo de contratos, este pedido nЦo ") 
AADD(aHelpPor,"poderА ser alterado por esse processo.")

AADD(aHelpSpa,"Pedido de Venda generado por el Modulo")
AADD(aHelpSpa,"Gestion de Contratos, este pedido no ")
AADD(aHelpSpa,"podra modificarse o borrarse ")
AADD(aHelpSpa,"por esta rutina.	")

AADD(aHelpEng,"Sales Order generated by the Contracts ")
AADD(aHelpEng," Management Module, therefore this ") 
AADD(aHelpEng,"order cannot be modified or deleted by ")
AADD(aHelpEng,"this routine. ")  

PutHelp("PMTA410AGCT",aHelpPor,aHelpEng,aHelpSpa,.T.) 

aHelpPor := {}
aHelpSpa := {}
aHelpEng := {}

AADD(aHelpPor,"Estorne a mediГЦo referente ao pedido.   ")

AADD(aHelpSpa,"Revierta la medicion referente al pedido.")

AADD(aHelpEng,"Reverse measurement related to order.    ")
       
PutHelp("SMTA410AGCT",aHelpPor,aHelpEng,aHelpSpa,.T.)

aHelpPor := {}
aHelpSpa := {}
aHelpEng := {}

AADD(aHelpPor,"O preГo nЦo poderА ser alterado pois foi ")
AADD(aHelpPor,"aplicado desconto ao item do pedido de ") 
AADD(aHelpPor,"venda.")

AADD(aHelpSpa,"El precio no poderА ser cambiado pues fue")
AADD(aHelpSpa,"aplicado descuento al Мtem del pedido de")
AADD(aHelpSpa,"venta.")

AADD(aHelpEng,"The price cannot be changed because it")
AADD(aHelpEng,"was applied disccount in sales order item") 

PutSX1Help("PA410PRCD",aHelpPor,aHelpEng,aHelpSpa)

aHelpPor := {}
aHelpSpa := {}
aHelpEng := {}

AADD(aHelpPor,"Elimine o desconto aplicado ao item do")
AADD(aHelpPor,"pedido e entЦo realize as alteraГУes") 
AADD(aHelpPor,"necessАrias.")

AADD(aHelpSpa,"Elimine el descuento aplicado al Мtem del")
AADD(aHelpSpa,"pedido y entonces realice los cambios")
AADD(aHelpSpa,"necesario.")

AADD(aHelpEng,"Remove the disccount applied in order")
AADD(aHelpEng,"item and then make the necessary changes.") 

PutSX1Help("SA410PRCD",aHelpPor,aHelpEng,aHelpSpa)

Return  

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠иммммммммммяммммммммммкмммммммяммммммммммммммммммммкммммммяммммммммммммм╩╠╠
╠╠╨Programa  |A410AjuSX3╨Autor  ЁMarcelo Alexandre   ╨ Data Ё 11/07/2007  ╨╠╠
╠╠лммммммммммьмммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Uso       Ё MATA410		                                              ╨╠╠
╠╠хммммммммммомммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╪╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/

Static Function A410AjuSX3()

Local aAreaSX3 := SX3->(GetArea())
Local aAreaAnt := GetArea()
Local nCntfor  := 0
Local nNrVend  := Fa440CntVen()
Local aPHelpPor := {"Indica se a ordem de separaГЦo (SIGAWMS)",;
					"deve ser gerada na liberaГЦo do pedido,",;
					"na montagem ou na unitizaГЦo da carga." }
Local aPHelpEng := {"Indica si el O.S. (SIGAWMS)",;
					"debe ser generada en el pedido,",;
					"en el montaje o en la unitization."}
Local aPHelpSpa := {"It indicates if generates S.O.(SIGAWMS)",;
					"in Sales Order, in Assemble Load",;
					"or in unitizacion."}

SX3->(dbSetOrder(2))

//-- SIGAWMS: Ajuste do ComboBox
If	SX3->(dbSeek("C5_GERAWMS"))
	If	SX3->(Empty(X3_CBOX) .Or. Empty(X3_CBOXSPA) .Or. Empty(X3_CBOXENG))
		RecLock("SX3",.F.)
		SX3->X3_CBOX    := "1=no Pedido;2=na Montagem da Carga;3=na Unitizacao da Carga"
		SX3->X3_CBOXSPA := "1=en el Pedido;2=en el Montaje de Carga;3=en la Unitizacion"
		SX3->X3_CBOXENG := "1=in Sales Order;2=in Assemble Load;3=in Unitization"
		MsUnLock()
	EndIf
	PutHelp("PC5_GERAWMS",aPHelpPor,aPHelpEng,aPHelpSpa,.T.)
EndIf

SX3->(dbSetOrder(2))
For nCntfor := 1 To nNrVend
	SX3->(dbSeek("C5_VEND"+Str(nCntfor,1)))
	If '.OR. EXISTCPO("SA3")' $ Upper(SX3->X3_VALID)
		RecLock("SX3",.F.)
		Replace SX3->X3_VALID With StrTran(AllTrim(Upper(SX3->X3_VALID)),'.OR. EXISTCPO("SA3")','')
		Replace SX3->X3_VALID With StrTran(AllTrim(Upper(SX3->X3_VALID)),'.AND. VAZIO()','.OR. VAZIO()')
		MsUnLock()
	Else
		Exit
	EndIf
Next nCntfor

If cPaisLoc == "ANG"
	If	SX3->(dbSeek("C5_DOCGER")) 
		If AllTrim(SX3->X3_CBOX) <> '1=Factura;2=Guia de Transporte;3=Entrega Futura'
			RecLock("SX3",.F.)
			SX3->X3_CBOX := '1=Factura;2=Guia de Transporte;3=Entrega Futura'
			MsUnLock()
		EndIf
	EndIf
EndIf
           
//Impede que o campo C6_QTDLIB deixe de ser "Usado". 
SX3->(dbSetOrder(2))
if SX3->( dbSeek( "C6_QTDLIB" ) ) .and. SX3->X3_USADO <> "──────────────═"
         RecLock("SX3", .F.) 
         SX3->X3_USADO  := "──────────────═" 
         SX3->( msUnlock() )
endif 

SX3->(dbSetOrder(2))
if SX3->( dbSeek( "D2_ITEMPV" ) ) .and. Alltrim(SX3->X3_PICTURE) <> "@!"
         RecLock("SX3", .F.) 
         SX3->X3_PICTURE  := "@!" 
         SX3->( msUnlock() )
endif

SX3->(dbSeek("C6_PRCVEN"))
If !("A410QtdGra" $ SX3->X3_VALID)
	RecLock("SX3",.F.)
	Replace SX3->X3_VALID With StrTran(SX3->X3_VALID,"A410MultT","A410QtdGra() .And. A410MultT")
	MsUnLock()
EndIf

SX3->(dbSeek("C6_BLQ"))
If !("A410QtdGra" $ SX3->X3_VALID)
	RecLock("SX3",.F.)
	Replace SX3->X3_VALID With StrTran(SX3->X3_VALID,"ExistCpo","A410QtdGra() .And. ExistCpo")
	MsUnLock()
EndIf

RestArea(aAreaSX3)
RestArea(aAreaAnt)
Return Nil
/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁPrograma  ЁA410LAICMSЁ Autor Ё Gustavo G. Rueda      Ё Data Ё05/12/2007Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁFuncao para montagem do GETDADOS do folder de lancamentos   Ё╠╠
╠╠Ё          Ё fiscais.                                                   Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁoLancApICMS -> Objeto criado pelo MSNEWGETDADOS             Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁoDlg -> Objeto pai onde o GETDADOS serah criado.            Ё╠╠
╠╠Ё          ЁaPos -> posicoes de criacao do objeto.                      Ё╠╠
╠╠Ё          ЁaHeadCDA -> array com o HEADER da tabela CDA                Ё╠╠
╠╠Ё          ЁaColsCDA -> array com o ACOLS da tabela CDA                 Ё╠╠
╠╠Ё          ЁlVisual -> Flag de visualizacao                             Ё╠╠
╠╠Ё          ЁlInclui -> Flag de inclusao                                 Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддбдддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё   DATA   Ё Programador   ЁManutencao efetuada                         Ё╠╠
╠╠цддддддддддедддддддддддддддедддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё          Ё               Ё                                            Ё╠╠
╠╠юддддддддддадддддддддддддддадддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Static Function A410LAICMS(oDlg,aPos,aHeadCDA,aColsCDA,lVisual,lInclui)
Local	oLancApICMS
Local	aCmps		:=	{}
Local	nI			:=	0
Local	aLAp		:=	A410LancAp()
Local	cMaskBs		:=	""
Local	cMaskAlq	:=	""
Local	cMaskVlr	:=	""

aMHead("CDA","CDA_TPMOVI/CDA_ESPECI/CDA_FORMUL/CDA_NUMERO/CDA_SERIE/CDA_CLIFOR/CDA_LOJA/",@aHeadCDA)
For nI := 1 To Len(aHeadCDA)
	aAdd(aCmps,aHeadCDA[nI,1])
	
	If "CDA_BASE"==AllTrim(aHeadCDA[nI,2])
		cMaskBs		:=	AllTrim(aHeadCDA[nI,3])
		
	ElseIf "CDA_ALIQ"==AllTrim(aHeadCDA[nI,2])
		cMaskAlq	:=	AllTrim(aHeadCDA[nI,3])
		
	ElseIf "CDA_VALOR"==AllTrim(aHeadCDA[nI,2])
		cMaskVlr	:=	AllTrim(aHeadCDA[nI,3])
	EndIf
Next nI

If Len(aLAp)==0
	aLAp	:=	{{"","","1",0,0,0,""}}
EndIf

oLancApICMS	:=	TWBrowse():New( aPos[1],aPos[2],aPos[3],aPos[4],,aCmps,,oDlg,,,,,,,,,,,,.F.,,.T.,,.F.,,, )
oLancApICMS:SetArray(aLAp)
oLancApICMS:bLine := {|| {aLAp[oLancApICMS:nAT,1],aLAp[oLancApICMS:nAT,7],aLAp[oLancApICMS:nAT,2],Iif(aLAp[oLancApICMS:nAT,3]=="1","Sim","NЦo"),Transform(aLAp[oLancApICMS:nAT,4],cMaskBs),Transform(aLAp[oLancApICMS:nAT,5],cMaskAlq),Transform(aLAp[oLancApICMS:nAT,6],cMaskVlr)} }
Return oLancApICMS
/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁPrograma  ЁA410LancApЁ Autor Ё Gustavo G. Rueda      Ё Data Ё05/12/2007Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Funcao para montar os lancamento fiscais para exibicao     Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁaLancAp -> Lancamentos montados em cima da MATXFIS          Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁnenhum                                                      Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддбдддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё   DATA   Ё Programador   ЁManutencao efetuada                         Ё╠╠
╠╠цддддддддддедддддддддддддддедддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё          Ё               Ё                                            Ё╠╠
╠╠юддддддддддадддддддддддддддадддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Static Function A410LancAp()
Local	aLancAp	:=	MaFisAjIt(,2)
Return aLancAp
/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁPrograma  ЁaMHead    Ё Autor Ё Gustavo G. Rueda      Ё Data Ё05/12/2007Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Funcao para montagem do HEADER do GETDADOS                 Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё.T.                                                         Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁcAlias -> Alias da tabela base para montagem do HEADER      Ё╠╠
╠╠Ё          ЁcNCmps -> Campos que nao serao considerados no HEADER       Ё╠╠
╠╠Ё          ЁaH -> array no qual o HEADER serah montado                  Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддбдддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё   DATA   Ё Programador   ЁManutencao efetuada                         Ё╠╠
╠╠цддддддддддедддддддддддддддедддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё          Ё               Ё                                            Ё╠╠
╠╠юддддддддддадддддддддддддддадддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Static Function aMHead(cAlias,cNCmps,aH)
Local	lRet	:=	.T.
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Salva a Integridade dos campos de Bancos de Dados            Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
dbSelectArea("SX3")
dbSetOrder(1)
dbSeek(cAlias)
While !Eof() .And. (X3_ARQUIVO==cAlias)
	IF X3USO(X3_USADO) .And. cNivel >= X3_NIVEL .and. !(AllTrim(X3_CAMPO)+"/"$cNCmps)
		AADD(aH,{ Trim(X3Titulo()), ;
			AllTrim(X3_CAMPO),;
			X3_PICTURE,;
			X3_TAMANHO,;
			X3_DECIMAL,;
			X3_VALID,;
			X3_USADO,;
			X3_TIPO,;
			X3_F3,;
			X3_CONTEXT,;
			X3_CBOX,;
			X3_RELACAO})
	Endif
	dbSkip()
Enddo
Return lRet

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠иммммммммммяммммммммммкмммммммяммммммммммммммммммммкммммммяммммммммммммм╩╠╠
╠╠╨Programa  ЁA410RemBen╨Autor  ЁAndre Anjos         ╨ Data Ё  04/12/08   ╨╠╠
╠╠лммммммммммьммммммммммймммммммоммммммммммммммммммммйммммммоммммммммммммм╧╠╠
╠╠╨Descricao Ё Exibe tela para marcacao do empenho relacionado a remessa  ╨╠╠
╠╠лммммммммммьмммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Uso       Ё MATA410                                                    ╨╠╠
╠╠хммммммммммомммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╪╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Static Function A410RemBen()
Local aArea     := GetArea()
Local lContinua := .T.
Local lFirst    := .T.
Local cMarca    := ""
Local cFiltro   := ""
Local aListSD4  := {}
Local nOpc      := 0
Local nX        := 0
Local nPos      := 0
Local lMarca    := .F.
Local nQtde1Tot := 0
Local nQtde2Tot := 0
Local aTamX3    := TamSX3("D4_QUANT")
Local aCampos   := {"",AllTrim(RetTitle("D4_OP")),AllTrim(RetTitle("D4_QUANT")),AllTrim(RetTitle("D4_QTSEGUM"))}
Local oOk       := LoadBitMap(GetResources(), "LBOK")
Local oNo       := LoadBitMap(GetResources(), "LBNO")

Local nPosCod 	:= aScan(aHeader,{|x| AllTrim(x[2])=="C6_PRODUTO"})
Local nPosTes 	:= aScan(aHeader,{|x| AllTrim(x[2])=="C6_TES"})
Local nPosLoc 	:= aScan(aHeader,{|x| AllTrim(x[2])=="C6_LOCAL"})
Local nPosItem  := aScan(aHeader,{|x| AllTrim(x[2])=="C6_ITEM"})
Local nPosQuant := aScan(aHeader,{|x| AllTrim(x[2])=="C6_QTDVEN"})
Local nPosQtSUM := aScan(aHeader,{|x| AllTrim(x[2])=="C6_UNSVEN"})
Local nPosLCtl  := aScan(aHeader,{|x| AllTrim(x[2])=="C6_LOTECTL"})
Local nPosLote  := aScan(aHeader,{|x| AllTrim(x[2])=="C6_NUMLOTE"})
Local nPosDtVal := aScan(aHeader,{|x| AllTrim(x[2])=="C6_DTVALID"})
Local nPosEnder := aScan(aHeader,{|x| AllTrim(x[2])=="C6_LOCALIZ"})
Local nPosNumS  := aScan(aHeader,{|x| AllTrim(x[2])=="C6_NUMSERI"})
Local nPosLib1  := aScan(aHeader,{|x| AllTrim(x[2])=="C6_QTDLIB"})
Local nPosLib2  := aScan(aHeader,{|x| AllTrim(x[2])=="C6_QTDLIB2"})
Local nPosPrc 	:= aScan(aHeader,{|x| AllTrim(x[2])=="C6_PRCVEN"})
Local nPosTot 	:= aScan(aHeader,{|x| AllTrim(x[2])=="C6_VALOR"})
Local l410remb  := Existblock("M410REMB")
Local nTpCtlBN  := A410CtEmpBN()
Local aQtdEnv   := {}
Local nQtdDigit := 0
Local nY        := 0
Local nPosDig   := 0

//-- Verifica se o tipo de pedido e Beneficiamento, se produto e tipo BN e se a TES e de remessa e atualiza estoque
If  Empty(M->C5_TIPO) .Or. AllTrim(M->C5_TIPO) != "B" .Or.;
	Empty(aCols[n,nPosCod]) .Or. (SB1->(dbSeek(xFilial("SB1")+aCols[n,nPosCod])) .And. Iif(l410remb,ExecBlock("M410REMB",.F.,.F.,{SB1->B1_COD}),AllTrim(SB1->B1_TIPO) != "BN")) .Or.;
	Empty(aCols[n,nPosTes]) .Or. (SF4->(dbSeek(xFilial("SF4")+aCols[n,nPosTes])) .And. AllTrim(SF4->F4_PODER3) != "R" .Or.;
	AllTrim(SF4->F4_ESTOQUE) != "S")
	lContinua := .F.
Else
	lContinua := .T.
EndIf

If lContinua
	cFiltro := "D4_FILIAL = '" +xFilial("SD4") +"' .And. D4_COD == '" +aCols[n,nPosCod] +"' .And. "
	cFiltro += "QtdComp(D4_QUANT) > QtdComp(0) .And. Posicione('SC2',1,xFilial('SC2')+D4_OP,'C2_TPOP') == 'F' "
	If nTpCtlBN == 1 // metodo antigo - unico envio
		cFiltro += " .And. Empty(D4_NUMPVBN+D4_ITEPVBN)	"
	EndIf
 	dbSelectArea("SD4")
	dbSetOrder(1)
	dbSetFilter({|| &cFiltro}, cFiltro)
	dbGoTop()
	cMarca := GetMark()
	If Bof() .and. Eof()
		Help(" ",1,"A410NOEMP")
	Else
		While !EOF()
			If nTpCtlBN == 1 // metodo antigo - unico envio
				nQtde1Tot := D4_QUANT
				nQtde2Tot := D4_QTSEGUM

				If aScan(aColsBn,{|x| x[2] == Recno() }) > 0
					dbSkip()
					Loop
				EndIf
			ElseIf nTpCtlBN == 2 // metodo novo - multiplos envios
				aQtdEnv   := A410QtEnBN(D4_OP, D4_COD, D4_LOCAL)
				nQtde1Tot := D4_QTDEORI - aQtdEnv[1]
				nQtde2Tot := ConvUM(D4_COD, nQtde1Tot, 0, 2)
				
				nQtdDigit := 0
				For nY := 1 to Len(aColsBn)
					nPosDig := aScan(aCols, {|x| x[1] == aColsBn[nY, 3]})
					If (nPosDig > 0  .and. n != nPosDig)
						If !(aCols[nPosDig, Len(aCols[nPosDig])]) .And. aCols[nPosDig, nPosCod] == aCols[n, nPosCod]
							nQtdDigit += aCols[nPosDig, nPosQuant]
						EndIf
					EndIf
				Next nY

				If QtdComp(nQtdDigit) > 0
					If QtdComp(nQtdDigit) >= QtdComp(nQtde1Tot) // ja informou a qtde. total disponivel para envio
						dbSkip()
						Loop
					Else
						nQtde1Tot -= nQtdDigit
						nQtde2Tot := ConvUM(D4_COD, nQtde1Tot, 0, 2)
					EndIf
				EndIf
			EndIf
			
			If Localiza(D4_COD)
				dbSelectArea("SDC")
				dbSetOrder(2)
				dbSeek(xFilial("SDC")+SD4->(D4_COD+D4_LOCAL+D4_OP+D4_TRT+D4_LOTECTL+D4_NUMLOTE))
				While !EOF() .And. DC_FILIAL+DC_PRODUTO+DC_LOCAL+DC_OP+DC_TRT+DC_LOTECTL+DC_NUMLOTE == xFilial("SDC")+SD4->(D4_COD+D4_LOCAL+D4_OP+D4_TRT+D4_LOTECTL+D4_NUMLOTE)
					If aScan(aColsBn,{|x| x[2] == Recno() }) > 0
						nQtde1Tot -= DC_QUANT
						nQtde2Tot -= DC_QTSEGUM
					EndIf
					dbSkip()
				End
				dbSelectArea("SD4")
			EndIf
			If nQtde1Tot > 0
				nPos := aScan(aListSD4,{|x| x[2] == SD4->D4_OP})
				If nPos > 0 
					aListSD4[nPos,3] += If(nTpCtlBN==1, D4_QUANT, nQtde1Tot)
					aListSD4[nPos,4] += If(nTpCtlBN==1, D4_QTSEGUM, nQtde2Tot)
				Else
					aAdd(aListSD4,{lMarca,SD4->D4_OP, If(nTpCtlBN==1, D4_QUANT, nQtde1Tot), If(nTpCtlBN==1, D4_QTSEGUM, nQtde2Tot)})
				EndIf
			EndIf
			SD4->(dbSkip())
		End
		If !Empty(aListSD4) 
			aSort(aListSD4,,,{|x,y| x[2] < y[2]})
			DEFINE MSDIALOG oDlg FROM 50,40 TO 285,750 TITLE STR0119+AllTrim(aCols[n,nPosItem]) Of oMainWnd PIXEL //"Selecione o empenho - Item"
			oListBox := TWBrowse():New(05,4,243,86,,aCampos,,oDlg,,,,,,,,,,,,.F.,,.T.,,.F.,,,)
			oListBox:SetArray(aListSD4)
			oListBox:bLDblClick := {|| aListSD4[oListBox:nAt,1] := !aListSD4[oListBox:nAt,1]}
			oListBox:bLine := {|| {If(aListSD4[oListBox:nAt,1],oOk,oNo),aListSD4[oListBox:nAT,2],;
											Str(aListSD4[oListBox:nAT,3],aTamX3[1],aTamX3[2]),;
											Str(aListSD4[oListBox:nAT,4],aTamX3[1],aTamX3[2]) }}
	
			oListBox:Align := CONTROL_ALIGN_ALLCLIENT
			ACTIVATE MSDIALOG oDlg CENTERED ON INIT EnchoiceBar(oDlg,{||(nOpc := 1,oDlg:End())},{||(nOpc := 0,oDlg:End())})
		Else
			Help(" ",1,"A410NOEMP")
		EndIf
	EndIf
Else
	Help(" ",1,"A410NOBN")
EndIf

SD4->(dbClearFilter())
If nOpc == 1
	nPos := n
	For nX := 1 To Len(aListSD4) 
		If aListSD4[nX,1] == .F.	
			Loop
		EndIf
		dbSelectArea("SD4")
		dbSetOrder(2)
		dbSeek(xFilial("SD4")+aListSD4[nX,2]+aCols[n,nPosCod])  
		While !EOF() .And. D4_FILIAL+D4_OP+D4_COD == xFilial("SD4")+aListSD4[nX,2]+aCols[n,nPosCod]
			If nTpCtlBN == 1 // metodo antigo - unico envio
				nQtde1Tot := D4_QUANT
				nQtde2Tot := D4_QTSEGUM
			ElseIf nTpCtlBN == 2 // metodo novo - multiplos envios
				aQtdEnv   := A410QtEnBN(D4_OP, D4_COD, D4_LOCAL)
				nQtde1Tot := D4_QTDEORI - aQtdEnv[1]
				nQtde2Tot := ConvUM(D4_COD, nQtde1Tot, 0, 2)
				
				nQtdDigit := 0
				For nY := 1 to Len(aColsBn)
					nPosDig := aScan(aCols, {|x| x[1] == aColsBn[nY, 3]})
					If (nPosDig > 0  .and. n != nPosDig)
						If !(aCols[nPosDig, Len(aCols[nPosDig])]) .And. aCols[nPosDig, nPosCod] == aCols[n, nPosCod]
							nQtdDigit += aCols[nPosDig, nPosQuant]
						EndIf
					EndIf
				Next nY

				If QtdComp(nQtdDigit) > 0
					If QtdComp(nQtdDigit) >= QtdComp(nQtde1Tot) // ja informou a qtde. total disponivel para envio
						dbSkip()
						Loop
					Else
						nQtde1Tot -= nQtdDigit
						nQtde2Tot := ConvUM(D4_COD, nQtde1Tot, 0, 2)
					EndIf
				EndIf
			EndIf

			If Localiza(aCols[n,nPosCod])
				dbSelectArea("SDC")
				dbSetOrder(2)
				dbSeek(xFilial("SDC")+SD4->(D4_COD+D4_LOCAL+D4_OP+D4_TRT+D4_LOTECTL+D4_NUMLOTE))
				While !EOF() .And. DC_FILIAL+DC_PRODUTO+DC_LOCAL+DC_OP+DC_TRT+DC_LOTECTL+DC_NUMLOTE == xFilial("SDC")+SD4->(D4_COD+D4_LOCAL+D4_OP+D4_TRT+D4_LOTECTL+D4_NUMLOTE)
					If !lFirst
						aAdd(aCols,aClone(aCols[n]))
						nPos := Len(aCols)
						aCols[nPos,nPosItem] := Soma1(aCols[Len(aCols)-1,nPosItem])
						aCols[nPos,nPosTes] := aCols[n,nPosTes]
						aCols[nPos,nPosPrc] := aCols[n,nPosPrc]
						//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
						//Ё Monta o AcolsGrade e o AheadGrade para este item - apenas compatibilizacao  Ё
						//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды 
						If MaGrade()
							If FindFunction("MsMatGrade") .And. IsAtNewGrd()
								oGrade:MontaGrade(nPos,aCols[nPos,nPosCod],.T.,,.F.) 
							Else
								MatGrdMont(nPos,aCols[nPos,nPosCod],.T.)
							EndIf
						EndIf
					EndIf
					aCols[nPos,nPosLoc]   := DC_LOCAL
					aCols[nPos,nPosQuant] := If(nTpCtlBN==1, DC_QUANT, nQtde1Tot)
					aCols[nPos,nPosQtSUM] := If(nTpCtlBN==1, DC_QTSEGUM, nQtde2Tot)
					aCols[nPos,nPosLCtl]  := DC_LOTECTL
					aCols[nPos,nPosLote]  := DC_NUMLOTE
					aCols[nPos,nPosEnder] := DC_LOCALIZ
					aCols[nPos,nPosNumS]  := DC_NUMSERI
					aCols[nPos,nPosTot]   := aCols[nPos,nPosQuant] * aCols[nPos,nPosPrc]
					If mv_par01 == 1
						aCols[nPos,nPosLib1] := aCols[nPos,nPosQuant]
						aCols[nPos,nPosLib2] := aCols[nPos,nPosQtSUM]
					EndIf
					
					If nTpCtlBN == 1
						aAdd(aColsBn,{"SDC",Recno(),aCols[nPos,nPosItem]})
					ElseIf nTpCtlBN == 2 // metodo novo - multiplos envios
						If aScan(aColsBn,{|x| x[1] == "SDC" .And. x[2] == Recno() .And. x[3] == aCols[n, nPosItem] }) == 0 // nao eh o mesmo item
							aAdd(aColsBn,{"SDC",Recno(),aCols[nPos,nPosItem]})
						EndIf
						If aScan(aColsBn,{|x| x[1] == "SD4" .And. x[2] == SD4->(Recno()) .And. x[3] == aCols[n, nPosItem] }) == 0 // nao eh o mesmo item
							aAdd(aColsBn,{"SD4",SD4->(Recno()),aCols[nPos,nPosItem]}) // para garantir a gravacao na SGO
						EndIf
					EndIf
					
					If lFirst
						lFirst := .F.
					EndIf

					nQtde1Tot -= DC_QUANT
					nQtde2Tot -= DC_QTSEGUM
					dbSkip()
				End
				dbSelectArea("SD4")
			EndIf
			
			If nQtde1Tot > 0
				If !lFirst
					aAdd(aCols,aClone(aCols[n]))
					nPos := Len(aCols)
					aCols[nPos,nPosItem] := Soma1(aCols[Len(aCols)-1,nPosItem])
					aCols[nPos,nPosTes]  := aCols[n,nPosTes]
					aCols[nPos,nPosPrc]  := aCols[n,nPosPrc]
					//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					//Ё Monta o AcolsGrade e o AheadGrade para este item - apenas compatibilizacao  Ё
					//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды 
					If MaGrade()
						If FindFunction("MsMatGrade") .And. IsAtNewGrd()
							oGrade:MontaGrade(nPos,aCols[nPos,nPosCod],.T.,,.F.) 
						Else
							MatGrdMont(nPos,aCols[nPos,nPosCod],.T.)
						EndIf
					EndIf
				EndIf
				aCols[nPos,nPosLoc]   := D4_LOCAL
				aCols[nPos,nPosQuant] := If(nTpCtlBN==1, D4_QUANT, nQtde1Tot)
				aCols[nPos,nPosQtSUM] := If(nTpCtlBN==1, D4_QTSEGUM, nQtde2Tot)
				aCols[nPos,nPosLCtl]  := D4_LOTECTL
				aCols[nPos,nPosLote]  := D4_NUMLOTE
				aCols[nPos,nPosDtVal] := D4_DTVALID
				aCols[nPos,nPosEnder] := CriaVar("C6_LOCALIZ",.F.)
				aCols[nPos,nPosNumS]  := CriaVar("C6_NUMSERI",.F.)
				aCols[nPos,nPosTot]   := aCols[nPos,nPosQuant] * aCols[nPos,nPosPrc]
				If mv_par01 == 1
					aCols[nPos,nPosLib1] := aCols[nPos,nPosQuant]
					aCols[nPos,nPosLib2] := aCols[nPos,nPosQtSUM]
				EndIf

				If nTpCtlBN == 1
					aAdd(aColsBn,{"SD4",Recno(),aCols[nPos,nPosItem]})
				ElseIf nTpCtlBN == 2
					If aScan(aColsBn,{|x| x[1] == "SD4" .And. x[2] == Recno() .And. x[3] == aCols[n, nPosItem] }) == 0 // nao eh o mesmo item
						aAdd(aColsBn,{"SD4",Recno(),aCols[nPos,nPosItem]})
					EndIf
				EndIf
				
				If lFirst
					lFirst := .F.
				EndIf				
			EndIf
			SD4->(dbSkip())
		End
	Next nX
	Ma410Rodap()
EndIf

RestArea(aArea)
Return


/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠иммммммммммяммммммммммкмммммммяммммммммммммммммммммкммммммяммммммммммммм╩╠╠
╠╠╨Programa  ЁA410CarBen╨Autor  ЁAndre Anjos		 ╨ Data Ё  07/04/09   ╨╠╠
╠╠лммммммммммьммммммммммймммммммоммммммммммммммммммммйммммммоммммммммммммм╧╠╠
╠╠╨Descricao Ё Carrega relacionamentos entre SC6 e SD4,SDC para produtos  ╨╠╠
╠╠╨          Ё tipo BN.													  ╨╠╠
╠╠лммммммммммьмммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Uso       Ё MATA410, FATXFUN                                       	  ╨╠╠
╠╠хммммммммммомммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╪╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Function A410CarBen(cNumPV,cItemPV)
Local aArea    := GetArea()
Local aRet     := {}
Local cFiltro  := ""
Local nTpCtlBN := A410CtEmpBN()

Default cNumPv := ""

cFiltro := "DC_FILIAL == '" +xFilial("SDC") +"' .And. DC_ORIGEM == 'SC2' .And. DC_PEDIDO == '" +cNumPv +"'"
If !Empty(cItemPv)
	cFiltro += " .And. DC_ITEM == '" +cItemPV +"'"
EndIf

If nTpCtlBN != 0 
	If nTpCtlBN == 1 // metodo antigo - unico envio
		dbSelectArea("SD4")
		dbSetOrder(6)
		dbSeek(xFilial("SD4")+cNumPv+If(!Empty(cItemPv),cItemPv,""))
		While !EOF() .And. D4_FILIAL+D4_NUMPVBN+If(!Empty(cItemPv),D4_ITEPVBN,"") == xFilial("SD4")+cNumPv+If(!Empty(cItemPv),cItemPv,"")
			aAdd(aRet,{"SD4",Recno(),D4_ITEPVBN})
			dbSkip()
		EndDo
	ElseIf nTpCtlBN == 2 // metodo novo - multiplos envios
	    dbSelectArea("SGO")
	    dbSetOrder(2)
	    dbSeek(xFilial("SGO")+cNumPV+If(!Empty(cItemPv),cItemPv,""))
	    While !Eof() .And. GO_FILIAL+GO_NUMPV+If(!Empty(cItemPv),GO_ITEMPV,"") == xFilial("SGO")+cNumPv+If(!Empty(cItemPv),cItemPv,"")
			aAdd(aRet,{"SD4",GO_RECNOD4,GO_ITEMPV})
			dbSkip()
		EndDo
	EndIf
	
	dbSelectArea("SDC")
	dbSetFilter({|| &cFiltro}, cFiltro)
	dbGoTop()
	While !EOF()
		aAdd(aRet,{"SDC",Recno(),DC_ITEM})
		dbSkip()
	End
	dbClearFilter()

	RestArea(aArea)
EndIf

Return aRet

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠иммммммммммяммммммммммкмммммммяммммммммммммммммммммкммммммяммммммммммммм╩╠╠
╠╠╨Programa  ЁA410KeyF9 ╨Autor  ЁAndre Anjos         ╨ Data Ё  27/07/09   ╨╠╠
╠╠лммммммммммьммммммммммймммммммоммммммммммммммммммммйммммммоммммммммммммм╧╠╠
╠╠╨Descricao Ё Funcao da tecla F9 na inclusao de pedido de venda.         ╨╠╠
╠╠лммммммммммьмммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Uso       Ё MATA410													  ╨╠╠
╠╠хммммммммммомммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╪╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Function A410KeyF9()
Local oDlg	      := NIL
Local oGet01      := NIL
Local aCampos     := {}
Local aTitle      := {}
Local aCpsComis   := {}
Local aVendors    := {}
Local aStruCNA    := CNA->(dbStruct())
Local cConsSxb    := "CN9003"
Local cLine       := ""
Local cArqCNA     := ""
Local cQuery	  := ""
Local nOpca		  := 0
LOcal nX		  := 0
Local nY		  := 0
Local nPercCNF	  := 0
Local n
Local oOk  		  := LoadBitmap(GetResources(),"LBTIK")
Local oNo  		  := LoadBitmap(GetResources(),"LBNO")
Local lSugVal     := GetNewPar("MV_CNSUGME","1") == "1"
Local lRealMed    := GetNewPar( "MV_CNREALM", "S" ) == "S"
Local lReajMed    := GetNewPar( "MV_CNREAJM", "S" ) == "S"
Local lFisico     := .F.

//здддддддддддддддддддддддддддддддддддддд©
//Ё Variaveis para manipulacao do aCols. Ё
//юдддддддддддддддддддддддддддддддддддддды
Local nITEM    := aScan(aHeader,{|x| AllTrim(x[2]) == "C6_ITEM"})
Local nPRODUTO := aScan(aHeader,{|x| AllTrim(x[2]) == "C6_PRODUTO"})
Local nUM      := aScan(aHeader,{|x| AllTrim(x[2]) == "C6_UM"})
Local nQTDVEN  := aScan(aHeader,{|x| AllTrim(x[2]) == "C6_QTDVEN"})
Local nPRCVEN  := aScan(aHeader,{|x| AllTrim(x[2]) == "C6_PRCVEN"})
Local nVALOR   := aScan(aHeader,{|x| AllTrim(x[2]) == "C6_VALOR"})
Local nSEGUM   := aScan(aHeader,{|x| AllTrim(x[2]) == "C6_SEGUM"})
Local nTES     := aScan(aHeader,{|x| AllTrim(x[2]) == "C6_TES"})
Local nUNSVEN  := aScan(aHeader,{|x| AllTrim(x[2]) == "C6_UNSVEN"})
Local nLOCAL   := aScan(aHeader,{|x| AllTrim(x[2]) == "C6_LOCAL"})
Local nCF      := aScan(aHeader,{|x| AllTrim(x[2]) == "C6_CF"})
Local nENTREG  := aScan(aHeader,{|x| AllTrim(x[2]) == "C6_ENTREG"})
Local nDESCRI  := aScan(aHeader,{|x| AllTrim(x[2]) == "C6_DESCRI"})
Local nPRUNIT  := aScan(aHeader,{|x| AllTrim(x[2]) == "C6_PRUNIT"})
Local nSUGENTR := aScan(aHeader,{|x| AllTrim(x[2]) == "C6_SUGENTR"})
Local nITEMED  := aScan(aHeader,{|x| AllTrim(x[2]) == "C6_ITEMED"})
Local nDESCONT := aScan(aHeader,{|x| AllTrim(x[2]) == "C6_DESCONT"})
Local nVALDESC := aScan(aHeader,{|x| AllTrim(x[2]) == "C6_VALDESC"})

Private aHeadDcs  := {}

For nX := 1 To 5
	aAdd(aCpsComis, aScan(aHeader,{|x| AllTrim(x[2]) == ("C6_COMIS"+Str(nX,1))}))
Next nX

//зддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Se posicionado no cliente, tipo de pedido normal  Ё
//Ё e parametro MV_CNPEDVE ativo, abre janela para    Ё
//Ё selecao do contrato a vincular (SIGAGCT).         Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддды
If SuperGetMV("MV_CNPEDVE",.F.,.F.) .And. ReadVar() == "M->C5_CLIENTE" .And. M->C5_TIPO == "N"
	If !Empty(CND->( FieldPos( "CND_PARCEL" ) ))
		aTitle := { "",CNA->(RetTitle("CNA_NUMERO")),CNA->(RetTitle("CNF_PARCEL")),CNA->(RetTitle("CNA_DTINI")),CNA->(RetTitle("CNA_VLTOT")),CNA->(RetTitle("CNA_DTFIM")),CNA->(RetTitle("CNA_FORNEC")),CNA->(RetTitle("CNA_LJFORN")),CNA->(RetTitle("CNA_CRONOG")),CNA->(RetTitle("CNF_VLPREV"))}
	Else                                                                                                         
		aTitle := { "",CNA->(RetTitle("CNA_NUMERO")),CNA->(RetTitle("CNA_DTINI")),CNA->(RetTitle("CNA_VLTOT")),CNA->(RetTitle("CNA_DTFIM")),CNA->(RetTitle("CNA_FORNEC")),CNA->(RetTitle("CNA_LJFORN")),CNA->(RetTitle("CNA_CRONOG")),CNA->(RetTitle("CNF_VLPREV"))}
	EndIf
	
	//-- Variaveis private para manipulacao na CN120VlCon
	Private oBrowse := NIL
	Private oCbx	:= NIL
	Private lMedeve := .F.//medicao Eventual
	
	//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁAdiciona parcela e valor previsao a estrutura da planilha    Ё
	//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	aAdd(aStruCNA,{"CNF_PARCEL","C",TamSX3("CNF_PARCEL")[1],TamSX3("CNF_PARCEL")[2]})
	aAdd(aStruCNA,{"CNF_VLPREV","N",TamSX3("CNF_VLPREV")[1],TamSX3("CNF_VLPREV")[2]})
	
	//зддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁCria arquivo temporario                            Ё
	//юддддддддддддддддддддддддддддддддддддддддддддддддддды
	cArqCNA := CriaTrab(aStruCNA)
	dbUseArea(.T.,,cArqCNA,"TRBCNA",.F.,.F.)
	
	//зддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁConfigura campos exibidos na inclusao de medicoes  Ё
	//юддддддддддддддддддддддддддддддддддддддддддддддддддды
	If !Empty(CND->( FieldPos( "CND_PARCEL" ) ))
		aCampos := {"",TRBCNA->CNA_NUMERO,TRBCNA->CNF_PARCEL,TRBCNA->CNA_DTINI,TRBCNA->CNA_VLTOT,TRBCNA->CNA_DTFIM,TRBCNA->CNA_FORNEC,TRBCNA->CNA_LJFORN,TRBCNA->CNA_CRONOG,TRBCNA->CNF_VLPREV}
	Else
		aCampos := {"",TRBCNA->CNA_NUMERO,TRBCNA->CNA_DTINI,TRBCNA->CNA_VLTOT,TRBCNA->CNA_DTFIM,TRBCNA->CNA_FORNEC,TRBCNA->CNA_LJFORN,TRBCNA->CNA_CRONOG,TRBCNA->CNF_VLPREV}
	EndIf
	
	If ExistBlock("CN120SXB")
		cConsSxb := If(Valtype(cConsSxb:=ExecBlock("CN120SXB",.F.,.F.))=="C",cConsSxb,"CN9003") 
	EndIf
	
	DEFINE MSDIALOG oDlg TITLE STR0135 From 74,7 TO 400,606 PIXEL //VМnculo a Contrato - SIGAGCT
	
	@ 10,04   SAY OemToansi(RetTitle("CN9_NUMERO")) SIZE 73, 8 OF oDlg PIXEL
	@ 09,37   MSGET oGet01 VAR cContra PICTURE PesqPict("CN9","CN9_NUMERO") F3 cConsSxb SIZE 60,9 VALID A410VldGCT() .And. CN120VlCon(1) OF oDlg PIXEL
	
	@ 10,104  SAY OemToansi(RetTitle("CNF_COMPET")) SIZE 73, 8 OF oDlg PIXEL
	@ 09,137 ComboBox oCbx Var cCompet ON CHANGE CN120Compet() SIZE 50,9 OF oDlg PIXEL
	
	If !Empty(CND->( FieldPos( "CND_PARCEL" ) ))
		cLine := "{If((cPlan+cParcel==(TRBCNA->CNA_NUMERO+TRBCNA->CNF_PARCEL)),oOk,oNo),TRBCNA->CNA_NUMERO,TRBCNA->CNF_PARCEL,TRBCNA->CNA_DTINI,Transform(TRBCNA->CNA_VLTOT,PesqPict('CNA','CNA_VLTOT')),TRBCNA->CNA_DTFIM,TRBCNA->CNA_FORNEC,TRBCNA->CNA_LJFORN,TRBCNA->CNA_CRONOG,Transform(TRBCNA->CNF_VLPREV,PesqPict('CNF','CNF_VLPREV')),"
	Else
		cLine := "{If((cPlan==TRBCNA->CNA_NUMERO),oOk,oNo),TRBCNA->CNA_NUMERO,TRBCNA->CNA_DTINI,Transform(TRBCNA->CNA_VLTOT,PesqPict('CNA','CNA_VLTOT')),TRBCNA->CNA_DTFIM,TRBCNA->CNA_FORNEC,TRBCNA->CNA_LJFORN,TRBCNA->CNA_CRONOG,Transform(TRBCNA->CNF_VLPREV,PesqPict('CNF','CNF_VLPREV')),"
	EndIf
	
	//зддддддддддддддддддддддддддддд©
	//ЁFinaliza construcao do cLine Ё
	//юддддддддддддддддддддддддддддды
	cLine := substr(cLine,1,len(cLine)-1)+"}"
	
	//здддддддддддддддддддд©
	//ЁConfigura browse    Ё
	//юдддддддддддддддддддды
	If !Empty(CND->( FieldPos( "CND_PARCEL" ) ))
		oBrowse := TWBrowse():New( 30, 4,__DlgWidth(oDlg)-8,__DlgHeight(oDlg)-58, {|| {aCampos} },aTitle,{030,090},oDlg,,,,,,,,,,,,,"TRBCNA", .T. )
		oBrowse:bLine := &( "{ || " + cLine + " }" )
		oBrowse:bLDblClick := {|| If(!Empty(TRBCNA->CNA_NUMERO),((cPlan:= TRBCNA->CNA_NUMERO,cParcel:=TRBCNA->CNF_PARCEL), oBrowse:Refresh()),) }
	Else		
		oBrowse := TWBrowse():New( 30, 4,__DlgWidth(oDlg)-8,__DlgHeight(oDlg)-58, {|| {aCampos} },aTitle,{030,090},oDlg,,,,,,,,,,,,,"TRBCNA", .T. )	
		oBrowse:bLine := &( "{ || " + cLine + " }" )
		oBrowse:bLDblClick := {|| If(!Empty(TRBCNA->CNA_NUMERO),(cPlan := TRBCNA->CNA_NUMERO, oBrowse:Refresh()),) }
	EndIf
	
	DEFINE SBUTTON FROM 150, 240 When .T. TYPE 1 ACTION (If(Empty(cPlan),Help(" ",1,"CNTA120_01"),(oDlg:End(),nOpca:=1))) ENABLE OF oDlg//"Selecione uma planilha"
	DEFINE SBUTTON FROM 150, 270 When .T. TYPE 2 ACTION (oDlg:End(),nOpca:=2) ENABLE OF oDlg
	
	ACTIVATE MSDIALOG oDlg CENTERED
	
	//зддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁApaga arquivo temporario                           Ё
	//юддддддддддддддддддддддддддддддддддддддддддддддддддды
	TRBCNA->(dbCloseArea())
	FErase(cArqCNA+".DBF")
	FErase(cArqCNA+OrdBagExt())
	
	If nOpca == 1
		lFisico := ((CN1->(FieldPos("CN1_CROFIS")) > 0) .And. Posicione("CN1",1,xFilial("CN1")+CN9->CN9_TPCTO,"CN1_CROFIS") == "1")
		If !lSugVal
			lSugVal := lFisico
		EndIf	
		//зддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁAtualiza campos do cabecalho do pedido (SC5)       Ё
		//юддддддддддддддддддддддддддддддддддддддддддддддддддды
		aVendors := CtaVend(cContra)
		M->C5_MDCONTR := cContra
		M->C5_MDPLANI := cPlan
		M->C5_CLIENTE := CN9->CN9_CLIENT
		M->C5_LOJACLI := CN9->CN9_LOJACL
		M->C5_CONDPAG := CN9->CN9_CONDPG
		M->C5_MOEDA	  := CN9->CN9_MOEDA
		M->C5_TIPOCLI := Posicione("SA1",1,xFilial("SA1")+CN9->(CN9_CLIENT+CN9_LOJACL),"A1_TIPO")
		If !Empty(aVendors)
			For nX := 1 To Len(aVendors)
				&("M->C5_VEND"+Str(nX,1,0)) := aVendors[nX,1]
				&("M->C5_COMIS"+Str(nX,1,0)) := aVendors[nX,2]
			Next nX
		Else
			For nX := 1 To 5
				&("M->C5_VEND"+Str(nX,1,0)) := CriaVar("C5_VEND1",.F.)
				&("M->C5_COMIS"+Str(nX,1,0)) := CriaVar("C5_COMIS1",.F.)
			Next nX
		EndIf
				
		//здддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Se for contrato fixo (com planilha), realiza |
		//| o preenchimento do aCols do PV com os itens  Ё
		//| da planilha (CNB).							 Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддды
		If lFixo		
			//Limpa aCols, pois so aceita itens da planilha
			aCols := {}
			
			//Posiciona na planilha
			CNA->(dbSeek(xFilial("CNA")+cContra+cRevisa+cPlan))
				
			dbSelectArea("CNB")
			dbSetOrder(1)
			dbSeek(xFilial("CNB")+cContra+cRevisa+cPlan)		
			While CNB->(CNB_FILIAL+CNB_CONTRA+CNB_REVISA+CNB_NUMERO) == xFilial("CNB")+cContra+cRevisa+cPlan
				If CNB->CNB_SLDMED > 0
					aAdd(aCols,Array(Len(aHeader)+1))
					aCols[Len(aCols),Len(aHeader)+1] := .F.
			
					//здддддддддддддддддддддддддддддддддддддддддддддддд©              
					//Ё Inicializa campos da getdados                  Ё
					//юдддддддддддддддддддддддддддддддддддддддддддддддды
					For nX :=1 to Len(aHeader)
						If !(IsHeadRec(aHeader[nX,2]) .OR. IsHeadAlias(aHeader[nX,2]))
							aCols[Len(aCols),nX] := CriaVar(aHeader[nX,2])
						EndIf
					Next nX
					
					SB1->(dbSeek(xFilial("SB1")+CNB->CNB_PRODUT))
			                              
					aCols[Len(aCols),nITEM]    := PadL(Right(CNB->CNB_ITEM,TamSX3("C6_ITEM")[1]),TamSX3("C6_ITEM")[1],"0")
					aCols[Len(aCols),nITEMED]  := PadL(Right(CNB->CNB_ITEM,TamSX3("C6_ITEMED")[1]),TamSX3("C6_ITEMED")[1],"0")
					aCols[Len(aCols),nPRODUTO] := CNB->CNB_PRODUT
					aCols[Len(aCols),nUM]		:= SB1->B1_UM
					aCOls[Len(aCols),nSEGUM]	:= SB1->B1_SEGUM
					aCOls[Len(aCols),nLOCAL]	:= SB1->B1_LOCPAD
					aCols[Len(aCols),nTES]     := SB1->B1_TS
					aCols[Len(aCols),nDESCRI]  := SB1->B1_DESC
					aCols[Len(aCols),nCF]	    := Posicione("SF4",1,xFilial("SF4")+SB1->B1_TS,"F4_CF")
					aCols[Len(aCols),nPRUNIT]  := A410Arred(CNB->CNB_VLUNIT * If(CN9->CN9_FLGCAU=='1' .And. CN9->CN9_TPCAUC=='2',(1 - (CN9->CN9_MINCAU / 100)),1),"C6_PRUNIT")
					aCols[Len(aCols),nPRCVEN]  := A410Arred(aCols[Len(aCols),nPRUNIT],"C6_PRCVEN")
					If !Empty(CNB->CNB_DESC)
						aCols[len(aCols),nDESCONT] := A410Arred(CNB->CNB_DESC,"C6_DESCONT")
						aCols[Len(aCols),nPRCVEN]  -= A410Arred((CNB->CNB_VLUNIT * CNB->CNB_DESC) / 100,"C6_PRCVEN")
					EndIf
					//зддддддддддддддддддддддддддддддддддддддд©
					//Ё Verifica se o item e comissionado     Ё
					//юддддддддддддддддддддддддддддддддддддддды
					If CNB->CNB_FLGCMS == "1"
						//зддддддддддддддддддддддддддддддддддддддддддддддддддддд©
						//Ё Complementa as comissoes de acordo com os contratos Ё
						//юддддддддддддддддддддддддддддддддддддддддддддддддддддды
						For nX := 1 to Len(aVendors)
							If !Empty(aCpsComis[nX])
								aCols[Len(aCols),aCpsComis[nX]] := aVendors[nX,2]
							EndIf
						Next nX
					EndIf
			
					If !lMedeve .Or. lSugVal
						dbSelectArea("CNF")
						If !Empty(cParcel) .And. !Empty(CND->(FieldPos("CND_PARCEL")))
							dbSetOrder(3)
							dbSeek(xFilial("CNF")+cContra+cRevisa+CNA->CNA_CRONOG+cParcel)
						Else
							dbSetOrder(2)
							dbSeek(xFilial("CNF")+cContra+cRevisa+CNA->CNA_CRONOG+cCompet)
						EndIf
						
						CN0->(dbSetOrder(1))
						CN0->(dbSeek(xFilial("CN0")+CN9->CN9_TIPREV))
										
						If (!lRealMed .And. CN0->CN0_TIPO == '3') .Or. (!lReajMed .And. CN0->CN0_TIPO == '2')									 	
							cQuery := "SELECT SUM(CNB.CNB_VLTOT) AS CNB_VLTOT "
							cQuery += "FROM " +RetSqlName("CNB") +" CNB "
							cQuery += "WHERE CNB.CNB_FILIAL = '"+xFilial("CNB")  +"' AND "
							cQuery += "CNB_CONTRA = '" +CNA->CNA_CONTRA +"' AND "
							cQuery += "CNB_REVISA = '" +CNA->CNA_REVISA +"' AND "
							cQuery += "CNB_VLUNIT <> CNB_PRCORI AND "
							cQuery += "CNB.D_E_L_E_T_  =  ' '"
																		
							cQuery := ChangeQuery(cQuery)				
							dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),"CNBTMP")
							TCSetField("CNBTMP","CNB_VLTOT","N",TamSX3("CNB_VLTOT")[1],TamSX3("CNB_VLTOT")[2])
							 									
							//здддддддддддддддддддддддддддддддддддддддддддддддддддд©
							//ЁCalcula percentual da parcela com o total dos itens Ё
							//Ёda planilha a serem medidos                         Ё
							//юдддддддддддддддддддддддддддддддддддддддддддддддддддды
							If !("CNBTMP")->(Eof())  
								nPercCNF := (CNF->CNF_SALDO*100)/("CNBTMP")->CNB_VLTOT
							Else
								nPercCNF := (CNF->CNF_SALDO*100)/CNA->CNA_VLTOT
							EndIf					   		                    
							("CNBTMP")->(dbCloseArea())  
						Else
							nPercCNF := (CNF->CNF_SALDO*100)/CNA->CNA_VLTOT
						EndIf 						
						
						If !lFisico      
							aCols[Len(aCols),nQTDVEN] := A410Arred(Min((CNB->CNB_QUANT * nPercCNF)/100,CNB->CNB_SLDMED),"C6_QTDVEN")
							aCols[Len(aCols),nUNSVEN] := ConvUM(CNB->CNB_PRODUT,aCols[Len(aCols),nQTDVEN],0,2)
						Else
							dbSelectArea("CNS")
							dbSetOrder(1)						
							If dbSeek(xFilial("CNS")+cContra+cRevisa+CNF->CNF_NUMERO+CNF->CNF_PARCEL+CNB->CNB_ITEM)
								aCols[Len(aCols),nQTDVEN] := A410Arred(Min(If((CNS->CNS_SLDQTD > 0),CNS->CNS_SLDQTD,0),CNB->CNB_SLDMED),"C6_QTDVEN")
								aCols[Len(aCols),nUNSVEN] := ConvUM(CNB->CNB_PRODUT,aCols[Len(aCols),nQTDVEN],0,2)
							EndIf
						EndIf									
						aCols[Len(aCols),nVALOR] := A410Arred(aCols[Len(aCols),nQTDVEN] * aCols[Len(aCols),nPRCVEN],"C6_VALOR")
						aCols[len(aCols),nVALDESC] := A410Arred((aCols[Len(aCols),nQTDVEN] * aCols[Len(aCols),nPRUNIT]) - aCols[Len(aCols),nVALOR],"C6_VALDESC")
					EndIf
			        
					aCols[Len(aCols),nENTREG]  := dDataBase
					aCols[Len(aCols),nSUGENTR] := dDataBase
				EndIf			
				dbSelectArea("CNB")
				dbSkip()
			EndDo
			Ma410Rodap()
		Else
			//зддддддддддддддддддддддддддддддддддддддддддд©              
			//Ё Ajusta itens que ja possam estar no aCols Ё
			//юддддддддддддддддддддддддддддддддддддддддддды
			For nX :=1 to Len(aCols)
				aCols[nX,nITEMED] := CriaVar("C6_ITEMED",.F.)
				aCols[nX,nPRUNIT] := CriaVar("C6_PRUNIT",.F.)

				//зддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Complementa as comissoes de acordo com os contratos Ё
				//юддддддддддддддддддддддддддддддддддддддддддддддддддддды
				If !Empty(aVendors)
					For nY := 1 to Len(aVendors)
						If !Empty(aCpsComis[nY])
							aCols[nX,aCpsComis[nY]] := aVendors[nY,2]
						EndIf
					Next nY
				Else
					For nY := 1 to Len(aCpsComis)
						aCols[nx,aCpsComis[nY]] := CriaVar("C6_COMIS1",.F.)
					Next nY
				EndIf
			Next nX
		EndIf
	EndIf
EndIf

Return

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠иммммммммммяммммммммммкмммммммяммммммммммммммммммммкммммммяммммммммммммм╩╠╠
╠╠╨Programa  |A410VldGCT╨Autor  ЁMicrosiga           ╨ Data Ё  31/07/09   ╨╠╠
╠╠лммммммммммьммммммммммймммммммоммммммммммммммммммммйммммммоммммммммммммм╧╠╠
╠╠╨Descricao Ё Valida se o contrato possui alcada. Em caso positivo alerta╨╠╠
╠╠╨          Ё e bloqueia, pois devera ter a medicao aprovada.            ╨╠╠
╠╠лммммммммммьмммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Uso       Ё MATA410                                                    ╨╠╠
╠╠хммммммммммомммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╪╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Static Function A410VldGCT()
Local lRet := .T.
CN9->(DbSetOrder(1))
CN9->(dbSeek(xFilial("CN9")+&(ReadVar())))

If !Empty(CN1->(FieldPos("CN1_GRPAPR"))) .And. SuperGetMV("MV_CNMDALC",.F.,"N") == "S"
	CN1->(dbSetOrder(1))
	CN1->(dbSeek(xFilial("CN1")+CN9->CN9_TPCTO))
	If !Empty(CN1->CN1_GRPAPR)
		Aviso(STR0127,STR0132,{"Ok"}) //SIGAGCT - Este contrato possui controle de alГadas e por isto exige a prИvia inclusЦo de mediГУes.
		lRet := .F.
	EndIf
EndIf                       
If CN1->(dbSeek(xFilial("CN1")+CN9->CN9_TPCTO)) .aND. CN1->CN1_ESPCTR <> '2'
	Aviso(STR0127,STR0158,{"Ok"}) //SIGAGCT - O CONTRATO SELECIONADO NAO E DE VENDA!
	lRet := .F.
EndIf

Return lRet

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤ao    ЁAjustaHelp Ё Autor ЁAndre Anjos	        Ё Data Ё10/12/2008Ё╠╠
╠╠цддддддддддедддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤ao ЁAjusta Help's da Rotina                                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё MATA410                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/
Static Function AjustaHelp()

Local aArea 	:= GetArea()
Local aHelpPor	:= {}
Local aHelpEng	:= {}
Local aHelpSpa	:= {}

PutHelp("PA410NOEMP",	{"NЦo hА empenhos deste produto para ","realizaГЦo de remessa para ","beneficiamento."} ,;
						{"No existen reservas de este producto ","para realizacion de envio para ","mejora."} ,;
						{"There's no reservation of this product","to execute referral to","beneficiation."} ,;
					     .T.)
PutHelp("SA410NOEMP",	{"NЦo se aplica."},;
						{"No se aplica."},;
						{"Not applicable"},;
						 .T. )
						 
PutHelp("PA410NOBN",	{"Tipo de pedido, produto ou tipo de ","saМda invАlido para a utilizaГЦo ","deste recurso."} ,;
						{"Tipo de pedido, producto o tipo de ","salida invalido para la utilizacion ","de este recurso."} ,;
						{"Type of order, product or type of ","output invalid to use this appeal"} ,;
					     .T.)
PutHelp("SA410NOBN",	{"Verifique como estЦo configurados ","estes cadastros."},;
						{"Verifique como estЦo configurados ","estes cadastros."},;
						{"Check how are configured this files."},;
						 .T. )
PutHelp("PA410NOSDADT",	{"Saldo de adiantamento ","Insuficiente "} ,;
						{"Saldo de adiantamento ","Insuficiente "} ,;
						{"Saldo de adiantamento ","Insuficiente "} ,;
					     .T.)

//зддддддддддддддддддддддддддддддддддддд©
//Ё Help utilizado na funГЦo A410FATPED Ё
//юддддддддддддддддддддддддддддддддддддды
aHelpPor :=	{"Este produto nЦo pode ser alterado, ",;
			 "pois jА foi totalmente faturado."}

aHelpSpa :=	{"Como el producto ya fue liquidado ",;
			 "este no se puede modificar."}

aHelpEng :=	{"This product can not be changed, ",;
			 "it was already invoiced."}
 
PutHelp("PA410PRODFA",aHelpPor,aHelpEng,aHelpSpa,.T.) 

aHelpPor :=	{"Para permitir alteraГЦo de produto ",;
			 "jА faturado, И necessАrio alterar o ",;
			 "parБmetro MV_ALTPED no MСdulo ",;
			 "do Configurador."}

aHelpSpa :=	{"Para permitir la modificaciСn de un ",;
			 "producto liquidado, por el mСdulo ",;
			 "Configurador, modifique el ",;
			 "parАmetro MV_ALTPED."}

aHelpEng :=	{"To allow making changes of products ",;
			 "that were already invoiced, mandatory ",;
			 "to change the parameter MV_ALTPED ",;
			 "in the Configurator Module."}

PutHelp("SA410PRODFA",aHelpPor,aHelpEng,aHelpSpa,.T.) 

//ajuste A410TIPO9
aHelpPor :=	{"Esse pedido possui condiГЦo de ",; 
			 "pagamento do Tipo 9 e serЦo geradas",;
		 	 "duplicatas no valor das parcelas, ",;
			 "mas a soma das parcelas nЦo batem ",;
			 "com o total do pedido."}
		
aHelpSpa :=	{"Este pedido tiene condicion de pago",;
		   	 " del Tipo 9 y se generara titulos de",;
		 	 " credito por el valor de las cuotas,",; 
			 " pero la suma de las cuotas no",; 
			 " concuerdan con el total del pedido."}
				
aHelpEng :=	{"This order has payment format ",; 
		 	 "Type 9 and trade notes are generated ",;
		 	 "in the installments value, but their ",;
		 	 "sum does not match to the total order."}
              
PutHelp("PA410TIPO9",aHelpPor,aHelpEng,aHelpSpa,.T.) 
RestArea(aArea)

Return


/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠иммммммммммяммммммммммкмммммммяммммммммммммммммммммкммммммяммммммммммммм╩╠╠
╠╠╨Programa  ЁA410Adiant╨Autor  ЁVendas CRM 		 ╨ Data Ё  24/08/09   ╨╠╠
╠╠лммммммммммьммммммммммймммммммоммммммммммммммммммммйммммммоммммммммммммм╧╠╠
╠╠╨Descricao Ё Abre tela para selecao de titulos do financeiro quando a   ╨╠╠
╠╠╨          Ёcondicao de pagto. permite o uso de Adiantamento.      	  ╨╠╠
╠╠лммммммммммьмммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Uso       Ё MATA410, FATXFUN                                       	  ╨╠╠
╠╠хммммммммммомммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╪╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Function A410Adiant(cNumPedido, cCondPagto, nTotalPed, aRecnoSE1, lCarregaTotal, cCodCli, cCodLoja, lGravaRelacao,aRatCTBPC,aAdtPC,cCondPAdt)
Local aAreaSE4 	:= SE4->(GetArea())
Local aVenc		:= {}
Local nValRetImp:= 0
Local cImpRet	:="" 
Local cNatureza :=""
Local lPParc	:=.F.
Default nTotalPed := 0 
Default lCarregaTotal := .T.      
Default lGravaRelacao := .F.

If cPaisLoc=="EQU"
	DbSelectArea("SA1")
	SA1->(DbSetOrder(1))
	If SA1->(DbSeek(xFilial("SA1")+cCodCli+cCodLoja))
		cNatureza:=SA1->A1_NATUREZ
		IF !Empty(cNatureza)
			lPParc:=Posicione("SED",1,xFilial("SED")+cNatureza,"ED_RATRET")=="1"	
		Endif
	Endif
Endif
If nTotalPed <= 0 .AND. lCarregaTotal
	nTotalPed := A410VlrTot() 
EndIf         

If lCarregaTotal
	If SFB->FB_JNS == 'J' .And. cPaisLoc == 'COL'
		dbSelectArea("SFC")
		dbSetOrder(2)
		If dbSeek(xFilial("SFC") + SF4->F4_CODIGO + "RV0" )
			nValRetImp 	:= MaFisRet(,"NF_VALIV2")
			Do Case
				Case FC_INCDUPL == '1'
					nTotalPed := nTotalPed - nValRetImp
				Case FC_INCDUPL == '2'
					nTotalPed := nTotalPed + nValRetImp
				Otherwise
					nTotalPed := nTotalPed
			EndCase
		Elseif dbSeek(xFilial("SFC") + SF4->F4_CODIGO + "RF0" )
			nValRetImp 	:= MaFisRet(,"NF_VALIV4")
			Do Case
				Case FC_INCDUPL == '1'
					nTotalPed := nTotalPed - nValRetImp
				Case FC_INCDUPL == '2'
					nTotalPed :=nTotalPed + nValRetImp
				Otherwise
					nTotalPed := nTotalPed
			EndCase
		Elseif dbSeek(xFilial("SFC") + SF4->F4_CODIGO + "RC0" )
			nValRetImp 	:= MaFisRet(,"NF_VALIV7")
			Do Case
				Case FC_INCDUPL == '1'
					nTotalPed := nTotalPed - nValRetImp
				Case FC_INCDUPL == '2'
					nTotalPed := nTotalPed + nValRetImp
				Otherwise
					nTotalPed := nTotalPed
			EndCase
		Endif
	ElseIf cPaisLoc=="EQU" .And. lPParc
		DbSelectArea("SFC")
		SFC->(dbSetOrder(2))
		If DbSeek(xFilial("SFC") + SF4->F4_CODIGO + "RIR") //RetenГЦo IVA
			cImpRet		:= SFC->FC_IMPOSTO
			DbSelectArea("SFB")
			SFB->(dbSetOrder(1))
			If SFB->(DbSeek(xFilial("SFB")+AvKey(cImpRet,"FB_CODIGO")))
				nValRetImp 	:= MaFisRet(,"NF_VALIV"+SFB->FB_CPOLVRO)
		    Endif       
		    DbSelectArea("SFC")      
   			Do Case
				Case SFC->FC_INCDUPL == '1'
					nTotalPed := nTotalPed - nValRetImp
				Case SFC->FC_INCDUPL == '2'
					nTotalPed := nTotalPed + nValRetImp
				Otherwise
					nTotalPed := nTotalPed
			EndCase
	    Endif
	EndIf
	aVenc := Condicao(nTotalPed,cCondPagto,0.00,dDataBase,0.00,{},,0)
	If Len(aVenc)>0
		nTotalPed := aVenc[1,2] 
	EndIf
EndIf

If A410UsaAdi( cCondPagto,@cCondPAdt )	 
	
	//зддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁChamada da tela de Recebimento do Financeiro.Ё
	//юддддддддддддддддддддддддддддддддддддддддддддды   
	aRecnoSE1 := FPEDADT("R", cNumPedido, nTotalPed, aRecnoSE1, cCodCli, cCodLoja)
	If Len(aRecnoSE1) > 0 .AND. lGravaRelacao
		// Grava quando И proveniente da Nota.
		FPedAdtGrv( "R", 1, cNumPedido, aRecnoSE1 )
	EndIf	
Else                                                                                        
	MsgAlert(STR0126) // "Por favor, selecione uma condiГЦo de pagamento que utilize Adiantamento."
EndIf    
RestArea(aAreaSE4)
Return .T.                

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠иммммммммммяммммммммммкмммммммяммммммммммммммммммммкммммммяммммммммммммм╩╠╠
╠╠╨Programa  ЁA410VlrTot╨Autor  ЁVendas CRM 		 ╨ Data Ё  24/08/09   ╨╠╠
╠╠лммммммммммьммммммммммймммммммоммммммммммммммммммммйммммммоммммммммммммм╧╠╠
╠╠╨Descricao Ё Retorna o valor total do Pedido.                           ╨╠╠
╠╠╨          Ё                                                       	  ╨╠╠
╠╠лммммммммммьмммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Uso       Ё MATA410, FATXFUN                                       	  ╨╠╠
╠╠хммммммммммомммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╪╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Function A410VlrTot()
Local nTotPed 	:= 0
Local nTotDes	:= 0
Local nX     	:= 0
Local nY        := 0
Local nMaxFor	:= Len(aCols)
Local nDescCab  := 0
Local nUsado    := Len(aHeader)
Local lTestaDel := nUsado <> Len(aCols[1])
Local nPosTotal := aScan(aHeader,{|x| AllTrim(x[2])=="C6_VALOR"})
Local nPosDesc  := aScan(aHeader,{|x| AllTrim(x[2])=="C6_VALDESC"})
Local nPPrUnit  := aScan(aHeader,{|x| AllTrim(x[2])=="C6_PRUNIT"})
Local nPQtdVen  := aScan(aHeader,{|x| AllTrim(x[2])=="C6_QTDVEN"})
Local nPPrcVen  := aScan(aHeader,{|x| AllTrim(x[2])=="C6_PRCVEN"})
	
//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁSoma as variaveis do aCols                                     Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
For nY := 1 To 2
	For nX := 1 To nMaxFor
		If ( (lTestaDel .And. !aCols[nX][nUsado+1]) .Or. !lTestaDel )
			If ( nPosDesc > 0 .And. nPPrUnit > 0 .And. nPPrcVen > 0 .And. nPQtdVen > 0)
				If ( aCols[nX][nPPrUnit]==0 )
					nTotDes	+= aCols[nX][nPosDesc ]
				Else
					nTotDes += A410Arred(aCols[nX][nPPrUnit]*aCols[nX][nPQtdVen],"C6_VALDESC")-;
						A410Arred(aCols[nX][nPPrcVen]*aCols[nX][nPQtdVen],"C6_VALDESC")
				EndIf
			EndIf
			If ( nPosTotal > 0 )
				nTotPed	+=	aCols[nX][nPosTotal]
			EndIf
		EndIf
	Next nX
	nDescCab := M->C5_DESC4
	nTotPed  -= M->C5_DESCONT
	nTotDes  += M->C5_DESCONT
	nTotDes  += A410Arred(nTotPed*M->C5_PDESCAB/100,"C6_VALOR")
	nTotPed  -= A410Arred(nTotPed*M->C5_PDESCAB/100,"C6_VALOR")
	If nY == 1
		If FtRegraDesc(3,nTotPed+nTotDes,@M->C5_DESC4) == nDescCab
			Exit
		Else
			nTotPed	:=	0
			nTotDes	:=	0
		EndIf
	EndIf
Next nY	

Return (nTotPed+M->C5_FRETE+M->C5_SEGURO+M->C5_DESPESA)

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠иммммммммммяммммммммммкмммммммяммммммммммммммммммммкммммммяммммммммммммм╩╠╠
╠╠╨Programa  ЁA410UsaAdi╨Autor  ЁVendas CRM 		 ╨ Data Ё  24/08/09   ╨╠╠
╠╠лммммммммммьммммммммммймммммммоммммммммммммммммммммйммммммоммммммммммммм╧╠╠
╠╠╨Descricao Ё Verifica se a condicao de pagto utiliza Adiantamento.      ╨╠╠
╠╠╨          Ё                                                       	  ╨╠╠
╠╠лммммммммммьмммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Uso       Ё MATA410, FATXFUN                                       	  ╨╠╠
╠╠хммммммммммомммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╪╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/        
Function A410UsaAdi( cCondPagto,cCondPAdt )
Local aAreaSE4 := SE4->(GetArea())
Local lRet := .F.
Default cCondPAdt:="0"
#IFDEF TOP
	If cPaisLoc $ "ANG|BRA|MEX" .AND. !Empty(cCondPagto) .AND. FindFunction("FPedAdtGrv") .and. AliasInDic("FIE")
		DbSelectArea("SE4")
		DbSetOrder(1)
		If 	SE4->(FieldPos("E4_CTRADT")) > 0 .AND.;
			DbSeek(xFilial("SE4")+cCondPagto) .AND.; 		
			SE4->E4_CTRADT == "1"
			
			lRet:= .T.
			cCondPAdt:="1"
		EndIf
	EndIf    
	RestArea(aAreaSE4)
#ENDIF
Return lRet

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠иммммммммммяммммммммммкмммммммяммммммммммммммммммммкммммммяммммммммммммм╩╠╠
╠╠╨Programa  ЁA410GrvMed╨Autor  ЁAndre Anjos 		 ╨ Data Ё  25/11/09   ╨╠╠
╠╠лммммммммммьммммммммммймммммммоммммммммммммммммммммйммммммоммммммммммммм╧╠╠
╠╠╨Descricao Ё Funcao que executa a rotina automatica da medicao (CNTA120)╨╠╠
╠╠лммммммммммьмммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Uso       Ё MATA410			                                       	  ╨╠╠
╠╠хммммммммммомммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╪╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/   
Static Function A410GrvMed(aCab,aItens)
Local aArea := GetArea()

If Type("lMsErroAuto") # "L"
	PRIVATE lMsErroAuto := .F.
Else
	lMsErroAuto := .F.
EndIf

//-- Gera a medicao
MsExecAuto({|a,b,c|,CNTA120(a,b,c)},aCab,aItens,3)		

//-- Encerra a medicao
If !lMsErroAuto
	MsExecAuto({|a,b,c|,CNTA120(a,b,c)},aCab,aItens,6)
EndIf

If lMsErroAuto
	MostraErro()
	RecLock("SC5",.F.)
	Replace SC5->C5_MDNUMED With CriaVar("C5_MDNUMED")
	Replace SC5->C5_MDCONTR With CriaVar("C5_MDNUMED")
	Replace SC5->C5_MDPLANI With CriaVar("C5_MDNUMED")
	MsUnLock()
	Aviso(STR0127,STR0133 +SC5->C5_NUM +STR0134,{"Ok"}) //SIGAGCT - O pedido de venda nЗmero ### nЦo foi vinculado ao contrato selecionado.
EndIf

RestArea(aArea)
Return            

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    Ё A410CCPed   Ё Autor Ё Vendas Clientes      Ё Data Ё 22/12/09 Ё╠╠
╠╠цддддддддддедддддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescricao Ё Verifica se existe amarracao do Ped. de vendas com o Ped.    Ё╠╠
╠╠Ё			 Ё de Compras Caso exista, e o Ped. de vendas seja alterado, a  Ё╠╠
╠╠Ё          Ё mesma alteracao devera ser feita no Ped. de Compras.			Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ Param01 - aCols do Ped. de Vendas com as alteracoes para o   Ё╠╠
╠╠Ё          Ё Ped.	de Compras							                    Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso		 Ё MATA410		                							    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/  
Static Function A410CCPed(aCols,aHeader,aMTA177PER,nOpcao)   
Local cFilPed  := SC6->C6_FILPED      // Filial do Ped. de compras
Local cNumDoc  := SC6->C6_PEDCOM      // Numero do Doc do Ped.  Compras
Local cItem    := SC6->C6_ITPC        // Numero do Item do Pedido
Local nProd    := aScan( aHeader,{|x| Trim(x[2]) == "C6_PRODUTO" } )  
Local nITem    := aScan( aHeader,{|x| Trim(x[2]) == "C6_ITEM"    } )  
Local nQuant   := aScan( aHeader,{|x| Trim(x[2]) == "C6_QTDVEN"  } )   
Local nPreco   := aScan( aHeader,{|x| Trim(x[2]) == "C6_PRCVEN"  } )   
Local nI       := 0                    // variavel para controle do for
Local nOpc     := 0                    // Variavel para tipo do MSExecAuto - 5 Exclui - 3 Inclui
Local aCbPC    := {}                   // Array para Cabecalho do Ped. de Compras
Local aItPC    := {}                   // Array para Itens do Ped. de Compras
Local aArea    := GetArea() 
Local cTxtLog  := ""                   // variavel para controle de errro no MSExecAuto
Local aMsgErr  := {}                   // array para guardar as mensagens de erro  
Local cDefine  := If(nOpcao == 1 ,STR0139 ,STR0138)                       //"Exclui Pedido de Compra" / "Atualiza Pedido de Compra"
Local lPedCom  := If(!Empty(cNumDoc ),MsgYesNo(cDefine, STR0137), .F. )  //"Pedidos de Compra"
Local cFilBkp  := cFilAnt    
Local cFilCent := ""                   //Filial Centralizadora do SC7     
Local cTES     := ""                   // Tes para geracao do Pedido de compra  
Local cFilAIB  := ""                   // Filia da Dabela AIB.
Local nPrUnit  := 0                    // Preco unitario.       
Local lRet     := .T.

	
DEFAULT aCols       := {}
DEFAULT aMTA177PER  := {}   
DEFAULT nOpcao      := 1   // Se for 1 Deleta o Doc de Compra se for 2 Exclui para a inclusao com a alteracao 

PRIVATE lMsErroAuto := .F.


DbSelectArea("SC7")
DbSetOrder(1)
DbSeek(cFilPed + cNumDoc + cItem ) 

cFilAnt := If (!Empty(SC7->C7_FILIAL), SC7->C7_FILIAL, cFilAnt )  
cFilCent:= SC7->C7_FILCEN
If !Empty(cNumDoc ) .AND. lPedCom  //CABECARIO DO PEDIDO DE COMPRAS 
    	aAdd(aCbPC, {'C7_NUM'		, SC7->C7_NUM		    , Nil})	//--Numero do Pedido
		aAdd(aCbPC, {'C7_EMISSAO'	, SC7->C7_EMISSAO		, Nil})	//--Data de Emissao
		aAdd(aCbPC, {'C7_FORNECE'	, SC7->C7_FORNECE	    , Nil})	//--Fornecedor
		aAdd(aCbPC, {'C7_LOJA'		, SC7->C7_LOJA			, Nil})	//--Loja do Fornecedor
		aAdd(aCbPC, {'C7_CONTATO'	, SC7->C7_CONTATO     	, Nil})	//--Contato
		aAdd(aCbPC, {'C7_COND'		, SC7->C7_COND			, Nil})	//--Condicao de Pagamento
		aAdd(aCbPC, {'C7_FILENT'	, SC7->C7_FILENT		, Nil})	//--Filial de Entrega			
		
	For nI := 1 to Len(aCols)  //ITENS DO PEDIDO DE COMPRAS
		cTES := RetFldProd(aCols[nI][nProd], 'B1_TE')  
		If aMTA177PER[1][1] == 1
			nPrUnit := RetFldProd(aCols[nI][nProd], 'B1_UPRC')
		ElseIf aMTA177PER[1][1] == 2
		//-- Obtem o preco unitario atraves da tabela informada e para o fornecedor do pedido de compra
			cFilAIB := If( Empty( xFilial('AIB') ), xFilial('AIB'), cFilAnt ) 
			nPrUnit := GetAdvFval( 'AIB','AIB_PRCCOM', cFilAIB + aCbPC[3][2] + aCbPC[4][2] + aMTA177PER[1][2] + aCols[nI][nProd]	, 2 )
		Else
			nPrUnit := RetFldProd(aCols[nI][nProd], 'B1_CUSTD')
		EndIf	
		AAdd(aItPC, {{ 'C7_ITEM'	 , PaDL(Val(aCols[nI][nITEM]),4,"0") 	 , NIL },;
						  { 'C7_PRODUTO' , aCols[nI][nProd]					 , NIL },;
						  { 'C7_CODTAB'  , If(aMTA177PER[1][1] = 2, aMTA177PER[1][2], Criavar('C7_CODTAB',.F.))				     , NIL },;
						  { 'C7_QUANT'	 , aCols[nI][nQuant]				 , NIL },;
						  { 'C7_PRECO'	 , nPrUnit		 					 , NIL },;
						  { 'C7_TES'	 , cTES	        	    			 , NIL },;
						  { 'C7_FILCEN'	 , cFilCent							 , Nil },;
						  { 'C7_TPOP'	 , 'F'								 , NIL } } )						
	Next nI 
	If Len( aCbPC ) > 0 .And. Len( aItPC ) > 0   
   		Begin Transaction
		For nI := 1 to nOpcao
			lMsErroAuto := .F.
			MSExecAuto( {|v,x,y,z,w| MATA120(v,x,y,z,w)}, 1, aCbPC, aItPC,If (nI = 1 ,5 , 3), .F. )
			If lMsErroAuto 
				Exit
			EndIf 
		Next nI	
		If lMsErroAuto
			cTxtLog := NomeAutoLog()
			If ValType( cTxtLog ) == 'C'
				AAdd( aMsgErr, Memoread( cTxtLog ) )
			EndIf		
			Disarmtransaction()
		EndIf
		End Transaction	
	EndIf	
EndIf
If Len( aMsgErr ) > 0
	For nI := 1 To Len( aMsgErr )
		AutoGrLog( aMsgErr[nI] )
	Next  nI
	AutoGrLog('')
	MostraErro()
EndIf 
cFilAnt := cFilBkp
RestArea(aArea)	
Return(NIL)      

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддддд©╠╠
╠╠ЁProgram   ЁMA521VerSC6 Ё Rev.  Ё Vendas Clientes       Ё Data Ё 26/12/2009 Ё╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁFuncao que verifica se existe amarracao no Pedido de venda com  Ё╠╠ 
╠╠Ё          ЁPedido de Compra, Caso exista e se jah foi feito recebimento de Ё╠╠
╠╠Ё          Ёde alguma quantidade no pedido de compra o Pedido de venda nao  Ё╠╠    
╠╠Ё          Ёpodera ser cancelado.                                           Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁLogico .T. para cancelar - .F. nao Cancela                      Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁPARAM01 - Filial da nota de Saida (SF2)                         Ё╠╠  
╠╠Ё          ЁPARAM02 - Numero do Documento                                   Ё╠╠  
╠╠Ё          ЁPARAM03 - Serie do Documento                                    Ё╠╠  
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Function A410VerISC6(cFilDoc,cNumDoc,aCols,aHeader)
Local lRet     := .T.
Local aArea    := GetArea()   
Local cFilPCom := ""        // Filial do Pedido de Compras 
Local cPedCom  := ""        // Numero do Pedido de Compras
Local cProd    := ""        // Cod do Produto 
Local cItemPC  := ""        // Item do Pedido de Compra
Local nITem    := aScan( aHeader,{|x| Trim(x[2]) == "C6_ITEM"    } )   
Local cNumC7   := ""        // Guarda o num para nao correr a Tabela Inteira.                               

Default cFilDoc   := ""
Default cNumDoc   := ""
Default aCols     := {}

If SC6->(FieldPos("C6_PEDCOM")) > 0 .AND.  SC6->(FieldPos("C6_ITPC")) > 0 .AND. SC6->(FieldPos("C6_FILPED")) > 0 
	If !Empty(cFilDoc) .AND. !Empty(cNumDoc) .AND. Len(aCols) > 0 
		DbSelectArea("SC6")
		DbSetOrder(1)
		If DbSeek(cFilDoc + cNumDoc + aCols[1][nItem] )
			cFilPCom  := SC6->C6_FILPED	
			cProd     := SC6->C6_PRODUTO
			cPedCom   := SC6->C6_PEDCOM
			cItemPC   := SC6->C6_ITPC
			If Empty (cFilPCom)
				lRet := .T.
			Else
				DbSelectArea("SC7")
				DbSetOrder(4)
				If DbSeek(cFilPCom + cProd + cPedCom + cItemPC ) 
				    cNumC7 := SC7->C7_NUM    
					While !Eof()		
						lRet := If(SC7->C7_QUJE > 0, .F., .T. )
						If !lRet .OR. cNumC7 <> SC7->C7_NUM 
						   Exit
						EndIf
						SC7->(DbSkip()) 
					End
				EndIf
			EndIf	
		EndIf
	EndIf	
EndIf	
RestArea(aArea)
Return (lRet)  

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    Ё AjustaSX6Ё Autor Ё VENDAS CRM			Ё Data Ё 12/11/10 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Ajuste do dicionario de dados		                      Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё MATA410  		                        				  |╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Static Function AjustaSX6()
Local aArea 	:= GetArea()

If ( cPaisLoc != "BRA" )
	dbSelectArea("SX6")
	dbSetOrder(1)
	If dbSeek(xFilial("SX6")+"MV_ALTTXFT") .And. SX6->X6_DESCRIC <> 'Selecionar taxa do Faturamento ("0" taxa SC5,' ;
		.And.	SX6->X6_DESC1 <> '"1" taxa do SM2,"2" abre tela para digitar taxa)'
		RecLock("SX6",.F.)
		X6_DESCRIC := 'Selecionar taxa do Faturamento ("0" taxa SC5,'  
		X6_DSCSPA  :=  'Seleccionar tasa de Facturacion ("0" tasa SC5,'
		X6_DSCENG  :=  'Select the Billing rate("0" rate SC5, "1" rate'
		X6_DESC1   := '"1" taxa do SM2,"2" abre tela para digitar taxa)'
		X6_DSCSPA1 :=  '"1" tasa del SM2,"2" abre pantalla para digitar'
		X6_DSCENG1 :=  'SM2 "2" opens window to enter rate.'
		X6_DESC2   :=  ''   
		X6_DSCSPA2 :=  'tasa)'
		X6_DSCENG2 :=  ''
		MsUnlock()
	EndIf
Endif	

RestArea(aArea)

Return

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠иммммммммммяммммммммммкмммммммяммммммммммммммммммммкммммммяммммммммммммм╩╠╠
╠╠╨Programa  Ёa410RatCC  ╨Autor  ЁMicrosiga           ╨ Data Ё  06/18/10   ╨╠╠
╠╠лммммммммммьммммммммммймммммммоммммммммммммммммммммйммммммоммммммммммммм╧╠╠
╠╠╨Desc.     Ё                                                            ╨╠╠
╠╠╨          Ё                                                            ╨╠╠
╠╠лммммммммммьмммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Uso       Ё AP                                                        ╨╠╠
╠╠хммммммммммомммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╪╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Function a410RatCC(aHeadAGG,aColsAGG,lAltera,nAt)

Local aArea       := GetArea()
Local aSavaRotina := aClone(aRotina)
Local aColsCC     := {}
Local aButtons	  := {}
Local aButtonUsr  := {}
Local aHeadSC7    := {}
Local aColsSC7    := {}
Local aNoFields   := {"AGG_CUSTO1","AGG_CUSTO2","AGG_CUSTO3","AGG_CUSTO4","AGG_CUSTO5"}
Local bSavKeyF4   := SetKey(VK_F4 ,Nil)
Local bSavKeyF5   := SetKey(VK_F5 ,Nil)
Local bSavKeyF6   := SetKey(VK_F6 ,Nil)
Local bSavKeyF7   := SetKey(VK_F7 ,Nil)
Local bSavKeyF8   := SetKey(VK_F8 ,Nil)
Local bSavKeyF9   := SetKey(VK_F9 ,Nil)
Local bSavKeyF10  := SetKey(VK_F10,Nil)
Local bSavKeyF11  := SetKey(VK_F11,Nil)
Local nPItemNF	  := aScan(aHeader,{|x| AllTrim(x[2]) == "C6_ITEM"} )
Local nPCC	      := aScan(aHeader,{|x| AllTrim(x[2]) == "C6_CC"} )
Local nPConta	  := aScan(aHeader,{|x| AllTrim(x[2]) == "C6_CONTA"} )
Local nPItemCta   := aScan(aHeader,{|x| AllTrim(x[2]) == "C6_ITEMCTA"} )
Local nPCLVL	  := Ascan(aHeader,{|x| AllTrim(x[2]) == "C6_CLVL"} )
Local nPDECC	  := 0
Local nPDEConta	  := 0
Local nPDEItemCta := 0
Local nPDECLVL	  := 0
Local nPRateio    := aScan(aHeader,{|x| AllTrim(x[2]) == "C6_RATEIO"} )
Local nColTotal   := aScan(aHeader,{|x| AllTrim(x[2]) == "C6_TOTAL"} )
Local nItem  	   := aScan(aColsAGG,{|x| Alltrim(x[1]) == Alltrim(aCols[n][nPItemNF])})
Local nX          := 0
Local nSavN       := nAT
Local nPPercAGG   := 0
Local nTotPerc    := 0
Local nOpcA       := 0
Local nNewTam     := 0
Local lContinua   := .T.
Local lRet        := .T.
Local oDlg
Local cCampo      := ReadVar()
Local nAviso      := 0
Local ca410Num    := M->C5_NUM

DEFAULT aHeadAGG  := {}
DEFAULT aColsAGG  := {}
DEFAULT lAltera   := .T.

Private aOrigHeader := aClone(aHeader)
Private aOrigAcols  := aClone(aCols)
Private oGetMan
Private nOrigN      := nAT
Private nPercRat    := 0
Private nPercARat	:= 100
Private oPercRat
Private oPercARat
Private oGetDad
Private N := nAT


//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Ativa os botoes da toolbar                                          Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If (CtbInUse())
	If !__lPyme
		aadd(aButtons,{'AUTOM',{|| AdmRatExt(aHeadAGG,oGetDad:aCols,{ |x,y,z,w| a410CarCC(x,y,@z,w) }) },STR0144,OemToAnsi(STR0151)}) //"Rateio"##'Escolha de Rateio Pre-Configurado'
	EndIF
EndIf

//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Impede de executar a rotina quando a tecla F3 estiver ativa		   Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If Type("InConPad") == "L" 
	lContinua := !InConPad
EndIf

If nSavN == 0 
	lContinua := .F.
EndIf

If lContinua
	//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Montagem do aHeader do AGG                                          Ё
	//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If Empty(aHeadAGG)
		dbSelectArea("SX3")
		dbSetOrder(1)
		MsSeek("AGG")
		While !EOF() .And. (SX3->X3_ARQUIVO == "AGG")
			IF X3USO(SX3->X3_USADO) .AND. cNivel >= SX3->X3_NIVEL .And. !"AGG_CUSTO"$SX3->X3_CAMPO
				AADD(aHeadAGG,{ TRIM(x3Titulo()),;
				SX3->X3_CAMPO,;
				SX3->X3_PICTURE,;
				SX3->X3_TAMANHO,;
				SX3->X3_DECIMAL,;
				SX3->X3_VALID,;
				SX3->X3_USADO,;
				SX3->X3_TIPO,;
				SX3->X3_F3,;
				SX3->X3_CONTEXT } )
			EndIf
			dbSelectArea("SX3")
			dbSkip()
		EndDo
	EndIf
	
	//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Montagem do aCols do AGG                                            Ё
	//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If nItem > 0
		aColsCC := aClone(aColsAGG[nItem][2])
		
		//зддддддддддддддддддддддддд©
		//Ё Totaliza o % ja Rateado Ё
		//юддддддддддддддддддддддддды
		nPercRat := 0
		For nX   := 1  To  Len(aColsCC)
			nPercRat += aColsCC[nX][aScan(aHeadAGG,{|x| AllTrim(x[2])=="AGG_PERC"})]
		Next nX
		
		nPercARat := 100 - nPercRat
	Else
		//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё aHeader e aCols do SC7 devem ser salvos pois a FillGetDados destroe Ё
		//Ё ambos por serem PRIVATE, independente da construcao do aColsCC.     Ё
		//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		aHeadSC7 := aClone(aHeader)
		aColsSC7 := aClone(aCols)
		aHeadAGG := {}
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Sintaxe da FillGetDados(nOpcX,Alias,nOrdem,cSeek,bSeekWhile,uSeekFor,aNoFields,aYesFields,lOnlyYes,cQuery,bMontCols,lEmpty,aHeaderAux,aColsAux,bAfterCols,bBeforeCols,bAfterHeader,cAliasQry |
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		FillGetDados(IIf(lAltera,3,2),"AGG",1,,,,aNoFields,,,,,.T.,aHeadAGG,aColsCC,,,)
		aColsCC[1][aScan(aHeadAGG,{|x| Trim(x[2])=="AGG_ITEM"})] := StrZero(1,Len(AGG->AGG_ITEM))
		
		aHeader := aHeadSC7
		aCols   := aColsSC7
		
	EndIf
	If !(Type('l410Auto') <> 'U' .And. l410Auto)
		aHeadSC7 := aClone(aHeader)
		aColsSC7 := aClone(aCols)
		DEFINE MSDIALOG oDlg FROM 100,100 TO 350,600 TITLE STR0152 Of oMainWnd PIXEL //"Rateio por Centro de Custo"
		@ 018,003 SAY RetTitle("C6_NUM")  OF oDlg PIXEL SIZE 20,09
		@ 018,026 SAY ca410Num            OF oDlg PIXEL SIZE 50,09
		@ 018,096 SAY RetTitle("C6_ITEM") OF oDlg PIXEL SIZE 20,09
		@ 018,120 SAY aCols[N][nPItemNF]  OF oDlg PIXEL SIZE 20,09
		oGetDad := MsNewGetDados():New(030,005,105,245,IIF(lAltera,GD_INSERT+GD_UPDATE+GD_DELETE,0),"a410RatLOk","a410RatTOk","+AGG_ITEM",,,999,/*fieldok*/,/*superdel*/,/*delok*/,oDlg,aHeadAGG,aColsCC)
		oGetMan := oGetDad
		@ 110,005 Say OemToAnsi(STR0149) FONT oDlg:oFont OF oDlg PIXEL	 // "% Rateada: "
		@ 110,035 Say oPercRat VAR nPercRat Picture PesqPict("AGG","AGG_PERC") FONT oDlg:oFont COLOR CLR_HBLUE OF oDlg PIXEL
		@ 110,184 Say OemToAnsi(STR0150) FONT oDlg:oFont OF oDlg PIXEL	 // "% A Ratear: "
		@ 110,217 Say oPercARat VAR nPercARat Picture PesqPict("AGG","AGG_PERC") FONT oDlg:oFont COLOR CLR_HBLUE OF oDlg PIXEL
		ACTIVATE MSDIALOG oDlg CENTERED ON INIT EnchoiceBar(oDlg,{||IIF(oGetDad:TudoOk(),(nOpcA:=1,oDlg:End()),(nOpcA:=0))},{||oDlg:End()},,aButtons)
		If lAltera
			aColsCC := Aclone(oGetDad:aCols)
		Else
			aHeader := aHeadSC7
			aCols   := aColsSC7
		EndIf
	Else
		nOpcA := 1
	EndIf
	nPPercAGG := aScan(aHeadAGG,{|x| AllTrim(x[2])=="AGG_PERC"})
	nTotPerc := 0
	
	aColsPar :={}
	AEval( aColsCC, { |x| If( !x[ Len(aHeadAGG) + 1], AAdd( aColsPar, x ), ) } )
	aColsCC := aClone( aColsPar )
	
	For nX := 1 To Len(aColsCC)
		nTotPerc += aColsCC[nX][nPPercAGG]
	Next nX
	
	nPDECC	      := aScan(aHeadAGG,{|x| AllTrim(x[2]) == "AGG_CC"} )
	nPDIt	      := aScan(aHeadAGG,{|x| AllTrim(x[2]) == "AGG_ITEM"} )
	nPDEConta	  := aScan(aHeadAGG,{|x| AllTrim(x[2]) == "AGG_CONTA"} )
	nPDEItemCta   := aScan(aHeadAGG,{|x| AllTrim(x[2]) == "AGG_ITEMCT"} )
	nPDECLVL	  := Ascan(aHeadAGG,{|x| AllTrim(x[2]) == "AGG_CLVL"} )
	
	If nOpcA == 1 .And. lAltera
		If nTotPerc > 0
			//Acerta a numeraГЦo do Item
			For nX := 1 to Len(aColsCC)
				aColsCC[nX][nPDIt] := StrZero(nX,TamSX3("AGG_ITEM")[1])
			Next nX
			If nItem > 0
				aColsAGG[nItem][2]	:= aClone(aColsCC)
			Else
				aadd(aColsAGG,{aCols[N][nPItemNF],aClone(aColsCC)})
			EndIf
			
			aCols[N][nPRateio] := "1"
			If nPCC <> 0 .And. nPDECC <> 0
				aCols[N][nPCC]     := Space(Len(aCols[N][nPCC]))
			EndIf
			If nPConta <> 0 .And. nPDEConta <> 0
				aCols[N][nPConta]  := Space(Len(aCols[N][nPConta]))
			EndIf
			If nPItemCta <> 0 .And. nPDEItemCta <> 0
				aCols[N][nPItemCta]:= Space(Len(aCols[N][nPItemCta]))
			EndIf
			If nPCLVL <> 0 .And. nPDECLVL <> 0
				aCols[N][nPCLVL]   := Space(Len(aCols[N][nPCLVL]))
			EndIf
			If N == 1 .And. Len(aCols)>1
				nAviso := Aviso(STR0153,STR0154,{STR0155,STR0156,STR0157}) //"AtenГЦo"###"Replicar informaГУes para os demais itens do documento?"###"Sim"###"NЦo"###"Todos"
				If nAviso == 3
					aColsAGG := {}
				EndIf
				If nAviso <> 2
					For nX := 1 To Len(aCols)
						nItem  	  := aScan(aColsAGG,{|x| x[1] == aCols[nX][nPItemNF]})
						If nItem == 0
							aadd(aColsAGG,{aCols[nX][nPItemNF],aClone(aColsCC)})
							
							aCols[nX][nPRateio] := "1"
							
							If nPCC <> 0 .And. nPDECC <> 0
								aCols[NX][nPCC]     := Space(Len(aCols[NX][nPCC]))
							EndIf
							If nPConta <> 0 .And. nPDEConta <> 0
								aCols[NX][nPConta]  := Space(Len(aCols[NX][nPConta]))
							EndIf
							If nPItemCta <> 0 .And. nPDEItemCta <> 0
								aCols[NX][nPItemCta]:= Space(Len(aCols[NX][nPItemCta]))
							EndIf
							If nPCLVL <> 0 .And. nPDECLVL <> 0
								aCols[NX][nPCLVL]   := Space(Len(aCols[NX][nPCLVL]))
							EndIf
							
						EndIf
					Next nX
				EndIf
			EndIf
		Else
			If nItem > 0
				aColsAGG[nItem][2]	:= aClone(aColsCC)
			Else
				aadd(aColsAGG,{aCols[N][nPItemNF],aClone(aColsCC)})
			EndIf
			aCols[nSavN][nPRateio] := "2"
		EndIf
	Else
		If nTotPerc > 0 .And. nItem > 0
			If "C6_RATEIO" $ cCampo
				&cCampo := "1"
			EndIf
			aCols[nSavN][nPRateio] := "1"
		Else
			If "C6_RATEIO" $ cCampo
				&cCampo := "2"
			EndIf
			aCols[nSavN][nPRateio] := "2"
		EndIf
	EndIf
EndIf

//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Restaura a integridade da rotina                                    Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
aRotina	:= aClone(aSavaRotina)
N := nSavN
SetKey(VK_F4 ,bSavKeyF4)
SetKey(VK_F5 ,bSavKeyF5)
SetKey(VK_F6 ,bSavKeyF6)
SetKey(VK_F7 ,bSavKeyF7)
SetKey(VK_F8 ,bSavKeyF8)
SetKey(VK_F9 ,bSavKeyF9)
SetKey(VK_F10,bSavKeyF10)
SetKey(VK_F11,bSavKeyF11)
RestArea(aArea)
Return(.T.)

/*
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤└o	 Ёa410AdtSld Ё Autor Ё Totvs                Ё Data Ё 05/09/09 Ё╠╠
╠╠цддддддддддедддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤└o Ё Retorna o saldo do Relacionamento do pedido                Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpA1 = Numero do pedido						      		  Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Static Function a410AdtSld(cNumPed,aAdtPC,nAutoAdt,nF)         

Local nSaldo	:= 0
Local aArea     := GetArea()
Local aTmpPed   :={}
Local aValidAdt :={}
Local nXT_Adt   :=0
Local nX,yX     :=0
Local cBuscaAdt :=""
Local lXAdt     :=.T.
Local nXT_Relac :=0
Local nVlAdt  :=0
Local lCkAdt    :=.T.
Local cCamposAdt :=""

Local nPos_FILIAL := 0
Local nPos_PREFIX := 0
Local nPos_NUM    := 0
Local nPos_PARCEL := 0
Local nPos_TIPO   := 0
Local nPos_VALOR  := 0
Local nPos_CART   := 0
Local nPos_CLIENT := 0
Local nPos_LOJA   := 0

Default nAutoAdt:=0
Default aAdtPC  :={}
Default nF      := 1

If nAutoAdt==3 .OR. nAutoAdt==4 
	
	//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁPosiГЦo dos campos e verifica se os campos obrigatСrios estЦo presentesЁ
	//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	lCkAdt := VlAdtCpo(aAdtPC)
	
	If lCkAdt
		DbSelectArea("SE1")
		DbSetOrder(1)
		For nX := 1 to Len(aAdtPC)    //NOVO RELACIONAMENTO
			nPos_FILIAL := AScan(aAdtPC[nX], { |x| Alltrim(x[1])=="FIE_FILIAL"} )
			nPos_PREFIX := AScan(aAdtPC[nX], { |x| Alltrim(x[1])=="FIE_PREFIX"} )
			nPos_NUM    := AScan(aAdtPC[nX], { |x| Alltrim(x[1])=="FIE_NUM"   } )
			nPos_PARCEL := AScan(aAdtPC[nX], { |x| Alltrim(x[1])=="FIE_PARCEL"} )
			nPos_TIPO   := AScan(aAdtPC[nX], { |x| Alltrim(x[1])=="FIE_TIPO"}   )
			nPos_VALOR  := AScan(aAdtPC[nX], { |x| Alltrim(x[1])=="FIE_VALOR"}  )
			nPos_CART   := AScan(aAdtPC[nX], { |x| Alltrim(x[1])=="FIE_CART"}   )
			nPos_CLIENT := AScan(aAdtPC[nX], { |x| Alltrim(x[1])=="FIE_CLIENT"} )
			nPos_LOJA   := AScan(aAdtPC[nX], { |x| Alltrim(x[1])=="FIE_LOJA"}   ) 
			
			If DbSeek(aAdtPC[nX][nPos_FILIAL][2]+aAdtPC[nX][nPos_PREFIX][2]+aAdtPC[nX][nPos_NUM][2]+aAdtPC[nX][nPos_PARCEL][2]+aAdtPC[nX][nPos_TIPO][2])
				Aadd(aTmpPed ,{cNumPed,SE1->(Recno()),aAdtPC[nX][nPos_VALOR][2]})  //PEDIDO+RECNO DO SE2+NOVO VLR DE RELACIO
				Aadd(aValidAdt,{SE1->E1_FILIAL,aAdtPC[nX][nPos_CART][2],SE1->E1_CLIENTE,SE1->E1_LOJA,SE1->E1_PREFIXO,SE1->E1_NUM,SE1->E1_PARCELA,SE1->E1_TIPO,aAdtPC[nX][nPos_VALOR][2],SE1->E1_VALOR}) //ARMAZENA CHAVE DO RELAC NO SE2
			Endif
			
		Next
		DbSelectArea( "FIE" )
		FIE->( DbSetOrder( 2 ) )
		For nX := 1 to Len(aTmpPed)
			cBuscaAdt:=(aValidAdt[nX][1]+aValidAdt[nX][2]+aValidAdt[nX][3]+aValidAdt[nX][4]+aValidAdt[nX][5]+aValidAdt[nX][6]+aValidAdt[nX][7]+aValidAdt[nX][8])
			nXT_Adt:=0                 //VALOR DO RELACIONAMENTO ATUAL
			nXT_Relac+=aTmpPed[nX][3]  //SOMA TOTAL DO NOVO REALCIONAMENTO
			If FIE->( DbSeek( cBuscaAdt ) )  //BUSCA RELACIONAMENTO EXISTENTE COM SE1
				While !FIE->(EOF()) .and. cBuscaAdt==(FIE->FIE_FILIAL+FIE->FIE_CART+FIE->FIE_CLIENT+FIE->FIE_LOJA+FIE->FIE_PREFIX+FIE->FIE_NUM+FIE->FIE_PARCEL+FIE->FIE_TIPO)
					nXT_Adt+=FIE->FIE_VALOR   //SOMA DE TODOS RELAC EXISTENTES PARA O PA USADO
					FIE->(DbSkip())
				Enddo
				If nAutoAdt==4 .and. nF=2
					cBuscaAdt:=(aValidAdt[nX][1]+aValidAdt[nX][2]+aValidAdt[nX][3]+aValidAdt[nX][4]+aValidAdt[nX][5]+aValidAdt[nX][6]+aValidAdt[nX][7]+aValidAdt[nX][8])
					nVlAdt  :=0
					If FIE->( DbSeek( cBuscaAdt ) )  //BUSCA RELACIONAMENTO EXISTENTE COM SE1
						While !FIE->(EOF()) .and. cBuscaAdt==(FIE->FIE_FILIAL+FIE->FIE_CART+FIE->FIE_CLIENT+FIE->FIE_LOJA+FIE->FIE_PREFIX+FIE->FIE_NUM+FIE->FIE_PARCEL+FIE->FIE_TIPO)
							If FIE->FIE_PEDIDO==cNumPed
								nVlAdt+=FIE->FIE_VALOR
							Endif
							FIE->(DbSkip())
						Enddo
					Endif
					If ((nXT_Adt-nVlAdt)+aTmpPed[nX][3]) > aValidAdt[nX][10] //Valor Novo Realc - Valor Ataul Realc.+
						Help(" ",1,"A410NOSDADT")
						lXAdt:=.F.
						Exit
					Endif
				Else
					If (nXT_Adt+aTmpPed[nX][3]) > aValidAdt[nX][10] //SE (SOMA RELAC DO PA USADOS+NOVO RELAC) FOR > O VALOR DO PA
						If nF>=1
							Help(" ",1,"A410NOSDADT")
						Endif
						lXAdt:=.F.
						Exit
					Endif
				Endif
			Endif
		Next
		If lXAdt
			nSaldo:=1
		Else
			nSaldo:=0
		Endif
	Else
		nSaldo:=0
	EndIf
Endif
RestArea(aArea)
Return nSaldo
/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддбдддддддбддддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    Ёa410CarCC Ё Autor Ё Wagner Mobile Costa    Ё Data Ё21.10.2002Ё╠╠
╠╠цддддддддддедддддддддадддддддаддддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁCarrega as definicoes de rateio externo                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё MATA103                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Function a410CarCC(aCols, aHeader, cItem, lPrimeiro)

Local lCusto		:= CtbMovSaldo("CTT")
Local lItem	 		:= CtbMovSaldo("CTD")
Local lCLVL	 		:= CtbMovSaldo("CTH")
Local nPosPerc		:= aScan(aHeader,{|x| AllTrim(x[2]) == "AGG_PERC" } )
Local nPosItem		:= aScan(aHeader,{|x| AllTrim(x[2]) == "AGG_ITEM" } )
Local nPosCC		:= aScan(aHeader,{|x| AllTrim(x[2]) == "AGG_CC"} )
Local nPosConta		:= aScan(aHeader,{|x| AllTrim(x[2]) == "AGG_CONTA"} )
Local nPosItemCta	:= aScan(aHeader,{|x| AllTrim(x[2]) == "AGG_ITEMCT"} )
Local nPosCLVL		:= aScan(aHeader,{|x| AllTrim(x[2]) == "AGG_CLVL"} )
Local nHeader     := 0
Local aMT103PRE     := {}

If lPrimeiro
	//-- Se ja foi informado algum rateio, limpar o aCols
	If aCols[Len(aCols)][nPosPerc] <> 0
		aCols := {}
		Aadd(aCols, Array(Len(aHeader) + 1))
		For nHeader := 1 To Len(aHeader)
			If Trim(aHeader[nHeader][2]) <> "AGG_ALI_WT" .And. Trim(aHeader[nHeader][2]) <> "AGG_REC_WT"
				aCols[Len(aCols)][nHeader] := CriaVar(aHeader[nHeader][2])
			Endif
		Next
	EndIf
	cItem := Soma1(cItem)
	aCols[Len(aCols)][nPosItem]  := cItem
	aCols[Len(aCols)][Len(aHeader)+1] := .F.
Else
	If aCols[Len(aCols)][nPosPerc] = 0
		nCols := Len(aCols)
		cItem := aCols[nCols][nPosItem]
	Else
		If Len(aCols) > 0
			cItem := aCols[Len(aCols)][nPosItem]
		Endif
		Aadd(aCols, Array(Len(aHeader) + 1))
		cItem := Soma1(cItem)
	EndIf
	
	For nHeader := 1 To Len(aHeader)
		If Trim(aHeader[nHeader][2]) <> "AGG_ALI_WT" .And. Trim(aHeader[nHeader][2]) <> "AGG_REC_WT"
			aCols[Len(aCols)][nHeader] := CriaVar(aHeader[nHeader][2])
		EndIf
	Next
	
	aCols[Len(aCols)][nPosItem] := cItem
	
	// Interpreto os campos incluida possibilidade de variaveis de memoria
	If !Empty(CTJ->CTJ_DEBITO)
		aCols[Len(aCols)][nPosConta]	:= CTJ->CTJ_DEBITO
	Else
		aCols[Len(aCols)][nPosConta]	:= CTJ->CTJ_CREDIT
	Endif
	
	
	If lCusto
		If ! Empty(CTJ->CTJ_CCD)
			aCols[Len(aCols)][nPosCc]	:= CTJ->CTJ_CCD
		Else
			aCols[Len(aCols)][nPosCc]	:= CTJ->CTJ_CCC
		Endif
	EndIf
	
	If lItem
		If ! Empty(CTJ->CTJ_ITEMD)
			aCols[Len(aCols)][nPosItemCta]	:= CTJ->CTJ_ITEMD
		Else
			aCols[Len(aCols)][nPosItemCta]	:= CTJ->CTJ_ITEMC
		Endif
	EndIf
	
	If lClVl
		If ! Empty(CTJ->CTJ_CLVLDB)
			aCols[Len(aCols)][nPosClVl]	:= CTJ->CTJ_CLVLDB
		Else
			aCols[Len(aCols)][nPosClVl]	:= CTJ->CTJ_CLVLCR
		Endif
	EndIf
	aCols[Len(aCols)][nPosPerc] := CTJ->CTJ_PERCEN
	aCols[Len(aCols)][Len(aHeader) + 1] := .F.
	
EndIf

Return .T.
/*/
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбддддддддддд©╠╠
╠╠ЁFun┤└o    Ёa410RatLok Ё Autor Ё Eduardo Riera         Ё Data Ё15.10.2002 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддаддддддддддд╢╠╠
╠╠Ё          ЁValidacao da linhaok dos itens do rateio dos itens do documenЁ╠╠
╠╠Ё          Ёto de entrada                                                Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ                                                             Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁExpL1: Indica se a linha esta valida                         Ё╠╠
╠╠Ё          Ё                                                             Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁDescri┤└o ЁEsta rotina tem como objetivo validar a linhaok do rateio dosЁ╠╠
╠╠Ё          Ёitens do documento de entrada                                Ё╠╠
╠╠Ё          Ё                                                             Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё Materiais                                                   Ё╠╠
╠╠юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function a410RatLOk()

Local nPPerc    := aScan(aHeader,{|x| AllTrim(x[2]) == "AGG_PERC"} )
Local lRetorno  := .T.
Local nX        := 0

If !aCols[N][Len(aCols[N])]
	If aCols[N][nPPerc] == 0
		Help(" ",1,"A103PERC")
		lRetorno := .F.
	EndIf
EndIf

If lRetorno
	nPercRat := 0
	nPercARat:= 0
	For nX	:= 1 To Len(aCols)
		If !aCols[nX][Len(aCols[nX])]
			nPercRat += aCols[nX][nPPerc]
		EndIf
	Next
	nPercARat := 100 - nPercRat
	If Type("oPercRat")=="O"
		oPercRat:Refresh()
		oPercARat:Refresh()
	Endif
EndIf     

If lRetorno .And. ExistBlock("MRatLOk")
	lRetorno := ExecBlock("MRatLOk",.F.,.F.)
EndIf

Return(lRetorno)

/*/
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбддддддддддд©╠╠
╠╠ЁFun┤└o    Ёa410RatLok Ё Autor Ё Eduardo Riera         Ё Data Ё15.10.2002 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддаддддддддддд╢╠╠
╠╠Ё          ЁValidacao da TudoOk dos itens do rateio dos itens do documen-Ё╠╠
╠╠Ё          Ёto de entrada                                                Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ                                                             Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁExpL1: Indica se a todas as linhas estao validas             Ё╠╠
╠╠Ё          Ё                                                             Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁDescri┤└o ЁEsta rotina tem como objetivo validar a tudook do rateio dos Ё╠╠
╠╠Ё          Ёitens do documento de entrada                                Ё╠╠
╠╠Ё          Ё                                                             Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё Materiais                                                   Ё╠╠
╠╠юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function a410RatTok()

Local nPPerc   := aScan(aHeader,{|x| AllTrim(x[2]) == "AGG_PERC"} )
Local nTotal   := 0
Local nX       := 0
Local lRetorno := .T.
Local n_SaveLin

For nX	:= 1 To Len(aCols)
	If !aCols[nX][Len(aCols[nX])]
		nTotal += aCols[nX][nPPerc]
	EndIf
Next
If nTotal > 0 .And. nTotal <> 100
	Help(" ",1,"A103TOTRAT")
	lRetorno := .F.
EndIf

Return(lRetorno) 


/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠иммммммммммяммммммммммкмммммммяммммммммммммммммммммкммммммяммммммммммммм╩╠╠
╠╠╨Programa  ЁA410FRat   ╨Autor  ЁMicrosiga           ╨ Data Ё  06/23/10   ╨╠╠
╠╠лммммммммммьммммммммммймммммммоммммммммммммммммммммйммммммоммммммммммммм╧╠╠
╠╠╨Desc.     ЁCarrega o vetor dos rateios do pedido                       ╨╠╠
╠╠╨          Ё                                                            ╨╠╠
╠╠лммммммммммьмммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Uso       Ё AP                                                        ╨╠╠
╠╠хммммммммммомммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╪╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Static Function A410FRat(aHeadAGG,aColsAGG)
Local lQuery    := .T.
Local aStruAGG  := AGG->(dbStruct())
Local cAliasAGG := "AGG" 
Local nX		:= 0
Local nY		:= 0  
Local aBackAGG    := {}
Local cItemAGG  := ""
Local nItemAGG	:= 0
//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Monta o Array contendo as registros do AGG           Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
DbSelectArea("AGG")
DbSetOrder(1)
cAliasAGG := "AGG"		

#IFDEF TOP
	If TcSrvType()<>"AS/400"
		lQuery    := .T.
		aStruAGG  := AGG->(dbStruct())
		cAliasAGG := "A120NFISCAL"
		cQuery    := "SELECT AGG.*,AGG.R_E_C_N_O_ AGGRECNO "
		cQuery    += "FROM "+RetSqlName("AGG")+" AGG "
		cQuery    += "WHERE AGG.AGG_FILIAL='"+xFilial("AGG")+"' AND "
		cQuery    += "AGG.AGG_PEDIDO='"+SC5->C5_NUM+"' AND "
		cQuery    += "AGG.AGG_FORNEC='"+SC5->C5_CLIENTE+"' AND "
		cQuery    += "AGG.AGG_LOJA='"+SC5->C5_LOJACLI+"' AND "
		cQuery    += "AGG.D_E_L_E_T_=' ' "
		cQuery    += "ORDER BY "+SqlOrder(AGG->(IndexKey()))

		cQuery := ChangeQuery(cQuery)

		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasAGG,.T.,.T.)
		For nX := 1 To Len(aStruAGG)
			If aStruAGG[nX,2]<>"C"
				TcSetField(cAliasAGG,aStruAGG[nX,1],aStruAGG[nX,2],aStruAGG[nX,3],aStruAGG[nX,4])
			EndIf
		Next nX
		
	Else
#ENDIF
		MsSeek(xFilial("AGG")+SC5->C5_NUM+SC5->C5_CLIENTE+SC5->C5_LOJACLI)
#IFDEF TOP
	EndIf
#ENDIF

dbSelectArea(cAliasAGG)
While ( !Eof() .And. ;
		xFilial('AGG') == (cAliasAGG)->AGG_FILIAL .And.;
		SC5->C5_NUM == (cAliasAGG)->AGG_PEDIDO .And.;
		SC5->C5_CLIENTE == (cAliasAGG)->AGG_FORNEC .And.;
		SC5->C5_LOJACLI == (cAliasAGG)->AGG_LOJA )
	If Empty(aBackAGG)
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Montagem do aHeader                                          Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		DbSelectArea("SX3")
		DbSetOrder(1)
		MsSeek("AGG")
		While ( !EOF() .And. SX3->X3_ARQUIVO == "AGG" )
			If X3USO(SX3->X3_USADO) .AND. cNivel >= SX3->X3_NIVEL .And. !"AGG_CUSTO"$SX3->X3_CAMPO
				aadd(aBackAGG,{ TRIM(X3Titulo()),;
					SX3->X3_CAMPO,;
					SX3->X3_PICTURE,;
					SX3->X3_TAMANHO,;
					SX3->X3_DECIMAL,;
					SX3->X3_VALID,;
					SX3->X3_USADO,;
					SX3->X3_TIPO,;
					SX3->X3_F3,;
					SX3->X3_CONTEXT })
			EndIf
			DbSelectArea("SX3")
			dbSkip()
		EndDo
	EndIf
	aHeadAGG  := aBackAGG
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Adiciona os campos de Alias e Recno ao aHeader para WalkThru.Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	ADHeadRec("AGG",aHeadAGG)    

	If cItemAGG <> 	(cAliasAGG)->AGG_ITEMPD
		cItemAGG	:= (cAliasAGG)->AGG_ITEMPD
		aadd(aColsAGG,{cItemAGG,{}})
		nItemAGG++
	EndIf

	aadd(aColsAGG[nItemAGG][2],Array(Len(aHeadAGG)+1))
	For nY := 1 to Len(aHeadAGG)
		If IsHeadRec(aHeadAGG[nY][2])
			aColsAGG[nItemAGG][2][Len(aColsAGG[nItemAGG][2])][nY] := IIf(lQuery , (cAliasAGG)->AGGRECNO , AGG->(Recno())  )
		ElseIf IsHeadAlias(aHeadAGG[nY][2])
			aColsAGG[nItemAGG][2][Len(aColsAGG[nItemAGG][2])][nY] := "AGG"
		ElseIf ( aHeadAGG[nY][10] <> "V")
			aColsAGG[nItemAGG][2][Len(aColsAGG[nItemAGG][2])][nY] := (cAliasAGG)->(FieldGet(FieldPos(aHeadAGG[nY][2])))
		Else
			aColsAGG[nItemAGG][2][Len(aColsAGG[nItemAGG][2])][nY] := (cAliasAGG)->(CriaVar(aHeadAGG[nY][2]))
		EndIf
		aColsAGG[nItemAGG][2][Len(aColsAGG[nItemAGG][2])][Len(aHeadAGG)+1] := .F.
	Next nY

	DbSelectArea(cAliasAGG)
	dbSkip()
EndDo

If lQuery
	DbSelectArea(cAliasAGG)
	dbCloseArea()
	DbSelectArea("AGG")
EndIf

Return


/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠иммммммммммяммммммммммкмммммммяммммммммммммммммммммкммммммяммммммммммммм╩╠╠
╠╠╨Programa  ЁMATA410   ╨Autor  ЁMicrosiga           ╨ Data Ё  07/15/10   ╨╠╠
╠╠лммммммммммьммммммммммймммммммоммммммммммммммммммммйммммммоммммммммммммм╧╠╠
╠╠╨Desc.     ЁCarrega o array de rateio com as informaГУes da execauto    ╨╠╠
╠╠╨          Ё                                                            ╨╠╠
╠╠лммммммммммьмммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Uso       Ё AP                                                        ╨╠╠
╠╠хммммммммммомммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╪╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Static Function M410AutRat(aRatCTBPC)
Local aTmpRat := {}
Local aAux := {}
Local aColsAGG := {}
Local xZ
Local nY

For xZ:=1 to Len(aRatCTBPC)
	aTmpRat:={}
	aAux := aRatCTBPC[xZ][2]
	For nY:= 1 to Len(aAux)
		Aadd(aTmpRat,{aAux[nY][AScan(aAux[nY], { |x| Alltrim(x[1])=="AGG_ITEM"} 	 )][2],;
		aAux[nY][AScan(aAux[nY], { |x| Alltrim(x[1])=="AGG_PERC"} 	 )][2],;
		aAux[nY][AScan(aAux[nY], { |x| Alltrim(x[1])=="AGG_CC"}	 )][2],;
		aAux[nY][AScan(aAux[nY], { |x| Alltrim(x[1])=="AGG_CONTA"}	 )][2],;
		aAux[nY][AScan(aAux[nY], { |x| Alltrim(x[1])=="AGG_ITEMCTA"} )][2],;
		aAux[nY][AScan(aAux[nY], { |x| Alltrim(x[1])=="AGG_CLVL"} 	 )][2],;
		.F.})
	Next
	Aadd(aColsAGG,{aRatCTBPC[xZ][1],aTmpRat})
Next

Return aColsAGG

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠иммммммммммяммммммммммкмммммммяммммммммммммммммммммкммммммяммммммммммммм╩╠╠
╠╠╨Programa  ЁVlAdtCpo  ╨Autor  ЁMicrosiga           ╨ Data Ё  07/15/10   ╨╠╠
╠╠лммммммммммьммммммммммймммммммоммммммммммммммммммммйммммммоммммммммммммм╧╠╠
╠╠╨Desc.     ЁValida se todos os campos da rotina automatica de adianta   ╨╠╠
╠╠╨          Ёmento                                                       ╨╠╠
╠╠лммммммммммьмммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Uso       Ё AP                                                        ╨╠╠
╠╠хммммммммммомммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╪╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Static Function VlAdtCpo(aAdtPC)
Local lRet := .T.
Local aCpo := {}
Local nX   := 0
Local nPos := 0           
Local nY   := 0
Local cCpoAdt := ""

aAdd(aCpo,"FIE_FILIAL")
aAdd(aCpo,"FIE_PREFIX")
aAdd(aCpo,"FIE_NUM")
aAdd(aCpo,"FIE_PARCEL")
aAdd(aCpo,"FIE_TIPO")
aAdd(aCpo,"FIE_VALOR")
aAdd(aCpo,"FIE_CART")
aAdd(aCpo,"FIE_CLIENT")
aAdd(aCpo,"FIE_LOJA")

For nX := 1 to Len(aAdtPC)
	For nY := 1 to Len(aCpo)
		nPos := AScan(aAdtPC[nX], { |x| Alltrim(x[1]) == aCpo[nY]} )
		If nPos == 0
			lRet := .F.
			cCpoAdt += aCpo[nY] + " "
		EndIf	
	Next
	If !lRet
		Exit
	EndIf
Next nX

If !lRet 
	Help(" ",1,"A410CPOSADT",,"CAMPOS"+" [ "+cCpoAdt+" ] "+"NAO INFORMADO NA ESTRUTURA!")
EndIf

Return lRet

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддддбдддддддбдддддддддддддддддддддддбдддддбдддддддддд©╠╠
╠╠ЁFuncao    Ё lCkAdtFR3   Ё Autor Ё Totvs                 ЁData Ё 27/10/10 Ё╠╠
╠╠цддддддддддедддддддддддддадддддддадддддддддддддддддддддддадддддадддддддддд╢╠╠
╠╠ЁDescricao Ё Verifica se houve efetivacao do relacionamento com FIE apos  Ё╠╠
╠╠Ё			 Ё emissao da nota fiscal                                       Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ Nenhum                                                       Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё0 - Nenhuma efetivacao                                        Ё╠╠
╠╠Ё          Ё1 - Foi encontrado efetivacao                                 Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/  
Static Function lCkAdtFR3( cNumPed, nAutoAdt )
Local aAreaFR3	:= {}
Local nRet 		:= 0

If AliasInDic( "FR3" )
	aAreaFR3	:= FR3->( GetArea() )
	SIX->(dbSetOrder(1))
	If SIX->(MsSeek("FR34"))
		FR3->( DbsetOrder( 4 ) )
		If FR3->( DbSeek( xFilial( "FR3" ) + "R" + cNumPed ) )
			nRet := 1
		EndIf
	EndIf
	RestArea( aAreaFR3 )
EndIf

Return nRet

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддддбдддддддбдддддддддддддддддддддддбдддддбдддддддддд©╠╠
╠╠ЁFuncao    Ё A410CtEmpBN Ё Autor Ё Emerson Rony Oliveira ЁData Ё 22/12/10 Ё╠╠
╠╠цддддддддддедддддддддддддадддддддадддддддддддддддддддддддадддддадддддддддд╢╠╠
╠╠ЁDescricao Ё Verifica qual e o metodo utilizado para envio de materiais doЁ╠╠
╠╠Ё			 Ё tipo BN para beneficiamento.                                 Ё╠╠
╠╠Ё          Ё Caso a entidade SGO exista no dicionario, sera adotado o novoЁ╠╠
╠╠Ё          Ё metodo para controle atraves de envios parciais.             Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ Nenhum                                                       Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё0 - Nenhuma forma de controle esta ativada.                   Ё╠╠
╠╠Ё          Ё1 - Esta sendo utilizado o metodo antigo de controle, ou seja,Ё╠╠
╠╠Ё          Ё    apenas um envio por SD4.                                  Ё╠╠
╠╠Ё          Ё2 - Esta sendo utilizado o novo metodo de controle, ou seja,  Ё╠╠
╠╠Ё          Ё    sera possivel realizar envios parciais.                   Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso		 Ё MATA410, MATA103                                             Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/  
Function A410CtEmpBN()
Local lEmpBN := SuperGetMV("MV_EMPBN",.F.,.F.)
Local nRet   := 0

If lEmpBN
	If AliasInDic('SGO') // metodo novo
		nRet := 2
	ElseIf SD4->(FieldPos("D4_NUMPVBN")) > 0 // metodo antigo
		nRet := 1
	EndIf
Else
	nRet := 0 // Nao controla envios para beneficiamento
EndIf

Return nRet


/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддддбдддддддбдддддддддддддддддддддддбдддддбдддддддддд©╠╠
╠╠ЁFuncao    Ё A410QtEnBN  Ё Autor Ё Emerson Rony Oliveira ЁData Ё 22/12/10 Ё╠╠
╠╠цддддддддддедддддддддддддадддддддадддддддддддддддддддддддадддддадддддддддд╢╠╠
╠╠ЁDescricao Ё Funcao que retorna a quantidade ja enviada com base nos      Ё╠╠
╠╠Ё			 Ё empenhos ja realizados na tabela SGO.                        Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ cOP      - Numero da OP                                      Ё╠╠
╠╠Ё          Ё cProduto - Codigo do produto                                 Ё╠╠
╠╠Ё          Ё cLocal   - Codigo do armazem                                 Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁnRet - Quantidade na 1a. e 2a. UM ja enviada.                 Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso		 Ё MATA410                                                      Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/  
Static Function A410QtEnBN(cOP, cProduto, cLocal)
Local aRet     := {}
Local cSeek    := ""
Local nQtdEnv  := 0
Local nQtdEnv2 := 0
Local aArea    := GetArea()

dbSelectArea('SGO')
dbSetOrder(1) // GO_FILIAL+GO_OP+GO_COD+GO_LOCAL
dbSeek(cSeek := xFilial('SD4')+cOP+cProduto+cLocal)
Do While !Eof() .And. cSeek == GO_FILIAL+GO_OP+GO_COD+GO_LOCAL
	nQtdEnv  += GO_QUANT
	nQtdEnv2 += GO_QTSEGUM
	dbSkip()
EndDo
aAdd(aRet, nQtdEnv)
aAdd(aRet, nQtdEnv2)

RestArea(aArea)
Return aRet

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠иммммммммммяммммммммммкмммммммяммммммммммммммммммммкммммммяммммммммммммм╩╠╠
╠╠╨Programa  Ёa410RatAut╨Autor  ЁMicrosiga           ╨ Data Ё  06/11/10   ╨╠╠
╠╠лммммммммммьммммммммммймммммммоммммммммммммммммммммйммммммоммммммммммммм╧╠╠
╠╠╨Desc.     Ё                                                            ╨╠╠
╠╠╨          Ё                                                            ╨╠╠
╠╠лммммммммммьмммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Uso       Ё AP                                                         ╨╠╠
╠╠хммммммммммомммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╪╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Static Function a410RatAut(aRateioPC) 

Local nPosCC	:= 0
Local nPosPerc	:= 0
Local nTam1		:= 0
Local nTam2		:= 0
Local nTotal	:= 0
Local nX		:= 0
Local nY		:= 0

Local cCCusto		:= ""
Local cTodosCCusto	:= ""
Local lNaoAchouCCusto:= .F.
Local lError100Perc	:=  .F.
Local lDuploCCusto  :=  .F.
Local lContinua		:=  .T.

Default aRateioPC	:=  {}

/*/
If (Type("l410Auto") <> 'U' .And. l410Auto)
Endif
/*/

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё No modo Automatico checa Rateio e Adiantamento                       Ё
//Ё    1 - A soma dos percentuais de rateios dos C.Custo eh igual a 100% Ё
//Ё    2 - Cada C.Custo rateado existe na tabela SCC                     Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды	

//Rateio por Centro de Custo

If len(aRateioPC) > 0
    // vetor aRatCTBPC
	// cada elemento possui um vetor de 2 elementos
	//   elemento 1 - Nro do item do pedido de compra
	//   elemento 2 - vetor contendo todos os campos de cada rateio
    //
    // 1o elemento 
    // 1o elemento, 2O elemento { '01', VETOR }
    //              (1o elemento RATEIO1, 2o elemento RATEIO 2, 3o elemento RATEIO 3) VETOR DE RATEIOS
    //              (1o elemento CAMPO1, 2o elemento CAMPO2, 30 elemento CAMPO3, N elemento CAMPO N) VETOR DE CAMPOS
	//              (1o elemento Nome Campo, 2 elemento CONTEUDO, 3 elemento .T.)  VETOR DE 3 ELEMENTOS
	//

	//Ache a posicao do vetor contendo o nome do campo = "CH_PERC" no 1o elemento [1][2][1] / Pedido de Venda
	nPosPerc  		:= AScan(aRateioPC[1][2][1], { |x| Alltrim(x[1]) =="AGG_PERC"} )
	nPosCC			:= AScan(aRateioPC[1][2][1], { |x| Alltrim(x[1]) =="AGG_CC"  } )

	lNaoAchouCCusto := .F.  // 1- Tratamento Centro de Custo nao cadastrado
	lError100Perc   := .F.  // 2- Tratamento Soma dos percentuais diferente de 100%
	lDuploCCusto    := .F.  // 3- Tratamento Centro de Custo duplicato no rateio por item do pedido (compra/venda)
	LErrorDados		:= .F.	//   - Erro identificado  

	If nPosPerc > 0 .And. nPosCC > 0

		dbSelectArea("CTT")
		dbSetOrder(1)

		nTam1		:= Len(aRatCTBPC)
		For nX := 1 To nTam1

			cTodosCCusto:= '/'

			nTotal := 0
			nTam2  := Len( aRateioPC[nX][2] )
			For nY := 1 to nTam2

				//Soma de percentuais
				nTotal  += aRateioPC[nX][2][nY][nPosPerc][2]
	            cCCusto := aRateioPC[nX][2][nY][nPosCC  ][2]

				//Verifique se existe o(s) C.Custo(s) rateado(s) 
                If cCCusto $ cTodosCCusto
					lDuploCCusto := .T.
					lErrorDados  := .T.
					Exit
				Endif
				
				If !Empty(cCCusto)
					If (lNaoAchouCCusto := !MsSeek(xFilial("CTT") + cCCusto) )
						lErrorDados := .T.
						Exit
					Endif
					cTodosCCusto += cCCusto + '/'
				Endif
			Next

			If lErrorDados
				Exit
			Else
			    If nTotal > 0 .And. nTotal <> 100
					lError100Perc := .T.
					Exit
				Endif
			Endif
		Next
	Endif
    
    //Inconsistencias - Observacao
    //Se o CCusto nao for encontrado, a soma do percental eh descontinuada
	//
	Do case
	   case nPosPerc = 0  .Or. nPosCC = 0
			Help(' ',1,'Erro na estrutura do vetor de rateio. Procura nao encontrada!')
			lContinua := .F.
	   case lNaoAchouCCusto
			Help(' ',1,'Codigo Centro Custo inexistente.')
			lContinua := .F.
	   case lDuploCCusto
			Help(' ',1,'Codigo Centro Custo duplicado no item do pedido de venda.')
			lContinua := .F.
	   case lError100Perc
			Help(' ',1,'A103TOTRAT')
			lContinua := .F.
	Endcase
Endif
Return lContinua

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠иммммммммммяммммммммммкмммммммяммммммммммммммммммммкммммммяммммммммммммм╩╠╠
╠╠╨Programa  ЁA410InGrdM╨Autor  ЁAndre Anjos		 ╨ Data Ё  21/01/11   ╨╠╠
╠╠лммммммммммьммммммммммймммммммоммммммммммммммммммммйммммммоммммммммммммм╧╠╠
╠╠╨Descricao Ё Funcao para inicialiar a grade multicampo				  ╨╠╠
╠╠лммммммммммьмммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Uso       Ё MATA410                                                    ╨╠╠
╠╠хммммммммммомммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╪╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Static Function A410InGrdM(lEdit)
Local nPQTDVEN := 0
Local nPPRCVEN := 0

Default lEdit := .F.

If "MATA410" $ SuperGetMV("MV_GRDMULT",.F.,"")
	aAdd(oGrade:aCposCtrlGrd,{"C6_PRCVEN",.F.,{},{},.T.})
	aAdd(oGrade:aCposCtrlGrd,{"C6_VALOR",.F.,{},{},.F.})
	aAdd(oGrade:aCposCtrlGrd,{"C6_VALDESC",.F.,{},{},.F.})
	aAdd(oGrade:aCposCtrlGrd,{"C6_DESCRI",.F.,{},{},.F.})
	aAdd(oGrade:aCposCtrlGrd,{"C6_PRUNIT",.F.,{},{},.F.})
	
	If lEdit
		nPQTDVEN := aScan(oGrade:aCposCtrlGrd, {|x| x[1] == "C6_QTDVEN"})
		nPPRCVEN := aScan(oGrade:aCposCtrlGrd, {|x| x[1] == "C6_PRCVEN"})
		
		If Len(oGrade:aCposCtrlGrd[nPQTDVEN]) == 3
			aAdd(oGrade:aCposCtrlGrd[nPQTDVEN],{ }) //Array de gatilhos
			aAdd(oGrade:aCposCtrlGrd[nPQTDVEN],.T.) //Flag de obrigatoriedade		
		EndIf
		
		//-- Campos a atualizar ao confirmar a tela da grade
		aAdd(oGrade:aCposCtrlGrd[nPQTDVEN,3],{"C6_DESCRI",{|| Posicione("SB1",1,xFilial("SB1")+oGrade:GetNameProd(,nLinha,nColuna),"B1_DESC")}})
		
		//-- Campos a atualizar na grade multicampo
		aAdd(oGrade:aCposCtrlGrd[nPQTDVEN,4],{"C6_PRCVEN",{|| A410GrInPr(nLinha,nColuna) }})
		aAdd(oGrade:aCposCtrlGrd[nPQTDVEN,4],{"C6_VALOR",{|| A410Arred(&(ReadVar()) * oGrade:GetFieldMC("C6_PRCVEN"),"C6_VALOR")}})
		aAdd(oGrade:aCposCtrlGrd[nPPRCVEN,4],{"C6_VALOR",{|| A410Arred(&(ReadVar()) * oGrade:GetFieldMC("C6_QTDVEN"),"C6_VALOR")}})
	EndIf
EndIf

Return

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠иммммммммммяммммммммммкмммммммяммммммммммммммммммммкммммммяммммммммммммм╩╠╠
╠╠╨Programa  ЁA410GrInPr╨Autor  ЁAndre Anjos		 ╨ Data Ё  27/01/11   ╨╠╠
╠╠лммммммммммьммммммммммймммммммоммммммммммммммммммммйммммммоммммммммммммм╧╠╠
╠╠╨Descricao Ё Funcao para inicializacao do preco unitario na grade multi-╨╠╠
╠╠╨          Ё campo                                                      ╨╠╠
╠╠лммммммммммьмммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Uso       Ё MATA410													  ╨╠╠
╠╠хммммммммммомммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╪╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Static Function A410GrInPr(nLin,nCol)
Local nRet := 0

If !Empty(&(ReadVar())) //-- Se preencheu a quantidade
	If Empty(oGrade:aColsFieldByName("C6_PRCVEN",,nLin,nCol,.T.)) //-- Se nao foi digitado preco
		nRet := A410InPrcV(oGrade:GetNameProd(,nLin,nCol),oGrade:nPosLinO,oGrade:aColsFieldByName("C6_QTDVEN",,nLin,nCol))
	Else
		nRet := oGrade:aColsFieldByName("C6_PRCVEN",,nLin,nCol,.T.)
	EndIf
EndIf

Return nRet

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠иммммммммммяммммммммммкмммммммяммммммммммммммммммммкммммммяммммммммммммм╩╠╠
╠╠╨Programa  ЁA410InPrcV╨Autor  Ё Andre Anjos		 ╨ Data Ё  03/02/10   ╨╠╠
╠╠лммммммммммьммммммммммймммммммоммммммммммммммммммммйммммммоммммммммммммм╧╠╠
╠╠╨Descricao Ё Funcao que inicializa o preco de venda quando digitada     ╨╠╠
╠╠╨          Ё quantidade na grade multicampo.                            ╨╠╠
╠╠лммммммммммьмммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Uso       Ё MATA410                                                    ╨╠╠
╠╠хммммммммммомммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╪╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Function A410InPrcV(cProduto,nLinGetD,nQtd)
Local nPreco   := 0
Local cCliente := M->C5_CLIENTE
Local cLojaCli := If(Empty(M->C5_LOJAENT),M->C5_LOJACLI,M->C5_LOJAENT)
Local nColuna  := aScan(oGrade:aHeadGrade[nLinGetD],{|x| ValType(x) # "C" .And. AllTrim(x[2]) == StrTran(ReadVar(),"M->","")})
Local nPRCVEN  := aScan(oGrade:aHeadAux,{|x| AllTrim(x[2]) == "C6_PRCVEN"})
Local nPDESCON := aScan(oGrade:aHeadAux,{|x| AllTrim(x[2]) == "C6_DESCONT"})
Local nPGRDVAL := oGrade:GetFieldGrdPos("C6_VALOR")
Local nPGRDVDE := oGrade:GetFieldGrdPos("C6_VALDESC")
Local nPGRDPRU := oGrade:GetFieldGrdPos("C6_PRUNIT")
Local lPrcDec  := SuperGetMV("MV_PRCDEC",,.F.)

If &(ReadVar()) > 0
	//-- Toma como base o preco de tabela
	nPreco := A410Tabela(cProduto,M->C5_TABELA,nLinGetD,nQtd,cCliente,cLojaCli,,,,,,.T.)
	
	//-- Caso nao tenha tabela, quando inclusao, assume preco unitario digitado pelo usuario
	If Empty(nPreco) .And. INCLUI
		nPreco := oGrade:aColsAux[nLinGetD,nPRCVEN]
	Else
		oGrade:aColsGrade[nLinGetD,n,nColuna,nPGRDPRU] := nPreco
	EndIf
	
	//-- Aplica descontos do cabecalho
	nPreco := FtDescCab(nPreco,{M->C5_DESC1,M->C5_DESC2,M->C5_DESC3,M->C5_DESC4},If(cPaisLoc=="CHI".And.lPrcDec,M->C5_MOEDA,NIL))
	
	//Aplica desconto do item
	nPreco := FtDescItem(0,@nPreco,nQtd,@oGrade:aColsGrade[nLinGetD,n,nColuna,nPGRDVAL],@oGrade:aColsAux[nLinGetD,nPDESCON],@oGrade:aColsGrade[nLinGetD,n,nColuna,nPGRDVDE],0,1,nQtd,If(cPaisLoc=="CHI" .And. lPrcDec,M->C5_MOEDA,NIL))	
Else
	oGrade:aColsGrade[nLinGetD,n,nColuna,nPGRDPRU] := 0
EndIf

Return nPreco

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠иммммммммммяммммммммммкмммммммяммммммммммммммммммммкммммммяммммммммммммм╩╠╠
╠╠╨Programa  ЁMATA410_V ╨Autor  ЁAndre Anjos         ╨ Data Ё  03/05/10   ╨╠╠
╠╠лммммммммммьммммммммммймммммммоммммммммммммммммммммйммммммоммммммммммммм╧╠╠
╠╠ЁDescri┤┘o Ё Funcao utilizada para verificar a ultima versao do fonte   Ё╠╠
╠╠Ё			 Ё MATA410.PRW aplicado no rpo do cliente, assim verificando  Ё╠╠
╠╠Ё			 Ё a necessidade de uma atualizacao neste fonte.              Ё╠╠
╠╠лммммммммммьмммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Uso       Ё FATURAMENTO                    	                          ╨╠╠
╠╠хммммммммммомммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╪╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Function MATA410_V()
Return 20110131
