#INCLUDE "TCBROWSE.CH"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "rwmake.CH"

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Programa   	FA60FIL
// Autor 		Alexandre Dalpiaz
// Data 		14/10/10
// Descricao  	Filtra títulos a receber para montagem do borderô. Pega somente os títulos da empresa corrente.
// Uso         	LaSelva
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
User Function FA60FIL()
///////////////////////

U_LS_MATRIZ()
_cFiltro := "E1_MATRIZ == '" + cFilAnt + "'"

Return(_cFiltro)

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
User Function FA060QRY()
///////////////////////

U_LS_MATRIZ()
_cFiltro := "E1_MATRIZ = '" + cFilAnt + "'"

Return(_cFiltro)

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// confirmação do bordero. verifica se a filial corrente é a matriz da empresa
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
User Function F060VBCO()
//////////////////////
_lRet := .t.
cFilAnt := right(cNumEmp,2)
If !(cFilAnt $ GetMv('LS_MATRIZ'))
	MsgBox('Esta rotina deve ser executada em uma filial centralizadora','ATENÇÃO!!!!','ALERT')
	_lRet := .f.
Else
	If !(SA6->A6_EMPRESA $ GetMv('MV_EMPGRP'))
		MsgBox('Verifique Banco/Agência/Conta' + _cEnter + 'Inválido para esta empresa','ATENÇÃO!!!','ALERT')
		_lRet := .f.
	EndIf
EndIf
Return(_lRet)

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// seleção do título para transferência: se for NF-e e ainda não está autorizada, não pode ser selecionado
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
User Function FA060TRF()
////////////////////////
Local _lRet := .t., _cChave

If alltrim(SE1->E1_ORIGEM) == 'MATA460' .and. alltrim(SE1->E1_SERIE) $ ('89')
	_cQuery := "SELECT STATUS STATUSNFE "
	_cQuery += "FROM SPED050 (NOLOCK) "
	_cQuery += "WHERE ID_ENT = dbo.FN_GETIDSPED('" + SE1->E1_MSFIL + "') "
	_cQuery += "AND NFE_ID = '" +SE1->E1_SERIE + SE1->E1_NUM + "'"
	_cQuery += "AND D_E_L_E_T_ = ''"
	DbUseArea(.T., "TOPCONN", TCGenQry(,,_cQuery), 'TRB', .F., .T.)
	If TRB->STATUSNFE <> 6
		MsgBox('NF-e ainda não aprovada pela SEFAZ. Não pode ser utilizada','ATENÇÃO!!!','ALERT')
		_lRet := .f.
	EndIf
	DbCloseArea()
EndIf
Return(_lRet)
