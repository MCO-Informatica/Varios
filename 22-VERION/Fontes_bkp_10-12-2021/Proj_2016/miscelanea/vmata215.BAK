#INCLUDE "MATA215.CH"
#INCLUDE "PROTHEUS.CH"
#DEFINE MAXPASSO 16
/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³Mata215   ³ Rev   ³ Eduardo Riera         ³ Data ³30.03.2001 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Rotina de atualizacao dos acumulados do MRP                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³Mata215(ExpL1)							                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpL1: Indica se o processo sera' Batch                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Esta rotina tem como objetivo atualizar os acumulados do MRP ³±±
±±³          ³das seguintes tabelas:                                       ³±±
±±³          ³A) SA1 -> Cadastro de Clientes - Saldo de pedidos.           ³±±
±±³          ³B) SB2 -> Saldos fisico e financeiro - Dados do MRP          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Materiais                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Descri‡…o ³ PLANO DE MELHORIA CONTINUA        ³Programa: MATA215.PRX    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ITEM PMC  ³ Responsavel              ³ Data                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³      01  ³Marcos V. Ferreira        ³ 24/01/2006                       ³±±
±±³      02  ³Marcos V. Ferreira        ³ 14/02/2006 - Verificado          ³±±
±±³      03  ³Marcos V. Ferreira        ³ 23/08/2006 - Bops 00000104684    ³±±
±±³      04  ³Ricardo Berti             ³ 19/05/2006 - Bops 00000099271    ³±±
±±³      05  ³Marcos V. Ferreira        ³ 23/08/2006 - Bops 00000104684    ³±±
±±³      06  ³                          ³                                  ³±±
±±³      07  ³Marcos V. Ferreira        ³ 24/01/2006                       ³±±
±±³      08  ³Marcos V. Ferreira        ³ 14/02/2006 - Verificado          ³±±
±±³      09  ³                          ³                                  ³±±
±±³      10  ³Ricardo Berti             ³ 19/05/2006 - Bops 00000099271    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
/*/

User Function vMATA215(lBat)       

MsgStop("Rotina Desabilitada !! Use a rotina padrão (Miscelanea -> Ajustes -> Refaz Acumulados)")

Return 



//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local cCadastro:= OemtoAnsi(STR0001)  		//"Refaz Acumulados"

DEFAULT lBat := .F.

#IFDEF TOP
	TCInternal(5,"*OFF")   // Desliga Refresh no Lock do Top
#ENDIF

If IsBlind()
	BatchProcess(cCadastro,STR0002+CRLF+STR0002,NIL,{|| vMA215Do(.T.)})
ElseIf !lBat
	BatchProcess(cCadastro,STR0002+CRLF+STR0002,NIL,{|| vMA215Do(lBat)})
Else
	vMa215Do(lBat)
EndIf         
Return 


/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³Ma215Do   ³Autor  ³Rodrigo de A. Sartorio ³ Data ³04.07.2002 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Rotina de chamada do MATA215                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³Ma215Do(ExpL1)							                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpL1: Indica se o processo sera' Batch                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Materiais                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function vMA215Do(lBat)

Local oObj 

If lBat
	Ma215Proc(lBat,oObj)
Else
	oObj := MsNewProcess():New({|lEnd| Ma215Proc(lBat,oObj)},"","",.F.)
	oObj :Activate()
EndIf	
Return


/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³Ma215Proc ³ Rev   ³ Eduardo Riera         ³ Data ³30.03.2001 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Rotina de Processamento do MATA215                           ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³Ma215Proc(ExpL1,ExpO1)					                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpL1: Indica se o processo sera' Batch                      ³±±
±±³          ³ExpO1: nome do obj da regua de processamento                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Esta rotina tem como objetivo processar os acumulados do MRP-³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Materiais                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Ma215Proc(lBat,oObj)
Local aStru     := {}
Local aSaldos   := {}
Local aTravas   := {}
Local aAreaSB8  := {}
Local aAreaSB6  := {}
Local cQuery    := ""
Local cChave    := ""
Local lQuery    := .F.
Local lContinua := .F.
Local lLiberOk	:= .T.
Local lResidOk	:= .T.
Local lFaturOk	:= .F.
Local lEIC		:=	(GetMV('MV_EASY')=="S")
Local lAchou    := .F.
Local lSC6Ok    := .F.
Local cQuebra   := ""
Local cAliasSB2 := "SB2"
Local cAliasSB6 := "SB6"
Local cAliasSB8 := "SB8"
Local cAliasSBF := "SBF"
Local cAliasSC0 := "SC0"
Local cAliasSC1 := "SC1"
Local cAliasSC2 := "SC2"
Local cAliasSC6 := "SC6"
Local cAliasSC7 := "SC7"
Local cAliasSC9 := "SC9"
Local cAliasSD4 := "SD4"
Local cAliasAFJ := "AFJ"
Local cAliasSDC := "SDC"
Local cAliasSD1 := "SD1"
Local caliasSD2 := "SD2"
Local cSavFil   := cFilAnt
Local cFirst    := ""
Local nX        := 0
Local nMax      := 0
Local nMin      := 0
Local nQtdLib2  := 0 
Local nRegSB6	:= 0

Local nEmpenho  := 0 
Local nEmpenh2  := 0
Local nBaixaEmp := 0 
Local nBaixaEm2 := 0 

Local cSeek     := ""
Local cSeekSDC  := ""
Local cCompara  := ""
Local cMensagem := ""
Local cEstoque  := ""
Local nTempoIni := 0,nTempoFim:=0,cTempo:=""
Local cLocOriSB6:= ""
Local lEmpPrev  := If(SuperGetMV("MV_QTDPREV")== "S",.T.,.F.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Abre todos os arquivos de forma exclusiva                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
/*
If !(	MA280FLock("SA1") .And.;
	 	MA280FLock("SB1") .And.;
	 	MA280FLock("SB2") .And.;
	 	MA280FLock("SB8") .And.;
		MA280FLock("SC0") .And.;
		MA280FLock("SC6") .And.;
		MA280FLock("SC7") .And.;
		MA280FLock("SC9") .And.;
		MA280FLock("SD2") .And.;
		MA280FLock("SD1") .And.;
		MA280FLock("SD4") .And.;
		MA280FLock("SDC") .And.;
		MA280FLock("SDD") .And.;
		MA280FLock("SC1") .And.;
		MA280FLock("SC2") .And.;
		MA280FLock("SB6") .And.;
		MA280FLock("SBF") .And.;
		MA280FLock("SDA") .And.;
		MA280FLock("SCQ") )
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Fecha todos os arquivos e reabre-os de forma compartilhada   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbCloseAll()
	OpenFile(SubStr(cNumEmp,1,2))
Else
	OpenIndx("SA1")
	OpenIndx("SB1")
	OpenIndx("SB2")
	OpenIndx("SB8")
	OpenIndx("SBF")
	OpenIndx("SDA")
	OpenIndx("SC0")
	OpenIndx("SC6")
	OpenIndx("SC7")
	OpenIndx("SC9")
	OpenIndx("SD1")
	OpenIndx("SD2")
	OpenIndx("SD4")
	OpenIndx("SDC")
	OpenIndx("SDD")
	OpenIndx("SC1")
	OpenIndx("SC2")
	OpenIndx("SB6")
	OpenIndx("SCQ")
EndIf
*/

OpenIndx("SA1")
OpenIndx("SB1")
OpenIndx("SB2")
OpenIndx("SB8")
OpenIndx("SBF")
OpenIndx("SDA")
OpenIndx("SC0")
OpenIndx("SC6")
OpenIndx("SC7")
OpenIndx("SC9")
OpenIndx("SD1")
OpenIndx("SD2")
OpenIndx("SD4")
OpenIndx("SDC")
OpenIndx("SDD")
OpenIndx("SC1")
OpenIndx("SC2")
OpenIndx("SB6")
OpenIndx("SCQ")

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Posiciona na Primeira Filial                                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nTempoIni:=Seconds()
dbSelectArea("SM0")
dbSetOrder(1)
If Empty(xFilial("SA1")) .Or. Empty(xFilial("SB2"))
	MsSeek(cEmpAnt)
	cFirst := SM0->M0_CODFIL
Else
	MsSeek(cEmpAnt+cFilAnt)
	cFirst := SM0->M0_CODFIL
EndIf
While !Eof() .And. cEmpAnt == SM0->M0_CODIGO
	If !lBat
		oObj:SetRegua1(MAXPASSO)
		oObj:IncRegua1(STR0007+" - "+AllTrim(SM0->M0_NOME)+"/"+SM0->M0_FILIAL) //"Atualizando acumulados"
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Altera a Filial do Sistema                                   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cFilAnt := SM0->M0_CODFIL
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se há registros pendentes no SC9                    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If FtVldJobFt()
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Zerar os dados a ser atualizado                              ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If (!Empty(xFilial("SA1")) .Or. cFilAnt == cFirst )
			dbSelectArea("SX2")
			dbSetOrder(1)
			MsSeek("SA1")
			cMensagem := AllTrim(X2Nome())
			cMensagem := Lower(cMensagem)
			cMensagem := Upper(SubStr(cMensagem,1,1))+SubStr(cMensagem,2)
			#IFDEF TOP
				If TcSrvType()<>"AS/400"
					cAliasSA1 := "SA1MA215PROC"
					
					cQuery := "SELECT MIN(R_E_C_N_O_) MINRECNO,"
					cQuery += "MAX(R_E_C_N_O_) MAXRECNO "
					cQuery += "FROM "+RetSqlName("SA1")+" "
					cQuery += "WHERE A1_FILIAL='"+xFilial("SA1")+"' AND "
					cQuery += "D_E_L_E_T_=' '"
					cQuery := ChangeQuery(cQuery)
			
					dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSA1)
					nMax := (cAliasSA1)->MAXRECNO
					nMin := (cAliasSA1)->MINRECNO
					dbCloseArea()
					dbSelectArea("SA1")
					cQuery := "UPDATE "
					cQuery += RetSqlName("SA1")+" "	
					cQuery += "SET A1_SALPED = 0, "
					cQuery += "A1_SALPEDL = 0, "
					cQuery += "A1_SALPEDB = 0 "
					cQuery += "WHERE A1_FILIAL='"+xFilial("SA1")+"' AND "
					cQuery += "D_E_L_E_T_=' ' AND "
					If !lBat
						oObj:SetRegua2(Int(nMax/4096)+1)
					EndIf	
					For nX := nMin To nMax+4096 STEP 4096
						cChave := "R_E_C_N_O_>="+Str(nX,10,0)+" AND R_E_C_N_O_<="+Str(nX+4096,10,0)+""
						TcSqlExec(cQuery+cChave)
						If !lBat
							oObj:IncRegua2(cMensagem)
						EndIf	
					Next nX
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³A tabela eh fechada para restaurar o buffer da aplicacao³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					dbSelectArea("SA1")
					dbCloseArea()
					ChkFile("SA1",.T.)
				Else
			#ENDIF
				If !lBat
					oObj:SetRegua2(SA1->(LastRec()))
				EndIf	
				dbSelectArea("SA1")
				dbSetOrder(1)
				MsSeek(xFilial("SA1"))
				While !Eof() .And. SA1->A1_FILIAL == xFilial("SA1")
					msgInfo(SA1->A1_COD+"/"+SA1->A1_LOJA)
					RecLock("SA1",.F.)
					Replace A1_SALPED  With 0
					Replace A1_SALPEDL With 0
					Replace A1_SALPEDB With 0
					MsUnLock()
					dbSelectArea("SA1")
					dbSkip()
					If !lBat
						oObj:IncRegua2(cMensagem)
					EndIf	
				EndDo
			#IFDEF TOP
				EndIf
			#ENDIF
		EndIf
		If !lBat
			oObj:IncRegua1(STR0007+" - "+AllTrim(SM0->M0_NOME)+"/"+SM0->M0_FILIAL) //"Atualizando acumulados"
		EndIf	
		If (!Empty(xFilial("SB2")) .Or. cFilAnt == cFirst )
			dbSelectArea("SX2")
			dbSetOrder(1)
			MsSeek("SB2")
			cMensagem := AllTrim(X2Nome())
			cMensagem := Lower(cMensagem)
			cMensagem := Upper(SubStr(cMensagem,1,1))+SubStr(cMensagem,2)
			#IFDEF TOP
				If TcSrvType()<>"AS/400"
					cAliasSB2 := "SB2MA215PROC"
					
					cQuery := "SELECT MIN(R_E_C_N_O_) MINRECNO,"
					cQuery += "MAX(R_E_C_N_O_) MAXRECNO "
					cQuery += "FROM "+RetSqlName("SB2")+" "
					cQuery += "WHERE B2_FILIAL='"+xFilial("SB2")+"' AND "
					cQuery += "D_E_L_E_T_=' '"
					cQuery := ChangeQuery(cQuery)
				
					dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSB2)
					nMax := (cAliasSB2)->MAXRECNO
					nMin := (cAliasSB2)->MINRECNO
					dbCloseArea()
					dbSelectArea("SB2")
					cQuery := "UPDATE "
					cQuery += RetSqlName("SB2")+" "
					cQuery += "SET B2_RESERVA = 0,"
					cQuery += "B2_RESERV2 = 0,"								
					cQuery += "B2_QEMP    = 0,"
					cQuery += "B2_QEMP2   = 0,"	
					cQuery += "B2_QEMPN   = 0,"
					cQuery += "B2_QEMPN2  = 0,"	
					cQuery += "B2_NAOCLAS = 0,"
					cQuery += "B2_SALPEDI = 0,"
					cQuery += "B2_SALPED2 = 0,"	
					cQuery += "B2_QPEDVEN = 0,"
					cQuery += "B2_QPEDVE2 = 0,"
					cQuery += "B2_QTNP    = 0,"
					cQuery += "B2_QNPT    = 0,"
					cQuery += "B2_QTER    = 0,"
					cQuery += "B2_QEMPPRE = 0,"
					cQuery += "B2_SALPPRE = 0,"
					cQuery += "B2_QACLASS = 0,"
					cQuery += "B2_QEMPPRJ = 0,"
					cQuery += "B2_QEMPPR2 = 0,"	
					cQuery += "B2_QEMPSA  = 0 "
					cQuery += "WHERE B2_FILIAL='"+xFilial("SB2")+"' AND "
					cQuery += "D_E_L_E_T_=' ' AND "
					If !lBat
						oObj:SetRegua2(Int(nMax/4096)+1)
					EndIf	
					For nX := nMin To nMax+4096 STEP 4096
						cChave := "R_E_C_N_O_>="+Str(nX,10,0)+" AND R_E_C_N_O_<="+Str(nX+4096,10,0)+""
						TcSqlExec(cQuery+cChave)
						If !lBat
							oObj:IncRegua2(cMensagem)
						EndIf	
					Next nX
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³A tabela eh fechada para restaurar o buffer da aplicacao³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					dbSelectArea("SB2")
					dbCloseArea()
					ChkFile("SB2",.T.)
				Else
			#ENDIF
				If !lBat
					oObj:SetRegua2(SB2->(LastRec()))
				EndIf	
				dbSelectArea("SB2")
				dbSetOrder(1)
				MsSeek(xFilial("SB2"),.T.)
				While !Eof() .And. SB2->B2_FILIAL == xFilial("SB2")
					RecLock("SB2",.F.)
					Replace B2_RESERVA With 0
					Replace B2_RESERV2 With 0			
					Replace B2_QEMPPRJ WITH 0
					Replace B2_QEMPPR2 WITH 0
					Replace B2_QEMP    WITH 0
					Replace B2_QEMP2   WITH 0			
					Replace B2_QEMPN   With 0
					Replace B2_QEMPN2  With 0			
					Replace B2_NAOCLAS With 0
					Replace B2_SALPEDI With 0
					Replace B2_SALPED2 With 0
					Replace B2_QPEDVEN With 0
					Replace B2_QPEDVE2 With 0			
					Replace B2_QTNP    With 0
					Replace B2_QNPT    With 0
					Replace B2_QTER    With 0
					Replace B2_QEMPPRE With 0
					Replace B2_QEPRE2  With 0
					Replace B2_SALPPRE With 0
					Replace B2_QACLASS With 0
					Replace B2_QEMPSA  With 0
					MsUnLock()
					dbSelectArea("SB2")
					dbSkip()
					If !lBat
						oObj:IncRegua2(cMensagem)
					EndIf	
				EndDo
			#IFDEF TOP
				EndIf 
			#ENDIF
		EndIf
		If !lBat
			oObj:IncRegua1(STR0007+" - "+AllTrim(SM0->M0_NOME)+"/"+SM0->M0_FILIAL) //"Atualizando acumulados"
		EndIf	
		If (!Empty(xFilial("SB8")) .Or. cFilAnt == cFirst )
			dbSelectArea("SX2")
			dbSetOrder(1)
			MsSeek("SB8")
			cMensagem := AllTrim(X2Nome())
			cMensagem := Lower(cMensagem)
			cMensagem := Upper(SubStr(cMensagem,1,1))+SubStr(cMensagem,2)
			#IFDEF TOP
				If TcSrvType()<>"AS/400"
					cAliasSB8 := "SB8MA215PROC"
					cQuery := "SELECT MIN(R_E_C_N_O_) MINRECNO,"
					cQuery += "MAX(R_E_C_N_O_) MAXRECNO "
					cQuery += "FROM "+RetSqlName("SB8")+" "
					cQuery += "WHERE B8_FILIAL='"+xFilial("SB8")+"' AND "
					cQuery += "D_E_L_E_T_=' '"
					cQuery := ChangeQuery(cQuery)
				
					dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSB8)
					nMax := (cAliasSB8)->MAXRECNO
					nMin := (cAliasSB8)->MINRECNO
					dbCloseArea()
					dbSelectArea("SB8")
					cQuery := "UPDATE "
					cQuery += RetSqlName("SB8")+" "	
					cQuery += "SET B8_EMPENHO = 0, "
					cQuery += "B8_EMPENH2 = 0, "	
					cQuery += "B8_QACLASS = 0, "
					cQuery += "B8_QACLAS2 = 0, "								
					cQuery += "B8_QEMPPRE = 0 "
					cQuery += "WHERE B8_FILIAL='"+xFilial("SB8")+"' AND "
					cQuery += "D_E_L_E_T_=' ' AND "
					If !lBat
						oObj:SetRegua2(Int(nMax/4096)+1)
					EndIf	
					For nX := nMin To nMax+4096 STEP 4096
						cChave := "R_E_C_N_O_>="+Str(nX,10,0)+" AND R_E_C_N_O_<="+Str(nX+4096,10,0)+""
						TcSqlExec(cQuery+cChave)
						If !lBat
							oObj:IncRegua2(cMensagem)
						EndIf	
					Next nX
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³A tabela eh fechada para restaurar o buffer da aplicacao³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					dbSelectArea("SB8")
					dbCloseArea()
					ChkFile("SB8",.T.)
				Else
			#ENDIF
				If !lBat
					oObj:SetRegua2(SB8->(LastRec()))
				EndIf	
			 	dbSelectArea("SB8")
				dbSetOrder(1)
				MsSeek(xFilial("SB8"),.T.)
				While !Eof() .And. SB8->B8_FILIAL == xFilial("SB8")
					RecLock("SB8",.F.)
					Replace B8_EMPENHO With 0
					Replace B8_EMPENH2 With 0		
					Replace B8_QACLASS With 0
					Replace B8_QACLAS2 With 0								
					Replace B8_QEMPPRE With 0
					Replace B8_QEPRE2  With 0
					MsUnLock()
					dbSelectArea("SB8")
					dbSkip()
					If !lBat
						oObj:IncRegua2(cMensagem)
					EndIf	
				EndDo
			#IFDEF TOP
				EndIf
			#ENDIF
		EndIf
		If !lBat
			oObj:IncRegua1(STR0007+" - "+AllTrim(SM0->M0_NOME)+"/"+SM0->M0_FILIAL) //"Atualizando acumulados"
		EndIf	
		If (!Empty(xFilial("SBF")) .Or. cFilAnt == cFirst )
			dbSelectArea("SBF")
			dbSetOrder(1)
			MsSeek("SBF")
			cMensagem := AllTrim(X2Nome())
			cMensagem := Lower(cMensagem)
			cMensagem := Upper(SubStr(cMensagem,1,1))+SubStr(cMensagem,2)
			#IFDEF TOP
				If TcSrvType()<>"AS/400"
					cAliasSBF := "SBFMA215PROC"
				
					cQuery := "SELECT MIN(R_E_C_N_O_) MINRECNO,"
					cQuery += "MAX(R_E_C_N_O_) MAXRECNO "
					cQuery += "FROM "+RetSqlName("SBF")+" "
					cQuery += "WHERE BF_FILIAL='"+xFilial("SBF")+"' AND "
					cQuery += "D_E_L_E_T_=' '"
					cQuery := ChangeQuery(cQuery)
				
					dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSBF)
					nMax := (cAliasSBF)->MAXRECNO
					nMin := (cAliasSBF)->MINRECNO
					dbCloseArea()
					dbSelectArea("SBF")
					cQuery := "UPDATE "
					cQuery += RetSqlName("SBF")+" "
					cQuery += "SET BF_EMPENHO = 0, "
					cQuery += "BF_EMPEN2 = 0, "	
					cQuery += "BF_QEMPPRE = 0, "
					cQuery += "BF_QEPRE2 = 0 "
					cQuery += "WHERE BF_FILIAL='"+xFilial("SBF")+"' AND "
					cQuery += "D_E_L_E_T_=' ' AND "
					If !lBat
						oObj:SetRegua2(Int(nMax/4096)+1)
					EndIf	
					For nX := nMin To nMax+4096 STEP 4096
						cChave := "R_E_C_N_O_>="+Str(nX,10,0)+" AND R_E_C_N_O_<="+Str(nX+4096,10,0)+""
						TcSqlExec(cQuery+cChave)
						If !lBat
							oObj:IncRegua2(cMensagem)
						EndIf	
					Next nX
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³A tabela eh fechada para restaurar o buffer da aplicacao³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					dbSelectArea("SBF")
					dbCloseArea()
					ChkFile("SBF",.T.)
				Else
			#ENDIF
				If !lBat
					oObj:SetRegua2(SBF->(LastRec()))
				EndIf	
				dbSelectArea("SBF")
				dbSetOrder(1)
				MsSeek(xFilial("SBF"))
				While !Eof() .And. SBF->BF_FILIAL == xFilial("SBF")
					RecLock("SBF",.F.)
					Replace BF_EMPENHO With 0
					Replace BF_EMPEN2  With 0		
					Replace BF_QEMPPRE With 0
					Replace BF_QEPRE2  With 0
					MsUnLock()
					dbSelectArea("SBF")
					dbSkip()
					If !lBat
						oObj:IncRegua2(cMensagem)
					EndIf	
				EndDo
			#IFDEF TOP
				EndIf
			#ENDIF
		EndIf
		If !lBat
			oObj:IncRegua1(STR0007+" - "+AllTrim(SM0->M0_NOME)+"/"+SM0->M0_FILIAL) //"Atualizando acumulados"
		EndIf	
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Atualiza os dados acumulados da Solicitacao de Compra         ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If (!Empty(xFilial("SC1")) .Or. cFilAnt == cFirst )	
			dbSelectArea("SX2")
			dbSetOrder(1)
			MsSeek("SC1")
			cMensagem := AllTrim(X2Nome())
			cMensagem := Lower(cMensagem)
			cMensagem := Upper(SubStr(cMensagem,1,1))+SubStr(cMensagem,2)
			dbSelectArea("SC1")
			dbSetOrder(1)
			If !lBat
				oObj:SetRegua2(SC1->(LastRec()))
				oObj:IncRegua2(cMensagem)
			EndIf	
			#IFDEF TOP
				aStru     := SC1->(dbStruct())
				lQuery    := .T.
				cAliasSC1 := "SC1MA215PROC"
				
				cQuery := "SELECT SC1.*,SB1.B1_FILIAL "
				cQuery += "FROM "+RetSqlName("SC1")+" SC1,"
				cQuery += RetSqlName("SB1")+" SB1 "
				cQuery += "WHERE SC1.C1_FILIAL='"+xFilial("SC1")+"' AND "
				cQuery += "SC1.C1_QUJE < C1_QUANT AND "
				cQuery += "SC1.D_E_L_E_T_=' ' AND "
				cQuery += "SB1.B1_FILIAL='"+xFilial("SB1")+"' AND "
				cQuery += "SB1.B1_COD=SC1.C1_PRODUTO AND "
				cQuery += "SB1.D_E_L_E_T_=' ' "
				cQuery += "ORDER BY SC1.C1_FILIAL,SC1.C1_PRODUTO,SC1.C1_LOCAL"
			
				cQuery := ChangeQuery(cQuery)
				
				dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSC1,.T.,.T.)
				For nX := 1 To Len(aStru)
					If ( aStru[nX][2] <> "C" .And. aStru[nX][2] <> "M" ) 
						TcSetField(cAliasSC1,aStru[nX][1],aStru[nX][2],aStru[nX][3],aStru[nX][4])
					EndIf		
				Next nX	
			#ELSE
				cQuery := "C1_FILIAL=='"+xFilial("SC1")+"' .And. "
				cQuery += "C1_QUJE < C1_QUANT "
				Set Filter To &cQuery 
				MsSeek(xFilial("SC1"))
			#ENDIF
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Atualiza os dados acumulados da Solicitacao de compra         ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			While !Eof() .And. (cAliasSC1)->C1_FILIAL == xFilial("SC1")
				lContinua := .T.
				If !lQuery
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³Verifica a existencia de Cotacoes e filial do produto³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If (cAliasSC1)->C1_QUJE >= (cAliasSC1)->C1_QUANT .Or. !vA215FilOk((cAliasSC1)->C1_PRODUTO)
						lContinua := .F.
					EndIf
				Else
					lContinua := .T.
				EndIf
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Ponto de Entrada para Tratamentos Especiais         ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ		
				If ExistBlock("M215SC")
					If lQuery
						dbSelectArea("SC1")
						dbGoto((cAliasSC1)->SC1RECNO)
					EndIf
					If ExecBlock("M215SC",.f.,.f.)
						lContinua := .F.
					EndIf
				EndIf
				If lContinua
					MaAvalSC(cAliasSC1,1,,,.T.)
				EndIf
				va215Skip(cAliasSC1)
				If !lBat
					oObj:IncRegua2(cMensagem)
				EndIf	
			EndDo
			If lQuery
				dbSelectArea(cAliasSC1)
				dbCloseArea()
				dbSelectArea("SC1")
			EndIf
		EndIf
		If !lBat
			oObj:IncRegua1(STR0007+" - "+AllTrim(SM0->M0_NOME)+"/"+SM0->M0_FILIAL) //"Atualizando acumulados"
		EndIf	
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Atualiza os dados acumulados do Pedido de Compra              ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If (!Empty(xFilial("SC7")) .Or. cFilAnt == cFirst )
			dbSelectArea("SX2")
			dbSetOrder(1)
			MsSeek("SC7")
			cMensagem := AllTrim(X2Nome())
			cMensagem := Lower(cMensagem)
			cMensagem := Upper(SubStr(cMensagem,1,1))+SubStr(cMensagem,2)
			
			dbSelectArea("SC7")
			dbSetOrder(1)
			#IFDEF TOP
				aStru     := SC7->(dbStruct())
				lQuery    := .T.
				cAliasSC7 := "SC7MA215PROC"
				
				cQuery := "SELECT C7_FILIAL,C7_PRODUTO,C7_LOCAL,C7_QUJE,C7_QUANT,C7_RESIDUO,C7_FILENT,C7_TPOP,C7_QTSEGUM,C7_FORNECE,C7_LOJA,C7_NUM,C7_ITEM,C7_OP,C7_SEQMRP,C7_DATPRF,C7_TIPO,C7_NUMCOT,SB1.B1_FILIAL "
				cQuery += "FROM "+RetSqlName("SC7")+" SC7,"
				cQuery += RetSqlName("SB1")+" SB1 "
				cQuery += "WHERE SC7.C7_FILIAL='"+xFilial("SC7")+"' AND "
				cQuery += "SC7.C7_FILENT IN('  ','"+xFilial("SC7")+"') AND "
				cQuery += "SC7.C7_QUJE < C7_QUANT AND "
				cQuery += "SC7.C7_RESIDUO='"+Space(Len(SC7->C7_RESIDUO))+"' AND "
				cQuery += "SC7.D_E_L_E_T_=' ' AND "
				cQuery += "SB1.B1_FILIAL='"+xFilial("SB1")+"' AND "
				cQuery += "SB1.B1_COD=SC7.C7_PRODUTO AND "
				cQuery += "SB1.D_E_L_E_T_=' ' "
				cQuery += "UNION ALL "
				cQuery += "SELECT C7_FILIAL,C7_PRODUTO,C7_LOCAL,C7_QUJE,C7_QUANT,C7_RESIDUO,C7_FILENT,C7_TPOP,C7_QTSEGUM,C7_FORNECE,C7_LOJA,C7_NUM,C7_ITEM,C7_OP,C7_SEQMRP,C7_DATPRF,C7_TIPO,C7_NUMCOT,SB1.B1_FILIAL "
				cQuery += "FROM "+RetSqlName("SC7")+" SC7,"
				cQuery += RetSqlName("SB1")+" SB1 "
				cQuery += "WHERE SC7.C7_FILENT='"+xFilial("SC7")+"' AND "
				cQuery += "SC7.C7_FILIAL<>'"+xFilial("SC7")+"' AND "			
				cQuery += "SC7.C7_QUJE < C7_QUANT AND "
				cQuery += "SC7.C7_RESIDUO='"+Space(Len(SC7->C7_RESIDUO))+"' AND "
				cQuery += "SC7.D_E_L_E_T_=' ' AND "
				cQuery += "SB1.B1_FILIAL='"+xFilial("SB1")+"' AND "
				cQuery += "SB1.B1_COD=SC7.C7_PRODUTO AND "
				cQuery += "SB1.D_E_L_E_T_=' ' "
				cQuery += "ORDER BY 1,2,3 "
			
				cQuery := ChangeQuery(cQuery)
				
				dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSC7,.T.,.T.)
				For nX := 1 To Len(aStru)
					If ( aStru[nX][2] <> "C" .And. FieldPos(aStru[nX][1])<>0 )
						TcSetField(cAliasSC7,aStru[nX][1],aStru[nX][2],aStru[nX][3],aStru[nX][4])
					EndIf
				Next nX
			#ELSE
				cQuery := "((C7_FILIAL=='"+xFilial("SC7")+"' .And. C7_FILENT$'  #"+xFilial("SC7")+"') .Or. (C7_FILIAL<>'"+xFilial("SC7")+"' .And. C7_FILENT=='"+xFilial("SC7")+"')) .And. "
				cQuery += "C7_QUJE < C7_QUANT .And. "
				cQuery += "C7_RESIDUO='"+Space(Len(SC7->C7_RESIDUO))+"'"
				Set Filter To &cQuery	
				MsSeek(xFilial("SC7"))
			#ENDIF
			If !lBat
				oObj:SetRegua2(SC7->(LastRec()))
				oObj:IncRegua2(cMensagem)
			EndIf	
			While !Eof()
				lContinua := .T.
				If !lQuery
					If ( (cAliasSC7)->C7_QUJE >= C7_QUANT ) .Or.;
							!Empty((cAliasSC7)->C7_RESIDUO) .Or.;
							!vA215FilOk((cAliasSC7)->C7_PRODUTO)
						lContinua := .F.
					EndIf
				EndIf
				If lContinua
					MaAvalPC(cAliasSC7,1,,.T.)
				EndIf
				va215Skip(cAliasSC7)
				If !lBat
					oObj:IncRegua2(cMensagem)
				EndIf	
			EndDo
			If lQuery
				dbSelectArea(cAliasSC7)
				dbCloseArea()
				dbSelectArea("SC7")
			EndIf
		EndIf
		If !lBat
			oObj:IncRegua1(STR0007+" - "+AllTrim(SM0->M0_NOME)+"/"+SM0->M0_FILIAL) //"Atualizando acumulados"
		EndIf	
		If (!Empty(xFilial("SC2")) .Or. cFilAnt == cFirst )
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Atualiza os dados acumulados das Ordens de Producao           ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			dbSelectArea("SX2")
			dbSetOrder(1)
			MsSeek("SC2")
			cMensagem := AllTrim(X2Nome())
			cMensagem := Lower(cMensagem)
			cMensagem := Upper(SubStr(cMensagem,1,1))+SubStr(cMensagem,2)
			
			dbSelectArea("SC2")
			dbSetOrder(1)
			#IFDEF TOP
				aStru     := SC2->(dbStruct())
				lQuery    := .T.
				cAliasSC2 := "SC2MA215PROC"
				
				cQuery := "SELECT SC2.C2_FILIAL,SC2.C2_DATRF,SC2.C2_QUANT,SC2.C2_QUJE,SC2.C2_PERDA,SC2.C2_PRODUTO,SC2.C2_LOCAL,SC2.C2_TPOP,SB1.B1_FILIAL "
				cQuery += "FROM "+RetSqlName("SC2")+" SC2,"
				cQuery += RetSqlName("SB1")+" SB1 "
				cQuery += "WHERE SC2.C2_FILIAL='"+xFilial("SC2")+"' AND "
				cQuery += "((SC2.C2_DATRF='"+Dtos(Ctod(""))+"' AND "
				cQuery += "SC2.C2_QUANT-SC2.C2_QUJE-SC2.C2_PERDA>0) OR "
				cQuery += "(SC2.C2_DATRF<>'"+Dtos(Ctod(""))+"')) AND "
				cQuery += "SC2.D_E_L_E_T_=' ' AND "
				cQuery += "SB1.B1_FILIAL='"+xFilial("SB1")+"' AND "
				cQuery += "SB1.B1_COD=SC2.C2_PRODUTO AND "
				cQuery += "SB1.D_E_L_E_T_=' ' "
				cQuery += "ORDER BY C2_FILIAL,C2_PRODUTO,C2_LOCAL "
			
				cQuery := ChangeQuery(cQuery)
				
				dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSC2,.T.,.T.)
				For nX := 1 To Len(aStru)
					If ( aStru[nX][2] <> "C" .And. FieldPos(aStru[nX][1])<>0 )
						TcSetField(cAliasSC2,aStru[nX][1],aStru[nX][2],aStru[nX][3],aStru[nX][4])
					EndIf
				Next nX
			#ELSE
				lQuery := .F.
				MsSeek(xFilial("SC2"))
			#ENDIF
			If !lBat
				oObj:SetRegua2(SC2->(LastRec()))
				oObj:IncRegua2(cMensagem)
			EndIf	
			While ( !Eof() .And. (cAliasSC2)->C2_FILIAL == xFilial("SC2") )
				lContinua := .T.
				If !lQuery 
					If aSC2Sld(cAliasSC2) <= 0 .Or. !vA215FilOk(SC2->C2_PRODUTO)
						lContinua := .F.
					EndIf
				EndIf
				If lContinua
					dbSelectArea("SB2")
					dbSetOrder(1)
					If !MsSeek(xFilial("SB2")+(cAliasSC2)->C2_PRODUTO+(cAliasSC2)->C2_LOCAL)
						CriaSB2((cAliasSC2)->C2_PRODUTO,(cAliasSC2)->C2_LOCAL)
					EndIf
					GravaB2Pre("+",IF(Empty((cAliasSC2)->C2_DATRF),aSC2Sld(cAliasSC2),0),(cAliasSC2)->C2_TPOP)
				EndIf
				va215Skip(cAliasSC2)
				If !lBat
					oObj:IncRegua2(cMensagem)
				EndIf	
			EndDo
			If lQuery
				dbSelectArea(cAliasSC2)
				dbCloseArea()
				dbSelectArea("SC2")
			EndIf
		EndIf
		If !lBat
			oObj:IncRegua1(STR0007+" - "+AllTrim(SM0->M0_NOME)+"/"+SM0->M0_FILIAL) //"Atualizando acumulados"
		EndIf	
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Atualiza os dados acumulados dos empenhos da OP               ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If (!Empty(xFilial("SD4")) .Or. cFilAnt == cFirst )
			dbSelectArea("SX2")
			dbSetOrder(1)
			MsSeek("SD4")
			cMensagem := AllTrim(X2Nome())
			cMensagem := Lower(cMensagem)
			cMensagem := Upper(SubStr(cMensagem,1,1))+SubStr(cMensagem,2)
			
			dbSelectArea("SD4")
			dbSetOrder(1)
			#IFDEF TOP
				aStru     := SD4->(dbStruct())
				lQuery    := .T.
				cAliasSD4 := "SD4MA215PROC"
				
				cQuery := "SELECT SD4.*,SB1.B1_FILIAL "
				cQuery += "FROM "+RetSqlName("SD4")+" SD4,"
				cQuery += RetSqlName("SB1")+" SB1 "
				cQuery += "WHERE SD4.D4_FILIAL='"+xFilial("SD4")+"' AND "
				cQuery += "SD4.D4_QUANT > 0 AND "
				cQuery += "SD4.D_E_L_E_T_=' ' AND "
				cQuery += "SB1.B1_FILIAL='"+xFilial("SB1")+"' AND "
				cQuery += "SB1.B1_COD=SD4.D4_COD AND "
				cQuery += "SB1.D_E_L_E_T_=' ' "
				cQuery += "ORDER BY D4_FILIAL,D4_COD,D4_LOCAL"
			
				cQuery := ChangeQuery(cQuery)
				
				dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSD4,.T.,.T.)
				For nX := 1 To Len(aStru)
					If ( aStru[nX][2] <> "C" .And. aStru[nX][2] <> "M" )
						TcSetField(cAliasSD4,aStru[nX][1],aStru[nX][2],aStru[nX][3],aStru[nX][4])
					EndIf
				Next nX
			#ELSE
				MsSeek(xFilial("SD4"))
			#ENDIF
			If !lBat
				oObj:SetRegua2(SD4->(LastRec()))
				oObj:IncRegua2(cMensagem)
			EndIf	
			While !Eof() .And. (cAliasSD4)->D4_FILIAL == xFilial("SD4")
				lContinua := .T.
				If !lQuery
					If !vA215FilOk((cAliasSD4)->D4_COD) .Or. (cAliasSD4)->D4_QUANT <= 0
						lContinua := .F.
					EndIf
				EndIf
				If lContinua
			 		If Localiza((cAliasSD4)->D4_COD)
						#IFDEF TOP
							lQuery := .T.
							aStru     := SDC->(dbStruct())
							lQuery    := .T.
							cAliasSDC := "SDCMA215PROC1"
				
							cQuery := "SELECT * "
							cQuery += "FROM "+RetSqlName("SDC")+" SDC "
							cQuery += "WHERE SDC.DC_FILIAL='"+xFilial("SDC")+"' AND "
							cQuery += "SDC.DC_PRODUTO='"+(cAliasSD4)->D4_COD+"' AND "
							cQuery += "SDC.DC_LOCAL='"+(cAliasSD4)->D4_LOCAL+"' AND "
							cQuery += "SDC.DC_OP='"+(cAliasSD4)->D4_OP+"' AND "
							cQuery += "SDC.DC_TRT='"+(cAliasSD4)->D4_TRT+"' AND "
							cQuery += "SDC.DC_LOTECTL='"+(cAliasSD4)->D4_LOTECTL+"' AND "
							cQuery += "SDC.DC_NUMLOTE='"+(cAliasSD4)->D4_NUMLOTE+"' AND "
							cQuery += "SDC.D_E_L_E_T_=' ' "
							cQuery += "ORDER BY "+SqlOrder(SDC->(IndexKey()))
			
							cQuery := ChangeQuery(cQuery)
				
							dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSDC,.T.,.T.)
							For nX := 1 To Len(aStru)
								If ( aStru[nX][2] <> "C" .And. aStru[nX][2] <> "M" )
									TcSetField(cAliasSDC,aStru[nX][1],aStru[nX][2],aStru[nX][3],aStru[nX][4])
								EndIf
							Next nX
						#ELSE
							dbSelectArea("SDC")
							dbSetOrder(2)
							MsSeek(xFilial("SDC")+(cAliasSD4)->D4_COD+(cAliasSD4)->D4_LOCAL+(cAliasSD4)->D4_OP+(cAliasSD4)->D4_TRT+(cAliasSD4)->D4_LOTECTL+(cAliasSD4)->D4_NUMLOTE)
						#ENDIF
						lAchou := .F.
						While !Eof() .And. (cAliasSDC)->DC_FILIAL == xFilial("SDC") .And.;
											(cAliasSDC)->DC_PRODUTO == (cAliasSD4)->D4_COD .And.;
											(cAliasSDC)->DC_LOCAL == (cAliasSD4)->D4_LOCAL .And.;
											(cAliasSDC)->DC_OP == (cAliasSD4)->D4_OP .And.;
											(cAliasSDC)->DC_TRT == (cAliasSD4)->D4_TRT .And.;
											(cAliasSDC)->DC_LOTECTL == (cAliasSD4)->D4_LOTECTL .And.;
											(cAliasSDC)->DC_NUMLOTE == (cAliasSD4)->D4_NUMLOTE
							//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
							//³ Atualiza arquivo de empenhos               ³
							//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
							lAchou := .T.
							GravaEmp((cAliasSDC)->DC_PRODUTO,;
								(cAliasSDC)->DC_LOCAL,;
								(cAliasSDC)->DC_QUANT,;
								(cAliasSDC)->DC_QTSEGUM,;
								(cAliasSDC)->DC_LOTECTL,;
								(cAliasSDC)->DC_NUMLOTE,;
								(cAliasSDC)->DC_LOCALIZ,;
								(cAliasSDC)->DC_NUMSERIE,;
								(cAliasSDC)->DC_OP,;
								(cAliasSDC)->DC_TRT,;
								NIL,;
								NIL,;
								"SC2",;
								NIL,;
								NIL,;
								NIL,;
								.F.,;
								.F.,;
								.T.,;
								.F.,;
								NIL,;
								.T.,;
								.F.)
							va215Skip(cAliasSDC)
						EndDo
						If lQuery
							dbSelectArea(cAliasSDC)
							dbCloseArea()
							dbSelectArea("SDC")
						EndIf
						If !lAchou
							//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
							//³ Atualiza arquivo de empenhos               ³
							//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
							GravaEmp((cAliasSD4)->D4_COD,;
								(cAliasSD4)->D4_LOCAL,;
								(cAliasSD4)->D4_QUANT,;
								(cAliasSD4)->D4_QTSEGUM,;
								(cAliasSD4)->D4_LOTECTL,;
								(cAliasSD4)->D4_NUMLOTE,;
								NIL,;
								NIL,;
								(cAliasSD4)->D4_OP,;
								(cAliasSD4)->D4_TRT,;
								NIL,;
								NIL,;
								"SC2",;
								(cAliasSD4)->D4_OPORIG,;
								(cAliasSD4)->D4_DATA,;
								@aTravas,;
								.F.,;
								.F.,;
								.T.,;
								.F.,;
								NIL,;
								.T.,;
								.F.)
						EndIf
					Else
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Atualiza arquivo de empenhos               ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						GravaEmp((cAliasSD4)->D4_COD,;
							(cAliasSD4)->D4_LOCAL,;
							(cAliasSD4)->D4_QUANT,;
							(cAliasSD4)->D4_QTSEGUM,;
							(cAliasSD4)->D4_LOTECTL,;
							(cAliasSD4)->D4_NUMLOTE,;
							NIL,;
							NIL,;
							(cAliasSD4)->D4_OP,;
							(cAliasSD4)->D4_TRT,;
							NIL,;
							NIL,;
							"SC2",;
							(cAliasSD4)->D4_OPORIG,;
							(cAliasSD4)->D4_DATA,;
							NIL,;
							.F.,;
							.F.,;
							.T.,;
							.F.,;
							NIL,;
							.T.,;
							.F.)
					EndIf
				EndIf
				va215Skip(cAliasSD4)
				If !lBat
					oObj:IncRegua2(cMensagem)
				EndIf	
			EndDo
			If lQuery
				dbSelectArea(cAliasSD4)
				dbCloseArea()
				dbSelectArea("SD4")
			EndIf
		EndIf
		If !lBat
			oObj:IncRegua1(STR0007+" - "+AllTrim(SM0->M0_NOME)+"/"+SM0->M0_FILIAL) //"Atualizando acumulados"
		EndIf	
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Varre o SDD e refaz os bloqueios existentes                ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If (!Empty(xFilial("SDD")) .Or. cFilAnt == cFirst )
			dbSelectArea("SX2")
			dbSetOrder(1)
			MsSeek("SDD")
			cMensagem := AllTrim(X2Nome())
			cMensagem := Lower(cMensagem)
			cMensagem := Upper(SubStr(cMensagem,1,1))+SubStr(cMensagem,2)
			dbSelectArea("SDD")
			dbSetOrder(1)
			MsSeek(xFilial())
			If !lBat
				oObj:SetRegua2(SDD->(LastRec()))
				oObj:IncRegua2(cMensagem)
			EndIf	
			While !Eof() .And. DD_FILIAL == xFilial()
				If DD_SALDO > 0 .And. vA215FilOk(SDD->DD_PRODUTO)
					Reclock("SDD",.F.)
					Replace DD_QUANT With DD_SALDO
					MsUnlock()
					cSeekSDC := xFilial("SDC")+SDD->DD_PRODUTO+SDD->DD_LOCAL+"SDD"+;
						SDD->DD_DOC+Criavar("DC_ITEM")+Criavar("DC_SEQ")+;
						SDD->DD_LOTECTL+SDD->DD_NUMLOTE+If(!Empty(SDD->DD_LOCALIZ),SDD->DD_LOCALIZ,"")+;
						If(!Empty(SDD->DD_NUMSERI),SDD->DD_NUMSERI,"")
					SDC->(dbSetOrder(1))
					aTravas  := {}
					If Localiza(SDD->DD_PRODUTO) .And. SDC->(MsSeek(cSeekSDC))
						dbSelectArea("SDC")
						While !Eof() .And. DC_FILIAL+DC_PRODUTO+DC_LOCAL+DC_ORIGEM+;
								DC_PEDIDO+DC_ITEM+DC_SEQ+DC_LOTECTL+DC_NUMLOTE+;
								If(!Empty(SDD->DD_LOCALIZ),SDC->DC_LOCALIZ,"")+If(!Empty(SDD->DD_NUMSERI),SDC->DC_NUMSERI,"") == cSeekSDC
							//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
							//³ Atualiza arquivo de empenhos               ³
							//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
							GravaEmp(SDC->DC_PRODUTO,;
								SDC->DC_LOCAL,;
								SDC->DC_QUANT,;
								SDC->DC_QTSEGUM,;
								SDC->DC_LOTECTL,;
								SDC->DC_NUMLOTE,;
								SDC->DC_LOCALIZ,;
								SDC->DC_NUMSERI,;
								Nil,;
								Nil,;
								SDC->DC_PEDIDO,;
								Nil,;
								"SDD",;
								Nil,;
								Nil,;
								Nil,;
								.F.,;
								.F.,;
								.T.,;
								.F.,;
								.T.,;
								.T.,;
								.F.)
							va215Skip('SDC')
						EndDo
					Else
						ProcSDD(.F.)
					EndIf
				EndIf		
				va215Skip('SDD')
				If !lBat
					oObj:IncRegua2(cMensagem)
				EndIf	
			EndDo
		EndIf
		If !lBat
			oObj:IncRegua1(STR0007+" - "+AllTrim(SM0->M0_NOME)+"/"+SM0->M0_FILIAL) //"Atualizando acumulados"
		EndIf	
		If (!Empty(xFilial("SCQ")) .Or. cFilAnt == cFirst )
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Corre SCQ para Atualizar B2_QEMP  em Produtos SB2.           ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			dbSelectArea("SX2")
			dbSetOrder(1)
			MsSeek("SCQ")
			cMensagem := AllTrim(X2Nome())
			cMensagem := Lower(cMensagem)
			cMensagem := Upper(SubStr(cMensagem,1,1))+SubStr(cMensagem,2)
			dbSelectArea("SCQ")
			#IFDEF TOP
				cFilSCQ := 'CQ_QTDISP > 0'
				MsFilter(cFIlSCQ)
			#ENDIF
			dbSetOrder(1)
			MsSeek(xFilial())
			If !lBat
				oObj:SetRegua2(SCQ->(LastRec()))
				oObj:IncRegua2(cMensagem)
			EndIf	
			While !Eof() .And. CQ_FILIAL == xFilial()
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Atualizar Qtde Empenhada  em Produtos SB2.          ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If SCQ->CQ_QTDISP > 0 .And. Empty(SCQ->CQ_NUMREQ) .And. vA215FilOk(SCQ->CQ_PRODUTO)
					dbSelectArea("SB2")
					cSeek := xFilial()+SCQ->CQ_PRODUTO+SCQ->CQ_LOCAL
					MsSeek(cSeek)
					If Eof()
						CriaSB2(SCQ->CQ_PRODUTO,SCQ->CQ_LOCAL)
					Else
						RecLock("SB2",.F.)
					EndIf
					Replace B2_QEMPSA     With B2_QEMPSA + (SCQ->CQ_QTDISP)
					MsUnLock()
				EndIf
				va215Skip('SCQ')
				If !lBat
					oObj:IncRegua2(cMensagem)
				EndIf	
			EndDo
			RETINDEX("SCQ")
			dbClearFilter()
		EndIf
		If !lBat
			oObj:IncRegua1(STR0007+" - "+AllTrim(SM0->M0_NOME)+"/"+SM0->M0_FILIAL) //"Atualizando acumulados"
		EndIf	
		If (!Empty(xFilial("SDA")) .Or. cFilAnt == cFirst )
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ A partir do SDA atualiza B2_QACLASS e B8_QACLASS             ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			dbSelectArea("SX2")
			dbSetOrder(1)
			MsSeek("SDA")
			cMensagem := AllTrim(X2Nome())
			cMensagem := Lower(cMensagem)
			cMensagem := Upper(SubStr(cMensagem,1,1))+SubStr(cMensagem,2)
			dbSelectArea("SDA")
			#IFDEF TOP
				cFilSDA := 'DA_SALDO > 0'
				MsFilter(cFilSDA)
			#ENDIF
			dbSetOrder(1)
			MsSeek(xFilial())
			If !lBat
				oObj:SetRegua2(SDA->(LastRec()))
				oObj:IncRegua2(cMensagem)
			EndIf	
			While !Eof() .And. DA_FILIAL == xFilial()
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Atualizar Qtde Prevista de Produtos SB2.            ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If DA_SALDO > 0 .And. vA215FilOk(SDA->DA_PRODUTO)
					dbSelectArea("SB2")
					cSeek := xFilial()+SDA->DA_PRODUTO+SDA->DA_LOCAL
					MsSeek(cSeek)
					If Eof()
						CriaSB2(SDA->DA_PRODUTO,SDA->DA_LOCAL)
					EndIf
					Reclock("SB2",.F.)
					Replace B2_QACLASS With B2_QACLASS + SDA->DA_SALDO
					MsUnlock()
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Caso use rastreabilidade, GRAVA a quantidade do SDA   ³
					//³ no SB8 para que o Saldo por Sub-Lote fique bloqueado  ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If Rastro(SDA->DA_PRODUTO)
						dbSelectArea("SB8")
						aAreaSB8:=GetArea()
						dbSetOrder(3)
						If Rastro(SDA->DA_PRODUTO,"S")
							cSeek:=xFilial()+SDA->DA_PRODUTO+SDA->DA_LOCAL+SDA->DA_LOTECTL+SDA->DA_NUMLOTE
							cCompara:="B8_FILIAL+B8_PRODUTO+B8_LOCAL+B8_LOTECTL+B8_NUMLOTE"
						Else
							cSeek:=xFilial()+SDA->DA_PRODUTO+SDA->DA_LOCAL+SDA->DA_LOTECTL
							cCompara:="B8_FILIAL+B8_PRODUTO+B8_LOCAL+B8_LOTECTL"
						EndIf
						MsSeek(cSeek)
						nEmpenho:=SDA->DA_SALDO
						nEmpenh2:=SDA->DA_QTSEGUM  
						Do While !Eof() .And. nEmpenho > 0 .And. cSeek == &(cCompara)
							nBaixaEmp:=Min(nEmpenho,SB8Saldo(Nil,.T.,Nil,Nil,Nil,lEmpPrev))
							nBaixaEm2:=Min(nEmpenh2,SB8Saldo(Nil,.T.,Nil,.T.,Nil,lEmpPrev))
							RecLock("SB8",.F.)
							Replace B8_QACLASS With B8_QACLASS + nBaixaEmp
							Replace B8_QACLAS2 With B8_QACLAS2 + nBaixaEm2						
							nEmpenho -= nBaixaEmp   
							nEmpenh2 -= nBaixaEm2 
							MsUnlock()
							va215Skip()
						EndDo
						RestArea(aAreaSB8)
					EndIf
				EndIf
				va215Skip('SDA')
			 	If !lBat
			 		oObj:IncRegua2(cMensagem)
			 	EndIf	
			EndDo
			RETINDEX("SDA")
			dbClearFilter()
		EndIf
		If !lBat
			oObj:IncRegua1(STR0007+" - "+AllTrim(SM0->M0_NOME)+"/"+SM0->M0_FILIAL) //"Atualizando acumulados"
		EndIf	
		If (!Empty(xFilial("SD1")) .Or. cFilAnt == cFirst )
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Atualiza os dados acumulados da Pre-Nota de Entrada           ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			dbSelectArea("SX2")
			dbSetOrder(1)
			MsSeek("SD1")
			cMensagem := AllTrim(X2Nome())
			cMensagem := Lower(cMensagem)
			cMensagem := Upper(SubStr(cMensagem,1,1))+SubStr(cMensagem,2)
			dbSelectArea("SD1")
			dbSetOrder(1)
			#IFDEF TOP
			If TcSrvType()<>"AS/400"
				cAliasSD1 := "SD1MA215PROC"

				cQuery := "SELECT MIN(R_E_C_N_O_) MINRECNO,"
				cQuery += "MAX(R_E_C_N_O_) MAXRECNO "
				cQuery += "FROM "+RetSqlName("SD1")+" SD1 "
				cQuery += "WHERE SD1.D1_FILIAL='"+xFilial("SD1")+"' AND "
				cQuery += "SD1.D1_IDENTB6<>'"+Space(Len(SD1->D1_IDENTB6))+"' AND "
				cQuery += "( SD1.D1_QTDEDEV > 0 OR SD1.D1_VALDEV > 0 ) AND "
				cQuery += "SD1.D_E_L_E_T_=' ' "
				cQuery := ChangeQuery(cQuery)
			
				dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSD1)
				nMax := (cAliasSD1)->MAXRECNO
				nMin := (cAliasSD1)->MINRECNO
				dbCloseArea()
				dbSelectArea("SD1")
				cQuery := "UPDATE "
				cQuery += RetSqlName("SD1")+" "
				cQuery += "SET D1_QTDEDEV = 0, D1_VALDEV = 0 "
				cQuery += "WHERE D1_FILIAL='"+xFilial("SD1")+"' AND "
				cQuery += "D1_IDENTB6<>'"+Space(Len(SD1->D1_IDENTB6))+"' AND "
				cQuery += "( D1_QTDEDEV > 0 OR D1_VALDEV > 0 ) AND "
				cQuery += "D_E_L_E_T_=' ' AND "
				For nX := nMin To nMax+4096 STEP 4096
					cChave := "R_E_C_N_O_>="+Str(nX,10,0)+" AND R_E_C_N_O_<="+Str(nX+4096,10,0)+""
					TcSqlExec(cQuery+cChave)
				Next nX
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³A tabela eh fechada para restaurar o buffer da aplicacao³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				dbSelectArea("SD1")
				dbCloseArea()
				ChkFile("SD1",.T.)

				aStru     := SD1->(dbStruct())
				lQuery    := .T.
				cAliasSD1 := "SD1MA215PROC"
				
				cQuery := "SELECT SD1.*,SB1.B1_FILIAL "
				cQuery += "FROM "+RetSqlName("SD1")+" SD1,"
				cQuery += RetSqlName("SB1")+" SB1 "
				cQuery += "WHERE SD1.D1_FILIAL='"+xFilial("SD1")+"' AND "
				cQuery += "SD1.D1_TES = '"+Space(Len(SD1->D1_TES))+"' AND "
				#IFDEF SHELL
					cQuery += "SD1.D1_CANCEL = 'S' AND "
				#ENDIF
				cQuery += "SD1.D_E_L_E_T_=' ' AND "
				cQuery += "SB1.B1_FILIAL='"+xFilial("SB1")+"' AND "
				cQuery += "SB1.B1_COD=SD1.D1_COD AND "
				cQuery += "SB1.D_E_L_E_T_=' ' "
				
				If ExistBlock("MA215SD1")
					cQuery := ExecBlock("MA215SD1",.F.,.F.,cQuery)
				EndIf
				
				cQuery += "ORDER BY D1_FILIAL,D1_COD,D1_LOCAL"
			
				cQuery := ChangeQuery(cQuery)
				
				dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSD1,.T.,.T.)
				For nX := 1 To Len(aStru)
					If ( aStru[nX][2] <> "C" .And. aStru[nX][2] <> "M" )
						TcSetField(cAliasSD1,aStru[nX][1],aStru[nX][2],aStru[nX][3],aStru[nX][4])
					EndIf
				Next nX
			Else
			#ENDIF
				MsSeek(xFilial("SD1"))
			#IFDEF TOP
			EndIf
			#ENDIF
				
			If !lBat
				oObj:SetRegua2(SD1->(LastRec()))
				oObj:IncRegua2(cMensagem)
			EndIf	
			While !Eof() .And. (cAliasSD1)->D1_FILIAL == xFilial("SD1")
				lContinua := .T.
				If !lQuery
					If !Empty((cAliasSD1)->D1_IDENTB6) .And. ((cAliasSD1)->D1_QTDEDEV > 0 .Or. (cAliasSD1)->D1_VALDEV > 0)
						RecLock(cAliasSD1,.F.)
						(cAliasSD1)->D1_QTDEDEV := 0
						(cAliasSD1)->D1_VALDEV  := 0
						MsUnLock()
					EndIf				
					#IFDEF SHELL
						If D1_CANCEL == "S"
							lContinua := .F.
						EndIf
					#ENDIF
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Atualizar Qtde Nao Classificada  em Saldos SB2.     ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If !Empty((cAliasSD1)->D1_TES) .Or. !vA215FilOk((cAliasSD1)->D1_COD)
						lContinua := .F.
					EndIf
				EndIf
				If lContinua
					dbSelectArea("SB2")
					dbSetOrder(1)
					If !MsSeek(xFilial("SB2")+(cAliasSD1)->D1_COD+(cAliasSD1)->D1_LOCAL)
						CriaSB2((cAliasSD1)->D1_COD,(cAliasSD1)->D1_LOCAL)
					EndIf
					RecLock("SB2",.F.)
					Replace B2_NAOCLAS With B2_NAOCLAS + (cAliasSD1)->D1_QUANT
					MsUnLock()
				EndIf
				va215Skip(cAliasSD1)
				If !lBat
					oObj:IncRegua2(cMensagem)
				EndIf	
			EndDo
			If lQuery
				dbSelectArea(cAliasSD1)
				dbCloseArea()
				dbSelectArea("SD1")
			EndIf
		EndIf
		If !lBat
			oObj:IncRegua1(STR0007+" - "+AllTrim(SM0->M0_NOME)+"/"+SM0->M0_FILIAL) //"Atualizando acumulados"
		EndIf	
		If (!Empty(xFilial("SC0")) .Or. cFilAnt == cFirst )
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Atualiza os dados acumulados das Reserva de Faturamento       ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			dbSelectArea("SX2")
			dbSetOrder(1)
			MsSeek("SC0")
			cMensagem := AllTrim(X2Nome())
			cMensagem := Lower(cMensagem)
			cMensagem := Upper(SubStr(cMensagem,1,1))+SubStr(cMensagem,2)
			dbSelectArea("SC0")
			dbSetOrder(1)
			#IFDEF TOP
				aStru     := SC0->(dbStruct())
				lQuery    := .T.
				cAliasSC0 := "SC0MA215PROC"
				
				cQuery := "SELECT SC0.*,SB1.B1_FILIAL "
				cQuery += "FROM "+RetSqlName("SC0")+" SC0,"
				cQuery += RetSqlName("SB1")+" SB1 "
				cQuery += "WHERE SC0.C0_FILIAL='"+xFilial("SC0")+"' AND "
				cQuery += "SC0.C0_QUANT > 0 AND "
				cQuery += "SC0.D_E_L_E_T_=' ' AND "
				cQuery += "SB1.B1_FILIAL='"+xFilial("SB1")+"' AND "
				cQuery += "SB1.B1_COD=SC0.C0_PRODUTO AND "
				cQuery += "SB1.D_E_L_E_T_=' ' "
				cQuery += "ORDER BY C0_FILIAL,C0_PRODUTO,C0_LOCAL "
			
				cQuery := ChangeQuery(cQuery)
				
				dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSC0,.T.,.T.)
				For nX := 1 To Len(aStru)
					If ( aStru[nX][2] <> "C" .And. aStru[nX][2] <> "M" )
						TcSetField(cAliasSC0,aStru[nX][1],aStru[nX][2],aStru[nX][3],aStru[nX][4])
					EndIf
				Next nX
			#ELSE
				MsSeek(xFilial("SC0"))
			#ENDIF
			If !lBat
				oObj:SetRegua2(SC0->(LastRec()))
				oObj:IncRegua2(cMensagem)
			EndIf	
			While !Eof() .And. (cAliasSC0)->C0_FILIAL == xFilial("SC0")
				lContinua := .T.
				If !lQuery
					If !( (cAliasSC0)->C0_QUANT > 0 ) .And. vA215FilOk((cAliasSC0)->C0_PRODUTO)
						lContinua := .F.
					EndIf
				EndIf
				
				If lContinua   
					Ma215Res(cAliasSC0, (cAliasSC0)->C0_QUANT)
				EndIf
				
				va215Skip(cAliasSC0)
				If !lBat
					oObj:IncRegua2(cMensagem)
				EndIf	
			EndDo
			
			If lQuery
				dbSelectArea(cAliasSC0)
				dbCloseArea()
				dbSelectArea("SC0")
			EndIf
		EndIf
		If !lBat
			oObj:IncRegua1(STR0007+" - "+AllTrim(SM0->M0_NOME)+"/"+SM0->M0_FILIAL) //"Atualizando acumulados"
		EndIf	
		If ( !Empty(xFilial("SC6")) .Or. cFilAnt == cFirst )
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Atualiza os dados acumulados do PV e seus anexos              ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			lQuery := .F.
			dbSelectArea("SX2")
			dbSetOrder(1)
			MsSeek("SC6")
			cMensagem := AllTrim(X2Nome())
			cMensagem := Lower(cMensagem)
			cMensagem := Upper(SubStr(cMensagem,1,1))+SubStr(cMensagem,2)
			dbSelectArea("SC6")
			dbSetOrder(1)
			#IFDEF TOP
				If TcSrvType()<>"AS/400"
				cAliasSC6 := "SC6MA215PROC"
				
				cQuery := "SELECT MIN(R_E_C_N_O_) MINRECNO,"
				cQuery += "MAX(R_E_C_N_O_) MAXRECNO "
				cQuery += "FROM "+RetSqlName("SC6")+" SC6 "
				cQuery += "WHERE SC6.C6_FILIAL='"+xFilial("SC6")+"' AND "
				cQuery += "C6_BLQ <> 'S"+Space(Len(SC6->C6_BLQ)-1)+"' AND "
				cQuery += "C6_BLQ <> 'R"+Space(Len(SC6->C6_BLQ)-1)+"' AND "
				cQuery += "(C6_QTDVEN > C6_QTDENT OR (C6_QTDVEN <= C6_QTDENT AND C6_ENTREG>='"+DTOS(dDataBase-31)+"')) AND "
				cQuery += "SC6.D_E_L_E_T_=' ' "
				cQuery := ChangeQuery(cQuery)
			
				dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSC6)
				nMax := (cAliasSC6)->MAXRECNO
				nMin := (cAliasSC6)->MINRECNO
				dbCloseArea()
				dbSelectArea("SC6")
				cQuery := "UPDATE "
				cQuery += RetSqlName("SC6")+" "
				cQuery += "SET C6_QTDEMP = 0, C6_QTDENT = 0, C6_QTDEMP2 = 0, C6_QTDENT2 = 0 "
				cQuery += "WHERE C6_FILIAL='"+xFilial("SC6")+"' AND "
				cQuery += "C6_BLQ <> 'S"+Space(Len(SC6->C6_BLQ)-1)+"' AND "
				cQuery += "C6_BLQ <> 'R"+Space(Len(SC6->C6_BLQ)-1)+"' AND "
				cQuery += "(C6_QTDVEN > C6_QTDENT OR (C6_QTDVEN <= C6_QTDENT AND C6_ENTREG>='"+Dtos(dDataBase-31)+"')) AND "
				cQuery += "D_E_L_E_T_=' ' AND "
				For nX := nMin To nMax+4096 STEP 4096
					cChave := "R_E_C_N_O_>="+Str(nX,10,0)+" AND R_E_C_N_O_<="+Str(nX+4096,10,0)+""
					TcSqlExec(cQuery+cChave)
				Next nX
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³A tabela eh fechada para restaurar o buffer da aplicacao³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				dbSelectArea("SC6")
				dbCloseArea()
				ChkFile("SC6",.T.)
			
				aStru     := SC6->(dbStruct())
				lQuery    := .T.
				cAliasSC6 := "SC6MA215PROC"
			
				cQuery := "SELECT SC6.C6_FILIAL,SC6.C6_NUM,SC6.C6_ITEM,SC6.C6_PRODUTO,SC6.C6_BLQ,"
				cQuery += "SC6.C6_NUMOS,SC6.C6_QTDEMP,SC6.C6_NUMOSFA,"
				cQuery += "SC6.C6_QTDENT,SC6.C6_RESERVA,SC6.C6_TES,SC6.C6_NUMLOTE,"
				cQuery += "SC6.C6_CLI,SC6.C6_LOCAL,SC6.C6_LOTECTL,SC6.C6_LOJA,SC6.C6_QTDVEN,"
				cQuery += "SC6.C6_OP,SC6.C6_NUMSERI,SC6.C6_LOCALIZ,SC6.C6_QTDRESE,SC6.C6_DTVALID,"
				cQuery += "SC6.C6_QTDLIB,SC6.C6_PRCVEN,"
				cQuery += "SC6.C6_PRUNIT,SC6.C6_VALDESC,"
				cQuery += "SC6.C6_VALOR,SC6.C6_UNSVEN,SC6.C6_ENTREG,SC6.C6_DATFAT, "
				cQuery += "SC6.C6_DESCONT,SC6.C6_QTDLIB2,"
				cQuery += "SC6.R_E_C_N_O_ SC6RECNO,SB1.B1_FILIAL "
				
				If SC6->(FieldPos("C6_QTDEMP2")) > 0
					cQuery += ",SC6.C6_QTDEMP2 "
				Endif                       
				
				If SC6->(FieldPos("C6_QTDENT2")) > 0
					cQuery += ",SC6.C6_QTDENT2 "
				Endif                       
				If cPaisLoc <> "BRA"
					cQuery += ",SC6.C6_GERANF,SC6.C6_CONTRAT,SC6.C6_ITEMCON "
				EndIf
				cQuery += "FROM "+RetSqlName("SC6")+" SC6, "+RetSqlName("SB1")+" SB1 "
				cQuery += "WHERE SC6.C6_FILIAL='" + xFilial( "SC6" ) + "' AND "
				cQuery += "SC6.C6_BLQ <> 'S ' AND SC6.C6_BLQ <> 'R ' AND "
				cQuery += "(SC6.C6_QTDVEN > SC6.C6_QTDENT OR (SC6.C6_QTDVEN <= SC6.C6_QTDENT AND SC6.C6_ENTREG>='"+DTOS(dDataBase-31)+"')) AND "
				cQuery += "SC6.D_E_L_E_T_=' ' AND "
				cQuery += "SB1.B1_FILIAL='" + xFilial( "SB1" ) + "' AND "
				cQuery += "SB1.B1_COD=SC6.C6_PRODUTO AND "
				cQuery += "SB1.D_E_L_E_T_=' ' "
				cQuery += "ORDER BY C6_FILIAL,C6_NUM,C6_ITEM,C6_PRODUTO "
				cQuery := ChangeQuery(cQuery)
			
				dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSC6,.T.,.T.)
				For nX := 1 To Len(aStru)
					If ( aStru[nX][2] <> "C" .And. FieldPos(aStru[nX][1])<>0 )
						TcSetField(cAliasSC6,aStru[nX][1],aStru[nX][2],aStru[nX][3],aStru[nX][4])
					EndIf
				Next nX
				Else
			#ENDIF
					MsSeek(xFilial("SC6"))
			#IFDEF TOP
				EndIf
			#ENDIF
			lLiberOk  := .T.
			lResidOk  := .T.
			lFaturOk  := .F.
			If !lBat
				oObj:SetRegua2(SC6->(LastRec()))
				oObj:IncRegua2(cMensagem)
			EndIf	
			While !(cAliasSC6)->( Eof() ) .And. (cAliasSC6)->C6_FILIAL == xFilial("SC6")
				lContinua := .T.
				cQuebra   := (cAliasSC6)->C6_NUM
				BEGIN TRANSACTION
				If !lQuery 
					If !vA215FilOk(SC6->C6_PRODUTO) .And. AllTrim(SC6->C6_BLQ)$"RS"
						lContinua := .F.
					EndIf
					If lContinua 
						RecLock("SC6",.F.)
						SC6->C6_QTDEMP  := 0
						SC6->C6_QTDENT  := 0
						SC6->C6_QTDEMP2 := 0
						SC6->C6_QTDENT2 := 0										
						MsUnlock()
					EndIf
				EndIf
				If lContinua
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³Atualiza os acumulados dos pedidos de venda entregue          ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If cAliasSC6<>"SC6"
						lSC6Ok := .F.
					Else
						lSC6Ok := .T.
					EndIf
					dbSelectArea("SD2")
					dbSetOrder(8)
					#IFDEF TOP
						If TcSrvType()<>"AS/400"
							aStru     := SD2->(dbStruct())
							lQuery    := .T.
							cAliasSD2 := "SD2MA215PROC1"
					
							cQuery := "SELECT D2_FILIAL,D2_PEDIDO,D2_ITEMPV,D2_QUANT,D2_QTSEGUM,D2_COD,D2_DOC,D2_SERIE,D2_EMISSAO,D2_REMITO "
							cQuery += "FROM "+RetSqlName("SD2")+" SD2 "
							cQuery += "WHERE SD2.D2_FILIAL='"+xFilial("SD2")+"' AND "
							cQuery += "SD2.D2_PEDIDO = '"+(cAliasSC6)->C6_NUM+"' AND "
							cQuery += "SD2.D2_ITEMPV = '"+(cAliasSC6)->C6_ITEM+"' AND "
							cQuery += "SD2.D2_COD = '"+(cAliasSC6)->C6_PRODUTO+"' AND "
							cQuery += "SD2.D_E_L_E_T_=' ' "	
							If ExistBlock("MA215SD2")
								cQuery := ExecBlock("MA215SD2",.F.,.F.,cQuery)
							EndIf
						
							cQuery += "ORDER BY "+SqlOrder(SD2->(IndexKey()))
							cQuery := ChangeQuery(cQuery)
				
							dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSD2,.T.,.T.)
							For nX := 1 To Len(aStru)
								If ( aStru[nX][2] <> "C" .And. FieldPos(aStru[nX][1])<>0 )
									TcSetField(cAliasSD2,aStru[nX][1],aStru[nX][2],aStru[nX][3],aStru[nX][4])
								EndIf
							Next nX	
						Else
					#ENDIF
							MsSeek(xFilial("SD2")+(cAliasSC6)->C6_NUM+(cAliasSC6)->C6_ITEM)
					#IFDEF TOP
						EndIf
					#ENDIF
					While !Eof() .And. xFilial("SD2") == (cAliasSD2)->D2_FILIAL .And.;
						(cAliasSC6)->C6_NUM == (cAliasSD2)->D2_PEDIDO .And.;
						(cAliasSC6)->C6_ITEM == (cAliasSD2)->D2_ITEMPV
						If (cAliasSD2)->D2_COD == (cAliasSC6)->C6_PRODUTO
							If lQuery
								SC6->(MsGoto((cAliasSC6)->SC6RECNO))
								lSC6Ok := .T.
							EndIf
							vMaAvalSC6("SC6",5,"SC5",.F.,.F.,@lLiberOk,@lResidOk,@lFaturOk,.T.,,cAliasSD2)
						EndIf					
						dbSelectArea(cAliasSD2)
						dbSkip()
					EndDo
					If lQuery
						dbSelectArea(cAliasSD2)
						dbCloseArea()
						dbSelectArea("SD2")
					EndIf							
	
					If lQuery
						SC6->(MsGoto((cAliasSC6)->SC6RECNO))
						lSC6Ok := .T.
					EndIf          
					
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Passa as variaveis lLiberok, lResidOk e lFaturOk sem referencia para nao modificar o conteudo ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					
					If !Empty((cAliasSC6)->C6_RESERVA) .And.(cAliasSC6)->C6_QTDRESE > 0
						SC0->(dbSetOrder(1))
						If SC0->(MsSeek(xFilial("SC0")+(cAliasSC6)->C6_RESERVA+(cAliasSC6)->C6_PRODUTO+(cAliasSC6)->C6_LOCAL))
							Ma215Res("SC0",(cAliasSC6)->C6_QTDRESE)
						Endif	
					Endif					
					
					vMaAvalSC6(IIf(lSC6Ok,"SC6",cAliasSC6),1,"SC5",.F.,.F.,lLiberOk,lResidOk,lFaturOk,.T.)
					
					dbSelectArea("SC9")
					dbSetOrder(1)
					#IFDEF TOP
						If TcSrvType()<>"AS/400"
						aStru     := SC9->(dbStruct())
						lQuery    := .T.
						cAliasSC9 := "SC9MA215PROC1"
				
						cQuery := "SELECT * "
						cQuery += "FROM "+RetSqlName("SC9")+" SC9 "
						cQuery += "WHERE SC9.C9_FILIAL='"+xFilial("SC9")+"' AND "
						cQuery += "SC9.C9_PEDIDO = '"+(cAliasSC6)->C6_NUM+"' AND "
						cQuery += "SC9.C9_ITEM = '"+(cAliasSC6)->C6_ITEM+"' AND "
						cQuery += "SC9.C9_PRODUTO = '"+(cAliasSC6)->C6_PRODUTO+"' AND "
						cQuery += "SC9.C9_NFISCAL = '"+Space(Len(SC9->C9_NFISCAL))+"' AND "
						cQuery += "SC9.C9_REMITO = '"+Space(Len(SC9->C9_REMITO))+"' AND "
						cQuery += "SC9.D_E_L_E_T_=' ' "	
						cQuery += "ORDER BY "+SqlOrder(SC9->(IndexKey()))
						cQuery := ChangeQuery(cQuery)
				
						dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSC9,.T.,.T.)
						For nX := 1 To Len(aStru)
							If ( aStru[nX][2] <> "C" .And. aStru[nX][2] <> "M" )
								TcSetField(cAliasSC9,aStru[nX][1],aStru[nX][2],aStru[nX][3],aStru[nX][4])
							EndIf
						Next nX	
						Else
					#ENDIF
							MsSeek(xFilial("SC9")+(cAliasSC6)->C6_NUM+(cAliasSC6)->C6_ITEM)
					#IFDEF TOP
						EndIf
					#ENDIF
					While !Eof() .And. xFilial("SC9") == (cAliasSC9)->C9_FILIAL .And.;
						(cAliasSC6)->C6_NUM == (cAliasSC9)->C9_PEDIDO .And. ;
						(cAliasSC6)->C6_ITEM == (cAliasSC9)->C9_ITEM
							
						If Empty((cAliasSC9)->C9_NFISCAL) .And. Empty((cAliasSC9)->C9_REMITO)		
							If  (cAliasSC9)->C9_PRODUTO == (cAliasSC6)->C6_PRODUTO
								aSaldos := {}
								If Localiza((cAliasSC9)->C9_PRODUTO) .And. ;
									(!IntDL((cAliasSC9)->C9_PRODUTO) .Or. !((cAliasSC9)->C9_BLWMS$'01;02;03'))
									cAliasSDC := "SDC"
									dbSelectArea("SDC")
									dbSetOrder(1)
									cSeekSDC := xFilial("SDC")+;
												(cAliasSC9)->C9_PRODUTO+;
												(cAliasSC9)->C9_LOCAL+;
												"SC6"+;
												(cAliasSC9)->C9_PEDIDO+;
												(cAliasSC9)->C9_ITEM
									#IFDEF TOP
										If TcSrvType()<>"AS/400"
										aStru     := SDC->(dbStruct())
										lQuery    := .T.
										cAliasSDC := "SDCMA215PROC2"
															
										cQuery := "SELECT * "
										cQuery += "FROM "+RetSqlName("SDC")+" SDC "
										cQuery += "WHERE SDC.DC_FILIAL='"+xFilial("SDC")+"' AND "
										cQuery += "SDC.DC_PRODUTO = '"+(cAliasSC9)->C9_PRODUTO+"' AND "
										cQuery += "SDC.DC_LOCAL = '"+(cAliasSC9)->C9_LOCAL+"' AND "
										cQuery += "SDC.DC_ORIGEM = 'SC6' AND "
										cQuery += "SDC.DC_PEDIDO = '"+(cAliasSC9)->C9_PEDIDO+"' AND "
										cQuery += "SDC.DC_ITEM = '"+(cAliasSC9)->C9_ITEM+"' AND "
										cQuery += "SDC.DC_SEQ = '"+(cAliasSC9)->C9_SEQUEN+"' AND "
										cQuery += "SDC.D_E_L_E_T_=' ' "	
										cQuery += "ORDER BY "+SqlOrder(SDC->(IndexKey()))
										cQuery := ChangeQuery(cQuery)
								
										dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSDC,.T.,.T.)
										For nX := 1 To Len(aStru)
											If ( aStru[nX][2] <> "C" .And. aStru[nX][2] <> "M" )
												TcSetField(cAliasSDC,aStru[nX][1],aStru[nX][2],aStru[nX][3],aStru[nX][4])
											EndIf
										Next nX	
										Else
									#ENDIF
											MsSeek(cSeekSDC)
									#IFDEF TOP
										EndIf
									#ENDIF
									While !Eof() .And. DC_FILIAL+DC_PRODUTO+DC_LOCAL+DC_ORIGEM+DC_PEDIDO+DC_ITEM == cSeekSDC
										//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
										//³ Atualiza arquivo de empenhos               ³
										//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
										
										If (cAliasSDC)->DC_SEQ == (cAliasSC9)->C9_SEQUEN
	
											aadd(aSaldos,{	(cAliasSDC)->DC_LOTECTL,;
															(cAliasSDC)->DC_NUMLOTE,;
															(cAliasSDC)->DC_LOCALIZ,;
															(cAliasSDC)->DC_NUMSERI,;
															(cAliasSDC)->DC_QUANT,;
															If( Empty( (cAliasSDC)->DC_QTSEGUM ), ConvUm((cAliasSDC)->DC_PRODUTO,(cAliasSDC)->DC_QUANT,0,2),(cAliasSDC)->DC_QTSEGUM ),;
															(cAliasSC9)->C9_DTVALID,;
															"",;
															"",;
															"",;
															(cAliasSDC)->DC_LOCAL})
	
															
										EndIf
										dbSelectArea(cAliasSDC)
										dbSkip()
									EndDo
									If lQuery
										dbSelectArea(cAliasSDC)
										dbCloseArea()
										dbSelectArea("SDC")
									EndIf						
								Else
									nQtdLib2 := If(Empty((cAliasSC9)->C9_QTDLIB2),ConvUm((cAliasSC9)->C9_PRODUTO,(cAliasSC9)->C9_QTDLIB,0,2),(cAliasSC9)->C9_QTDLIB2)
									aSaldos := {{ "","","","",(cAliasSC9)->C9_QTDLIB,nQtdLib2,	Ctod(""),"","","",(cAliasSC9)->C9_LOCAL}}
								EndIf
								//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
								//³Atualiza os acumulados da liberacao do pedido de venda        ³
								//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	
								MaAvalSC9(cAliasSC9,1,aSaldos,,.T.)
							EndIf
						EndIf
						dbSelectArea(cAliasSC9)
						dbSkip()
					EndDo    
					
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³Atualiza os acumulados do item do pedido de venda             ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If lQuery
						SC6->(MsGoto((cAliasSC6)->SC6RECNO))
						lSC6Ok := .T.
					EndIf		
			
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Verificacao dos acumulados do SC5                                      ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If ( SC6->C6_QTDVEN > (SC6->C6_QTDEMP+SC6->C6_QTDENT) .And. AllTrim(SC6->C6_BLQ)<>"R")
						lLiberOk := .F.
					EndIf
					If (! "R" $ SC6->C6_BLQ )
						lResidOk := .F.
					EndIf
					If ( SC6->C6_QTDVEN > SC6->C6_QTDENT .And. AllTrim(SC6->C6_BLQ)<>"R")
						lFaturOk := .T.
					EndIf
					
					If lQuery
						dbSelectArea(cAliasSC9)
						dbCloseArea()
						dbSelectArea("SC9")
					EndIf
				EndIf
				dbSelectArea(cAliasSC6)
				dbSkip()
				If !lBat
					oObj:IncRegua2(cMensagem)
				EndIf	
				If cQuebra <> (cAliasSC6)->C6_NUM
					dbSelectArea("SC5")
					dbSetOrder(1)
					If MsSeek(xFilial("SC5")+cQuebra)
						MaAvalSC5("SC5",1,.F.,.F.,@lLiberOk,@lResidOk,@lFaturOk,.T.)
					EndIf
					lLiberOk  := .T.
					lResidOk  := .T.
					lFaturOk  := .F.
				EndIf
				End Transaction
			EndDo
			If lQuery
				dbSelectArea(cAliasSC6)
				dbCloseArea()
				dbSelectArea("SC6")
			EndIf
		EndIf
		If !lBat
			oObj:IncRegua1(STR0007+" - "+AllTrim(SM0->M0_NOME)+"/"+SM0->M0_FILIAL) //"Atualizando acumulados"
		EndIf	
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Atualiza os dados acumulados do PODER de 3 e EM 3.            ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If ( !Empty(xFilial("SB6")) .Or. cFilAnt == cFirst )	
			dbSelectArea("SX2")
			dbSetOrder(1)
			MsSeek("SB6")
			cMensagem := AllTrim(X2Nome())
			cMensagem := Lower(cMensagem)
			cMensagem := Upper(SubStr(cMensagem,1,1))+SubStr(cMensagem,2)
			dbSelectArea("SB6")
			dbSetOrder(1)
			#IFDEF TOP
				aStru     := SB6->(dbStruct())
				lQuery    := .T.
				cAliasSB6 := "SB6MA215PROC"
			
				cQuery := "SELECT SB6.*,SB1.B1_FILIAL "
				cQuery += "FROM "+RetSqlName("SB6")+" SB6, "
				cQuery += RetSqlName("SB1")+" SB1 "
				cQuery += "WHERE SB6.B6_FILIAL='"+xFilial("SB6")+"' AND "
				cQuery += "SB6.B6_QUANT > 0 AND "
				cQuery += "SB6.D_E_L_E_T_=' ' AND "
				cQuery += "SB1.B1_FILIAL='"+xFilial("SB1")+"' AND "	
				cQuery += "SB1.B1_COD=SB6.B6_PRODUTO AND "
				cQuery += "SB1.D_E_L_E_T_=' ' "
				cQuery += "ORDER BY B6_FILIAL,B6_PRODUTO,B6_LOCAL "
				cQuery := ChangeQuery(cQuery)
			
				dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSB6,.T.,.T.)
				For nX := 1 To Len(aStru)
					If ( aStru[nX][2] <> "C" .And. aStru[nX][2] <> "M" )
						TcSetField(cAliasSB6,aStru[nX][1],aStru[nX][2],aStru[nX][3],aStru[nX][4])
					EndIf
				Next nX
			#ELSE
				MsSeek(xFilial("SB6"))
			#ENDIF
			If !lBat
				oObj:SetRegua2(SB6->(LastRec()))
				oObj:IncRegua2(cMensagem)
			EndIf	
			While !Eof() .And. xFilial("SB6") == (cAliasSB6)->B6_FILIAL
				lContinua := .T.
				cLocOriSB6:= ""
				If !lQuery
					If !vA215FilOk((cAliasSB6)->B6_PRODUTO)
						lContinua := .F.
					EndIf
				EndIf
				
				dbSelectArea("SF4")
				dbSetOrder(1)
				MsSeek(xFilial("SF4")+(cAliasSB6)->B6_TES)
					
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Quando for devolucao de material que estava em terceiros,     ³
				//|posicionar sempre no local de origem da remessa. 			 |
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If SF4->F4_PODER3 == "D" .And. (cAliasSB6)->B6_TES < "501"
					nRegSB6 := SB6->(RecNo())
					aAreaSB6:= SB6->(GetArea())
					dbSelectArea("SB6")
					dbSetOrder(3)
					If SB6->(MsSeek(xFilial("SB6")+(cAliasSB6)->B6_IDENT+(cAliasSB6)->B6_PRODUTO+"R"))
						cLocOriSB6 := SB6->B6_LOCAL //Local de Origem
					EndIf	
					RestArea(aAreaSB6)
					SB6->(MsGoto(nRegSB6))
				EndIf	

				dbSelectArea("SB2")
				dbSetOrder(1)
				If MsSeek(xFilial("SB2")+(cAliasSB6)->B6_PRODUTO+IIf(Empty(cLocOriSB6),(cAliasSB6)->B6_LOCAL,cLocOriSB6))
					RecLock("SB2",.F.)
				Else
					CriaSB2((cAliasSB6)->B6_PRODUTO,(cAliasSB6)->B6_LOCAL)
				EndIf
	            RecLock("SB2")
	
			    cEstoque := (cAliasSB6)->B6_ESTOQUE
	
	 	       //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	 	       //³Atualiza os Campos B2_QTER, B2_QNPT, B2_QTNP.                 ³
		       //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	            
	            IF SF4->F4_CODIGO <= "500"   // Documentos de Entrada.
	               If ( SF4->F4_PODER3 $ "DR" )
	                  If ( SF4->F4_PODER3 == "D" ) // Devolucoes.
				         If IIf(Empty(cEstoque),SF4->F4_ESTOQUE,cEstoque)=="N"
					        Replace B2_QTER with B2_QTER - (cAliasSB6)->B6_QUANT
				         Else
					        Replace B2_QNPT With B2_QNPT - (cAliasSB6)->B6_QUANT
				         EndIf
			          Else  // Remessas.
				         If IIf(Empty(cEstoque),SF4->F4_ESTOQUE,cEstoque)=="N"
					        Replace B2_QTER With B2_QTER + (cAliasSB6)->B6_QUANT
				         Else
					        Replace B2_QTNP With B2_QTNP + (cAliasSB6)->B6_QUANT
				         EndIf
			          EndIf
		           EndIf
	   		    Else  // Doucmentos de Saida.
		           If ( SF4->F4_PODER3 $ "DR" )
			          If ( SF4->F4_PODER3 == "D" )  // Devolucoes.
				         If IIf(Empty(cEstoque),SF4->F4_ESTOQUE,cEstoque)=="N"
				            Replace B2_QTER With B2_QTER - (cAliasSB6)->B6_QUANT
				         Else
				            Replace B2_QTNP With B2_QTNP - (cAliasSB6)->B6_QUANT
				         EndIf
			          Else  //Remessas
				         If IIf(Empty(cEstoque),SF4->F4_ESTOQUE,cEstoque)=="N"
					        Replace B2_QTER With B2_QTER + (cAliasSB6)->B6_QUANT
				         Else
					        Replace B2_QNPT With B2_QNPT + (cAliasSB6)->B6_QUANT
				         EndIf
			          EndIf
		           EndIf
			    Endif  
	
	            SB2->(MsUnLock())
				va215Skip(cAliasSB6)
				If !lBat
					oObj:IncRegua2(cMensagem)
				EndIf	
			EndDo
			If lQuery
				dbSelectArea(cAliasSB6)
				dbCloseArea()
				dbSelectArea("SB6")
			EndIf
		EndIf
		If !lBat
			oObj:IncRegua1(STR0007+" - "+AllTrim(SM0->M0_NOME)+"/"+SM0->M0_FILIAL) //"Atualizando acumulados"
		EndIf	
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Atualiza os dados acumulados dos empenhos do SIGAPMS          ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If (!Empty(xFilial("AFJ")) .Or. cFilAnt == cFirst )
			dbSelectArea("SX2")
			dbSetOrder(1)
			MsSeek("AFJ")
			cMensagem := AllTrim(X2Nome())
			cMensagem := Lower(cMensagem)
			cMensagem := Upper(SubStr(cMensagem,1,1))+SubStr(cMensagem,2)
			
			dbSelectArea("AFJ")
			dbSetOrder(1)
			#IFDEF TOP
				aStru     := AFJ->(dbStruct())
				lQuery    := .T.
				cAliasAFJ := "AFJMA215PROC"
				
				cQuery := "SELECT AFJ.*,SB1.B1_FILIAL "
				cQuery += "FROM "+RetSqlName("AFJ")+" AFJ,"
				cQuery += RetSqlName("SB1")+" SB1 "
				cQuery += "WHERE AFJ.AFJ_FILIAL='"+xFilial("AFJ")+"' AND "
				cQuery += "AFJ.AFJ_QEMP > AFJ.AFJ_QATU AND "
				cQuery += "AFJ.D_E_L_E_T_=' ' AND "
				cQuery += "SB1.B1_FILIAL='"+xFilial("SB1")+"' AND "
				cQuery += "SB1.B1_COD=AFJ.AFJ_COD AND "
				cQuery += "SB1.D_E_L_E_T_=' ' "
				cQuery += "ORDER BY AFJ_FILIAL,AFJ_COD,AFJ_LOCAL"
			
				cQuery := ChangeQuery(cQuery)
				
				dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasAFJ,.T.,.T.)
				For nX := 1 To Len(aStru)
					If ( aStru[nX][2] <> "C" .And. aStru[nX][2] <> "M" )
						TcSetField(cAliasAFJ,aStru[nX][1],aStru[nX][2],aStru[nX][3],aStru[nX][4])
					EndIf
				Next nX
			#ELSE
				MsSeek(xFilial("AFJ"))
			#ENDIF
			If !lBat
				oObj:SetRegua2(AFJ->(LastRec()))
				oObj:IncRegua2(cMensagem)
			EndIf	
			While !Eof() .And. (cAliasAFJ)->AFJ_FILIAL == xFilial("AFJ")
				lContinua := .T.
				If !lQuery
					If !vA215FilOk((cAliasAFJ)->AFJ_COD) .Or. ((cAliasAFJ)->AFJ_QEMP <= (cAliasAFJ)->AFJ_QATU .And. (AFJ->(FieldPos("AFJ_QEMPPR")) > 0 .And. Empty(AFJ->AFJ_QEMPPR)))
						lContinua := .F.
					EndIf
				EndIf
				If lContinua
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Atualiza arquivo de empenhos               ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					PmsAtuEmp(	(cAliasAFJ)->AFJ_PROJET,;
								(cAliasAFJ)->AFJ_TAREFA,;
								(cAliasAFJ)->AFJ_COD,;
								(cAliasAFJ)->AFJ_LOCAL,;
								(cAliasAFJ)->AFJ_QEMP-(cAliasAFJ)->AFJ_QATU,;
								"+",;
								.F.,;
								(cAliasAFJ)->AFJ_QEMP2-(cAliasAFJ)->AFJ_QATU2,;
								(cAliasAFJ)->AFJ_TRT)
					If AFJ->(FieldPos("AFJ_QEMPPR")) > 0
						PmsAtuEmp(	(cAliasAFJ)->AFJ_PROJET,;
									(cAliasAFJ)->AFJ_TAREFA,;
									(cAliasAFJ)->AFJ_COD,;
									(cAliasAFJ)->AFJ_LOCAL,;
									(cAliasAFJ)->AFJ_QEMPPR,;
									"+",;
									.F.,;
									(cAliasAFJ)->AFJ_QEMPP2,;
									(cAliasAFJ)->AFJ_TRT,;
									,,,;
									.T.)
					EndIf
				EndIf
				va215Skip(cAliasAFJ)
				If !lBat
					oObj:IncRegua2(cMensagem)
				EndIf
			EndDo
			If lQuery
				dbSelectArea(cAliasAFJ)
				dbCloseArea()
				dbSelectArea("AFJ")
			EndIf
		EndIf
		If cPaisLoc <> "BRA" .And. lEic
			vMaAvalSW6()
		EndIf
	EndIf
	If !lBat
		oObj:IncRegua1(STR0007+" - "+AllTrim(SM0->M0_NOME)+"/"+SM0->M0_FILIAL) //"Atualizando acumulados"
	EndIf	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se deve continuar a atualizar as demais filiais     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If (!Empty(xFilial("SA1")) .And. !Empty(xFilial("SB2")) )
		Exit
	EndIf
	
	dbSelectArea("SM0")	
	dbSkip()
EndDo
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Fecha todos os arquivos e reabre-os de forma compartilhada   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nTempoFim:=Seconds()
cFilAnt := cSavFil
dbCloseAll()
OpenFile(SubStr(cNumEmp,1,2))

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Envia mensagem de aviso apos termino da rotina               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cTempo:=StrZero((nTempoFim-nTempoIni)/60,5,0)
MEnviaMail("021",{Substr(cUsuario,7,15),SubStr(cNumEmp,1,2),SubStr(cNumEmp,3,2),cTempo})
Return


/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A215FilOk   ³ Autor ³Rodrigo de A Sartorio³ Data ³ 01/03/00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Verifica no SB1 a existencia do produto                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ A215FilOk(ExpC1)                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 := Codigo do produto a ser pesquisado                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ .T. = encontrou o produto em SB1, .F.= nao encontrou       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA215                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function vA215FilOk(cProd)
Return SB1->(MsSeek(xFilial("SB1")+cProd))


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao    ³A215Skip    ³ Autor ³Fernando Joly Siquini³ Data ³ 26/03/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡ao ³ Se apos o dbSkip um dos Campos Chave (Filial, Produto ou   ³±±
±±³          ³ Armazem) for alterado, a funcao MsUnlockAll() e executada. ³±±
±±³          ³ Este procedimento evita a sobrecarga de LOCKS em ambientes ³±±
±±³          ³ CDX e ADS(Em ambientes TOP a funcao nunca sera executada). ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ A215Skip(ExpC1)                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 := Alias no qual o "dbSkip" sera executado.          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ .T. caso o MsUnlockAll() seja executado, .F. caso contrario³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA215                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function va215Skip(c215Alias)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis Locais                                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local cChaveA    := ''
Local cChaveB    := ''
Local cPrefix    := ''
Local cCpoFil    := ''
Local cCpoCod    := ''
Local cCpoLoc    := ''
Local lRet       := .F.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Redefine Variaveis passadas como parametros                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Default c215Alias  := Alias()

If Select(c215Alias) > 0
	dbSelectArea(c215Alias)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Monta Strings com nomes dos campos FILIAL, PRODUTO e LOCAL   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cPrefix := If(SubStr(Upper(c215Alias),1,1)=='S',SubStr(Upper(c215Alias),2,2),SubStr(Upper(c215Alias),1,3))+'_'
	cCpoFil := If(FieldPos(cCpoFil:=cPrefix+'FILIAL')>0,cCpoFil,'')
	cCpoCod := If(FieldPos(cCpoCod:=cPrefix+'PRODUTO')>0,cCpoCod,If(FieldPos(cCpoCod:=cPrefix+'COD')>0,cCpoCod,''))
	cCpoLoc := If(FieldPos(cCpoLoc:=cPrefix+'LOCAL')>0,cCpoLoc,If(FieldPos(cCpoLoc:=cPrefix+'ALMOX')>0,cCpoLoc,''))
	If !((cCpoFil+cCpoCod+cCpoLoc)=='')
		cChaveA  := IIf(!Empty(cCpoFil),FieldGet(FieldPos(cCpoFil)),'')
		cChaveA  += IIf(!Empty(cCpoCod),FieldGet(FieldPos(cCpoCod)),'')
		cChaveA  += IIf(!Empty(cCpoLoc),FieldGet(FieldPos(cCpoLoc)),'')
		dbSkip()
		cChaveB := IIf(!Empty(cCpoFil),FieldGet(FieldPos(cCpoFil)),'')
		cChaveB += IIf(!Empty(cCpoCod),FieldGet(FieldPos(cCpoCod)),'')
		cChaveB += IIf(!Empty(cCpoLoc),FieldGet(FieldPos(cCpoLoc)),'')
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Executa o MsUnlockAll caso o dbSkip tenha alterado a Chave   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !(cChaveA==cChaveB)
			MsUnLockall()
			lRet := .T.
		EndIf
	Else
		dbSkip()
	EndIf	
EndIf
Return lRet


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao    ³ Ma215Res   ³ Autor ³Henry Fila           ³ Data ³ 19/05/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡ao ³ Avaliacao da reserva por pedido de venda ou SC9            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Ma215Res(ExpC1,ExpN1)                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do SC0 		                              ³±±
±±³          ³ ExpN2-Quantidade a reservar  			                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA215                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/

Static Function Ma215Res(cAliasSC0,nQtdRes)

Local aArea    := GetArea()
Local aAreaSC0 := SC0->(GetArea())
Local aAreaSDC := SDC->(GetArea())

Local cSeekSDC := ""
Local cAliasSDC:= "SDC"
#IFDEF TOP
	Local aStru    := {}
	Local nX       := 0
	Local cQuery   := ""
#ENDIF
Local lQuery   := .F.

If Localiza((cAliasSC0)->C0_PRODUTO) .And. !IntDL((cAliasSC0)->C0_PRODUTO)
	cSeekSDC := xFilial("SDC")+;
		(cAliasSC0)->C0_PRODUTO+;
		(cAliasSC0)->C0_LOCAL+"SC0"+;
		(cAliasSC0)->C0_NUM+;
		Criavar("DC_ITEM")+;
		Criavar("DC_SEQ")+;
		(cAliasSC0)->C0_LOTECTL+;
		(cAliasSC0)->C0_NUMLOTE+;
		(cAliasSC0)->C0_LOCALIZ+;
		(cAliasSC0)->C0_NUMSERI
		
	dbSelectArea("SDC")
	dbSetOrder(1)
	#IFDEF TOP
		aStru     := SDC->(dbStruct())
		lQuery    := .T.
		cAliasSDC := "SDCMA215PROC1"

		cQuery := "SELECT * "
		cQuery += "FROM "+RetSqlName("SDC")+" SDC "
		cQuery += "WHERE SDC.DC_FILIAL='"+xFilial("SDC")+"' AND "
		cQuery += "SDC.DC_PRODUTO='"+(cAliasSC0)->C0_PRODUTO+"' AND "
		cQuery += "SDC.DC_LOCAL='"+(cAliasSC0)->C0_LOCAL+"' AND "
		cQuery += "SDC.DC_ORIGEM='SC0' AND "
		cQuery += "SDC.DC_PEDIDO='"+(cAliasSC0)->C0_NUM+"' AND "
		cQuery += "SDC.DC_ITEM='"+Criavar("DC_ITEM")+"' AND "
		cQuery += "SDC.DC_SEQ='"+Criavar("DC_SEQ")+"' AND "
		cQuery += "SDC.DC_LOTECTL='"+(cAliasSC0)->C0_LOTECTL+"' AND "
		cQuery += "SDC.DC_NUMLOTE='"+(cAliasSC0)->C0_NUMLOTE+"' AND "
		cQuery += "SDC.DC_LOCALIZ='"+(cAliasSC0)->C0_LOCALIZ+"' AND "
		cQuery += "SDC.DC_NUMSERI='"+(cAliasSC0)->C0_NUMSERI+"' AND "
		cQuery += "SDC.D_E_L_E_T_=' ' "
		cQuery += "ORDER BY "+SqlOrder(SDC->(IndexKey()))

		cQuery := ChangeQuery(cQuery)

		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSDC,.T.,.T.)
		For nX := 1 To Len(aStru)
			If ( aStru[nX][2] <> "C" .And. aStru[nX][2] <> "M" )
				TcSetField(cAliasSDC,aStru[nX][1],aStru[nX][2],aStru[nX][3],aStru[nX][4])
			EndIf
		Next nX
	#ELSE		
		MsSeek(cSeekSDC)
	#ENDIF			
	While !Eof() .And. DC_FILIAL+DC_PRODUTO+DC_LOCAL+DC_ORIGEM+DC_PEDIDO+DC_ITEM+DC_SEQ+DC_LOTECTL+DC_NUMLOTE+DC_LOCALIZ+DC_NUMSERI == cSeekSDC
		GravaEmp((cAliasSDC)->DC_PRODUTO,;
			(cAliasSDC)->DC_LOCAL,;
			(cAliasSDC)->DC_QUANT,;
			(cAliasSDC)->DC_QTSEGUM,;
			(cAliasSDC)->DC_LOTECTL,;
			(cAliasSDC)->DC_NUMLOTE,;
			(cAliasSDC)->DC_LOCALIZ,;
			(cAliasSDC)->DC_NUMSERI,;
			Nil,;
			Nil,;
			(cAliasSDC)->DC_PEDIDO,;
			Nil,;
			"SC0",;
			Nil,;
			Nil,;
			Nil,;
			.F.,;
			.F.,;
			.T.,;
			.F.,;
			NIL,;
			.T.,;
			.F.)
		va215Skip(cAliasSDC)
	EndDo
	If lQuery
		dbSelectArea(cAliasSDC)
		dbCloseArea()
		dbSelectArea("SDC")
	EndIf
Else
	GravaEmp((cAliasSC0)->C0_PRODUTO,;
		(cAliasSC0)->C0_LOCAL,;
		nQtdRes,;
		NIL,;
		(cAliasSC0)->C0_LOTECTL,;
		(cAliasSC0)->C0_NUMLOTE,;
		(cAliasSC0)->C0_LOCALIZ,;
		(cAliasSC0)->C0_NUMSERI,;
		Nil,;
		Nil,;
		(cAliasSC0)->C0_NUM,;
		Nil,;
		"SC0",;
		Nil,;
		Nil,;
		Nil,;
		.F.,;
		.F.,;
		.T.,;
		.F.,;
		Nil,;
		!Empty((cAliasSC0)->C0_LOTECTL+(cAliasSC0)->C0_NUMLOTE+(cAliasSC0)->C0_LOCALIZ+(cAliasSC0)->C0_NUMSERI)) //22
EndIf

RestArea(aAreaSC0)
RestArea(aAreaSDC)
RestArea(aArea)

Return


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MaAvalSW6³ Autor ³ Guilherme Leal        ³ Data ³ 28/11/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Atualizar empenhos das importacoes                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function vMaAvalSW6()
Local cAlias  := "SD1"
Local cFilSW6 := xFilial("SW6")
Local cFilSF1 := xFilial("SF1")
Local cFilSD1 := xFilial("SD1")

#IFDEF TOP
	Local aStru   := {}
	Local cQuery
	Local nX	
	
	
	DbSelectArea("SX3")
	DbSetOrder(2)
	DbSeek("D1_QUANT")
	AAdd(aStru, {SX3->X3_CAMPO, SX3->X3_TIPO, SX3->X3_TAMANHO, SX3->X3_DECIMAL})
	DbSeek("D1_QTSEGUM")
	AAdd(aStru, {SX3->X3_CAMPO, SX3->X3_TIPO, SX3->X3_TAMANHO, SX3->X3_DECIMAL})

	If TcSrvType()<>"AS/400"
		cAlias := "QRY"

		cQuery := "SELECT D1_COD, D1_LOCAL, D1_OP, D1_NUMLOTE, D1_LOTECTL, D1_NUMSEQ, D1_ITEM, D1_QUANT, D1_QTSEGUM " + ;
		"FROM " + RetSqlName("SW6") + " SW6" + ;
		"	INNER JOIN " + RetSqlName("SF1") + " SF1 ON W6_HAWB = F1_HAWB" + ;
		"	INNER JOIN " + RetSqlName("SD1") + " SD1 ON F1_DOC = D1_DOC AND F1_SERIE = D1_SERIE AND F1_FORNECE = D1_FORNECE AND F1_LOJA = D1_LOJA AND F1_TIPODOC = D1_TIPODOC " + ;
		"WHERE W6_DT_ENCE = '' AND SW6.D_E_L_E_T_ = ' ' AND" + ;
		"	SF1.D_E_L_E_T_ = ' ' AND" + ;                                         
		"	SD1.D_E_L_E_T_ = ' ' AND" + ;
		"	SD1.D1_TES    <> ' ' AND" + ;
		"	SF1.F1_TIPO_NF = '5' AND" + ;
		"	W6_FILIAL = '" + xFilial("SW6") + "' AND" + ;
		"	F1_FILIAL = '" + xFilial("SF1") + "' AND" + ;
		"	D1_FILIAL = '" + xFilial("SD1") + "' " //+ ;
//		"GROUP BY D1_COD, D1_LOCAL, D1_OP, D1_NUMLOTE, D1_LOTECTL, D1_NUMSEQ,D1_ITEM"

		cQuery := ChangeQuery(cQuery)

		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAlias)
		For nX := 1 To Len(aStru)
			If ( aStru[nX][2] <> "C" .And. FieldPos(aStru[nX][1])<>0 )
				TcSetField(cAlias,aStru[nX][1],aStru[nX][2],aStru[nX][3],aStru[nX][4])
			EndIf		
		Next nX	
		(cAlias)->(DbGoTop())

		While (cAlias)->(!Eof())
			AtuSaldos(cAlias)
			(cAlias)->(DbSkip())
		EndDo        
		DbSelectArea(cAlias)
		DbCloseArea()      
		DbSelectArea('SD1')
	Else
#ENDIF

		SF1->(DbSetOrder(5))
		SD1->(DbSetOrder(1))
		
		DbSelectArea("SW6")
		DbSetOrder(1)
		DbSeek(cFilSW6)
		While !EOF() .And. W6_FILIAL == cFilSW6
			If !Empty(W6_DT_ENCE)
				DbSkip()
				Loop
			EndIf
			If SF1->(DbSeek(cFilSF1+SW6->W6_HAWB)) .And. ;
				SD1->(DbSeek(cFilSD1+SF1->F1_DOC+SF1->F1_SERIE+SF1->F1_FORNECE+SF1->F1_LOJA))
		
				While SD1->(!Eof()) .And. SF1->(F1_FILIAL+F1_DOC+F1_SERIE+F1_FORNECE+F1_LOJA) == SD1->(D1_FILIAL+D1_DOC+D1_SERIE+D1_FORNECE+D1_LOJA)
					If SF1->F1_TIPODOC == SD1->D1_TIPODOC .And. !Empty(SD1->D1_TES)
						AtuSaldos(cAlias)
					EndIf
					SD1->(DbSkip())
				EndDo
			EndIf
			DbSkip()
		EndDo
#IFDEF TOP
	EndIf
#ENDIF

Return


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ AtuSaldos³ Autor ³ Guilherme Leal        ³ Data ³ 28/11/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Grava empenhos das importacoes                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ AtuSaldos(ExpC1) 	                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arq. a ser atualizado               	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function AtuSaldos(cAlias)
Local aArea 	:= GetArea()
Local nTotQtd2 :=	0    
Local nOrd		:=	0
Local	cSeek 	:= ''
Local	cCondSB8 :=	''
Local	nQtdEmp	:=	0
Local	nQtdEmp2	:=	0                               
Local	nEmpenha	:=	0
Local	nEmpenha2:=	0                               


DbSelectArea("SB2")
DbSetOrder(1) //B2_FILIAL+B2_COD+B2_LOCAL
If !DbSeek(xFilial("SB2") + (cAlias)->D1_COD + (cAlias)->D1_LOCAL)
	CriaSB2((cAlias)->D1_COD + (cAlias)->D1_LOCAL)
EndIf
SB1->(DbSetOrder(1))
SB1->(MsSeek(xFilial()+(cAlias)->D1_COD))
If Empty(SB1->B1_CONV) .And. !Empty((cAlias)->D1_QTSEGUM)
	nTotQtd2 := (cAlias)->D1_QTSEGUM
ElseIf !Empty(SB1->B1_CONV)
	nTotQtd2 := SB1->(ConvUm(SB1->B1_COD,((cAlias)->D1_QUANT),(cAlias)->D1_QTSEGUM,2))
EndIf               

RecLock("SB2",.F.)
If !Empty((cAlias)->D1_OP) // OP PREVISTA
	B2_QEMPPRE += (cAlias)->D1_QUANT
	B2_QEPRE2  += nTotQtd2
Else	// OP FIRME
	B2_QEMP  += (cAlias)->D1_QUANT
	B2_QEMP2 += nTotQtd2
EndIf
MsUnlock()

If Rastro((cAlias)->D1_COD)
	If Rastro((cAlias)->D1_COD,"S") 
		nOrd  := 2
		cSeek := (xFilial("SB8")+(cAlias)->D1_NUMLOTE+(cAlias)->D1_LOTECTL+(cAlias)->D1_COD+(cAlias)->D1_LOCAL)
		cCondSB8 := "B8_FILIAL+B8_NUMLOTE+B8_LOTECTL+B8_PRODUTO+B8_LOCAL=="
		cCondSB8 := "xFilial('SB8')+(cAlias)->D1_NUMLOTE+(cAlias)->D1_LOTECTL+(cAlias)->D1_COD+(cAlias)->D1_LOCAL"
	Else
		nOrd  := 3
		cSeek := (xFilial("SB8")+(cAlias)->D1_COD+(cAlias)->D1_LOCAL+(cAlias)->D1_LOTECTL)
		cCondSB8 := "B8_FILIAL+B8_PRODUTO+B8_LOCAL+B8_LOTECTL=="
		cCondSB8+="xFilial('SB8')+(cAlias)->D1_COD+(cAlias)->D1_LOCAL+(cAlias)->D1_LOTECTL"
	EndIf

  	nQtdEmp	:=	(cAlias)->D1_QUANT                                   
  	nQtdEmp2	:=	nTotQtd2
      	
	DbSelectArea("SB8")
	DbSetOrder(nOrd)
	If DbSeek(cSeek) 
		While !Eof() .And. &(cCondSB8) .And. nQtdEmp >	0
			If SB8->(FieldPos("B8_ITEM")) > 0
				If B8_ITEM == (cAlias)->D1_ITEM
    				nEmpenha	:=	IIf(nQtdEmp  > (SB8->B8_SALDO-SB8->B8_EMPENHO),(SB8->B8_SALDO-SB8->B8_EMPENHO),nQtdEmp)
               nEmpenha2:=	IIf(nQtdEmp2 > (SB8->B8_SALDO2-SB8->B8_EMPENH2),(SB8->B8_SALDO2-SB8->B8_EMPENH2),nQtdEmp2)
					nQtdEmp	-=	nEmpenha
					nQtdEmp2	-=	nEmpenha2
					RecLock("SB8",.F.)
					Replace SB8->B8_EMPENHO With (SB8->B8_EMPENHO + nEmpenha)
					Replace SB8->B8_EMPENH2 With (SB8->B8_EMPENH2 + nEmpenha2)
					MsUnlock()
				EndIf
			Else
           	nEmpenha	:=	IIf(nQtdEmp  > (SB8->B8_SALDO-SB8->B8_EMPENHO),(SB8->B8_SALDO-SB8->B8_EMPENHO),nQtdEmp)
           	nEmpenha2:=	IIf(nQtdEmp2 > (SB8->B8_SALDO2-SB8->B8_EMPENH2),(SB8->B8_SALDO2-SB8->B8_EMPENH2),nQtdEmp2)
				nQtdEmp	-=	nEmpenha
				nQtdEmp2	-=	nEmpenha2
				RecLock("SB8",.F.)
				Replace SB8->B8_EMPENHO With (SB8->B8_EMPENHO + nEmpenha)
				Replace SB8->B8_EMPENH2 With (SB8->B8_EMPENH2 + nEmpenha2)
				MsUnlock()
			EndIf
			DbSkip()
		EndDo			
	EndIf
EndIf

If Localiza((cAlias)->D1_COD) 
	DbSelectArea("SDA")
	DbSetOrder(1)
	If dbSeek(xFilial("SDA")+(cAlias)->D1_COD+(cAlias)->D1_LOCAL+(cAlias)->D1_NUMSEQ)
		RecLock("SDA",.F.)
		DA_EMPENHO  += (cAlias)->D1_QUANT
		DA_EMP2  	+= nTotQtd2
		MsUnlock()
	EndIf
EndIf
RestArea(aArea)
Return


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³MaAvalSC6 ³ Autor ³Eduardo Riera          ³ Data ³13.02.2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Rotina de avaliacao dos eventos do item do Pedido de Venda  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Alias da tabela dos itens do Pedido de Venda         ³±±
±±³          ³ExpN2: Codigo do Evento                                     ³±±
±±³          ³       [1] Implantacao do Pedido de Venda                   ³±±
±±³          ³       [2] Estorno  do Pedido de Venda                      ³±±
±±³          ³       [3] Liberacao do Pedido de Venda                     ³±±
±±³          ³       [4] Estorno da Liberacao do Pedido de Venda          ³±±
±±³          ³       [5] Preparacao da Nota Fiscal de Saida               ³±±
±±³          ³       [6] Estorno da Nota Fiscal de Saida                  ³±±
±±³          ³ExpC3: Alias do pedido de venda                             ³±±
±±³          ³ExpL4: Liberacao Parcial                                    ³±±
±±³          ³ExpL5: Transfere Locais                                     ³±±
±±³          ³ExpL6: Verifica se todos os itens foram liberados           ³±±
±±³          ³ExpL7: Verifica os residuos do pedido de venda              ³±±
±±³          ³ExpL8: Verifica se todos os itens foram faturados           ³±±
±±³          ³ExpL9: Atualiza somente os acumulados                       ³±±
±±³          ³ExpNA: Valor a ser adicionado ao limite de credito          ³±±
±±³          ³ExpCB: Alias do SD2 ( Uso Interno para otimizacao )         ³±±
±±³          ³ExpLC: Indica se esta o SC6 esta sendo baixado por un REMITO³±±
±±³          ³ExpCD: Moeda do pedido de venda ( opcional )                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Esta rotina tem como objetivo atualizar os eventos vinculado³±±
±±³          ³ao item do pedido de venda.                                 ³±±
±±³          ³A) Atualizacao das tabelas complementares.                  ³±±
±±³          ³B) Atualizacao das informacoes complementares do PV         ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Materiais                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Michel M. ³29/03/07³8.11  ³Bops: 119212 - Corrigido nao-conformidade   ³±±
±±³          ³        ³      ³ao tentar Travar registro com o SA1 em EOF. ³±±
±±³          ³        ³      ³                                        	  ³±±
±±³Norbert W.³12/04/07³8.11  ³Bops: 122632 - Removido o codigo onde o cam-³±±
±±³          ³        ³      ³po C5_BLQ era limpo a cada validacao do SC6,³±±
±±³          ³        ³      ³eliminando o bloqueio de um item anterior.  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function vMaAvalSC6(cAliasSC6,;
							nEvento,;
							cAliasSC5,;
							lLiber,;
							lTransf,;
							lLiberOk,;
							lResidOk,;
							lFaturOk,;
							lAcumulado,;
							nVlrCred,;
							cAliasSD2,;
							lRemito,;
							nMoeda)

Local aArea    := GetArea(cAliasSC6)
Local aAreaSA1 := SA1->(GetArea())
Local aAreaSB2 := SB2->(GetArea())
Local aAreaSF4 := SF4->(GetArea())
Local aProdDesc:= {}
Local nQtdRese := 0
Local nQtdLib  := 0
Local nQtdLib2 := 0
Local nSldPed  := 0
Local nSldPed2 := 0
Local nMCusto  := 0

Local nSaveSX8 := GetSX8Len()
Local cReserva := ""
Local cPedido  := ""
Local cItemPV  := ""
Local cProduto := ""
Local cTipoPV  := ""
Local cTipLib  := ""
Local cRet     := ""
Local dEmissao := Ctod("")
Local lContinua:= .T.
Local lCredito := .F.
Local lEstoque := .F.
Local lTipoAB7 := .F.
Local lResAut  := SuperGetMv("MV_RESAUT")
Local lTipRes  := SuperGetMv("MV_TIPRES")


#IFNDEF TOP
	Local nRecACM  := 0
#ENDIF

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Inicializa variaveis                                                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DEFAULT cAliasSC5 := "SC5"
DEFAULT cAliasSC6 := "SC6"
DEFAULT cAliasSD2 := "SD2"
DEFAULT lLiber    := .F.
DEFAULT lTransf   := .F.
DEFAULT lAcumulado:= .F.
DEFAULT lRemito   := .F.

If !xFilial("SC5")+(cAliasSC6)->C6_NUM==(cAliasSC5)->C5_FILIAL+(cAliasSC5)->C5_NUM .And.;
		!Empty((cAliasSC6)->C6_NUM)
	dbSelectArea("SC5")
	dbSetOrder(1)
	MsSeek(xFilial("SC5")+(cAliasSC6)->C6_NUM)
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Inicializa a moeda do pedido                                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DEFAULT nMoeda    := (cAliasSC5)->C5_MOEDA

cPedido  := (cAliasSC5)->C5_NUM
cItemPV  := (cAliasSC6)->C6_ITEM
cProduto := (cAliasSC6)->C6_PRODUTO
cTipoPV  := (cAliasSC5)->C5_TIPO
cTipLib  := (cAliasSC5)->C5_TIPLIB
dEmissao := (cAliasSC5)->C5_EMISSAO

Do Case
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Implantacao de um item do pedido de venda                               ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Case nEvento == 1

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Posiciona registros                                                     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If !cTipoPV$'DB'
		dbSelectArea("SA1")
		dbSetOrder(1)
		MsSeek(xFilial("SA1")+(cAliasSC5)->C5_CLIENTE+(cAliasSC5)->C5_LOJACLI,.F.)
	EndIf
	dbSelectArea("SF4")
	dbSetOrder(1)
	MsSeek(xFilial("SF4")+(cAliasSC6)->C6_TES)
	dbSelectArea("SB2")
	dbSetOrder(1)
	If !MsSeek(xFilial("SB2")+(cAliasSC6)->C6_PRODUTO+(cAliasSC6)->C6_LOCAL)
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Sempre criar o SB2 para otimizacao da Query do MATA461                  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		CriaSB2((cAliasSC6)->C6_PRODUTO,(cAliasSC6)->C6_LOCAL)
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Efetua travamento dos registros                                         ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lContinua .And. SF4->F4_DUPLIC=="S" .And. !cTipoPV$'DB'
		DBSelectArea("SA1")
		SA1->( DBSetOrder(1) )
		If ( lContinua := SA1->( MsSeek(xFilial("SA1")+(cAliasSC5)->C5_CLIENTE+(cAliasSC5)->C5_LOJACLI,.F.) ) )
			lContinua := RecLock("SA1")
		EndIf
	EndIf
	If lContinua .And. SF4->F4_ESTOQUE=="S"
		lContinua := RecLock("SB2")
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Avalia se existe regra de negocio ou bloqueio para verificar verba      ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	If !__lPyme

		If (cAliasSC5)->C5_TIPO == 'N' .And. !Empty(ACN->(LastRec()))
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Verifica verba de venda caso nao houve bloqueio de regra de negocio     ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If !lAcumulado
				nDescon   := (100 - ((cAliasSC6)->C6_PRCVEN / (cAliasSC6)->C6_PRUNIT) * 100 )
				aProdDesc := {{(cAliasSC6)->C6_PRODUTO,(cAliasSC6)->C6_ITEM,(cAliasSC6)->C6_PRCVEN,(cAliasSC6)->C6_PRUNIT,nDescon,0,"",0,Empty((cAliasSC6)->C6_NFORI)}}

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Avalia se Existe Bloqueio de Regra                                      ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If !ACN->(FtRegraNeg((cAliasSC5)->C5_CLIENTE,(cAliasSC5)->C5_LOJACLI,(cAliasSC5)->C5_TABELA,(cAliasSC5)->C5_CONDPAG,,@aProdDesc, .F., .T. ))
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³Avalia se existe Bloqueio de Venda                                      ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If aProdDesc[1,7] == "02"
						cRet := ACN->(FtVerbaVen(cAliasSC5,cAliasSC6,(cAliasSC5)->C5_CLIENTE,(cAliasSC5)->C5_LOJACLI,(cAliasSC5)->C5_VEND1,(cAliasSC6)->C6_PRODUTO, @aProdDesc[1],.T. ))
						If cRet <> "1"
							cRet := If(cRet == "3","1","2")
						Else
							cRet := "3"
						EndIf
					Else
						cRet := "1"
					EndIf
				Else
					cRet := "3"
				EndIf
				Do Case
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³Grava SC5 / SC6 bloqueados por Verba                                    ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					Case cRet == "2"
						RecLock("SC5", .F.)
						(cAliasSC5)->C5_BLQ := StrZero(2, Len(SC5->C5_BLQ))

						RecLock("SC6", .F.)	
						(cAliasSC6)->C6_BLOQUEI :=  StrZero(2, Len(SC6->C6_BLOQUEI))
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³Grava SC5 / SC6 bloqueado por Regra                                     ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					Case cRet == "1"
						RecLock("SC5", .F.)
						(cAliasSC5)->C5_BLQ := StrZero(1, Len(SC5->C5_BLQ))

						RecLock("SC6", .F.)
						(cAliasSC6)->C6_BLOQUEI := StrZero(1, Len(SC6->C6_BLOQUEI))
					OtherWise
						RecLock("SC6", .F.)
						(cAliasSC6)->C6_BLOQUEI := Space(Len(SC6->C6_BLOQUEI))
				EndCase
			EndIf
		EndIf
	EndIf

	If lContinua
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Complemento da atualizacao no item do pedido de venda                  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !lAcumulado

			(cAliasSC6)->C6_FILIAL	:= xFilial("SC6")
			(cAliasSC6)->C6_NUM		:= (cAliasSC5)->C5_NUM
			(cAliasSC6)->C6_CLI		:= (cAliasSC5)->C5_CLIENTE
			(cAliasSC6)->C6_LOJA 	:= (cAliasSC5)->C5_LOJACLI

			If cPaisLoc <> "BRA".And. Empty((cAliasSC6)->C6_GERANF)
				(cAliasSC6)->C6_GERANF := If((cAliasSC5)->C5_TIPOREM$"01A","S","N")
			EndIf
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Atualiza as Tabela Auxiliares                                          ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Efetua amarracao com o projeto - SIGATEC                         ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			SC5->(FkCommit())
			SC6->(FkCommit())

			ABI->( dbSetOrder( 1 ) )
			If ABI->( MsSeek( xFilial( "ABI" ) + (cAliasSC6)->C6_PROJET + (cAliasSC6)->C6_ITPROJ ) )
				Reclock( "ABI", .F. )
				ABI->ABI_NUMPV  := (cAliasSC6)->C6_NUM
				ABI->ABI_ITEMPV := (cAliasSC6)->C6_ITEM
				ABI->( MsUnlock() )
			EndIf

			dbSelectArea("AB8")
			dbSetOrder(1)
			If !Empty((cAliasSC6)->C6_NUMOS)
				If MsSeek(xFilial("AB8")+(cAliasSC6)->C6_NUMOS)
					RecLock("AB8")
					AB8->AB8_NUMPV := M->C5_NUM+SC6->C6_ITEM
					dbSelectArea("AB7")
					dbSetOrder(1)
					If MsSeek(xFilial("AB7")+SubStr(SC6->C6_NUMOS,1,8))
						RecLock("AB7")
						AB7->AB7_TIPO := AtTipoAB7()
					EndIf
				EndIf
			EndIf
			If !Empty((cAliasSC6)->C6_NUMOSFA)
				If ( MsSeek(xFilial("AB8")+(cAliasSC6)->C6_NUMOSFA ) )
					RecLock("AB8")
					AB8->AB8_NUMPVF := M->C5_NUM+SC6->C6_ITEM
					dbSelectArea("AB7")
					dbSetOrder(1)
					If MsSeek(xFilial("AB7")+SubStr(SC6->C6_NUMOSFA,1,8))
						RecLock("AB7")
						AB7->AB7_TIPO := AtTipoAB7()
					EndIf
				EndIf
			EndIf
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Atualiza o status da OS ( AB6 )                                  ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If !Empty((cAliasSC6)->C6_NUMOS) .OR. !Empty((cAliasSC6)->C6_NUMOSFA)
				dbSelectArea("AB6")
				dbSetOrder(1)
				If MsSeek(xFilial("AB6") + If( Empty( SubStr(SC6->C6_NUMOS,1,6) ),;
					SubStr(SC6->C6_NUMOSFA,1,6), SubStr(SC6->C6_NUMOS,1,6) ) )
					RecLock("AB6")
					AB6->AB6_STATUS := AtOsStatus( AB6->AB6_NUMOS )
				EndIf
			EndIf
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Atualiza Contratos de Parceria                                         ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If !Empty(SC6->C6_CONTRAT) .And. !Empty(SC6->C6_ITEMCON)
				dbSelectArea("ADB")
				dbSetOrder(1)
				If MsSeek(xFilial("ADB")+SC6->C6_CONTRAT+SC6->C6_ITEMCON)
					RecLock("ADB")
					If Empty(ADB->ADB_PEDCOB) .And. SC6->C6_TES==ADB->ADB_TESCOB
						ADB->ADB_PEDCOB := SC6->C6_NUM
					Else
						ADB->ADB_QTDEMP += SC6->C6_QTDVEN
						If ADB->ADB_QTDEMP > ADB->ADB_QUANT
							ADB->ADB_QTDEMP -= SC6->C6_QTDVEN
							(cAliasSC6)->C6_CONTRAT := ""
							(cAliasSC6)->C6_ITEMCON := ""
						EndIf
					EndIf
					MsUnLock()
					dbSelectArea("ADA")
					dbSetOrder(1)
					If MsSeek(xFilial("ADA")+SC6->C6_CONTRAT)
						Ft400StatCt()
					EndIf
				EndIf
			EndIf
		EndIf
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Atualiza os Saldos Fisicos e Financeiros - SB2                         ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !("S" $ (cAliasSC6)->C6_BLQ .Or. "R" $ (cAliasSC6)->C6_BLQ) //Se nao Bloqueado
			If SF4->F4_ESTOQUE == "S"
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Verificar se o material com lote deve ser reservado mesmo que nao haja  ³
				//³liberacao de pedido, a fim de garantir o lote na data do faturamento    ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If !lAcumulado

					If !Empty((cAliasSC6)->C6_RESERVA)
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³Se a reserva estiver preenchida, verificar se e valida                  ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						SC0->( dbSetOrder( 1 ) )
						If !SC0->( MsSeek( xFilial( "SC0" ) + (cAliasSC6)->C6_RESERVA + (cAliasSC6)->C6_PRODUTO) )
							(cAliasSC6)->C6_RESERVA := ""
							(cAliasSC6)->C6_QTDRESE := 0
						EndIf
					EndIf

					If lResAut .And. Empty((cAliasSC6)->C6_RESERVA) .And. Rastro((cAliasSC6)->C6_PRODUTO) .And. ;
						( (cAliasSC6)->C6_QTDVEN - (cAliasSC6)->C6_QTDEMP - (cAliasSC6)->C6_QTDENT > 0 )

						If !Empty((cAliasSC6)->C6_NUMLOTE) .Or. !Empty((cAliasSC6)->C6_LOTECTL)
							cReserva := CriaVar("C0_NUM")

							//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
							//³Verifica se a reserva sera feita pelo total da quantidade vendida³
							//³ou pelo saldo do lote.                                           ³
							//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
							If lTipRes
								nSaldo := (cAliasSC6)->C6_QTDVEN
							Else
								nSaldo   := SldAtuEst((cAliasSC6)->C6_PRODUTO,;
									(cAliasSC6)->C6_LOCAL,;
									(cAliasSC6)->C6_QTDVEN,;
									(cAliasSC6)->C6_LOTECTL,;
									(cAliasSC6)->C6_NUMLOTE,;
									(cAliasSC6)->C6_LOCALIZ,;
									(cAliasSC6)->C6_NUMSERI,;
									(cAliasSC6)->C6_RESERVA,;
									SF4->F4_PODER3<>"N" .Or. (SF4->(FieldPos("F4_TESP3"))<>0 .And. !Empty(SF4->F4_TESP3)),;
									,;
									(cAliasSC6)->C6_PROJPMS,;
									(cAliasSC6)->C6_TASKPMS,;
									(cAliasSC6)->C6_SERVIC)
							Endif

							If Empty(cReserva)
								cReserva := NextNumero("SC0",1,"C0_NUM",.T.)
							Else
								While ( GetSX8Len() > nSaveSX8 )
									ConfirmSx8()
								EndDo
							EndIf
							If a430Reserva({1,"VD",(cAliasSC6)->C6_NUM,"",cFilAnt},cReserva,;
									(cAliasSC6)->C6_PRODUTO,;
									(cAliasSC6)->C6_LOCAL,;
									nSaldo,;
									{(cAliasSC6)->C6_NUMLOTE,(cAliasSC6)->C6_LOTECTL,(cAliasSC6)->C6_LOCALIZ,(cAliasSC6)->C6_NUMSERI})

								(cAliasSC6)->C6_RESERVA := cReserva

							EndIf
						EndIf
					EndIf
				EndIf
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Atualiza previsoes de saida de material                                 ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				nSldPed := Max((cAliasSC6)->C6_QTDVEN-(cAliasSC6)->C6_QTDENT-(cAliasSC6)->C6_QTDEMP,0)
				nSldPed2:= SB1->(ConvUm(SB2->B2_COD,nSldPed,nSldPed2,2))
				RecLock("SB2")
				If (cAliasSC6)->C6_OP $ "01#03#05#08"
					SB2->B2_QEMPN  += nSldPed
					SB2->B2_QEMPN2 += nSldPed2
				EndIf
				SB2->B2_QPEDVEN += nSldPed
				SB2->B2_QPEDVE2 += nSldPed2
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Tratamento para Reserva de material                  ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If !lAcumulado
					If !Empty((cAliasSC6)->C6_RESERVA)
						If !cTipoPV$"CIP"
							dbSelectArea("SC0")
							dbSetOrder(1)
							If MsSeek(xFilial("SC0")+(cAliasSC6)->C6_RESERVA+(cAliasSC6)->C6_PRODUTO+(cAliasSC6)->C6_LOCAL)
								If !((cAliasSC6)->C6_NUMLOTE<>SC0->C0_NUMLOTE   .Or.;
									(cAliasSC6)->C6_LOTECTL<>SC0->C0_LOTECTL  .Or.;
									(cAliasSC6)->C6_LOCALIZ<>SC0->C0_LOCALIZ  .Or.;
									(cAliasSC6)->C6_NUMSERI<>SC0->C0_NUMSERI )
									RecLock("SC0")
									nQtdRese := Min(SC0->C0_QUANT,(cAliasSC6)->C6_QTDVEN)
									SC0->C0_QUANT  -= nQtdRese
									SC0->C0_TIPO   := "PD"
									SC0->C0_QTDPED += nQtdRese
								EndIf
							Endif
							//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
							//³ Atualiza o campo de qtd.reservada do Pedido de Venda ³
							//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
							(cAliasSC6)->C6_QTDRESE += nQtdRese
							//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
							//³Nao deve atualizar a Quantidade em Pedido de Venda quando houver Reserva³
							//³pois neste caso o estoque ja esta liberado.                             ³
							//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
							SB2->B2_QPEDVEN -= nQtdRese
							SB2->B2_QPEDVE2 -= ConvUM(SB2->B2_COD,nQtdRese,0,2)
						Else
							(cAliasSC6)->C6_RESERVA := ""
						EndIf
					Else
						(cAliasSC6)->C6_RESERVA := ""
					EndIf
				Else	
					SB2->B2_QPEDVEN -= (cAliasSC6)->C6_QTDRESE
					SB2->B2_QPEDVE2 -= ConvUM(SB2->B2_COD,(cAliasSC6)->C6_QTDRESE,0,2)
				EndIf
			EndIf
		EndIf
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Atualiza o Saldo de Pedidos - SA1                                       ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If SF4->F4_DUPLIC == "S" .And. !cTipoPV$"DB"
			nSldPed := Max((cAliasSC6)->C6_QTDVEN-(cAliasSC6)->C6_QTDENT-(cAliasSC6)->C6_QTDEMP,0)
			If nSldPed > 0 .And. !AllTrim((cAliasSC6)->C6_BLQ)$"RS"
				nMCusto :=  If(SA1->A1_MOEDALC > 0,SA1->A1_MOEDALC, Val(SuperGetMv("MV_MCUSTO")))
				SA1->A1_SALPED += xMoeda(nSldPed*(cAliasSC6)->C6_PRCVEN,nMoeda,nMCusto,dEmissao)
			EndIf
		EndIf
		If !lAcumulado .And. !("S" $ (cAliasSC6)->C6_BLQ .Or. "R" $ (cAliasSC6)->C6_BLQ)
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Verifica a necessidade de Liberacao do Item                             ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			SC5->(FkCommit())
			SC6->(FkCommit())
			vMaAvalSC6(cAliasSC6,3,cAliasSC5,lLiber,Nil,Nil,Nil,Nil,Nil,@nVlrCred)
		EndIf
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Verificacao dos acumulados do SC5                                       ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ( SC6->C6_QTDVEN > (SC6->C6_QTDEMP+SC6->C6_QTDENT) .And. AllTrim(SC6->C6_BLQ)<>"R")
		lLiberOk := .F.
	EndIf
	If (! "R" $ SC6->C6_BLQ )
		lResidOk := .F.
	EndIf
	If ( SC6->C6_QTDVEN > SC6->C6_QTDENT .And. AllTrim(SC6->C6_BLQ)<>"R" ) .Or. ( SF4->F4_QTDZERO=="1" .And. !lAcumulado )
		lFaturOk := .T.
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Grava os lancamentos nas contas orcamentarias SIGAPCO    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	PcoDetLan("000100","01","MATA410")

	SC5->(FkCommit())
	SC6->(FkCommit())
	SC9->(FkCommit())
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Estorno de um item do pedido de venda                                   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Case nEvento == 2


	If !__lPyme

		If !Empty(ACM->(LastRec()))

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Busca movimentacao das verbas                                           ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

			#IFDEF TOP

				cQuery := "SELECT ACM_OPERA, ACM.R_E_C_N_O_ RECACM, ACL.R_E_C_N_O_ RECACL FROM "
				cQuery += RetSqlName("ACM")+ " ACM, "
				cQuery += RetSqlName("ACL")+ " ACL  "
				cQuery += " WHERE "
				cQuery += "ACM_FILIAL = '"+xFilial("ACM")+"' AND "
				cQuery += "ACM_NUMPED = '"+(cAliasSC6)->C6_NUM+"' AND "
				cQuery += "ACM_ITEPED = '"+(cAliasSC6)->C6_ITEM+"' AND "
				cQuery += "ACM.D_E_L_E_T_ = ' ' AND "
				cQuery += "ACL_FILIAL = '"+xFilial("ACL")+"' AND "
				cQuery += "((ACL_CODPRO = '"+cProduto+"' AND ACL_CODVER = ACM_CODVER ) OR "
				cQuery += "(ACL_CODPRO = '"+Space(Len(SB1->B1_COD))+"' AND ACL_CODVER = ACM_CODVER )) AND "
				cQuery += "ACL.D_E_L_E_T_ = ' ' "

				cQuery := ChangeQuery(cQuery)

				dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),"QRYACL",.T.,.T.)

				While QRYACL->(!Eof())

					ACL->(MsGoto(QRYACL->RECACL))
					ACM->(MsGoto(QRYACL->RECACM))

					Do Case

						Case QRYACL->ACM_OPERA == "D"
							RecLock("ACL",.F.)
								ACL->ACL_SALDO += ACM->ACM_VALOR
							MsUnlock()
						Case QRYACL->ACM_OPERA == "C"
							RecLock("ACL",.F.)
								ACL->ACL_SALDO -= ACM->ACM_VALOR
							MsUnlock()
					EndCase

					RecLock("ACM",.F.)
						dbDelete()
					MsUnlock()

					QRYACL->(dbSkip())

				EndDo

				(cAliasSC6)->C6_BLOQUEI := Space(Len(SC6->C6_BLOQUEI))

				DbSelectArea("QRYACL")
				DbCloseArea()
				DbSelectArea("SC6")

			#ELSE

				ACM->(dbSetOrder(2))
				If ACM->(MsSeek(xFilial("ACM")+(cAliasSC6)->C6_NUM+(cAliasSC6)->C6_ITEM))

					nRecACM := 0

					While ACM->(!Eof()) .And. ACM->ACM_FILIAL == xFilial("ACM") .And. ;
												ACM->ACM_NUMPED == (cAliasSC6)->C6_NUM .And. ;
												ACM->ACM_ITEPED == (cAliasSC6)->C6_ITEM

						ACM->(dbSkip())
						nRecACM := ACM->(Recno())
						ACM->(dbSkip(-1))

						ACL->(dbSetOrder(2))
						If !ACL->(MsSeek(xFilial("ACL")+cProduto+ACM->ACM_CODVER))
							ACL->(MsSeek(xFilial("ACL")+Space(Len(SB1->B1_COD))+ACM->ACM_CODVER))	
						EndIf

						If ACL->(Found())

							Do Case

								Case ACM->ACM_OPERA == "D"
									RecLock("ACL",.F.)
										ACL->ACL_SALDO += ACM->ACM_VALOR
									MsUnlock()
								Case ACM->ACM_OPERA == "C"
									RecLock("ACL",.F.)
										ACL->ACL_SALDO -= ACM->ACM_VALOR
									MsUnlock()
							EndCase

						EndIf

						RecLock("ACM",.F.)
							dbDelete()
						MsUnlock()

						ACM->(MsGoto(nRecACM))

					Enddo

					(cAliasSC6)->C6_BLOQUEI := Space(Len(SC6->C6_BLOQUEI))

				EndIf

			#ENDIF

		EndIf
	Endif


	If !cTipoPV$'DB'
		dbSelectArea("SA1")
		dbSetOrder(1)
		MsSeek(xFilial("SA1")+(cAliasSC6)->C6_CLI+(cAliasSC6)->C6_LOJA,.F.)
	EndIf
	dbSelectArea("SF4")
	dbSetOrder(1)
	MsSeek(xFilial("SF4")+(cAliasSC6)->C6_TES)
	dbSelectArea("SB2")
	dbSetOrder(1)
	If !MsSeek(xFilial("SB2")+(cAliasSC6)->C6_PRODUTO+(cAliasSC6)->C6_LOCAL)
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Sempre criar o SB2 para otimizacao da Query do MATA461                  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		CriaSB2((cAliasSC6)->C6_PRODUTO,(cAliasSC6)->C6_LOCAL )
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Verifica a necessidade de estorno da liberacao                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	vMaAvalSC6(cAliasSC6,4,cAliasSC5,lLiber,Nil,Nil,Nil,Nil,Nil,@nVlrCred)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Efetua travamento dos registros                                         ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lContinua .And. SF4->F4_DUPLIC=="S" .And. !cTipoPV$'DB'
		lContinua := RecLock("SA1")
	EndIf
	If lContinua .And. SF4->F4_ESTOQUE=="S"
		lContinua := RecLock("SB2")
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Estorna as Tabela Auxiliares                                           ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lContinua
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Estorna o Saldo de Pedidos - SA1                                        ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If ( SF4->F4_DUPLIC=="S" .And. !cTipoPV$"DB" )
			dbSelectArea("SA1")
			dbSetOrder(1)
			If ( MsSeek(xFilial("SA1")+(cAliasSC6)->C6_CLI+(cAliasSC6)->C6_LOJA,.F.) )
				nSldPed := Max((cAliasSC6)->C6_QTDVEN-(cAliasSC6)->C6_QTDENT-(cAliasSC6)->C6_QTDEMP,0)
				If ( nSldPed > 0 .And. !AllTrim((cAliasSC6)->C6_BLQ)$"RS" )
					nMCusto :=  If(SA1->A1_MOEDALC > 0,SA1->A1_MOEDALC, Val(SuperGetMv("MV_MCUSTO")))
					SA1->A1_SALPED -= xMoeda(nSldPed*(cAliasSC6)->C6_PRCVEN,nMoeda,nMCusto,dEmissao)
				EndIf
			EndIf
		EndIf
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Atualiza os Saldos Fisicos e Financeiros - SB2                         ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !("S"$(cAliasSC6)->C6_BLQ .Or. "R"$(cAliasSC6)->C6_BLQ)
			If SF4->F4_ESTOQUE == "S"
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Tratamento para Reserva de material                  ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If !Empty((cAliasSC6)->C6_RESERVA)
					dbSelectArea("SC0")
					dbSetOrder(1)
					If MsSeek(xFilial("SC0")+(cAliasSC6)->C6_RESERVA+(cAliasSC6)->C6_PRODUTO+(cAliasSC6)->C6_LOCAL)
						RecLock("SC0")
						SC0->C0_TIPO   := If(SC0->C0_QTDPED==0,"VD","PD")
						SC0->C0_QUANT  += SC6->C6_QTDRESE
						SC0->C0_QTDPED -= SC6->C6_QTDRESE
					EndIf

					SB2->B2_QPEDVEN += (cAliasSC6)->C6_QTDRESE

					(cAliasSC6)->C6_QTDRESE := 0
					If SuperGetMv("MV_DELRES")
						(cAliasSC6)->C6_RESERVA := ""
						a430Reserva({3,SC0->C0_TIPO,SC0->C0_DOCRES,SC0->C0_SOLICIT,SC0->C0_FILRES},;
							SC0->C0_NUM,;
							SC0->C0_PRODUTO,;
							SC0->C0_LOCAL,;
							SC0->C0_QUANT,;
							{SC0->C0_NUMLOTE,;
							SC0->C0_LOTECTL,;
							SC0->C0_LOCALIZ,;
							SC0->C0_NUMSERI})
					EndIf
				EndIf

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Atualiza previsoes de saida de material                                 ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				nSldPed := Max((cAliasSC6)->C6_QTDVEN-(cAliasSC6)->C6_QTDENT-(cAliasSC6)->C6_QTDEMP,0)
				nSldPed2:= SB1->(ConvUm(SB2->B2_COD,nSldPed,nSldPed2,2))

				RecLock("SB2")
				If (cAliasSC6)->C6_OP $ "01#03#05#08"
					SB2->B2_QEMPN  -= nSldPed
					SB2->B2_QEMPN2 -= nSldPed2
				EndIf
				SB2->B2_QPEDVEN -= nSldPed
				SB2->B2_QPEDVE2 -= nSldPed2

			EndIf
		EndIf
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Atualiza Contratos de Parceria                                         ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If !Empty(SC6->C6_CONTRAT) .And. !Empty(SC6->C6_ITEMCON)
		dbSelectArea("ADB")
		dbSetOrder(1)
		If MsSeek(xFilial("ADB")+SC6->C6_CONTRAT+SC6->C6_ITEMCON)
			RecLock("ADB")
			If !Empty(ADB->ADB_PEDCOB) .And. SC6->C6_NUM==ADB->ADB_PEDCOB
				ADB->ADB_PEDCOB := ""
			Else
				ADB->ADB_QTDEMP -= SC6->C6_QTDVEN
			EndIf
			MsUnLock()
			dbSelectArea("ADA")
			dbSetOrder(1)
			If MsSeek(xFilial("ADA")+SC6->C6_CONTRAT)
				Ft400StatCt()
			EndIf
		EndIf
	EndIf	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Estorna amarracao com a Ordem de Servico - SIGATEC               ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ( !Empty((cAliasSC6)->C6_NUMOS) .Or. !Empty((cAliasSC6)->C6_NUMOSFA) )
		lTipoAb7 := .T.
		dbSelectArea("AB8")
		dbSetOrder(1)
		If ( MsSeek(xFilial("AB8")+SC6->C6_NUMOSFA) .And. !Empty(SC6->C6_NUMOSFA))
			RecLock("AB8")
			AB8->AB8_NUMPVF := ""
		EndIf
		If ( MsSeek(xFilial("AB8")+SC6->C6_NUMOS) .And. !Empty(SC6->C6_NUMOS))
			RecLock("AB8")
			AB8->AB8_NUMPV := ""
		EndIf
		If MsSeek(xFilial("AB8")+SubStr(SC6->C6_NUMOS,1,8))
			While ( !Eof() .And. xFilial("AB8") == AB8->AB8_FILIAL .And.;
					AB8->AB8_NUMOS+AB8->AB8_ITEM == SubStr(SC6->C6_NUMOS,1,8) )
				If ( !Empty(AB8->AB8_NUMPV) .Or. !Empty(AB8->AB8_NUMPVF) )
					lTipoAb7 := .F.
				EndIf
				dbSelectArea("AB8")
				dbSkip()
			EndDo
		EndIf
		If ( lTipoAb7 )
			dbSelectArea("AB7")
			dbSetOrder(1)

			If !Empty( SubStr(SC6->C6_NUMOS,1,8))
				If MsSeek(xFilial("AB7")+SubStr(SC6->C6_NUMOS,1,8))
					RecLock("AB7")
					AB7->AB7_TIPO := AtTipoAB7()
				EndIf
			EndIf

			If !Empty( SubStr(SC6->C6_NUMOSFA,1,8))
				If MsSeek(xFilial("AB7")+SubStr(SC6->C6_NUMOSFA,1,8))
					RecLock("AB7")
					AB7->AB7_TIPO := AtTipoAB7()
				EndIf
			EndIf
			If !Empty((cAliasSC6)->C6_NUMOS) .OR. !Empty((cAliasSC6)->C6_NUMOSFA)
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Atualiza o status da OS ( AB6 )                                  ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				dbSelectArea("AB6")
				dbSetOrder(1)
				If MsSeek(xFilial("AB6") + If( Empty( SubStr(SC6->C6_NUMOS,1,6) ),;
						SubStr(SC6->C6_NUMOSFA,1,6), SubStr(SC6->C6_NUMOS,1,6) ) )
					RecLock("AB6")
					AB6->AB6_STATUS := AtOsStatus( AB6->AB6_NUMOS )
				EndIf
			EndIf
		EndIf
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Estorna amarracao com o projeto - SIGATEC                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	ABI->( dbSetOrder( 3 ) )
	If ABI->( MsSeek( xFilial( "ABI" ) + (cAliasSC6)->C6_NUM + (cAliasSC6)->C6_ITEM ) )
		Reclock( "ABI", .F. )
		ABI->ABI_NUMPV  := Space( Len( ABI->ABI_NUMPV ) )
		ABI->ABI_ITEMPV := Space( Len( ABI->ABI_ITEMPV ) )
		ABI->( MsUnlock() )
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Liberacao de um item do pedido de venda                                 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Case nEvento == 3
	nQtdLib  := (cAliasSC6)->C6_QTDLIB
	nQtdLib2 := If( Empty( (cAliasSC6)->C6_QTDLIB2 ), Nil, (cAliasSC6)->C6_QTDLIB2 )
	If nQtdLib > 0 .Or. cTipoPV$"CIP" .Or. MaTesSel((cAliasSC6)->C6_TES)
		If cTipLib<>"2"
			MaLibDoFat(0,@nQtdLib,@lCredito,@lEstoque,.T.,.T.,lLiber,lTransf,Nil,Nil,Nil,Nil,Nil,@nVlrCred,@nQtdLib2)
		EndIf
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Estorno de um item do pedido de venda                                   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Case nEvento == 4
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Estornar as liberacoes efetuadas desde que nao possuam NFS              ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("SC9")
	dbSetOrder(1)
	MsSeek(xFilial("SC9")+cPedido+cItemPV)
	While !Eof() .And. xFilial("SC9")==SC9->C9_FILIAL .And.;
			cPedido==SC9->C9_PEDIDO .And.;
			cItemPv==SC9->C9_ITEM
		If cProduto==SC9->C9_PRODUTO
			If SC9->C9_BLCRED <> "ZZ" .And. SC9->C9_BLEST <> "ZZ" .And. SC9->C9_BLCRED <> "10" .And. SC9->C9_BLEST <> "10" .And. Empty(SC9->C9_REMITO)
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Efetua o estorno das movimentacoes internas                        ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				MaTrfLocal((cAliasSC6)->C6_PRODUTO, , , ,(cAliasSC6)->C6_NUM,.T.)
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Efetua o estorno do itens de pedido de venda liberados             ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				SC9->(a460Estorna(.T.,,@nVlrCred))
			EndIf
		EndIf
		dbSelectArea("SC9")
		dbSkip()
	EndDo
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Preparacao da Nota Fiscal de Saida                                      ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Case nEvento == 5
	If lAcumulado
		RecLock("SC6",.F.)
	EndIf
	If lRemito .Or. Empty( (cAliasSD2)->D2_REMITO )
		(cAliasSC6)->C6_QTDENT  += (cAliasSD2)->D2_QUANT
		(cAliasSC6)->C6_QTDENT2 += (cAliasSD2)->D2_QTSEGUM
		If !lAcumulado
			(cAliasSC6)->C6_QTDEMP  -= (cAliasSD2)->D2_QUANT
			(cAliasSC6)->C6_QTDEMP2 := Max( (cAliasSC6)->C6_QTDEMP2 - (cAliasSD2)->D2_QTSEGUM, 0 )
		EndIf
	Endif
	If !lRemito
		(cAliasSC6)->C6_NOTA   := (cAliasSD2)->D2_DOC
		(cAliasSC6)->C6_SERIE  := (cAliasSD2)->D2_SERIE
		(cAliasSC6)->C6_DATFAT := (cAliasSD2)->D2_EMISSAO
		If (cAliasSC6)->C6_QTDRESE == 0
			(cAliasSC6)->C6_RESERVA := ""
		EndIf
	Else
		(cAliasSC6)->C6_NOTA   := "REMITO"
		(cAliasSC6)->C6_SERIE  := (cAliasSD2)->D2_SERIE
	Endif
	If lAcumulado
		MsUnLock()
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Estorno da Nota fiscal de Saida                                         ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Case nEvento == 6
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Posiciona registros                                                     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If !cTipoPV $ 'DB'
		If !xFilial("SA1")+(cAliasSC6)->C6_CLI+(cAliasSC6)->C6_LOJA ==;
				SA1->A1_FILIAL+SA1->A1_COD+SA1->A1_LOJA
			dbSelectArea("SA1")
			dbSetOrder(1)
			MsSeek(xFilial("SA1")+(cAliasSC6)->C6_CLI+(cAliasSC6)->C6_LOJA)
		EndIf
		lContinua := RecLock("SA1")
	EndIf
	If !(xFilial("SC5")==SC5->C5_FILIAL .And.;
			SC5->C5_NUM==(cAliasSC6)->C6_NUM)
		dbSelectArea("SC5")
		dbSetOrder(1)
		MsSeek(xFilial("SC5")+(cAliasSC6)->C6_NUM)
	EndIf
	If !xFilial("SF4")+SC6->C6_TES == SF4->F4_FILIAL+SF4->F4_CODIGO
		dbSelectArea("SF4")
		dbSetOrder(1)
		MsSeek(xFilial("SF4")+SC6->C6_TES)
	EndIf
	dbSelectArea("SB2")
	dbSetOrder(1)
	If !MsSeek(xFilial("SB2")+(cAliasSC6)->C6_PRODUTO+(cAliasSC6)->C6_LOCAL)
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Sempre criar o SB2 para otimizacao da Query do MATA461                  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		CriaSB2((cAliasSC6)->C6_PRODUTO,(cAliasSC6)->C6_LOCAL )
	EndIf
	RecLock("SB2")
	If lContinua
		(cAliasSC6)->C6_NOTA  := ""
		(cAliasSC6)->C6_SERIE := ""	
		If "R" $ (cAliasSC6)->C6_BLQ
			vMaAvalSC6(cAliasSC6,2,cAliasSC5,lLiber,lTransf,lLiberOk,lResidOk,lFaturOk,lAcumulado,@nVlrCred,cAliasSD2,lRemito,nMoeda)
			(cAliasSC6)->C6_BLQ := ""
			vMaAvalSC6(cAliasSC6,1,cAliasSC5,lLiber,lTransf,lLiberOk,lResidOk,lFaturOk,lAcumulado,@nVlrCred,cAliasSD2,lRemito,nMoeda)
		EndIf
		If lRemito .Or. Empty( SD2->D2_REMITO )
			(cAliasSC6)->C6_QTDENT  -= SD2->D2_QUANT
			(cAliasSC6)->C6_QTDENT2 := Max( (cAliasSC6)->C6_QTDENT2 - SD2->D2_QTSEGUM, 0 )
		Else
			If !Empty(SD2->D2_REMITO)
				(cAliasSC6)->C6_NOTA  := "REMITO"
				(cAliasSC6)->C6_SERIE := SD2->D2_SERIREM
			Endif
		EndIf
		If (cAliasSC6)->C6_QTDENT == 0 .Or. !lRemito
			(cAliasSC6)->C6_DATFAT := Ctod("")
		EndIf
		If ( SF4->F4_DUPLIC == "S" .And. !SC5->C5_TIPO$"DB" .And. !AllTrim((cAliasSC6)->C6_BLQ)$"RS" ) .And. (!lRemito .And. Empty(SD2->D2_REMITO))
			nMCusto :=  If(SA1->A1_MOEDALC > 0,SA1->A1_MOEDALC, Val(SuperGetMv("MV_MCUSTO")))
			nSldPed := SD2->D2_QUANT
			nSldPed := If(nSldPed>(SC6->C6_QTDVEN-SC6->C6_QTDEMP+nSldPed-SC6->C6_QTDENT),SC6->C6_QTDVEN-SC6->C6_QTDEMP+nSldPed-SC6->C6_QTDENT,nSldPed)
			SA1->A1_SALPED += xMoeda(nSldPed*(cAliasSC6)->C6_PRCVEN,SC5->C5_MOEDA,nMCusto,SC5->C5_EMISSAO)
		EndIf
		If ( SF4->F4_ESTOQUE == "S" ) .And. ( lRemito .Or. Empty( SD2->D2_REMITO ) ) .And. !AllTrim((cAliasSC6)->C6_BLQ)$"RS"

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Atualiza previsoes de saida de material                                 ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			nSldPed := Max((cAliasSC6)->C6_QTDVEN-((cAliasSC6)->C6_QTDENT+SD2->D2_QUANT)-(cAliasSC6)->C6_QTDEMP,0)
			nSldPed := Max((cAliasSC6)->C6_QTDVEN-(cAliasSC6)->C6_QTDENT-(cAliasSC6)->C6_QTDEMP,0)-nSldPed

			nSldPed2:= SB1->(ConvUm(SB2->B2_COD,nSldPed,nSldPed2,2))
			If (cAliasSC6)->C6_OP $ "01#03#05#08"
				SB2->B2_QEMPN  += nSldPed
				SB2->B2_QEMPN2 += nSldPed2
			EndIf
			SB2->B2_QPEDVEN += nSldPed
			SB2->B2_QPEDVE2 += nSldPed2	
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³                Notas sobre o campo C6_OP                               ³
			//³ 01 - OP Gerada pelo MATA650                                            ³
			//³ 02 - Liberacao de PV bloqueada ( Sem OP )                              ³
			//³ 03 - Liberacao de PV bloqueada ( Item 01 )                             ³
			//³ 04 - Bloqueio de credito pelo MATA650                                  ³
			//³ 05 - OP nao gerada pois ha qtde em Estoque                             ³
			//³ 06 - Liberacao de PV liberada  ( Item 05 )                             ³
			//³ 07 - Liberacao de credito efetuada / Estoque pendente/bloqueada        ³
			//³ 08 - Liberacao de estoque efetuada ( item 01 )                         ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If SC6->C6_QTDVEN-SC6->C6_QTDEMP-SC6->C6_QTDENT > 0
				Do Case
				Case SC6->C6_OP == "02"
					SC6->C6_OP     := ""
				Case SC6->C6_OP == "08"
					SC6->C6_OP     := "01"
				Case SC6->C6_OP == "06"	
					SC6->C6_OP     := "05"
				EndCase
			EndIf
		EndIf
	EndIf

EndCase
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Restaura a entrada da rotina                                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
RestArea(aAreaSF4)
RestArea(aAreaSA1)
RestArea(aAreaSB2)
RestArea(aArea)

Return(.T.)
