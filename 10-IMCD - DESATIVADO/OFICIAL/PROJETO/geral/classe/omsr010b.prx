#INCLUDE "OMSR010b.CH" 

#IFNDEF WINDOWS
#DEFINE PSAY SAY
#ENDIF

#DEFINE CHRCOMP If(aReturn[4]==1,15,18)

/*

Ŀ
Programa  OMSR010b   Autor  Eduardo Riera          Data 09.05.2006
Ĵ
Descrio Lista de Precos                                             
Ĵ
Retorno   Nenhum                                                      
Ĵ
ParametrosNenhum                                                      
                                                                      
Ĵ
   DATA    Programador   Manutencao efetuada                         
Ĵ
 21/03/10  Molina         versao Makeni                              
ٱ

*/
User Function OMSR010b()
	Local oReport
	Local cAliasSB1 := "SB1"
	Local cAliasDA0 := "DA0"
	Local cAliasDA1 := "DA1"
	Private cMascgrd := SuperGetMv("MV_MASCGRD",.F.)

	//	oLogTXT := EPLOGTXT():NEW( UPPER(ALLTRIM(FUNNAME())) , "OMSR010b" , __cUserID )

	If FindFunction("TRepInUse") .And. TRepInUse()
		//Ŀ
		//Interface de impressao                                                  
		//
		oReport := ReportDef(@cAliasSB1,@cAliasDA0,@cAliasDA1)
		If !Empty(oReport:uParam)
			Pergunte(oReport:uParam,.F.)
		EndIf	
		oReport:PrintDialog()
	Else
		OMSR010R3()
	EndIf
Return


/*

Ŀ
Programa  ReportDef  Autor Eduardo Riera           Data 08.05.2006
Ĵ
Descrio A funcao estatica ReportDef devera ser criada para todos os 
          relatorios que poderao ser agendados pelo usuario.          
                                                                      
Ĵ
Retorno   ExpO1: Objeto do relatrio                                  
Ĵ
ParametrosNenhum                                                      
          ExpC1: Alias da tabela de produto                           
          ExpC2: Alias da tabela de preco                             
          ExpC3: Alias da tabela de itens da tabela de preco          
                                                                      
Ĵ
   DATA    Programador   Manutencao efetuada                         
Ĵ
                                                                     
ٱ

*/
Static Function ReportDef(cAliasSB1,cAliasDA0,cAliasDA1)
	Local oReport
	Local oTabPrc
	Local oItemTab
	Local oTabBase

	//Ŀ
	//Criacao do componente de impressao                                      
	//                                                                        
	//TReport():New                                                           
	//ExpC1 : Nome do relatorio                                               
	//ExpC2 : Titulo                                                          
	//ExpC3 : Pergunte                                                        
	//ExpB4 : Bloco de codigo que sera executado na confirmacao da impressao  
	//ExpC5 : Descricao                                                       
	//                                                                        
	//
	oReport := TReport():New("OMSR010","Listagem de Precos","OMR010",{|oReport| ReportPrint(oReport,@cAliasSB1,@cAliasDA0,@cAliasDA1)},"Este relatorio ira imprimir as tabelas de precos de acordo com os parametros informados pelo usuario.")	// "Listagem de Precos"###"Este relatorio ira imprimir as tabelas de precos de acordo com os parametros informados pelo usuario."
	//Ŀ
	//Criacao da secao utilizada pelo relatorio                               
	//                                                                        
	//TRSection():New                                                         
	//ExpO1 : Objeto TReport que a secao pertence                             
	//ExpC2 : Descricao da seao                                              
	//ExpA3 : Array com as tabelas utilizadas pela secao. A primeira tabela   
	//        sera considerada como principal para a seo.                   
	//ExpA4 : Array com as Ordens do relatrio                                
	//ExpL5 : Carrega campos do SX3 como celulas                              
	//        Default : False                                                 
	//ExpL6 : Carrega ordens do Sindex                                        
	//        Default : False                                                 
	//                                                                        
	//
	//Ŀ
	//Criacao da celulas da secao do relatorio                                
	//                                                                        
	//TRCell():New                                                            
	//ExpO1 : Objeto TSection que a secao pertence                            
	//ExpC2 : Nome da celula do relatrio. O SX3 ser consultado              
	//ExpC3 : Nome da tabela de referencia da celula                          
	//ExpC4 : Titulo da celula                                                
	//        Default : X3Titulo()                                            
	//ExpC5 : Picture                                                         
	//        Default : X3_PICTURE                                            
	//ExpC6 : Tamanho                                                         
	//        Default : X3_TAMANHO                                            
	//ExpL7 : Informe se o tamanho esta em pixel                              
	//        Default : False                                                 
	//ExpB8 : Bloco de cdigo para impressao.                                 
	//        Default : ExpC2                                                 
	//                                                                        
	//
	oTabPrc := TRSection():New(oReport,"Tabela de Preo",{"DA0"},{"Tipo","Grupo","Descrio de produto","Cdigo de produto"},.F.,.F.)	// "Tabela de Preo"###"Tipo"###"Grupo"###"Descrio de produto"###"Cdigo de produto"
	TRCell():New(oTabPrc,"DA0_CODTAB","DA0",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,{|| (cAliasDA0)->DA0_CODTAB})
	TRCell():New(oTabPrc,"DA0_DESCRI","DA0",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,{|| (cAliasDA0)->DA0_DESCRI})
	TRCell():New(oTabPrc,"DA0_DATDE","DA0",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/ ,{|| (cAliasDA0)->DA0_DATDE})
	TRCell():New(oTabPrc,"DA0_HORADE","DA0",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,{|| (cAliasDA0)->DA0_HORADE})
	TRCell():New(oTabPrc,"DA0_DATATE","DA0",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,{|| (cAliasDA0)->DA0_DATATE})
	TRCell():New(oTabPrc,"DA0_HORATE","DA0",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,{|| (cAliasDA0)->DA0_HORATE})

	oItemTab := TRSection():New(oTabPrc,"Produtos da tabela de preo",{"DA1","SB1"},/*aOrdem*/,.F.,.F.)	// "Produtos da tabela de preo"
	TRCell():New(oItemTab,"DA1_CODPRO","DA1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,{|| IIf((cAliasSB1)->B1_GRADE == "S",Substr((cAliasDA1)->DA1_CODPRO,1,Val(Substr(cMascgrd,1,2))),(cAliasDA1)->DA1_CODPRO)})
	TRCell():New(oItemTab,"B1_DESC"   ,"SB1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,{|| IIf(MV_PAR12 == 1.And.(cAliasSB1)->B1_GRADE=="S",SB4->B4_DESC,(cAliasSB1)->B1_DESC)})
	TRCell():New(oItemTab,"B1_UM"     ,"SB1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,{|| (cAliasSB1)->B1_UM})
	TRCell():New(oItemTab,"DA1_QTDLOT","DA1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,{|| (cAliasDA1)->DA1_QTDLOT})
	TRCell():New(oItemTab,"DA1_PRCBAS","DA1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,{|| (cAliasSB1)->B1_PRV1})
	TRCell():New(oItemTab,"DA1_PRCVEN","DA1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,{|| (cAliasDA1)->DA1_PRCVEN})
	TRCell():New(oItemTab,"DA1_MOEDA" ,"DA1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,{|| (cAliasDA1)->DA1_MOEDA})
	TRCell():New(oItemTab,"DA1_VLRDES","DA1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,{|| (cAliasDA1)->DA1_VLRDES})
	TRCell():New(oItemTab,"DA1_PERDES","DA1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,{|| (cAliasDA1)->DA1_PERDES})
	TRCell():New(oItemTab,"DA1_ESTADO","DA1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,{|| (cAliasDA1)->DA1_ESTADO})
	TRCell():New(oItemTab,"DA1_TPOPER","DA1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,{|| X3Combo("DA1_TPOPER",(cAliasDA1)->DA1_TPOPER)})

	oTabBase := TRSection():New(oReport,"Lista de Preos ( Base )",{"SB1"},{"Tipo","Grupo","Descrio de produto","Cdigo de produto"},.F.,.F.)	// "Lista de Preos ( Base )"###"Tipo"###"Grupo"###"Descrio de produto"###"Cdigo de produto"
	TRCell():New(oTabBase,"B1_TIPO"   ,"SB1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,{|| (cAliasSB1)->B1_TIPO})
	TRCell():New(oTabBase,"B1_GRUPO"  ,"SB1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,{|| (cAliasSB1)->B1_GRUPO})
	TRCell():New(oTabBase,"B1_COD"    ,"SB1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,{|| IIF((cAliasSB1)->B1_GRADE == "S",Substr((cAliasSB1)->B1_COD,1,Val(Substr(cMascgrd,1,2))),(cAliasSB1)->B1_COD)})
	TRCell():New(oTabBase,"B1_DESC"   ,"SB1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,{|| IIF((cAliasSB1)->B1_GRADE == "S" .And. MV_PAR12 == 1,Substr((cAliasSB1)->B1_COD,1,Val(Substr(cMascgrd,1,2))),(cAliasSB1)->B1_DESC)})
	TRCell():New(oTabBase,"B1_UM"     ,"SB1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,{|| (cAliasSB1)->B1_UM})
	TRCell():New(oTabBase,"B1_QE"     ,"SB1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,{|| RetFldProd((cAliasSB1)->B1_COD,"B1_QE",cAliasSB1)})
	TRCell():New(oTabBase,"B1_PRV1"   ,"SB1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,{|| (cAliasSB1)->B1_PRV1})

Return(oReport)

/*/


Ŀ
Programa  ReportPrin Autor Eduardo Riera           Data 08.05.2006
Ĵ
Descrio A funcao estatica ReportDef devera ser criada para todos os 
          relatorios que poderao ser agendados pelo usuario.          
                                                                      
Ĵ
Retorno   Nenhum                                                      
Ĵ
ParametrosExpO1: Objeto Report do Relatrio                           
          ExpC2: Alias da tabela de produto                           
          ExpC3: Alias da tabela de preco                             
          ExpC4: Alias da tabela de itens da tabela de preco          
                                                                      
Ĵ
   DATA    Programador   Manutencao efetuada                         
Ĵ
                                                                     
ٱ


/*/
Static Function ReportPrint(oReport,cAliasSB1,cAliasDA0,cAliasDA1)

	Local cTabAnt   := ""
	Local cProdRef  := ""
	Local lNotGrade := .T.
	Local lQuery    := .F. 
	Local cGrupos   := ""  
	Local cPart     := ""

	#IFNDEF TOP
	Local cArqTrb   := ""
	Local cCondicao := ""
	Local aCampos   := {}
	Local cAtivo    := "" 
	#ELSE
	Local cOrderBy  := ""
	#ENDIF
	//Ŀ
	//Transforma parametros Range em expressao SQL                            
	//
	MakeSqlExpr(oReport:uParam/*Nome da Pergunte*/)
	#IFDEF TOP
	If ( MV_PAR11 == 2 )
		//Ŀ
		//Query do relatrio da secao 1                                           
		//
		lQuery := .T.
		oReport:Section(1):BeginQuery()		
		Do Case
			Case oReport:Section(1):nOrder == 1
			cOrderBy := "%DA0_FILIAL,DA0_CODTAB,B1_TIPO%"
			Case oReport:Section(1):nOrder == 2
			cOrderBy :=  "%DA0_FILIAL,DA0_CODTAB,B1_GRUPO%"
			Case oReport:Section(1):nOrder == 3
			cOrderBy :=  "%DA0_FILIAL,DA0_CODTAB,B1_DESC%"
			Case oReport:Section(1):nOrder == 4
			cOrderBy :=  "%DA0_FILIAL,DA0_CODTAB,DA1_CODPRO%"
		EndCase

		cAtivo := Iif(Str(mv_par13,1) $ ("1/2") ,Str(mv_par13,1),"'1','2'") 
		If (Str(mv_par13,1) <> "3")
			cAtivo := "'" + cAtivo + "'"  
		Endif
		cAtivo := "(" + cAtivo + ")"
		cAtivo := "% " + cAtivo + " %" 

		cAliasDA0 := GetNextAlias()
		cAliasDA1 := cAliasDA0
		cAliasSB1 := cAliasDA1

		If (__cUserId $ GetMv("MV_TABPR94"))
			// Administracao Vendas, nada muda
			BeginSql Alias cAliasDA0
				SELECT DA0_CODTAB,DA0_DESCRI,DA0_DATDE,DA0_DATATE,DA0_HORADE,DA0_HORATE,
				DA1_FILIAL,DA1_CODTAB,DA1_CODPRO,DA1_QTDLOT,DA1_PRCVEN,DA1_MOEDA,DA1_VLRDES,DA1_PERDES,DA1_ESTADO,DA1_TPOPER,DA1_ATIVO,
				B1_TIPO,B1_GRUPO,B1_DESC,B1_UM,B1_GRADE,B1_PRV1
				FROM %table:SB1% SB1,%table:DA1% DA1,%table:DA0% DA0
				WHERE DA0.DA0_FILIAL = %xFilial:DA0% AND 
				DA0.DA0_CODTAB >= %Exp:mv_par01% AND
				DA0.DA0_CODTAB <= %Exp:mv_par02% AND
				DA0.%NotDel% AND 
				DA1.DA1_FILIAL = %xFilial:DA1% AND 
				DA1.DA1_CODTAB = DA0.DA0_CODTAB AND 
				DA1.DA1_ATIVO IN %Exp:cAtivo% AND
				DA1.%NotDel% AND 
				SB1.B1_FILIAL = %xFilial:SB1% AND 
				SB1.B1_COD   =  DA1.DA1_CODPRO AND 
				SB1.B1_TIPO  >= %Exp:mv_par03% AND
				SB1.B1_TIPO  <= %Exp:mv_par04% AND
				SB1.B1_GRUPO >= %Exp:mv_par05% AND
				SB1.B1_GRUPO <= %Exp:mv_par06% AND
				SB1.B1_COD   >= %Exp:mv_par07% AND
				SB1.B1_COD   <= %Exp:mv_par08% AND
				SB1.%NotDel% 
				ORDER BY %Exp:cOrderBy%
			EndSql 	
		Else
			// Quais os Grupos?
			If U_Gerente()
				// Gerente
				cGrupos := U_VGrps()

				If ( Len(cGrupos) > 1 )
					cPart := "%SB1.B1_SEGMENT IN " + cGrupos + " AND%"
				Else
					cPart := "%%"
				EndIf

				BeginSql Alias cAliasDA0

					SELECT DA0_CODTAB,DA0_DESCRI,DA0_DATDE,DA0_DATATE,DA0_HORADE,DA0_HORATE,
					DA1_FILIAL,DA1_CODTAB,DA1_CODPRO,DA1_QTDLOT,DA1_PRCVEN,DA1_MOEDA,DA1_VLRDES,DA1_PERDES,DA1_ESTADO,DA1_TPOPER,DA1_ATIVO,
					B1_TIPO,B1_GRUPO,B1_DESC,B1_UM,B1_GRADE,B1_PRV1
					FROM %table:SB1% SB1,%table:DA1% DA1,%table:DA0% DA0
					WHERE DA0.DA0_FILIAL = %xFilial:DA0% AND 
					DA0.DA0_CODTAB >= %Exp:mv_par01% AND
					DA0.DA0_CODTAB <= %Exp:mv_par02% AND
					DA0.%NotDel% AND 
					DA1.DA1_FILIAL = %xFilial:DA1% AND 
					DA1.DA1_CODTAB = DA0.DA0_CODTAB AND 
					DA1.DA1_ATIVO IN %Exp:cAtivo% AND
					DA1.%NotDel% AND 
					SB1.B1_FILIAL = %xFilial:SB1% AND 
					SB1.B1_COD   =  DA1.DA1_CODPRO AND 
					SB1.B1_TIPO  >= %Exp:mv_par03% AND
					SB1.B1_TIPO  <= %Exp:mv_par04% AND
					SB1.B1_GRUPO >= %Exp:mv_par05% AND
					SB1.B1_GRUPO <= %Exp:mv_par06% AND
					SB1.B1_COD   >= %Exp:mv_par07% AND
					SB1.B1_COD   <= %Exp:mv_par08% AND 
					%Exp:cPart%
					SB1.%NotDel% 
					ORDER BY %Exp:cOrderBy%
				EndSql 	
			Else
				// Vendedor
				cGrupos := U_VGrp()

				If ( Len(cGrupos) > 1 )
					cPart := "%SB1.B1_SEGMENT IN " + cGrupos + " AND%"
				Else
					cPart := "%%"
				EndIf

				BeginSql Alias cAliasDA0
					SELECT DA0_CODTAB,DA0_DESCRI,DA0_DATDE,DA0_DATATE,DA0_HORADE,DA0_HORATE,
					DA1_FILIAL,DA1_CODTAB,DA1_CODPRO,DA1_QTDLOT,DA1_PRCVEN,DA1_MOEDA,DA1_VLRDES,DA1_PERDES,DA1_ESTADO,DA1_TPOPER,DA1_ATIVO,
					B1_TIPO,B1_GRUPO,B1_DESC,B1_UM,B1_GRADE,B1_PRV1
					FROM %table:SB1% SB1,%table:DA1% DA1,%table:DA0% DA0
					WHERE DA0.DA0_FILIAL = %xFilial:DA0% AND 
					DA0.DA0_CODTAB >= %Exp:mv_par01% AND
					DA0.DA0_CODTAB <= %Exp:mv_par02% AND
					DA0.%NotDel% AND 
					DA1.DA1_FILIAL = %xFilial:DA1% AND 
					DA1.DA1_CODTAB = DA0.DA0_CODTAB AND 
					DA1.DA1_ATIVO IN %Exp:cAtivo% AND
					DA1.%NotDel% AND 
					SB1.B1_FILIAL = %xFilial:SB1% AND 
					SB1.B1_COD   =  DA1.DA1_CODPRO AND 
					SB1.B1_TIPO  >= %Exp:mv_par03% AND
					SB1.B1_TIPO  <= %Exp:mv_par04% AND
					SB1.B1_GRUPO >= %Exp:mv_par05% AND
					SB1.B1_GRUPO <= %Exp:mv_par06% AND
					SB1.B1_COD   >= %Exp:mv_par07% AND
					SB1.B1_COD   <= %Exp:mv_par08% AND 
					%Exp:cPart%
					SB1.%NotDel% 
					ORDER BY %Exp:cOrderBy%
				EndSql 	            
			EndIf 
		EndIf

		//Ŀ
		//Metodo EndQuery ( Classe TRSection )                                    
		//                                                                        
		//Prepara o relatrio para executar o Embedded SQL.                       
		//                                                                        
		//ExpA1 : Array com os parametros do tipo Range                           
		//                                                                        
		//
		oReport:Section(1):EndQuery(/*ExpA1*/)
		oReport:Section(1):Section(1):SetParentQuery()
	Else
		//Ŀ
		//Query do relatrio da secao 2                                           
		//
		oReport:Section(2):BeginQuery()	
		Do Case
			Case oReport:Section(1):nOrder == 1
			cOrderBy := "%B1_FILIAL,B1_TIPO,B1_COD%"
			Case oReport:Section(1):nOrder == 2
			cOrderBy :=  "%B1_FILIAL,B1_GRUPO,B1_COD%"
			Case oReport:Section(1):nOrder == 3
			cOrderBy :=  "%B1_FILIAL,B1_DESC%"
			Case oReport:Section(1):nOrder == 4
			cOrderBy :=  "%B1_FILIAL,B1_COD%"
		EndCase

		cAliasSB1 := GetNextAlias()

		If (__cUserId $ GetMv("MV_TABPR94"))
			// Administracao Vendas, nada muda
			BeginSql Alias cAliasSB1
				SELECT B1_TIPO,B1_GRUPO,B1_COD,B1_DESC,B1_UM,B1_GRADE,B1_PRV1,B1_QE
				FROM %table:SB1% SB1
				WHERE SB1.B1_FILIAL = %xFilial:SB1% AND 
				SB1.B1_TIPO  >= %Exp:mv_par03% AND
				SB1.B1_TIPO  <= %Exp:mv_par04% AND
				SB1.B1_GRUPO >= %Exp:mv_par05% AND
				SB1.B1_GRUPO <= %Exp:mv_par06% AND
				SB1.B1_COD   >= %Exp:mv_par07% AND
				SB1.B1_COD   <= %Exp:mv_par08% AND 
				SB1.%NotDel%
				ORDER BY %Exp:cOrderBy%
			EndSql
		Else
			// Quais os Grupos?
			If U_Gerente()
				// Gerente
				cGrupos := U_VGrps()

				If ( Len(cGrupos) > 1 )
					cPart := "%SB1.B1_SEGMENT IN " + cGrupos + " AND%"
				Else
					cPart := "%%"
				EndIf

				BeginSql Alias cAliasSB1
					SELECT B1_TIPO,B1_GRUPO,B1_COD,B1_DESC,B1_UM,B1_GRADE,B1_PRV1,B1_QE
					FROM %table:SB1% SB1
					WHERE SB1.B1_FILIAL = %xFilial:SB1% AND 
					SB1.B1_TIPO  >= %Exp:mv_par03% AND
					SB1.B1_TIPO  <= %Exp:mv_par04% AND
					SB1.B1_GRUPO >= %Exp:mv_par05% AND
					SB1.B1_GRUPO <= %Exp:mv_par06% AND
					SB1.B1_COD   >= %Exp:mv_par07% AND
					SB1.B1_COD   <= %Exp:mv_par08% AND 
					%Exp:cPart% 
					SB1.%NotDel%
					ORDER BY %Exp:cOrderBy%
				EndSql
			Else
				// Vendedor
				cGrupos := U_VGrp()

				If ( Len(cGrupos) > 1 )
					cPart := "%SB1.B1_SEGMENT IN " + cGrupos + " AND%"
				Else
					cPart := "%%"
				EndIf

				BeginSql Alias cAliasSB1
					SELECT B1_TIPO,B1_GRUPO,B1_COD,B1_DESC,B1_UM,B1_GRADE,B1_PRV1,B1_QE
					FROM %table:SB1% SB1
					WHERE SB1.B1_FILIAL = %xFilial:SB1% AND 
					SB1.B1_TIPO  >= %Exp:mv_par03% AND
					SB1.B1_TIPO  <= %Exp:mv_par04% AND
					SB1.B1_GRUPO >= %Exp:mv_par05% AND
					SB1.B1_GRUPO <= %Exp:mv_par06% AND
					SB1.B1_COD   >= %Exp:mv_par07% AND
					SB1.B1_COD   <= %Exp:mv_par08% AND 
					%Exp:cPart% 
					SB1.%NotDel%
					ORDER BY %Exp:cOrderBy%
				EndSql
			EndIf
		EndIf	

		//Ŀ
		//Metodo EndQuery ( Classe TRSection )                                    
		//                                                                        
		//Prepara o relatrio para executar o Embedded SQL.                       
		//                                                                        
		//ExpA1 : Array com os parametros do tipo Range                           
		//                                                                        
		//
		oReport:Section(2):EndQuery(/*ExpA1*/)
	EndIf
	#ELSE
	If ( MV_PAR11 == 2 )
		AADD(aCampos,{"DA0_CODTAB" ,"C",03,0}) 
		AADD(aCampos,{"DA1_CODPRO" ,"C",15,0}) 
		AADD(aCampos,{"DA1_DESPRO" ,"C",30,0}) 
		AADD(aCampos,{"DA1_TIPO"   ,"C",02,0}) 
		AADD(aCampos,{"DA1_GRUPO"  ,"C",04,0}) 
		AADD(aCampos,{"DA0RECNO"   ,"N",18,0}) 
		AADD(aCampos,{"DA1RECNO"   ,"N",18,0})
		AADD(aCampos,{"SB1RECNO"   ,"N",18,0}) 

		cArqTrb   := CriaTrab(aCampos,.T.)

		dbUseArea(.T.,,cArqTrb,"TRBPRC",.F.)

		Do Case
			Case oReport:Section(1):nOrder == 1
			IndRegua("TRBPRC",cArqTrb,"DA0_CODTAB+DA1_TIPO+DA1_CODPRO")
			Case oReport:Section(1):nOrder == 2		
			IndRegua("TRBPRC",cArqTrb,"DA0_CODTAB+DA1_GRUPO+DA1_CODPRO")
			Case oReport:Section(1):nOrder == 3		
			IndRegua("TRBPRC",cArqTrb,"DA0_CODTAB+DA1_DESPRO+DA1_CODPRO")
			Case oReport:Section(1):nOrder == 4
			IndRegua("TRBPRC",cArqTrb,"DA0_CODTAB+DA1_CODPRO")		
		EndCase

		dbSelectArea("DA0")
		dbSetOrder(1)
		MsSeek(xFilial("DA0")+mv_par01,.T.)

		While !Eof() .And. ( xFilial("DA0") == DA0->DA0_FILIAL ) .And.( DA0->DA0_CODTAB <= mv_par02 )

			dbSelectArea("DA1")
			dbSetOrder(1)
			If MsSeek(xFilial("DA1")+ DA0->DA0_CODTAB)
				While !Eof() .And. DA1->DA1_FILIAL+DA1->DA1_CODTAB == xFilial("DA1")+DA0->DA0_CODTAB

					dbSelectArea("SB1")
					dbSetOrder(1)
					If MsSeek(xFilial("SB1")+DA1->DA1_CODPRO)	    				
						If !ValidMasc(SB1->B1_COD,MV_PAR09) .Or. ( SB1->B1_TIPO < mv_par03 .Or. SB1->B1_TIPO > mv_par04 ) .Or. ;
						( SB1->B1_GRUPO < mv_par05 .Or. SB1->B1_GRUPO > mv_par06 ) .Or. ;
						( SB1->B1_COD < mv_par07 .Or. SB1->B1_COD > mv_par08 ) .Or. ;
						( !If(mv_par13==3,.T.,DA1->DA1_ATIVO==Str(mv_par13,1)) )
							DA1->(dbSkip())
							Loop
						EndIf
						dbSelectArea("TRBPRC")
						RecLock("TRBPRC",.T.)
						DA0_CODTAB := DA0->DA0_CODTAB
						DA1_CODPRO := DA1->DA1_CODPRO
						DA1_DESPRO := SB1->B1_DESC
						DA1_TIPO   := SB1->B1_TIPO
						DA1_GRUPO  := SB1->B1_GRUPO
						DA0RECNO   := DA0->(RecNo())
						DA1RECNO   := DA1->(RecNo())
						SB1RECNO   := SB1->(RecNo())
					EndIf
					dbSelectArea("DA1")
					dbSkip()
				EndDo
			EndIf

			dbSelectArea("DA0")
			dbSkip()					
		EndDo
	Else
		dbSelectArea("SB1")
		Do Case
			Case oReport:Section(2):nOrder == 1
			dbSetOrder(2)
			Case oReport:Section(2):nOrder == 2
			dbSetOrder(4)
			Case oReport:Section(2):nOrder == 3
			dbSetOrder(3)
			Case oReport:Section(2):nOrder == 4
			dbSetOrder(1)
		EndCase

		cCondicao := "B1_FILIAL=='"+xFilial("SB1")+"' .And. "
		cCondicao += "B1_TIPO>='"+mv_par03+"' .And. "
		cCondicao += "B1_TIPO<='"+mv_par04+"' .And. "
		cCondicao += "B1_GRUPO>='"+mv_par05+"' .And. "
		cCondicao += "B1_GRUPO<='"+mv_par06+"' .And. "
		cCondicao += "B1_COD>='"+mv_par07+"' .And. "
		cCondicao += "B1_COD<='"+mv_par08+"' "

		oReport:Section(2):SetFilter(cCondicao,SB1->(IndexKey()))
	EndIf
	#ENDIF
	//Ŀ
	//Metodo TrPosition()                                                     
	//                                                                        
	//Posiciona em um registro de uma outra tabela. O posicionamento ser     
	//realizado antes da impressao de cada linha do relatrio.                
	//                                                                        
	//                                                                        
	//ExpO1 : Objeto Report da Secao                                          
	//ExpC2 : Alias da Tabela                                                 
	//ExpX3 : Ordem ou NickName de pesquisa                                   
	//ExpX4 : String ou Bloco de cdigo para pesquisa. A string ser macroexe-
	//        cutada.                                                         
	//                                                                        				
	//
	TRPosition():New(oReport:Section(1):Section(1),"SB1",1,{|| xFilial("SB1")+(cAliasDA1)->DA1_CODPRO})
	TRPosition():New(oReport:Section(1):Section(1),"SB4",1,{|| xFilial("SB4")+Substr((cAliasDA1)->DA1_CODPRO,1,Val(Substr(cMascgrd,1,2)))})
	TRPosition():New(oReport:Section(2),"SB4",1,{|| xFilial("SB4")+Substr((cAliasSB1)->B1_COD,1,Val(Substr(cMascgrd,1,2)))})
	If ( MV_PAR11 == 2 )
		oReport:SetMeter(DA0->(LastRec()))

		If !lQuery
			TRBPRC->(dbGoTop())
			DA0->(MsGoto(TRBPRC->DA0RECNO))
			DA1->(MsGoto(TRBPRC->DA1RECNO))
			SB1->(MsGoto(TRBPRC->SB1RECNO))
		EndIf

		dbSelectArea(cAliasDA0)
		While !Eof() .And. !oReport:Cancel()	               
			oReport:Section(1):Init()
			oReport:Section(1):PrintLine()
			oReport:Section(1):Section(1):Init()

			lNotGrade := .T.
			cTabAnt   := (cAliasDA0)->DA0_CODTAB

			dbSelectArea(cAliasDA1)			
			While !oReport:Cancel() .And. !Eof() .And. (cAliasDA1)->DA1_FILIAL == xFilial("DA1") .And. (cAliasDA1)->DA1_CODTAB == cTabAnt

				If ValidMasc((cAliasDA1)->DA1_CODPRO,MV_PAR09)
					If MV_PAR12 == 1
						lNotGrade := .T.							
						If (cAliasSB1)->B1_GRADE == "S" .And. !Empty(cProdRef) .And. cProdRef == Substr((cAliasDA1)->DA1_CODPRO,1,Val(Substr(cMascgrd,1,2)))
							lNotGrade := .F.
						EndIf
					EndIf

					If lNotGrade
						oReport:Section(1):Section(1):PrintLine()
					EndIf
				EndIf
				cProdRef := Substr((cAliasDA1)->DA1_CODPRO,1,Val(Substr(cMascgrd,1,2)))

				If lQuery		
					dbSelectarea(cAliasDA1)
					dbSkip()
				Else
					TRBPRC->(dbSkip())
					DA0->(MsGoto(TRBPRC->DA0RECNO))
					DA1->(MsGoto(TRBPRC->DA1RECNO))
					SB1->(MsGoto(TRBPRC->SB1RECNO))
				EndIf			
			EndDo
			oReport:Section(1):Section(1):Finish()
			If mv_par10 == 1
				oReport:Section(1):SetPageBreak(.T.)
			Else			
				oReport:SkipLine()
				oReport:SkipLine()
			EndIf

			oReport:Section(1):Finish()
			oReport:IncMeter()

		EndDo

		If !lQuery
			dbSelectArea("TRBPRC")
			dbCloseArea()
			Ferase("TRBPRC"+GetDBExtension())
			Ferase("TRBPRC"+OrdBagExt())
			dbSelectArea("DA0")
		EndIf	
	Else			
		oReport:SetMeter(SB1->(LastRec()))

		dbSelectArea(cAliasSB1)
		dbGotop()

		While !oReport:Cancel() .And. !(cAliasSB1)->(Eof())		
			lNotGrade := .T.
			oReport:Section(2):Init()			
			While !oReport:Cancel() .And. !(cAliasSB1)->(Eof())
				If ValidMasc((cAliasSB1)->B1_COD,MV_PAR09)
					If MV_PAR12 == 1      
						lNotGrade := .T.
						If (cAliasSB1)->B1_GRADE == "S" .And. !Empty(cProdRef) .And. cProdRef == Substr((cAliasSB1)->B1_COD,1,Val(Substr(cMascgrd,1,2)))
							lNotGrade := .F.
						EndIf
					EndIf
					If lNotGrade			        
						oReport:Section(2):PrintLine()					
					EndIf
				EndIf
				cProdRef := Substr((cAliasSB1)->B1_COD,1,Val(Substr(cMascgrd,1,2)))
				dbSelectArea(cAliasSB1)
				dbSkip()		
			EndDo
			oReport:Section(2):Finish()		
		EndDo
	EndIf
Return

/*/


Ŀ
Programa  OMSR010R3  Autor  Henry Fila             Data  20.06.01 
Ĵ
Descrio Relatorio de Listagem de Precos                             
Ĵ
Retorno   Nenhum                                                      
Ĵ
ParametrosNenhum                                                      
                                                                      
Ĵ
   DATA    Programador   Manutencao efetuada                         
Ĵ
                                                                     
ٱ


/*/
Static Function OMSR010R3()

	//Ŀ
	//Define Variaveis                                                        
	//
	#IFDEF WINDOWS
	Local Titulo  := OemToAnsi("Listagem de Precos")
	Local cDesc1  := OemToAnsi("Este relatorio ira imprimir as tabelas de precos de acordo")
	Local cDesc2  := OemToAnsi("com os parametros informados pelo usuario")
	Local cDesc3  := OemToAnsi("") //""  // Descricao 3
	#ELSE
	Local Titulo  := "Listagem de Precos"
	Local cDesc1  := "Este relatorio ira imprimir as tabelas de precos de acordo"  
	Local cDesc2  := "com os parametros informados pelo usuario"
	Local cDesc3  := ""
	#ENDIF
	Local cString := "DA0"  // Alias utilizado na Filtragem
	Local lDic    := .F. // Habilita/Desabilita Dicionario
	Local lComp   := .T. // Habilita/Desabilita o Formato Comprimido/Expandido
	Local lFiltro := .T. // Habilita/Desabilita o Filtro
	Local wnrel   := "OMSR010"  // Nome do Arquivo utilizado no Spool
	Local nomeprog:= "OMSR010"  // nome do programa

	Private Tamanho := "M" // P/M/G
	Private Limite  := 132 // 80/132/220
	Private aOrdem  := {" Por Tipo           "," Por Grupo        ","Por descrio do produto","Por produto" }
	Private cPerg   := "OMR010"  // Pergunta do Relatorio
	Private aReturn := { "Zebrado", 1,"Administracao", 1, 2, 1, "",1 } //"Zebrado"###"Administracao"
	//[1] Reservado para Formulario
	//[2] Reservado para N de Vias
	//[3] Destinatario
	//[4] Formato => 1-Comprimido 2-Normal
	//[5] Midia   => 1-Disco 2-Impressora
	//[6] Porta ou Arquivo 1-LPT1... 4-COM1...
	//[7] Expressao do Filtro
	//[8] Ordem a ser selecionada
	//[9]..[10]..[n] Campos a Processar (se houver)

	Private lEnd    := .F.// Controle de cancelamento do relatorio
	Private m_pag   := 1  // Contador de Paginas
	Private nLastKey:= 0  // Controla o cancelamento da SetPrint e SetDefault

	//Ŀ
	// Funcao utilizada para verificar a ultima versao dos fontes      
	// SIGACUS.PRW, SIGACUSA.PRX e SIGACUSB.PRX, aplicados no rpo do   |
	//| cliente, assim verificando a necessidade de uma atualizacao     |
	//| nestes fontes. NAO REMOVER !!!							        
	//
	IF !(FindFunction("SIGACUS_V") .and. SIGACUS_V() >= 20050512)
		Final("Atualizar SIGACUS.PRW !!!")
	Endif
	IF !(FindFunction("SIGACUSA_V") .and. SIGACUSA_V() >= 20050512)
		Final("Atualizar SIGACUSA.PRX !!!")
	Endif
	IF !(FindFunction("SIGACUSB_V") .and. SIGACUSB_V() >= 20050512)
		Final("Atualizar SIGACUSB.PRX !!!")
	Endif

	Pergunte(cPerg,.F.)
	//Ŀ
	//Envia para a SetPrinter                                                 
	//
	wnrel:=SetPrint(cString,wnrel,cPerg,@titulo,cDesc1,cDesc2,cDesc3,lDic,aOrdem,lComp,Tamanho,lFiltro)
	If ( nLastKey==27 )
		dbSelectArea(cString)
		dbSetOrder(1)
		Set Filter to
		Return
	EndIf
	SetDefault(aReturn,cString)
	If ( nLastKey==27 )
		dbSelectArea(cString)
		dbSetOrder(1)
		Set Filter to
		Return
	EndIf
	#IFDEF WINDOWS
	RptStatus({|lEnd| ImpDet(@lEnd,wnRel,cString,nomeprog,Titulo)},Titulo)
	#ELSE
	ImpDet(.F.,wnrel,cString,nomeprog,Titulo)
	#ENDIF

Return(.T.)

/*/


Ŀ
Program    ImpDet    Autor  Eduardo Riera          Data 02.07.1998
Ĵ
Descrio Controle de Fluxo do Relatorio.                             
Ĵ
Retorno   Nenhum                                                      
Ĵ
ParametrosNenhum                                                      
                                                                      
Ĵ
   DATA    Programador   Manutencao efetuada                         
Ĵ
                                                                     
ٱ


/*/

Static Function ImpDet(lEnd,wnrel,cString,nomeprog,Titulo)

	Local lImp      := .F. // Indica se algo foi impresso
	Local cbCont    := 0   // Numero de Registros Processados
	Local cbText    := ""  // Mensagem do Rodape
	Local nXX := 0
	Local nXZ := 0
	Local nXY := 0
	Local cArqTrb   := ""

	#IFDEF TOP
	Local aStruQry  := {}            
	Local cQry      := ""
	Local nX        := 0
	#ENDIF

	//
	//Imprime a tabela de precos baseado no DA0 e no DA1 
	//
	If mv_par11 == 2

		#IFDEF TOP	      

		If TcSrvType() != "AS/400"	

			aSX3DA0 := FWSx3Util():GetAllFields("DA0",.F.)
			
			For nXX := 1 to len(aSX3DA0)

				if GetSX3Cache(aSX3DA0[nXX],"X3_CAMPO") == "DA0_DATDE"
					aadd(aStruQry,{"TRB_DATDE",GetCacheSX3(aSX3DA0[nXX],"X3_TIPO"),GetCacheSX3(aSX3DA0[nXX],"X3_TAMANHO"),GetCacheSX3(aSX3DA0[nXX],"X3_DECIMAL")})			
				Endif
				
				if GetSX3Cache(aSX3DA0[nXX],"X3_CAMPO") == "DA0_DATATE"
					aadd(aStruQry,{"TRB_DATATE",GetCacheSX3(aSX3DA0[nXX],"X3_TIPO"),GetCacheSX3(aSX3DA0[nXX],"X3_TAMANHO"),GetCacheSX3(aSX3DA0[nXX],"X3_DECIMAL")})			
				Endif				
			next nXX	

			aSX3DA1 := FWSx3Util():GetAllFields("DA1",.F.)

			For nXZ := 1 to len(aSX3DA1)

				if GetSX3Cache(aSX3DA0[nXZ],"X3_CAMPO") == "DA1_QTDLOT"
					aadd(aStruQry,{"TRB_QTDLOT",GetCacheSX3(aSX3DA0[nXZ],"X3_TIPO"),GetCacheSX3(aSX3DA0[nXZ],"X3_TAMANHO"),GetCacheSX3(aSX3DA0[nXZ],"X3_DECIMAL")})			
				Endif	
				
				if GetSX3Cache(aSX3DA0[nXZ],"X3_CAMPO") == "DA1_PRCVEN"
					aadd(aStruQry,{"TRB_PRCVEN",GetCacheSX3(aSX3DA0[nXZ],"X3_TIPO"),GetCacheSX3(aSX3DA0[nXZ],"X3_TAMANHO"),GetCacheSX3(aSX3DA0[nXZ],"X3_DECIMAL")})			
				Endif				

				if GetSX3Cache(aSX3DA0[nXZ],"X3_CAMPO") == "DA1_MOEDA"
					aadd(aStruQry,{"TRB_MOEDA",GetCacheSX3(aSX3DA0[nXZ],"X3_TIPO"),GetCacheSX3(aSX3DA0[nXZ],"X3_TAMANHO"),GetCacheSX3(aSX3DA0[nXZ],"X3_DECIMAL")})			
				Endif
				
				if GetSX3Cache(aSX3DA0[nXZ],"X3_CAMPO") == "DA1_VLRDES"
					aadd(aStruQry,{"TRB_VLRDES",GetCacheSX3(aSX3DA0[nXZ],"X3_TIPO"),GetCacheSX3(aSX3DA0[nXZ],"X3_TAMANHO"),GetCacheSX3(aSX3DA0[nXZ],"X3_DECIMAL")})			
				Endif
				
				if GetSX3Cache(aSX3DA0[nXZ],"X3_CAMPO") == "DA1_PERDES"
					aadd(aStruQry,{"TRB_PERDES",GetCacheSX3(aSX3DA0[nXZ],"X3_TIPO"),GetCacheSX3(aSX3DA0[nXZ],"X3_TAMANHO"),GetCacheSX3(aSX3DA0[nXZ],"X3_DECIMAL")})			
				Endif	
			
			next nXZ

			aSX3SB1 := FWSx3Util():GetAllFields("SB1",.F.)

			For nXY := 1 to len(aSX3SB1)
				if GetSX3Cache(aSX3DA0[nXY],"X3_CAMPO") == "B1_PRV1"
					aadd(aStruQry,{"TRB_PRV1",GetCacheSX3(aSX3DA0[nXY],"X3_TIPO"),GetCacheSX3(aSX3DA0[nXY],"X3_TAMANHO"),GetCacheSX3(aSX3DA0[nXY],"X3_DECIMAL")})			
				Endif							
			Next nXY


			cQry := "SELECT DA0_CODTAB TRB_CODTAB,DA0_DESCRI TRB_DESTAB,DA0_DATDE TRB_DATDE,DA0_HORADE TRB_HORADE,DA0_DATATE TRB_DATATE,"
			cQry += "DA0_HORATE TRB_HORATE,DA1_CODPRO TRB_CODPRO,DA1_QTDLOT TRB_QTDLOT, DA1_PRCVEN TRB_PRCVEN, DA1_MOEDA TRB_MOEDA, DA1_VLRDES TRB_VLRDES, "
			cQry += "DA1_PERDES TRB_PERDES, DA1_ESTADO TRB_ESTADO ,DA1_TPOPER TRB_TPOPER ,"
			cQry += "B1_TIPO TRB_TIPO ,B1_GRUPO TRB_GRUPO ,B1_DESC TRB_DESPRO ,B1_UM TRB_UM,B1_GRADE TRB_GRADE,B1_PRV1 TRB_PRV1"
			cQry += " FROM "+RetSqlName('SB1')+" SB1,"
			cQry += " "+RetSqlName('DA1')+" DA1,"
			cQry += " "+RetSqlName('DA0')+" DA0"		

			cQry += " WHERE "
			cQry += " DA0_FILIAL = '"+xFilial('DA1')+"'"
			cQry += " AND DA0_CODTAB >= '"+mv_par01+"' "
			cQry += " AND DA0_CODTAB <= '"+mv_par02+"' "
			cQry += " AND DA0.D_E_L_E_T_ = ' '"         

			cQry += " AND DA1_FILIAL = '"+xFilial('DA1')+"'"
			cQry += " AND DA1_CODTAB = DA0_CODTAB "

			If mv_par13 <> 3			
				cQry += " AND DA1_ATIVO = '"+Str(mv_par13,1)+"' "
			EndIf

			cQry += " AND DA1.D_E_L_E_T_ = ' '"         

			cQry += " AND B1_FILIAL = '"+xFilial('SB1')+"'"
			cQry += " AND B1_COD = DA1_CODPRO "
			cQry += " AND B1_TIPO >= '"+mv_par03+"' "		
			cQry += " AND B1_TIPO <= '"+mv_par04+"' "		
			cQry += " AND B1_GRUPO >='"+mv_par05+"' "		
			cQry += " AND B1_GRUPO <='"+mv_par06+"' "		
			cQry += " AND B1_COD >= '"+mv_par07+"' "	
			cQry += " AND B1_COD <='"+mv_par08+"' "	
			If .Not. (__cUserId $ GetMv("MV_TABPR94"))
				If U_Gerente()
					// Gerente
					cGrupos := U_VGrps()
				Else
					// Vendedor
					cGrupos := U_VGrp()
				EndIf	
				cQry += " AND B1_SEGMENT In " +cGrupos+" "	
			EndIf
			cQry += " AND SB1.D_E_L_E_T_ = ' '"

			Do Case
				Case aReturn[8] == 1
				cQry += " ORDER BY TRB_CODTAB,TRB_TIPO"
				Case aReturn[8] == 2
				cQry += " ORDER BY TRB_CODTAB,TRB_GRUPO"
				Case aReturn[8] == 3				        
				cQry += " ORDER BY TRB_CODTAB,TRB_DESPRO"
				Case aReturn[8] == 4
				cQry += " ORDER BY TRB_CODTAB,TRB_CODPRO"
			EndCase

			cQry := ChangeQuery(cQry)
			dBUseArea(.t.,"TOPCONN",TCGENQRY(,,cQry),"TRBPRC",.f.,.t.)

			For nX := 1 To Len(aStruQry)
				If ( aStruQry[nX][2] <> "C" )
					TcSetField("TRBPRC",aStruQry[nX][1],aStruQry[nX][2],aStruQry[nX][3],aStruQry[nX][4])
				EndIf
			Next nX

			lQuery := .T.
			lImp := .T.			

			cAliasSB1 := "TRBLIS"
			cAliasDA0 := "TRBLIS"
			cAliasDA1 := "TRBLIS"		

			dbSelectArea("TRBPRC")
			dbGotop()

		EndIf

		#ENDIF

		If !lQuery

			//Ŀ
			//Cria arquivo de trabalho
			//

			Omr010Trb(@cArqTrb,aReturn[8])

			dbSelectArea("DA0")
			SetRegua(LastRec())
			dbSetOrder(1)
			dbSeek(xFilial()+mv_par01,.T.)

			While !Eof() .And. ( xFilial()== DA0->DA0_FILIAL ) .And.( DA0->DA0_CODTAB <= mv_par02 )

				dbSelectArea("DA1")
				dbSetOrder(1)
				If dbSeek(xFilial("DA1")+ DA0->DA0_CODTAB)
					While !Eof() .And. DA1->DA1_FILIAL+DA1->DA1_CODTAB == xFilial("DA1")+DA0->DA0_CODTAB

						dbSelectArea("SB1")
						dbSetOrder(1)
						If dbSeek(xFilial("SB1")+DA1->DA1_CODPRO)

							lRet:=ValidMasc(SB1->B1_COD,MV_PAR09)

							If !lRet .Or. ( SB1->B1_TIPO < mv_par03 .Or. SB1->B1_TIPO > mv_par04 ) .Or. ;
							( SB1->B1_GRUPO < mv_par05 .Or. SB1->B1_GRUPO > mv_par06 ) .Or. ;
							( SB1->B1_COD < mv_par07 .Or. SB1->B1_COD > mv_par08 ) .Or. ;
							( !If(mv_par13==3,.T.,DA1->DA1_ATIVO==Str(mv_par13,1)) )
								DA1->(dbSkip())
								Loop
							EndIf

							dbSelectArea("TRBPRC")
							RecLock("TRBPRC",.T.)
							TRB_CODTAB := DA0->DA0_CODTAB
							TRB_DATDE  := DA0->DA0_DATDE
							TRB_HORADE := DA0->DA0_HORADE
							TRB_DATATE := DA0->DA0_DATATE
							TRB_HORATE := DA0->DA0_HORATE						
							TRB_DESTAB := DA0->DA0_DESCRI
							TRB_CODPRO := DA1->DA1_CODPRO
							TRB_DESPRO := SB1->B1_DESC
							TRB_UM     := SB1->B1_UM
							TRB_TIPO   := SB1->B1_TIPO
							TRB_GRUPO  := SB1->B1_GRUPO
							TRB_GRADE  := SB1->B1_GRADE
							TRB_QTDLOT := DA1->DA1_QTDLOT
							TRB_PRV1   := SB1->B1_PRV1
							TRB_PRCVEN := DA1->DA1_PRCVEN							
							TRB_MOEDA  := DA1->DA1_MOEDA
							TRB_VLRDES := DA1->DA1_VLRDES
							TRB_PERDES := DA1->DA1_PERDES						
							TRB_ESTADO := DA1->DA1_ESTADO							
							TRB_TPOPER := DA1->DA1_TPOPER                             

						EndIf

						dbSelectArea("DA1")
						dbSkip()

					EndDo

				EndIf

				dbSelectArea("DA0")
				dbSkip()

				cbCont++
				lImp := .T.

				IncRegua()
			EndDo

		EndIf

	EndIf

	Omr010Imp(lEnd,wnrel,cString,nomeprog,Titulo,mv_par11)

	//Ŀ
	//Se usou a tabela do DA0 fecha arquivo temporario
	//
	If mv_par11 == 2
		dbSelectArea("TRBPRC")
		dbClearFilter()
		dbCloseArea()
		If !lQuery
			Ferase(cArqTrb+GetDBExtension())
			Ferase(cArqTrb+OrdBagExt())
		EndIf
	EndIf	
	If ( lImp )
		Roda(cbCont,cbText,Tamanho)
	EndIf
	Set Device To Screen
	Set Printer To
	If ( aReturn[5] = 1 )
		dbCommitAll()
		OurSpool(wnrel)
	EndIf
	MS_FLUSH()
Return(.T.)

/*


ͻ
Programa  OMR010Trb   Autor  Henry Fila           Data   21/06/01 
͹
Desc.      Cria arquivo temporario para tabela de precos              
                                                                      
͹
ParametrosExpC1: Nome do arquivo que sera gerado por referencia       
          ExpN2: Ordem dos produtos a serem listados                  
͹
Uso        AP5dl                                                      
ͼ


*/
Static Function Omr010Trb(cArqTrba,nOrdem)

	Local aCampos := {}
	Local cAlias  := ""
	Local cArqTrb := ""
	Local aAreaSX3:= GetArea()
	Local nZZ := 0

	AADD(aCampos,{"TRB_CODTAB" ,"C",03,0}) 
	AADD(aCampos,{"TRB_DESTAB" ,"C",30,0}) 
	AADD(aCampos,{"TRB_DATDE"  ,"D",08,0}) 
	AADD(aCampos,{"TRB_HORADE" ,"C",05,0}) 
	AADD(aCampos,{"TRB_DATATE" ,"D",08,0}) 
	AADD(aCampos,{"TRB_HORATE" ,"C",05,0}) 
	AADD(aCampos,{"TRB_CODPRO" ,"C",15,0}) 
	AADD(aCampos,{"TRB_DESPRO" ,"C",30,0}) 
	AADD(aCampos,{"TRB_UM"     ,"C",02,0}) 
	AADD(aCampos,{"TRB_TIPO"   ,"C",02,0}) 
	AADD(aCampos,{"TRB_GRUPO"  ,"C",04,0}) 
	AADD(aCampos,{"TRB_GRADE"  ,"C",01,0}) 

	aSX3DA1 := FWSx3Util():GetAllFields("DA1",.F.)

	for nZZ := 1 to len(aSX3DA1)
	
		if GetSX3Cache(aSX3DA1[nZZ],"X3_CAMPO") == "DA1_QTDLOT"	
			AADD(aCampos,{"TRB_QTDLOT" ,GetCacheSX3(aSX3DA0[nZZ],"X3_TIPO"),GetCacheSX3(aSX3DA0[nZZ],"X3_TAMANHO"),GetCacheSX3(aSX3DA0[nZZ],"X3_DECIMAL")}) 
		endif

		if GetSX3Cache(aSX3DA1[nZZ],"X3_CAMPO") == "DA1_PRCBAS"	
			AADD(aCampos,{"TRB_PRV1" ,GetCacheSX3(aSX3DA0[nZZ],"X3_TIPO"),GetCacheSX3(aSX3DA0[nZZ],"X3_TAMANHO"),GetCacheSX3(aSX3DA0[nZZ],"X3_DECIMAL")}) 
		endif

		if GetSX3Cache(aSX3DA1[nZZ],"X3_CAMPO") == "DA1_PRCVEN"	
			AADD(aCampos,{"TRB_PRCVEN" ,GetCacheSX3(aSX3DA0[nZZ],"X3_TIPO"),GetCacheSX3(aSX3DA0[nZZ],"X3_TAMANHO"),GetCacheSX3(aSX3DA0[nZZ],"X3_DECIMAL")}) 
		endif

		if GetSX3Cache(aSX3DA1[nZZ],"X3_CAMPO") == "DA1_MOEDA"	
			AADD(aCampos,{"TRB_MOEDA" ,GetCacheSX3(aSX3DA0[nZZ],"X3_TIPO"),GetCacheSX3(aSX3DA0[nZZ],"X3_TAMANHO"),GetCacheSX3(aSX3DA0[nZZ],"X3_DECIMAL")}) 
		endif

		if GetSX3Cache(aSX3DA1[nZZ],"X3_CAMPO") == "DA1_VLRDES"	
			AADD(aCampos,{"TRB_VLRDES" ,GetCacheSX3(aSX3DA0[nZZ],"X3_TIPO"),GetCacheSX3(aSX3DA0[nZZ],"X3_TAMANHO"),GetCacheSX3(aSX3DA0[nZZ],"X3_DECIMAL")}) 
		endif

		if GetSX3Cache(aSX3DA1[nZZ],"X3_CAMPO") == "DA1_PERDES"	
			AADD(aCampos,{"TRB_PERDES" ,GetCacheSX3(aSX3DA0[nZZ],"X3_TIPO"),GetCacheSX3(aSX3DA0[nZZ],"X3_TAMANHO"),GetCacheSX3(aSX3DA0[nZZ],"X3_DECIMAL")}) 
		endif
	next nZZ

	AADD(aCampos,{"TRB_ESTADO" ,"C",02,0}) 
	AADD(aCampos,{"TRB_TPOPER" ,"C",01,2}) 


	If oTmpTable <> Nil
		oTmpTable:Delete()
		oTmpTable := Nil
	Endif

	cAlias  := "TRBPRC"	
	cArqTRB  := "TTBPRC"	
	oTmpTable := FWTemporaryTable():New( cAlias )  
	oTmpTable:SetFields(aCampos) 
	oTmpTable:AddIndex("1", {"TRB_CODTAB"})
	oTmpTable:Create()  

	//Ŀ
	//Mesmo em processadores velozes, a CriaTrab nunca provocar erros:
	//
	Do Case
		Case nOrdem == 1
		IndRegua("TRBPRC",cArqTrb,"TRB_CODTAB+TRB_TIPO+TRB_CODPRO")
		Case nOrdem == 2		
		IndRegua("TRBPRC",cArqTrb,"TRB_CODTAB+TRB_GRUPO+TRB_CODPRO")
		Case nOrdem == 3		
		IndRegua("TRBPRC",cArqTrb,"TRB_CODTAB+TRB_DESPRO+TRB_CODPRO")
		Case nOrdem == 4
		IndRegua("TRBPRC",cArqTrb,"TRB_CODTAB+TRB_CODPRO")		
	EndCase

	RestArea(aAreaSX3)

Return

/*


ͻ
Programa  Omr010Imp   Autor  Henry Fila           Data   21/06/01 
͹
Desc.      Cria arquivo temporario para tabela de precos              
                                                                      
͹
Uso        AP5dl                                                      
ͼ


*/

Static Function Omr010Imp(lEnd,wnrel,cString,nomeprog,Titulo,nTipo)

	Local cTabant  := ""
	Local cProdRef := ""                       
	Local li       := 100 // Contador de Linhas
	Local lRet     := .T.
	Local cKeySB1  := ""
	Local cIndSB1  := ""
	Local cCondSB1 := ""
	Local condicao := ""

	//
	//                                    1         2         3         4         5         6         7         8         9        10        11        12        13        14        15        16        17        18        19        20        21        22
	//                          01234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890
	//                          XXXXXXXXXXXXXXX XXXXXXXXXXXXXXXXXXXXXXXXXXXXXX XX    XXX,XXX.XX    XXX,XXX.XX  XXX,XXX.XX   XXX,XXX.XX  XXX.XXX  XX     XXXXXXXXXX
	Local cCabec1 := "CODIGO          DESCRICAO                      UM     FAIXA ATE         PRECO       PRECO MO- DESCONTO    FATOR  ESTADO TP.OPERACAO" 
	Local cCabec2 := "                                                                         BASE      TABELA EDA" 

	Local nTamRef :=Val(Substr(cMascgrd,1,2))
	Local nCol    := 0 
	Local nIndSB1  := 0

	If nTipo == 2

		dbSelectArea("TRBPRC")
		SetRegua(RecCount())
		dbGotop()	

		While !Eof()	

			lRet    := .T.
			cTabAnt := TRBPRC->TRB_CODTAB	

			If lEnd
				@ Prow()+1,001 PSAY "CANCELADO PELO OPERADOR" //"CANCELADO PELO OPERADOR"
				Exit
			EndIf

			If ( li > 58 )
				li := cabec(Titulo,cCabec1,cCabec2,nomeprog,Tamanho,CHRCOMP)
				li++
			EndIf

			@ li,000 PSAY "TABELA :" + TRBPRC->TRB_CODTAB + " - " +TRBPRC->TRB_DESTAB //"TABELA :"
			li++

			If ( li > 58 )
				li := cabec(Titulo,cCabec1,cCabec2,nomeprog,Tamanho,CHRCOMP)
				li++
			EndIf

			@ li,000 PSAY "VALIDADE :" + DtoC(TRBPRC->TRB_DATDE) + " as " + TRBPRC->TRB_HORADE + " Ate " + DtoC(TRBPRC->TRB_DATATE) + " as " + TRBPRC->TRB_HORATE //"VALIDADE :"
			li++

			If ( li > 58 )
				li := cabec(Titulo,cCabec1,cCabec2,nomeprog,Tamanho,CHRCOMP)
				li++
			EndIf

			@ li,000 PSAY __PrtThinLine()
			li++

			While !Eof() .And. TRBPRC->TRB_CODTAB == cTabAnt

				If ( li > 58 )
					li := cabec(Titulo,cCabec1,cCabec2,nomeprog,Tamanho,CHRCOMP)
					li++
				EndIf

				If mv_par12 == 1

					If TRBPRC->TRB_GRADE == "S" .And. !Empty(cProdRef) .And. cProdRef == Substr(TRBPRC->TRB_CODPRO,1,nTamRef)
						lRet := .F.
					Endif	

				Endif

				If lRet

					If TRBPRC->TRB_GRADE == "S"
						@ li,000 PSAY Substr(TRBPRC->TRB_CODPRO,1,nTamRef)

						If mv_par12 == 1
							dbSelectArea("SB4")
							If MsSeek(xFilial("SB4")+Substr(TRBPRC->TRB_CODPRO,1,nTamRef))
								@ li,016 PSAY SB4->B4_DESC
							Endif						 	
						Else 	                    
							@ li,016 PSAY TRBPRC->TRB_DESPRO
						Endif
					Else
						@ li,000 PSAY TRBPRC->TRB_CODPRO
						@ li,016 PSAY Substr(TRBPRC->TRB_DESPRO,1,30)					
					Endif	

					@ li,047 PSAY TRBPRC->TRB_UM
					@ li,050 PSAY TRBPRC->TRB_QTDLOT Picture Pesqpict("DA1","DA1_QTDLOT")
					@ li,064 PSAY TRBPRC->TRB_PRV1   Picture x3Picture("DA1_PRCBAS")		
					@ li,076 PSAY TRBPRC->TRB_PRCVEN Picture Pesqpict("DA1","DA1_PRCVEN")			 			
					@ li,090 PSAY TRBPRC->TRB_MOEDA
					@ li,094 PSAY TRBPRC->TRB_VLRDES Picture Pesqpict("DA1","DA1_VLRDES")
					@ li,108 PSAY TRBPRC->TRB_PERDES Picture Pesqpict("DA1","DA1_PERDES")
					@ li,115 PSAY TRBPRC->TRB_ESTADO

					Do case
						Case TRBPRC->TRB_TPOPER == "1"
						cDescOper := "Estadual"
						Case TRBPRC->TRB_TPOPER == "2"
						cDescOper := "Inter-Esta"
						Case TRBPRC->TRB_TPOPER == "3"
						cDescOper := "Norte/Nord"
						Case TRBPRC->TRB_TPOPER == "4"
						cDescOper := "Todos"

					EndCase	 					

					@ li,122 PSAY cDescOper

				Endif

				cProdRef:=Substr(TRBPRC->TRB_CODPRO,1,nTamRef)

				LI++
				dbSelectarea("TRBPRC")
				dbSkip()
			EndDo

			If mv_par10 == 1
				li := 80
			Else			
				li += 2	
			EndIf		
		EndDo	

	Else
		//
		//Imprime a tabela de precos baseado no SB1     
		//

		dbSelectArea("SB1")           
		SetRegua(RecCount())		// Total de Elementos da regua
		Do Case              
			Case aReturn[8] == 1
			cOrdem := OemtoAnsi("TIPO ")
			cCampo := "B1_TIPO"
			Case aReturn[8] == 2
			cOrdem := OemtoAnsi("GRUPO")
			cCampo := "B1_GRUPO"
			Case aReturn[8] == 3
			cOrdem := "     "
			cCampo := "B1_COD"
			Case aReturn[8] == 4
			cOrdem := "     "
			cCampo := "B1_COD"		
		EndCase

		titulo := "Listagem de Precos"          //"Listagem de Precos"
		cabec1 := cOrdem+" CODIGO           DESCRICAO  DO  PRODUTO         UM       QTD P/     "	//" CODIGO           DESCRICAO  DO  PRODUTO         UM       QTD P/     "
		cabec2 := Space(5)+"                                                          EMBAL."	//"                                                          EMBAL."

		nTipo:=IIF(aReturn[4]==1,GetMV("MV_COMP"),GetMV("MV_NORM")) 

		Do Case
			Case aReturn[8] == 1
			dbSetOrder(2)
			dbSeek(xFilial()+mv_par03,.T.)
			condicao := "B1_TIPO <= MV_PAR04"
			Case  aReturn[8] == 2
			dbSetOrder(4)
			dbSeek(xFilial()+mv_par05,.T.)
			condicao := "B1_GRUPO <= MV_PAR06"
			Case  aReturn[8] == 3                
			condicao := ".T."

			dbSetOrder(3)                           
			cKeySB1 := SB1->(IndexKey())
			cIndSB1 := CriaTrab(NIL,.F.)
			cCondSB1 += 'B1_FILIAL == "'+xFilial("SB1")+'" .And.' 
			cCondSB1 += 'B1_COD >= "'+mv_par07+'" .And.B1_COD <= "'+mv_par08+'" .And.'
			cCondSB1 += 'B1_TIPO >= "'+mv_par03+'" .And.B1_TIPO <= "'+mv_par04+'" .And.'
			cCondSB1 += 'B1_GRUPO >= "'+mv_par05+'" .And.B1_GRUPO <= "'+mv_par06+'"'			

			IndRegua("SB1",cIndSB1,cKeySB1,,cCondSB1) //"Selecionando Registros ..."    

			nIndSB1 := RetIndex("SB1")

			#IFNDEF TOP
			dbSetIndex(cIndSB1+OrdBagExT())
			#ENDIF   	

			dbSetOrder(nIndSB1+1)       
			dbGotop()     

			Case  aReturn[8] == 4
			condicao := ".T."

			dbSetOrder(1)
			cKeySB1 := SB1->(IndexKey())
			cIndSB1 := CriaTrab(NIL,.F.)
			cCondSB1 += 'B1_FILIAL == "'+xFilial("SB1")+'" .And.' 
			cCondSB1 += 'B1_COD >= "'+mv_par07+'" .And.B1_COD <= "'+mv_par08+'" .And.'
			cCondSB1 += 'B1_TIPO >= "'+mv_par03+'" .And.B1_TIPO <= "'+mv_par04+'" .And.'
			cCondSB1 += 'B1_GRUPO >= "'+mv_par05+'" .And.B1_GRUPO <= "'+mv_par06+'"'			

			IndRegua("SB1",cIndSB1,cKeySB1,,cCondSB1) //"Selecionando Registros ..."    

			nIndSB1 := RetIndex("SB1")

			#IFNDEF TOP
			dbSetIndex(cIndSB1+OrdBagExt())
			#ENDIF   	

			dbSetOrder(nIndSB1+1)       
			dbGotop()     

		EndCase

		While !Eof().And.B1_FILIAL=xFilial().And.&condicao

			IncRegua()  

			lRet := .T.

			//Ŀ
			// Valida o produto conforme a mascara         
			//
			If ValidMasc(SB1->B1_COD,MV_PAR09)

				IF lEnd
					@Prow()+1,001 PSAY "CANCELADO PELO OPERADOR"
					Exit
				EndIf

				cCodant := &cCampo

				While !Eof().And.SB1->B1_FILIAL==xFilial().And.cCodant == &cCampo

					If lEnd
						@ Prow()+1,001 PSAY "CANCELADO PELO OPERADOR"
						exit
					EndIf

					IncRegua()

					If aReturn[8] == 1
						IF B1_GRUPO < mv_par05 .Or. B1_GRUPO > mv_par06
							lRet := .F.
						EndIf
					Else
						IF B1_TIPO < mv_par03 .Or. B1_TIPO > mv_par04
							lRet := .F.
						EndIf
					EndIf


					IF SB1->B1_COD >= mv_par07 .And. SB1->B1_COD <= mv_par08 .And. ValidMasc(SB1->B1_COD,MV_PAR09) .And. lRet
						//Ŀ
						// Valida o produto conforme a mascara         
						//


						IF LI > 55
							li := cabec(titulo,cabec1,cabec2,nomeprog,tamanho,nTipo)
							li++
						EndIf

						If mv_par12 == 1

							If SB1->B1_GRADE == "S" .And. !Empty(cProdRef) .And. cProdRef == Substr(SB1->B1_COD,1,nTamRef)
								lRet := .F.
							Endif	

						Endif

						If lRet                          

							If aReturn[8] <> 3 .And. aReturn[8] <> 4
								@ li,00 PSAY &cCampo
							Endif

							@ li,06 PSAY IIF(SB1->B1_GRADE == "S",Substr(B1_COD,1,nTamRef),B1_COD)
							@ li,23 PSAY IIF(SB1->B1_GRADE == "S" .And. mv_par12 == 1,Substr(B1_COD,1,nTamRef),Substr(B1_DESC,1,30))
							@ li,54 PSAY B1_UM
							@ li,58 PSAY RetFldProd(SB1->B1_COD,"B1_QE") Picture AllTrim(X3PICTURE("B1_QE"))
							@ li,76 pSay SB1->B1_PRV1 Picture PesqPict("SB1", "B1_PRV1",18)

							cProdRef:=Substr(SB1->B1_COD,1,nTamRef)

							li++
						Endif						

					Endif	

					dbSelectArea("SB1")
					dbSkip()

				EndDo

				dbSelectArea("SB1")
				@ li,00 PSAY __PrtThinLine()
				li++

			Endif			

		EndDo
	EndIf

	RetIndex("SB1")
	dbSelectArea("SB1")
	dbSetOrder(1)

Return
