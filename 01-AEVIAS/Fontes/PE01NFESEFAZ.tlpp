#include 'protheus.ch'
#include 'tbiconn.ch'
#include 'topconn.ch'
#include 'fileio.ch'
#INCLUDE "rwmake.ch"

//---------------------------------------------------------------------------------
// Rotina | PE01NFESEFAZ  | Autor | Lucas Baia - UPDUO         | Data | 30/07/2021
//---------------------------------------------------------------------------------
// Descr. | Ponto de Entrada utilizado na Rotina de Nfe-Sefaz, serve para todos os 
//        | fatores assim como Mensagens, Dados de Produtos, Duplicatas e etc...
//---------------------------------------------------------------------------------
// Uso    | Aevias Onboard Supply Services
//---------------------------------------------------------------------------------
// Links  | https://tdn.totvs.com/pages/releaseview.action?pageId=274327446
//---------------------------------------------------------------------------------

USER FUNCTION PE01NFESEFAZ()
Local aArea     := GetArea()
Local aProd     := PARAMIXB[1] //array com dados do Produto (Código produto, descrição de prod, quantidade, lote, unidade de medida....)
Local cMensCli  := PARAMIXB[2] //mensagem do cliente na nota
Local cMensFis  := PARAMIXB[3] //mensagem fiscal da nota
Local aDest     := PARAMIXB[4] //array com dados do destinatario (cnpj, nome, endereço,....)
Local aNota     := PARAMIXB[5] //array com dados da nota (numero da nota, serie, data emissao ....)
Local aInfoItem := PARAMIXB[6] //array com as informações dos itens (numero do pedido, numero item pedido....)
Local aDupl     := PARAMIXB[7] //array com dados das duplicatas (prefixo, numero, parcela, vencimento.....)
Local aTransp   := PARAMIXB[8] //array com dados do transportador (cnpj, nome, ins. estadual, endereço....)
Local aEntrega  := PARAMIXB[9] //array com dados da entrega (cnpj, endereco, municipio, bairro, email,.....)
Local aRetirada := PARAMIXB[10] //array com dados do local de retirada (cnpj, endereço, municipio, bairro, email....)
Local aVeiculo  := PARAMIXB[11] //array com dados do veiculo transporte (placa, estado, ....)
Local aReboque  := PARAMIXB[12] //array com dados do segundo veiculo transporte (placa, estado, ....)
Local aNfVincRur:= PARAMIXB[13] //array com dados da Nota Fiscal de Produtor rural referenciada (nota, serie, emissao da nota, ...)
Local aEspVol   := PARAMIXB[14] //array com dados do volume da nota (peso liquido, peso bruto,....)
Local aNfVinc   := PARAMIXB[15] //array com dados do cupom fiscal (numero da nota, serie, emissao, chave da nota,....)
Local AdetPag   := PARAMIXB[16] //array com detalhes do pagamento (forma pagamento, valor .....)
Local aObsCont  := PARAMIXB[17] //array com observação do contribuinte
//Local aProcRef  := PARAMIXB[18] //array com dados da nota
Local aRetorno  := {}

fMensCli(aNota[04], @cMensCli) //aNota[04] = Tipo da Nota, cMensCli = "Local cMensCli  := PARAMIXB[2]"
cDescriProd(@aProd, @aNota) //aNota[04] = Tipo da Nota, aProd    = "Local aProd     := PARAMIXB[1]"

//O retorno deve ser exatamente nesta ordem e passando o conteúdo completo dos arrays
//pois no rdmake nfesefaz é atribuido o retorno completo para as respectivas variáveis
//Ordem:
//      aRetorno[1] -> aProd
//      aRetorno[2] -> cMensCli
//      aRetorno[3] -> cMensFis
//      aRetorno[4] -> aDest
//      aRetorno[5] -> aNota
//      aRetorno[6] -> aInfoItem
//      aRetorno[7] -> aDupl
//      aRetorno[8] -> aTransp
//      aRetorno[9] -> aEntrega
//      aRetorno[10] -> aRetirada
//      aRetorno[11] -> aVeiculo
//      aRetorno[12] -> aReboque
//      aRetorno[13] -> aNfVincRur
//      aRetorno[14] -> aEspVol
//      aRetorno[15] -> aNfVinc
//      aRetorno[16] -> AdetPag
//      aRetorno[17] -> aObsCont 
//      aRetorno[18] -> aProcRef 
 
aadd(aRetorno,aProd)
aadd(aRetorno,cMensCli)
aadd(aRetorno,cMensFis)
aadd(aRetorno,aDest)
aadd(aRetorno,aNota)
aadd(aRetorno,aInfoItem)
aadd(aRetorno,aDupl)
aadd(aRetorno,aTransp)
aadd(aRetorno,aEntrega)
aadd(aRetorno,aRetirada)
aadd(aRetorno,aVeiculo)
aadd(aRetorno,aReboque)
aadd(aRetorno,aNfVincRur)
aadd(aRetorno,aEspVol)
aadd(aRetorno,aNfVinc)
aadd(aRetorno,AdetPag)
aadd(aRetorno,aObsCont)
//aadd(aRetorno,aProcRef)
 
RestArea(aArea)

RETURN aRetorno

//---------------------------------------------------------------------------------
// Rotina | cDescriProd    | Autor | Lucas Baia - UPDUO        | Data | 12/08/2021
//---------------------------------------------------------------------------------
// Descr. | Criado para exibir a Descrição de Produto completa na DANFE e XML.
//---------------------------------------------------------------------------------
// Uso    | Aevias Onboard Supply Services
//-----------------------------------------------------------------------------

Static Function cDescriProd(aProd, aNota)
Local I
Local cDescProd := ""

IF aNota[4] == "1" .OR. aNota[4] == "2" //1 - NF de Saída e 2 - NF de Entrada.
    
    For I := 1 To Len(aProd)
        If !Empty(aProd[I][2])
            cDescProd   := Posicione("SB1",1,xFilial("SB1")+aProd[I][2],"B1_DESC")
            aProd[I][4] := AllTrim(cDescProd)
        EndIf
    Next
ENDIF

Return aProd

//*****************************************************/

//*****************************************************/

//---------------------------------------------------------------------------------
// Rotina | fMensCli      | Autor | Lucas Baia - UPDUO        | Data | 02/06/2021
//---------------------------------------------------------------------------------
// Descr. | Foi criado uma Static Function para que possa trabalhar de forma
//        | organizada no Fonte, neste caso seria para exibir Mensagens na DANFE.
//---------------------------------------------------------------------------------
// Uso    | Aevias Onboard Supply Services
//---------------------------------------------------------------------------------

Static Function fMensCli(c_TpNota, cMensCli)

IF c_TpNota == "0" //Nota Fical de Entrada.
    cMensCli := SF1->F1_XOBS
ELSE
    cMensCli := SF2->F2_XOBS
ENDIF

Return Nil

//*****************************************************/
