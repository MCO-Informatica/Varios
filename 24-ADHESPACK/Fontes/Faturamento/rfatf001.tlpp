#Include 'Protheus.ch'

//---------------------------------------------------------------------------------
// Rotina | RFATF001       | Autor | Lucas Baia          | Data |    06/09/2022	
//---------------------------------------------------------------------------------
// Descr. | Fonte Customizado para trazer as Notas Fiscais emitidas naquele
//        | Pedido posicionado na tela do Pedido de Vendas.
//---------------------------------------------------------------------------------
// Uso    | ADHESPACK COSMETICOS
//---------------------------------------------------------------------------------


User Function RFATF001(_cNum)

Local oDlg
Local cQuery        := ""
Local cAliasSF2     := GetNextAlias()
Local aCamposSF2    := {}
Local aCampos       := {}

DEFINE DIALOG oDlg TITLE "Notas Fiscais Emitidas" FROM 180,180 TO 550,900 PIXEL 
 
 // Cria browse que receberá as colunas 
 //DbSelectArea("SA1")
cQuery := "SELECT F2_EMISSAO, F2_DOC, F2_SERIE, F2_VALBRUT "
cQuery += "FROM "+RetSqlName("SF2")+" SF2 "
cQuery += "INNER JOIN "+RetSqlName("SD2")+" SD2 ON SF2.F2_DOC = SD2.D2_DOC AND SF2.F2_SERIE = SD2.D2_SERIE "
cQuery += "WHERE "
cQuery += "D2_FILIAL = '"+xFilial("SD2")+"' "
cQuery += "AND D2_PEDIDO = '"+_cNum+"' "
cQuery += "AND SF2.D_E_L_E_T_ = '' "
cQuery += "AND SD2.D_E_L_E_T_ = '' "
cQuery += "GROUP BY F2_EMISSAO, F2_DOC, F2_SERIE, F2_VALBRUT "
cQuery += "ORDER BY F2_DOC, F2_SERIE "

cQuery := ChangeQuery(cQuery)

dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSF2,.T.,.T.)

 IF (cAliasSF2)->(EOF())
    MsgAlert("Esse Pedido não existe Notas Faturadas!", "ATENÇÃO")
    Return
 ENDIF

 oBrowse:=TCBrowse():New(1,1,300,184,,{'Data de Emissão','Número da Nota Fiscal','Série da Nota Fiscal','Valor Total da Nota Fiscal'},,oDlg,,,,,,,,,,,,.F.,,.T.,,.F.,,,)
 /*
 // Cria colunas 
 oBrowse:AddColumn(TCColumn():New("Data de Emissão",{||STOD((cAliasSF2)->F2_EMISSAO) },,,,"LEFT",,.F.,.F.,,,,,)) 
 oBrowse:AddColumn(TCColumn():New("Número da Nota Fiscal" ,{||(cAliasSF2)->F2_DOC},,,,"LEFT",,.F.,.F.,,,,,)) 
 oBrowse:AddColumn(TCColumn():New("Série da Nota Fiscal" ,{||(cAliasSF2)->F2_SERIE},,,,"LEFT",,.F.,.F.,,,,,))
 oBrowse:AddColumn(TCColumn():New("Valor Total da Nota Fiscal" ,{||Transform((cAliasSF2)->F2_VALBRUT,"@E 99,999,999,999.99")},,,,"LEFT",,.F.,.F.,,,,,))
 */
/*
 aBrowse := { {(cAliasSF2)->F2_EMISSAO,;
              (cAliasSF2)->F2_DOC,;
              (cAliasSF2)->F2_SERIE,;
              (cAliasSF2)->F2_VALBRUT} }*/


While (cAliasSF2)->(!EOF())

    aCampos := {}
    aAdd(aCampos,(cAliasSF2)->F2_EMISSAO)
    aAdd(aCampos,(cAliasSF2)->F2_DOC)
    aAdd(aCampos,(cAliasSF2)->F2_SERIE)
    aAdd(aCampos,(cAliasSF2)->F2_VALBRUT)
    
    aAdd(aCamposSF2,aCampos)
    (cAliasSF2)->(DbSkip())

ENDDO
 // Seta vetor para a browse
 oBrowse:SetArray(aCamposSF2)

 //oBrowse :bLine := {||{aCamposSF2[oBrowse:nAt]}}

 oBrowse :bLine := {||{     STOD(aCamposSF2[oBrowse:nAt,01]),;
                            aCamposSF2[oBrowse:nAt,02],;
                            aCamposSF2[oBrowse:nAt,03],;
                            Transform(aCamposSF2[oBrowse:nAT,04],"@E 99,999,999,999.99") } }
 
// Evento de duplo click na celula
 oBrowse:bLDblClick := {|| Itens(aCamposSF2[oBrowse:nAt,02], aCamposSF2[oBrowse:nAt,03]) }

 TButton():New( 004, 310, "Fechar", oDlg,{|| (cAliasSF2)->(DbCloseArea()), oDlg:End() },40,010,,,.F.,.T.,.F.,,.F.,,,.F. )

oBrowse:SetFocus()
 
ACTIVATE DIALOG oDlg CENTERED
 
Return



Static Function Itens(_nDoc, _nSerie)

Local oDlg1
Local cQuery1        := ""
Local cAliasSD2     := GetNextAlias()
Local aCamposSD2    := {}
Local aCampos1      := {}
Local aCamSD2       := {}

DEFINE DIALOG oDlg1 TITLE "Itens da Nota Fiscal selecionada" FROM 180,180 TO 550,1200 PIXEL 
 
 // Cria browse que receberá as colunas 
 //DbSelectArea("SA1")
cQuery1 := "SELECT D2_ITEM, D2_COD, B1_DESC, D2_LOCAL, D2_QUANT, D2_PRCVEN, D2_TOTAL "
cQuery1 += "FROM "+RetSqlName("SD2")+" SD2 "
cQuery1 += "INNER JOIN "+RetSqlName("SB1")+" SB1 ON SD2.D2_COD = SB1.B1_COD "
cQuery1 += "WHERE "
cQuery1 += "D2_FILIAL = '"+xFilial("SD2")+"' "
cQuery1 += "AND D2_DOC = '"+_nDoc+"' "
cQuery1 += "AND D2_SERIE = '"+_nSerie+"' "
cQuery1 += "AND SD2.D_E_L_E_T_ = '' "
cQuery1 += "AND SB1.D_E_L_E_T_ = '' "
cQuery1 += "GROUP BY D2_ITEM, D2_COD, B1_DESC, D2_LOCAL, D2_QUANT, D2_PRCVEN, D2_TOTAL "
cQuery1 += "ORDER BY D2_ITEM, D2_COD "

cQuery1 := ChangeQuery(cQuery1)

dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery1),cAliasSD2,.T.,.T.)

aCamSD2 := {'Item', 'Cód. Prod.', 'Descr.', 'Armazem', 'Quant.', 'Vlr. Unit.', 'Vlr. Total'}

oBrowse1:=TCBrowse():New(1,1,450,184,,aCamSD2,,oDlg1,,,,,,,,,,,,.F.,,.T.,,.F.,,,)

While (cAliasSD2)->(!EOF())

    aCampos1 := {}
    aAdd(aCampos1,(cAliasSD2)->D2_ITEM)
    aAdd(aCampos1,(cAliasSD2)->D2_COD)
    aAdd(aCampos1,(cAliasSD2)->B1_DESC)
    aAdd(aCampos1,(cAliasSD2)->D2_LOCAL)
    aAdd(aCampos1,(cAliasSD2)->D2_QUANT)
    aAdd(aCampos1,(cAliasSD2)->D2_PRCVEN)
    aAdd(aCampos1,(cAliasSD2)->D2_TOTAL)
    
    aAdd(aCamposSD2,aCampos1)
    (cAliasSD2)->(DbSkip())

ENDDO
 // Seta vetor para a browse
 oBrowse1:SetArray(aCamposSD2)

 //oBrowse :bLine := {||{aCamposSF2[oBrowse:nAt]}}

 oBrowse1 :bLine := {||{    aCamposSD2[oBrowse1:nAt,01],;
                            aCamposSD2[oBrowse1:nAt,02],;
                            Alltrim(aCamposSD2[oBrowse1:nAt,03]),;
                            aCamposSD2[oBrowse1:nAT,04],;
                            Transform(aCamposSD2[oBrowse1:nAt,05],"@E 99999999.99"),; //
                            Transform(aCamposSD2[oBrowse1:nAt,06],"@E 99,999,999,999.99"),;
                            Transform(aCamposSD2[oBrowse1:nAt,07],"@E 99,999,999,999.99")  }}

 TButton():New( 004, 460, "Fechar", oDlg1,{|| (cAliasSD2)->(DbCloseArea()), oDlg1:End() },40,010,,,.F.,.T.,.F.,,.F.,,,.F. )

oBrowse1:SetFocus()
 
ACTIVATE DIALOG oDlg1 CENTERED
 
Return
